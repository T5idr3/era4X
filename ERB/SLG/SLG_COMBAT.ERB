;---------------------------------
; SLG COMBAT
;---------------------------------

;---------------------------------
; 戦闘
;---------------------------------
@COMBAT(ID)
#DIM ID
#DIM ITER, 2
#DIM COUNTER
#DIM COUNTRY_LIST, MAX_COUNTRY
#DIM SHIPS, 2, MAX_SHIP
#DIM SHIPS_LENGTH, 2
#DIM SHIP_ID
#DIM POS_X
#DIM POS_Y
#DIM SELECTED
#DIM SETTLEMENT_FLAG ;戦闘終了フラグ
#DIM BATTLE_COUNTRY, 2
#DIM SHOW_BATTLE

CALL GET_SHIP_LIST_FROM_POS(ID)
IF SHIP_LIST_LENGTH < 2
	RETURN
ENDIF

; 参加国の設定
FOR LOCAL, 0, SHIP_LIST_LENGTH
	COUNTRY_LIST:(SHIP_COUNTRY:(SHIP_LIST:LOCAL)) = 1
NEXT


; 互いの好感度を見て交戦判定
FOR BATTLE_COUNTRY, 0, MAX_COUNTRY
	; 宙域に艦船が存在しない国は省く
	SIF COUNTRY_LIST:BATTLE_COUNTRY == 0
		CONTINUE

	; 初期化
	FOR ITER, 0, MAX_SHIP
		DISTANCE:ITER = 0
		ATTACK_TARGET:ITER = 0
		SHIP_ACTION:ITER = 0
	NEXT

	FOR BATTLE_COUNTRY:1, BATTLE_COUNTRY, MAX_COUNTRY

		; 宙域に艦船が存在しない国は省く
		SIF COUNTRY_LIST:(BATTLE_COUNTRY:1) == 0
			CONTINUE
		IF BATTLE_COUNTRY:0 == PLAYER_COUNTRY || BATTLE_COUNTRY:1 == PLAYER_COUNTRY
			SHOW_BATTLE = 1
		ELSE
			SHOW_BATTLE = 0
		ENDIF

		;BATTLE_COUNTRY: 勢力A, (BATTLE_COUNTRY:1): 勢力B
		IF REL_LIKE:BATTLE_COUNTRY:(BATTLE_COUNTRY:1) <= -1000 || REL_LIKE:(BATTLE_COUNTRY:1):BATTLE_COUNTRY <= -1000
			; 双方の艦船リストを作成
			FOR ITER:1, 0, 2
				COUNTER = 0
				FOR ITER, 0, SHIP_LIST_LENGTH
					IF SHIP_COUNTRY:(SHIP_LIST:ITER) == BATTLE_COUNTRY:(ITER:1)
						SHIPS:(ITER:1):COUNTER = SHIP_LIST:ITER
						COUNTER ++
					ENDIF
				NEXT
				SHIPS_LENGTH:(ITER:1) = COUNTER
			NEXT
			
			; 艦船が存在しない場合は却下。書き方がださい
			SIF SHIPS_LENGTH:0 == 0
				CONTINUE
			SIF SHIPS_LENGTH:1 == 0
				CONTINUE
			
			;---------------------------------
			; 艦船の行動決定
			; 戦闘可能な艦は主砲距離で戦闘
			;---------------------------------
			LOCAL:2 = SHIPS_LENGTH:0 > SHIPS_LENGTH:1 ? SHIPS_LENGTH:0 # SHIPS_LENGTH:1
			
			$BATTLE_LOOP
			
			FOR ITER:1, 0, 2
				FOR ITER, 0, LOCAL:2
				
					SHIP_ID = SHIPS:(ITER:1):ITER
						
					; 距離初期化
					SIF DISTANCE:SHIP_ID == 0
						DISTANCE:SHIP_ID = 3

					; 非戦闘艦は退却
					IF SHIP_ACTION:SHIP_ID == 大破
						CONTINUE
					ELSEIF SHIP_TYPE:SHIP_ID == 偵察艇 || SHIP_TYPE:SHIP_ID == 輸送船 || SHIP_TYPE:SHIP_ID == 作業船
						IF DISTANCE:SHIP_ID == 3
							SHIP_ACTION:SHIP_ID = 撤退
						ELSE
							SHIP_ACTION:SHIP_ID = 後退
						ENDIF
					ELSE
						; 戦闘艦は体力の1/3を切ったら退却
						CALL GET_SHIP_TOTAL_HP(SHIP_ID)
						IF RESULT / 3 * 2 < SHIP_DAMAGE:SHIP_ID
							IF DISTANCE:SHIP_ID == 3
								SHIP_ACTION:SHIP_ID = 撤退
							ELSE
								SHIP_ACTION:SHIP_ID = 後退
							ENDIF
						ELSE
							CALL GET_MAIN_WEAPON(SHIP_EQUIP:主砲:SHIP_ID)
							; RESULT: 武器レンジ
							IF DISTANCE:SHIP_ID <= FIRE_RANGE
								SHIP_ACTION:SHIP_ID = 攻撃
								CALL GET_TARGET(SHIP_ID, SHIP_COUNTRY:SHIP_ID == BATTLE_COUNTRY ? BATTLE_COUNTRY:1 # BATTLE_COUNTRY)
								ATTACK_TARGET:SHIP_ID = RESULT
								SIF RESULT == -1
									THROW 攻撃対象選択時に異常な引数あるいは結果を検出しました
							ELSE
								SHIP_ACTION:SHIP_ID = 前進
							ENDIF
						ENDIF
					ENDIF
				NEXT
			NEXT

			;---------------------------------
			; 描画処理
			;---------------------------------
			$INPUT_LOOP
			LOCAL:2 = SHIPS_LENGTH:0 > SHIPS_LENGTH:1 ? SHIPS_LENGTH:0 # SHIPS_LENGTH:1

			; 艦船一覧表示
			IF SHOW_BATTLE
				DRAWLINE
				PRINTFORML ◆開戦
				LOCALS = 　
				FOR ITER, 0, LOCAL:2
					SHIP_ID = SHIPS:0:ITER
					IF ITER < SHIPS_LENGTH:0
						SIF SHIP_ACTION:SHIP_ID == 大破
							SETCOLOR 0X444444
						PRINTBUTTON @"[%SHIP_NAME:SHIP_ID, DISTANCE:SHIP_ID * 15, LEFT%]", SHIP_ID
						SIF SHIP_ACTION:SHIP_ID == 大破
							RESETCOLOR
					ELSE
						PRINTFORM %LOCALS, (DISTANCE:SHIP_ID * 15 + 2), LEFT%
					ENDIF

					SHIP_ID = SHIPS:1:ITER
					IF ITER < SHIPS_LENGTH:1
						SIF SHIP_ACTION:SHIP_ID == 大破
							SETCOLOR 0X444444
						PRINTBUTTON @"[%SHIP_NAME:SHIP_ID, DISTANCE:SHIP_ID * 15, RIGHT%]", SHIP_ID
						SIF SHIP_ACTION:SHIP_ID == 大破
							RESETCOLOR
					ELSE
						PRINTFORM %LOCALS, (DISTANCE:SHIP_ID * 15 + 2), RIGHT%
					ENDIF
					PRINTL 
				NEXT
				PRINTL
				PRINTBUTTON "[進行]", 9999
				
				INPUT
			ELSE
				RESULT = 9999
			ENDIF
			IF RESULT >= 0 && RESULT < MAX_SHIP
				SELECTED = RESULT
				FOR LOCAL:4, 0, SHIP_LIST_LENGTH
					;---------------------------------
					; 艦船詳細表示
					;---------------------------------
					IF SHIP_LIST:(LOCAL:4) == RESULT
						$DETAIL_LOOP
						SELECTCASE SHIP_ACTION:SELECTED
							CASE 撤退
								PRINTL 撤退中
							CASE 後退
								PRINTL 後退中
							CASE 攻撃
								PRINTFORML 攻撃中(%SHIP_NAME:(ATTACK_TARGET:SELECTED)%)
							CASE 前進
								PRINTL 前進中
							CASE 大破
								PRINTFORML %SHIP_NAME:(BROKEN_BY:SELECTED)%の攻撃により大破
						ENDSELECT
						CALL SHOW_SHIP_INFO(RESULT)
					ENDIF
				NEXT
				GOTO INPUT_LOOP
			ELSEIF RESULT == 9999
				;---------------------------------
				; 行動実行
				;---------------------------------
				COUNTER = 0
				SETTLEMENT_FLAG = 0
				LOCAL:2 = SHIPS_LENGTH:0 > SHIPS_LENGTH:1 ? SHIPS_LENGTH:0 # SHIPS_LENGTH:1
				FOR ITER:1, 0, 2
					FOR ITER, 0, LOCAL:2
						; 宙域にいる所属艦艇数以上は回さない
						SIF ITER >= SHIPS_LENGTH:(ITER:1)
							CONTINUE 
					
						SHIP_ID = SHIPS:(ITER:1):ITER
						;戦闘不能艦を数える
						SIF SHIP_ACTION:SHIP_ID == 撤退 || SHIP_ACTION:SHIP_ID == 大破
							COUNTER ++
						; 前進
						SIF SHIP_ACTION:SHIP_ID == 前進 && DISTANCE:SHIP_ID > 1
							DISTANCE:SHIP_ID --
						; 後退
						SIF SHIP_ACTION:SHIP_ID == 後退 && DISTANCE:SHIP_ID < 3
							DISTANCE:SHIP_ID ++
						; 攻撃
						IF SHIP_ACTION:SHIP_ID == 攻撃
							SIF SHOW_BATTLE
								PRINTFORML %SHIP_NAME:SHIP_ID%が%SHIP_NAME:(ATTACK_TARGET:SHIP_ID)%を攻撃
							CALL ATTACK(SHIP_ID)
							IF RESULT == 1
								SHIP_ACTION:(ATTACK_TARGET:SHIP_ID) = 大破
								BROKEN_BY:(ATTACK_TARGET:SHIP_ID) = SHIP_ID
							ENDIF
						ENDIF
					NEXT
					; 全艦戦闘不能
					IF COUNTER == SHIPS_LENGTH:(ITER:1)
						SIF SHOW_BATTLE
							PRINTFORMW %COUNTRY_NAME:(SHIP_COUNTRY:SHIP_ID)%敗退
						SETTLEMENT_FLAG = 1
					ENDIF
					SIF SETTLEMENT_FLAG == 1
						BREAK
				NEXT
				SIF SETTLEMENT_FLAG == 1
					CONTINUE
			ENDIF
			GOTO BATTLE_LOOP
		ENDIF
	NEXT
NEXT

;---------------------------------
; ターゲット取得
;---------------------------------
@GET_TARGET(SHIP_ID, TARGET_COUNTRY_ID)
#DIM SHIP_ID
#DIM TARGET_COUNTRY_ID
#DIM ITER
#DIM ENEMY_ID
#DIM RANGE_LIST, 3, MAX_SHIP
#DIM RANGE_LIST_LENGTH, 3

FOR ITER, 0, 3
	RANGE_LIST_LENGTH:ITER = 0
NEXT


SIF SHIP_LIST_LENGTH == -1
	RETURN -1

SIF SHIP_COUNTRY:SHIP_ID == TARGET_COUNTRY_ID
	RETURN -1

FOR ITER, 0, SHIP_LIST_LENGTH
	SIF SHIP_COUNTRY:(SHIP_LIST:ITER) != TARGET_COUNTRY_ID
		CONTINUE
	ENEMY_ID = SHIP_LIST:ITER
	IF SHIP_ACTION:ENEMY_ID == 大破
		CONTINUE
	ENDIF
	CALL GET_MAIN_WEAPON_RANGE(ENEMY_ID)
	SELECTCASE RESULT
		CASE 1
			RANGE_LIST:0:RANGE_LIST_LENGTH = ENEMY_ID
			RANGE_LIST_LENGTH ++
		CASE 2
			RANGE_LIST:1:(RANGE_LIST_LENGTH:1) = ENEMY_ID
			RANGE_LIST_LENGTH:1 ++
		CASEELSE
			RANGE_LIST:2:(RANGE_LIST_LENGTH:2) = ENEMY_ID
			RANGE_LIST_LENGTH:2 ++
	ENDSELECT
NEXT

; 近い敵から攻撃する
FOR ITER, 0, 3
	SIF RANGE_LIST_LENGTH:ITER > 0
		; TODO AI性能とかで弱った敵から攻撃できるようにしたい
		RETURN RANGE_LIST:(ITER):(RAND:(RANGE_LIST_LENGTH:ITER))
NEXT
RETURN -1

;---------------------------------
; 攻撃（RETURN 0: 攻撃完了, 1: 撃沈）
;---------------------------------
@ATTACK(SHIP_ID)
#DIM SHIP_ID
#DIM TARGET_ID
#DIM TOTAL_DAMAGE
#DIM ITER
#DIM ENEMY_AVOID
#DIM ENEMY_HP

TARGET_ID = ATTACK_TARGET:SHIP_ID
TOTAL_DAMAGE = 0

CALL GET_MAIN_WEAPON(SHIP_EQUIP:主砲:SHIP_ID)
CALL GET_AVOID(TARGET_ID)
ENEMY_AVOID = RESULT
FOR ITER, 0, FIRE_RATE
	;当たり判定
	SIF RAND:100 >= HIT_RATE
		CONTINUE
	;回避判定
	SIF RAND:100 >= ENEMY_AVOID
		CONTINUE
	
	TOTAL_DAMAGE += LIMIT(DAMAGE - RAND:(LIMIT(DAMAGE/10, 1, DAMAGE)), 0, DAMAGE)
NEXT

;敵のシールド値を取得し、シールドダメージを与える
;その後余剰ダメージを本体に与える
CALL GET_SHIP_SIELD(TARGET_ID)
;シールドダメージに終わる場合
IF RESULT > TOTAL_DAMAGE
	SIELD_DAMAGE:TARGET_ID += TOTAL_DAMAGE
	TOTAL_DAMAGE = 0
	RETURN
;シールド貫通した場合
ELSE
	SIELD_DAMAGE:TARGET_ID += RESULT
	TOTAL_DAMAGE -= RESULT
ENDIF

CALL GET_SHIP_HP(TARGET_ID)
ENEMY_HP = RESULT
IF ENEMY_HP < TOTAL_DAMAGE
	TOTAL_DAMAGE = ENEMY_HP
	SHIP_DAMAGE:TARGET_ID += TOTAL_DAMAGE
	RETURN 1
ENDIF
SHIP_DAMAGE:TARGET_ID += TOTAL_DAMAGE
RETURN 0


