;--------------------------
; 工場システム
;--------------------------

;--------------------------
; 工場
;--------------------------
@FACTORY(PORT_ID, COUNTRY_ID, IS_PORT)
#DIM PORT_ID
#DIM COUNTRY_ID
#DIM IS_PORT
#DIM ITEM_ID
#DIM 座標ID
#DIM IS_FREE
#DIMS ITEM_NAME
#DIMS ITEM_DESCRIPTION
#DIM ITEM_LIST, ITEM_LENGTH
#DIM MATERIAL_NUM

IS_FREE = 0
IF IS_PORT == 1
    座標ID = PORT_POSITION_ID:PORT_ID
    SIF PORT_OWNER:PORT_ID == PLAYER_COUNTRY
        IS_FREE = 1
ELSE
    座標ID = 星系座標ID:PORT_ID
    SIF STAR_OWNER:PORT_ID == PLAYER_COUNTRY
        IS_FREE = 1
ENDIF

CALL GET_ITEM_LIST_BY_CATEGORY(ITEM_LIST, "素材")
MATERIAL_NUM = RESULT

$INPUT_LOOP
PRINTL ◆民間工場
PRINTFORML     %"名前", 20, LEFT% 加工費 %"個数", 9, LEFT% 説明
FOR LOCAL, 0, MATERIAL_NUM
    ITEM_ID = ITEM_LIST:LOCAL

    CALL GET_ITEM_NAME(ITEM_ID)
    ITEM_NAME = %RESULTS%
    CALL GET_ITEM_DESCRIPTION(ITEM_ID)
    ITEM_DESCRIPTION = %RESULTS%
    
    IF GET_RESOURCE_NUM(座標ID, COUNTRY_ID, ITEM_ID) == 0
        RESULTS = %"0", 9, RIGHT%
    ELSE
        RESULTS = %TOSTR(GET_RESOURCE_NUM(座標ID, COUNTRY_ID, ITEM_ID), "#,###,###")%
    ENDIF
    IF IS_FREE == 1
        PRINTBUTTON @"[{ITEM_ID}] %ITEM_NAME, 20, LEFT% {ITEM_PRICE:ITEM_ID / 10, 3, RIGHT} %RESULTS, 9, RIGHT% %ITEM_DESCRIPTION%", ITEM_ID
    ELSE
        PRINTBUTTON @"[{ITEM_ID}] %ITEM_NAME, 20, LEFT% %"$0", 3, RIGHT% %RESULTS, 9, RIGHT% %ITEM_DESCRIPTION%", ITEM_ID
    ENDIF
    PRINTL
NEXT
PRINTBUTTON "[999] キャンセル", 999
PRINTL
INPUT
IF RESULT == 999
    RETURN
ELSEIF FINDELEMENT(ITEM_LIST, RESULT) != -1
    CALL FACTORY_SHOP(RESULT, PORT_ID, COUNTRY_ID, IS_FREE, IS_PORT)
    GOTO INPUT_LOOP
ELSE
    GOTO INPUT_LOOP
ENDIF

;--------------------------
; 個数選択
;--------------------------
@FACTORY_SHOP(ITEM_ID, PORT_ID, COUNTRY_ID, IS_FREE, IS_PORT)
#DIM ITEM_ID
#DIM PORT_ID
#DIM COUNTRY_ID
#DIM IS_FREE
#DIM IS_PORT
#DIM ITEM_COUNTER
#DIM USE_RESOURCE, ITEM_LENGTH
#DIM MAX_PROCESSING
#DIM ITER
#DIM RESOURCE_PRICE
#DIM 座標ID
#DIMS ITEM_NAME

VARSET USE_RESOURCE, 0
ITEM_COUNTER = 0

IF IS_PORT == 1
    座標ID = PORT_POSITION_ID:PORT_ID
ELSE
    座標ID = 星系座標ID:PORT_ID
ENDIF

; 最大加工数の算出
MAX_PROCESSING = 99999
CALL GET_RESOUCES_OF_ITEM(ITEM_ID, USE_RESOURCE)
FOR ITER, 0, ITEM_LENGTH
    SIF USE_RESOURCE:ITER == 0
        CONTINUE
    RESULT = GET_RESOURCE_NUM(座標ID, COUNTRY_ID, ITER)
    SIF RESULT < 0
        RESULT = 0
    SIF USE_RESOURCE:ITER * MAX_PROCESSING > RESULT
        MAX_PROCESSING = RESULT / USE_RESOURCE:ITER
NEXT
IF MAX_PROCESSING * ITEM_PRICE:ITEM_ID > MONEY
    MAX_PROCESSING = MONEY / ITEM_PRICE:ITEM_ID
ENDIF

$INPUT_LOOP
PRINTL ◆加工数選択
IF MAX_PROCESSING == 0
    SETCOLOR 0XFF0000
    PRINTL [CAUTION] 最大加工可能数が0です。(可能性: 素材不足/資金不足)
    RESETCOLOR
ENDIF
; 加工に必要な素材数
PRINTL 加工に必要な素材数
FOR ITER, 0, ITEM_LENGTH
    SIF USE_RESOURCE:ITER == 0
        CONTINUE
    CALL GET_ITEM_NAME(ITER)
    ITEM_NAME = %RESULTS%
    RESULTS = %TOSTR(ITEM_COUNTER * USE_RESOURCE:ITER, "#,###")%
    SIF ITEM_COUNTER == 0
        RESULTS = 0
    PRINTFORML %ITEM_NAME, 20, LEFT% %RESULTS, 4, RIGHT%
NEXT
PRINTL

PRINTBUTTON "[<<<]", -100
PRINTBUTTON "[<<]", -10
PRINTBUTTON "[<]", -1
LOCALS = %TOSTR(ITEM_COUNTER, "#,###")%
SIF ITEM_COUNTER == 0
    LOCALS = %"0"%
PRINTFORM %LOCALS, 4, RIGHT%
PRINTBUTTON "[>]", 1
PRINTBUTTON "[>>]", 10
PRINTBUTTON "[>>>]", 100
PRINTL
PRINTFORML 加工後の%ITEM_NAME%の数量: {ITEM_COUNTER + GET_RESOURCE_NUM(座標ID, COUNTRY_ID, ITEM_ID)}
DRAWLINE
RESOURCE_PRICE = ITEM_PRICE:ITEM_ID / 10 * ITEM_COUNTER
LOCALS = %TOSTR(RESOURCE_PRICE, "$###,###,###")%
SIF RESOURCE_PRICE == 0
    LOCALS = %"$0"%
SIF IS_FREE == 0
    PRINTFORML 加工費: %LOCALS%
PRINTL
PRINTBUTTON "[999] 決定", 999

INPUT
IF RESULT == 999
    FOR ITER, 0, ITEM_LENGTH
        SIF USE_RESOURCE:ITER == 0
            CONTINUE
        RESULT = REMOVE_RESOURCE(座標ID, COUNTRY_ID, ITER, ITEM_COUNTER * USE_RESOURCE:ITER)
    NEXT
    CALL ADD_RESOURCE(座標ID, COUNTRY_ID, ITEM_ID, ITEM_COUNTER)
    SIF IS_FREE == 0
        MONEY -= RESOURCE_PRICE
    RETURN
ELSEIF RESULT >= -100 && RESULT <=100
    ITEM_COUNTER += RESULT
    ITEM_COUNTER = LIMIT(ITEM_COUNTER, 0, MAX_PROCESSING)
    GOTO INPUT_LOOP
ELSE
    GOTO INPUT_LOOP
ENDIF

;--------------------------
; 全自動購入
; INT -XXX 素材不足で作れなかった個数
;--------------------------
@AUTO_PROCESSING(PORT_ID, COUNTRY_ID, ITEM_ID, ITEM_NUM)
#DIM PORT_ID
#DIM COUNTRY_ID
#DIM ITEM_ID
#DIM ITEM_NUM
#DIM USE_RESOURCE, ITEM_LENGTH
#DIM ITER
#DIM BUY_COUNT

VARSET USE_RESOURCE, 0

BUY_COUNT = ITEM_NUM
CALL GET_RESOUCES_OF_ITEM(ITEM_ID, USE_RESOURCE)

FOR ITER, 0, ITEM_LENGTH
    SIF USE_RESOURCE:ITER == 0
        CONTINUE
    RESULT = GET_RESOURCE_NUM(星系座標ID:PORT_ID, COUNTRY_ID, ITER)
    SIF USE_RESOURCE:ITER * BUY_COUNT > RESULT
        BUY_COUNT = RESULT / USE_RESOURCE:ITER
NEXT

FOR ITER, 0, ITEM_LENGTH
    SIF USE_RESOURCE:ITER == 0
        CONTINUE
    RESULT = REMOVE_RESOURCE(星系座標ID:PORT_ID, COUNTRY_ID, ITER, BUY_COUNT * USE_RESOURCE:ITER)
NEXT
CALL ADD_RESOURCE(星系座標ID:PORT_ID, COUNTRY_ID, ITEM_ID, BUY_COUNT)

RETURN ITEM_NUM - BUY_COUNT

;--------------------------
; 加工マップ (Obsolete, @GET_RESOUCES_OF_ITEMを使用ください)
;--------------------------
@PROCESSING(ITEM_ID, USE_RESOURCE)
#DIM ITEM_ID
#DIM REF USE_RESOURCE

SELECTCASE ITEM_ID
    CASE 武装合金
        USE_RESOURCE:赤原石 = 5
    CASE 装甲合金
        USE_RESOURCE:青原石 = 5
    CASE 重武装合金
        USE_RESOURCE:赤原石 = 10
        USE_RESOURCE:貴金属鉱石 = 10
    CASE 重装甲合金
        USE_RESOURCE:青原石 = 10
        USE_RESOURCE:貴金属鉱石 = 10
    CASE シールド発生装置
        USE_RESOURCE:貴金属鉱石 = 5
        USE_RESOURCE:エウスガス = 10
    CASE 多重シールド発生装置
        USE_RESOURCE:貴金属鉱石 = 10
        USE_RESOURCE:エウスガス = 20
        USE_RESOURCE:ゼスタガス = 5
    CASE 武装制御機器
        USE_RESOURCE:貴金属鉱石 = 5
        USE_RESOURCE:ビクニーガス = 5
        USE_RESOURCE:ジェルオイル = 5
    CASE 合金加工剤
        USE_RESOURCE:ゼスタガス = 5
        USE_RESOURCE:ジェルオイル = 5
    CASE 耐熱合金
        USE_RESOURCE:青原石 = 5
        USE_RESOURCE:グリッドオイル = 5
    CASE ジェットガス
        USE_RESOURCE:ビクニーガス = 10
    CASE 超電導回路
        USE_RESOURCE:貴金属鉱石 = 5
        USE_RESOURCE:ジェルオイル = 5
    CASE タイラーセンサー
        USE_RESOURCE:貴金属鉱石 = 5
        USE_RESOURCE:ゼスタガス = 5
        USE_RESOURCE:ジェルオイル = 5
    CASE パワードスーツ
        USE_RESOURCE:赤原石 = 20
        USE_RESOURCE:青原石 = 30
        USE_RESOURCE:ビクニーガス = 20
    CASE レース機
        USE_RESOURCE:青原石 = 20
        USE_RESOURCE:貴金属鉱石 = 10
        USE_RESOURCE:ビクニーガス = 10
        USE_RESOURCE:ジェルオイル = 10
    CASE 思考戦車
        USE_RESOURCE:赤原石 = 30
        USE_RESOURCE:青原石 = 40
        USE_RESOURCE:貴金属鉱石 = 20
        USE_RESOURCE:ビクニーガス = 10
        USE_RESOURCE: チェルニーオイル = 10
        USE_RESOURCE: グリッドオイル = 10
    CASE 異星生物育成キット
        USE_RESOURCE:エウスガス = 10
        USE_RESOURCE:グリッドオイル = 10
        USE_RESOURCE:ジェルオイル = 10
    CASE セクサロイド
        USE_RESOURCE:貴金属鉱石 = 10
        USE_RESOURCE:チェルニーオイル = 10
        USE_RESOURCE:ジェルオイル = 10
    CASE プラモデル
        USE_RESOURCE:ゼスタガス = 1
        USE_RESOURCE:グリッドオイル = 1
    CASE ビームセイバー
        USE_RESOURCE:青原石 = 1
        USE_RESOURCE:貴金属鉱石 = 1
        USE_RESOURCE:ビクニーガス = 1
ENDSELECT

RETURN


