;--------------------------
; 嗜好品の買い取り
; 他国の嗜好品を倍の価格で買い取ってくれるおいしい常設クエスト
;--------------------------

;--------------------------
; クエスト受注可能か
;--------------------------
@QUEST_ACCEPTABLE_2
SIF QUEST_PROGRESS:2 < 2
	RETURN 1
RETURN

;--------------------------
; クエストの名前を返却
;--------------------------
@QUEST_NAME_2
RESULTS = 嗜好品の買い取り
RETURN

;--------------------------
; クエスト本体
;--------------------------
@QUEST_BODY_2
#DIM ITEM_ID
#DIM STAR_ID
#DIM DONT_SALE_ITEM, ITEM_LENGTH

FOR ITEM_ID , 30, 10
	FOR STAR_ID, 0, 星系数
		SIF 星系座標ID:STAR_ID != PLAYER_POSITION
			CONTINUE
		IF GET_RESOURCE_NUM(PLAYER_POSITION, STAR_OWNER:STAR_ID, ITEM_ID) > 100
			DONT_SALE_ITEM:ITEM_ID = 1
		ENDIF
	NEXT
NEXT

PRINTFORML 「やあお兄さん、この国で流通していない嗜好品をお持ちでないですか？
PRINTFORML もしお持ちでしたら、私が高値で買い取らせていただきますよ」

$INPUT_LOOP

FOR ITEM_ID, 30, 40
	SIF DONT_SALE_ITEM:ITEM_ID == 1
		CONTINUE
	IF GET_RESOURCE_NUM(PLAYER_POSITION, PLAYER_COUNTRY, ITEM_ID) > 0
		PRINTBUTTON @"[{ITEM_ID}] %GET_RESOURCE_NAME(ITEM_ID), 30, LEFT%", ITEM_ID
		PRINTL
	ENDIF
NEXT
PRINTBUTTON "[99] 戻る", 99

INPUT

IF RESULT >= 30 && RESULT < 40
	SIF DONT_SALE_ITEM:RESULT == 1
		GOTO INPUT_LOOP
	CALL SALE_LUXULY_ITEM(RESULT)
ELSEIF RESULT == 99
	PRINTW 「それでは」
ELSE
	GOTO INPUT_LOOP
ENDIF

@SALE_LUXULY_ITEM(ITEM_ID)
#DIM ITEM_ID
#DIM SALE_COUNTER
#DIM SALE_PRICE = 2000

SALE_COUNTER = 0

$INPUT_LOOP
PRINTL ◆嗜好品売却
PRINTL 販売数を入力
PRINTBUTTON "[<<<]", -100
PRINTBUTTON "[<<]", -10
PRINTBUTTON "[<]", -1
IF SALE_COUNTER == 0
	LOCALS = %"0"%
ELSE
	LOCALS = %TOSTR(SALE_COUNTER, "#,###"), 4, RIGHT%
ENDIF
PRINTFORM  %LOCALS% 
PRINTBUTTON "[>]", 1
PRINTBUTTON "[>>]", 10
PRINTBUTTON "[>>>]", 100
PRINTL
DRAWLINE
IF SALE_COUNTER == 0
	LOCALS = %"$0"%
ELSE
	LOCALS = %TOSTR(SALE_COUNTER * SALE_PRICE, "$###,###")%
ENDIF
PRINTFORML 販売価格: %LOCALS%
PRINTBUTTON "[999] 決定", 999
INPUT
IF RESULT == 999
	SIF SALE_COUNTER == 0
		RETURN
	MONEY += SALE_COUNTER * SALE_PRICE
	LOCAL = REMOVE_RESOURCE(PLAYER_POSITION, PLAYER_COUNTRY, ITEM_ID, SALE_COUNTER)
	PRINTFORMW 販売後所持金: %TOSTR(MONEY, "$##,###,###,###")%
	RETURN
ELSEIF RESULT >= -100 && RESULT <= 100
	SALE_COUNTER += RESULT
	SALE_COUNTER = LIMIT(SALE_COUNTER, 0, GET_RESOURCE_NUM(PLAYER_POSITION, PLAYER_COUNTRY, ITEM_ID))
	GOTO INPUT_LOOP
ELSE
	GOTO INPUT_LOOP
ENDIF
