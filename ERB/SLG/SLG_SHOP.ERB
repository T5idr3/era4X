;-------------------------------------------------
; SLG SHOP画面
;-------------------------------------------------
@SLG_SHOP
#DIM SELECTED
#DIM ITER
#DIM CURRENT_PORTID
#DIM X_POS
#DIM Y_POS

$INPUT_LOOP
CALL DRAW_MAP

; 変数初期化
CURRENT_PORTID = -1
;状態表示とCONTROL表示
IF BOARDING_SHIP_ID != -1
	PRINTFORML 現在%SHIP_NAME:BOARDING_SHIP_ID%に搭乗しています
	CALL GET_ENGIN(SHIP_EQUIP:エンジン:BOARDING_SHIP_ID)
	IF SPEED == 1
		PRINTBUTTON "   [w]↑", "w"
		PRINTL 
		PRINTBUTTON "[a]←", "a"
		PRINT     
		PRINTBUTTON "→[d]", "d"
		PRINTL 
		PRINTBUTTON "   [s]↓", "s"
		PRINTL 
	ELSEIF SPEED == 2
		PRINTBUTTON "     [ww]↑", "ww"
		PRINTBUTTON "[w]↑↑", "w"
		PRINTL 
		PRINTBUTTON "[aa]←", "aa"
		PRINTBUTTON "[a]←←", "a"
		PRINT     
		PRINTBUTTON "→→[d]", "d"
		PRINTBUTTON "→[dd]", "dd"
		PRINTL 
		PRINTBUTTON "     [ss]↓", "ss"
		PRINTBUTTON "[s]↓↓", "s"
		PRINTL 
	ENDIF
ELSE
	FOR LOCAL, 0, 星系数
		SIF 星系座標ID:LOCAL == CHARACTER_POSITION
			PRINTFORML 現在 星＃{LOCAL} (%COUNTRY_NAME:(STAR_OWNER:LOCAL)%)に滞在しています
	NEXT
ENDIF

FOR ITER, 0, 星系数
	IF 星系座標ID:ITER == CHARACTER_POSITION
		PRINTBUTTON "[f] 宇宙港", "f"
		CURRENT_PORTID = ITER
		PRINTL
	ENDIF
NEXT

CALL ID2POS(CHARACTER_POSITION)
X_POS = RESULT:1
Y_POS = RESULT:2

; 資源回収
CALL IS_MINING(X_POS, Y_POS)
IF RESULT == 1
	PRINTBUTTON "[r] 採掘", "r"
	PRINTL
ENDIF

; アノマリー
STRLENFORM %ANOMALY_MAP:X_POS:Y_POS%
IF RESULT != 0
	PRINTBUTTON "[e] アノマリーを調査", "e"
	PRINTL
ENDIF
DRAWLINE

; 宇宙港建造
;CALL IS_BUILDING(X_POS, Y_POS)
;IF RESULT == 1
;	PRINTBUTTON "[t] 宇宙港建造", "t"
;	PRINTL
;ENDIF
;CALL IS_BUILDUP(X_POS, Y_POS)
;IF RESULT == 1
;	PRINTBUTTON "[t] 宇宙港建造", "t"
;	PRINTL
;ENDIF



;入力
INPUTS
TOINT RESULTS
SELECTED = RESULT
CALL ID2POS(SELECTED)
X_POS = LIMIT(RESULT:1, 0, MAP_WIDTH - 1)
Y_POS = LIMIT(RESULT:2, 0, MAP_HEIGHT - 1)

; 資源回収
IF RESULTS =="r"
	CALL ID2POS(SHIP_POSITION:BOARDING_SHIP_ID) 
	X_POS = RESULT:1
	Y_POS = RESULT:2
	CALL MINING(X_POS, Y_POS, PLAYER_COUNTRY)
	CALL SLG_TURN_END(1)
	GOTO INPUT_LOOP
ENDIF


; アノマリー
IF RESULTS == "e"
	CALL ID2POS(SHIP_POSITION:BOARDING_SHIP_ID) 
	X_POS = RESULT:1
	Y_POS = RESULT:2
	STRLENFORM %ANOMALY_MAP:X_POS:Y_POS%
	IF RESULT != 0
		TRYCALLFORM ANOMALY_BODY_%ANOMALY_MAP:X_POS:Y_POS%
	ENDIF
	GOTO INPUT_LOOP
ENDIF

; 宇宙港建造
IF RESULTS =="t"
	CALL ID2POS(SHIP_POSITION:BOARDING_SHIP_ID) 
	X_POS = RESULT:1
	Y_POS = RESULT:2
	CALL IS_BUILDING(X_POS, Y_POS)
	IF RESULT == 1
		CALL CREATE_STAR_PORT(SHIP_POSITION:BOARDING_SHIP_ID, PLAYER_COUNTRY)
		SIF RESULT == 1
			CALL SLG_TURN_END(1)
	ENDIF
	CALL IS_BUILDUP(X_POS, Y_POS)
	IF RESULT == 1
		CALL BUILD_PORT(SHIP_POSITION:BOARDING_SHIP_ID, PLAYER_COUNTRY)
		SIF RESULT == 1
			CALL SLG_TURN_END(1)
	ENDIF
	GOTO INPUT_LOOP
ENDIF


; 船による移動
IF BOARDING_SHIP_ID != -1 && (RESULTS == "w" || RESULTS == "d" || RESULTS == "a" || RESULTS == "s" || RESULTS == "ww" || RESULTS == "dd" || RESULTS == "aa" || RESULTS == "ss")
	IF SPEED == 2
		SELECTCASE RESULTS
			CASE "w"
				CALL SHIP_MOVE_FOR(8, BOARDING_SHIP_ID)
			CASE "d"
				CALL SHIP_MOVE_FOR(6, BOARDING_SHIP_ID)
			CASE "a"
				CALL SHIP_MOVE_FOR(4, BOARDING_SHIP_ID)
			CASE "s"
				CALL SHIP_MOVE_FOR(2, BOARDING_SHIP_ID)
			CASE "ww"
				CALL SHIP_MOVE_FOR(8, BOARDING_SHIP_ID, 1)
			CASE "dd"
				CALL SHIP_MOVE_FOR(6, BOARDING_SHIP_ID, 1)
			CASE "aa"
				CALL SHIP_MOVE_FOR(4, BOARDING_SHIP_ID, 1)
			CASE "ss"
				CALL SHIP_MOVE_FOR(2, BOARDING_SHIP_ID, 1)
		ENDSELECT
	ELSE
		SELECTCASE RESULTS
			CASE "w"
				CALL SHIP_MOVE_FOR(8, BOARDING_SHIP_ID)
			CASE "d"
				CALL SHIP_MOVE_FOR(6, BOARDING_SHIP_ID)
			CASE "a"
				CALL SHIP_MOVE_FOR(4, BOARDING_SHIP_ID)
			CASE "s"
				CALL SHIP_MOVE_FOR(2, BOARDING_SHIP_ID)
		ENDSELECT
	ENDIF
	CALL ID2POS(SHIP_POSITION:BOARDING_SHIP_ID)
	X_POS = RESULT:1
	Y_POS = RESULT:2
	CALL CLEAR_MAP(X_POS, Y_POS, SHIP_EQUIP:センサー:BOARDING_SHIP_ID)
	CALL SLG_TURN_END(1)
	GOTO INPUT_LOOP
ENDIF

; 宇宙港へ
IF RESULTS == "f" && CURRENT_PORTID != -1
	CALL STAR_MENU(CURRENT_PORTID)
	SIF RESULT == 1
		RETURN
	GOTO INPUT_LOOP
ENDIF

; マップ選択
SELECTCASE GET_MAP(X_POS, Y_POS)
	CASE 無
		PRINTFORML 未探索地域
	CASE 明瞭
		PRINTFORML 空白宙域
		CALL SHOW_POSITION_INFO(X_POS, Y_POS)
	CASE 星
		CALL MAPID2PORTID(SELECTED)
		PRINTFORML 中立星(所属: %COUNTRY_NAME:(STAR_OWNER:RESULT)%)
		;今いる星じゃなかったら返す
		CALL POS2ID(X_POS, Y_POS)
		IF RESULT != CHARACTER_POSITION
			GOTO INPUT_LOOP
		ENDIF
		CALL MAPID2PORTID(SELECTED)
		CALL STAR_MENU(RESULT)
		SIF RESULT == 1
			RETURN
	CASE 宇宙港
		PRINTFORML 宇宙港
		CALL SHOW_POSITION_INFO(X_POS, Y_POS)
	CASE 資源
		PRINTFORML ◆資源採掘可能エリア
		CALL SHOW_POSITION_INFO(X_POS, Y_POS)
	CASE ANOMALY
		PRINTFORML ◆アノマリー
		CALL SHOW_POSITION_INFO(X_POS, Y_POS)
ENDSELECT
GOTO INPUT_LOOP

;-------------------------------------------------
; 宙域詳細表示
;-------------------------------------------------
@SHOW_POSITION_INFO(X_POS, Y_POS)
#DIM X_POS
#DIM Y_POS
CALL POS2ID(X_POS, Y_POS)
CALL GET_SHIP_LIST_FROM_POS(RESULT)
IF MAP:X_POS:Y_POS == 資源
	PRINTFORM %GET_RESOURCE_NAME(RESOURCE_MAP:X_POS:Y_POS)%
	PRINT : 
	PRINTFORML %GET_RESOURCE_DESCRIPTION(RESOURCE_MAP:X_POS:Y_POS)%
ENDIF

SIF SHIP_LIST_LENGTH > 0
	PRINTL ◆宙域艦隊
FOR LOCAL, 0, SHIP_LIST_LENGTH
	PRINTFORM %COUNTRY_NAME:(SHIP_COUNTRY:(SHIP_LIST:LOCAL))%所属 %SHIP_NAME:(SHIP_LIST:LOCAL)%
	IF SHIP_MISSION:(SHIP_LIST:LOCAL) == パトロール
		PRINTL (パトロール任務中)
	ELSE
		PRINTL
	ENDIF
NEXT

;-------------------------------------------------
; 星メニュー
;-------------------------------------------------
@STAR_MENU(PORTID)
#DIM PORTID

$INPUT_LOOP
DRAWLINE
PRINTFORML 星＃{PORTID}
DRAWLINE

PRINTBUTTON "[0] 宇宙港", 0
PRINTBUTTON "[1] 艦船購入", 1
PRINTBUTTON "[2] 装備更新", 2
PRINTBUTTON "[3] タスクの割り振り", 3
PRINTBUTTON "[4] クエスト", 4
IF HUMAN_LAB == CHARACTER_POSITION && QUEST_PROGRESS:0 >= 3
	PRINTBUTTON "[5] 人体研究所",5
ENDIF
PRINTBUTTON "[6] 素材売買", 6
PRINTBUTTON "[7] 加工工場", 7
PRINTL
PRINTBUTTON "[99] 宙域画面へ", 99
PRINTBUTTON "[100] 奴隷の調教", 100

INPUT

IF RESULT == 0
	CALL SPACE_PORT(PORTID)
ELSEIF RESULT == 1
	CALL SLG_SHIP_SHOP(PORTID)
ELSEIF RESULT == 2
	CALL SLG_EQUIP_SHOP(PORTID)
ELSEIF RESULT == 3
	CALL SETUP_TASK_LIST
ELSEIF RESULT == 4
	CALL QUEST_MENU
ELSEIF RESULT == 5 && HUMAN_LAB == CHARACTER_POSITION && QUEST_PROGRESS:0 >= 3
	CALL HUMAN_LAB
ELSEIF RESULT == 6
	CALL SLG_ITEM_SHOP(PORTID)
ELSEIF RESULT == 7
	CALL PRIVATE_FACTORY(PORTID, PLAYER_COUNTRY)
ELSEIF RESULT == 99
	RETURN 0
ELSEIF RESULT == 100
	RETURN 1
ENDIF
GOTO INPUT_LOOP

;-------------------------------------------------
; 宇宙港
;-------------------------------------------------
@SPACE_PORT(PORTID)
#DIM PORTID
#DIM SELECTED
#DIMS LOCAL_SHIP_NAME
$INPUT_LOOP
DRAWLINE
PRINTL 宇宙港
DRAWLINE
PRINTL 停泊中の艦船
FOR LOCAL, 0, MAX_SHIP
	IF SHIP_POSITION:LOCAL == 星系座標ID:PORTID
		LOCAL_SHIP_NAME = %SHIP_NAME:LOCAL%(%COUNTRY_NAME:(SHIP_COUNTRY:LOCAL)%所属%GET_TYPE_NAME(SHIP_TYPE:LOCAL)%)
		SIF SHIP_COUNTRY:LOCAL == PLAYER_COUNTRY
			SETCOLOR 0X00FF00
		PRINTBUTTON LOCAL_SHIP_NAME, LOCAL
		RESETCOLOR
		PRINTL
	ENDIF
NEXT
PRINTBUTTON "[戻る]", 9999
PRINTL

INPUT
SELECTED = RESULT
IF RESULT == 9999
	RETURN
ENDIF
IF SHIP_COUNTRY:RESULT == PLAYER_COUNTRY
	FOR LOCAL, 0, CHARANUM
		IF BOARDING_SHIP_ID:LOCAL == RESULT
			PRINTFORML 搭乗者: %CALLNAME:LOCAL%
			BREAK
		ENDIF
	NEXT
	CALL SHOW_SHIP_INFO(RESULT)
	GOTO INPUT_LOOP
ELSEIF RESULT >= 0 && RESULT < MAX_SHIP
	CALL SHOW_SHIP_INFO(RESULT)
	GOTO INPUT_LOOP
ELSE
	GOTO INPUT_LOOP
ENDIF

;-------------------------------------------------
; 艦船購入メニュー
;-------------------------------------------------
@SLG_SHIP_SHOP(PORT_ID)
#DIM PORT_ID
#DIM SHIP_TYPE_ID

$INPUT_LOOP
PRINTFORML 所持金: ${MONEY}
FOR SHIP_TYPE_ID, 0, 9
	LOCALS = %TOSTR(SHIP_PRICE:SHIP_TYPE_ID, "$##,###,###,###")%
	PRINTBUTTON @"[{SHIP_TYPE_ID}] %GET_TYPE_NAME(SHIP_TYPE_ID), 10, RIGHT% %LOCALS, 12, RIGHT%", SHIP_TYPE_ID
	PRINTL 
NEXT
PRINTBUTTON "[戻る]", 99
INPUT
SELECTCASE RESULT
	CASE 偵察艇
		CALL BUILD_SHIP(PORT_ID, PLAYER_COUNTRY, RESULT, "偵察艇")
	CASE フリゲート
		CALL BUILD_SHIP(PORT_ID, PLAYER_COUNTRY, RESULT, "フリゲート")
	CASE コルベット
		CALL BUILD_SHIP(PORT_ID, PLAYER_COUNTRY, RESULT, "コルベット")
	CASE 駆逐艦
		CALL BUILD_SHIP(PORT_ID, PLAYER_COUNTRY, RESULT, "駆逐艦")
	CASE 巡洋艦
		CALL BUILD_SHIP(PORT_ID, PLAYER_COUNTRY, RESULT, "巡洋艦")
	CASE 戦艦
		CALL BUILD_SHIP(PORT_ID, PLAYER_COUNTRY, RESULT, "戦艦")
	CASE 空母
		CALL BUILD_SHIP(PORT_ID, PLAYER_COUNTRY, RESULT, "空母")
	CASE 輸送船
		CALL BUILD_SHIP(PORT_ID, PLAYER_COUNTRY, RESULT, "輸送船")
	CASE 作業船
		CALL BUILD_SHIP(PORT_ID, PLAYER_COUNTRY, RESULT, "作業船")
	CASE 99
		RETURN
	CASEELSE
		GOTO INPUT_LOOP
ENDSELECT

;-------------------------------------------------
; 装備更新
;-------------------------------------------------
@SLG_EQUIP_SHOP(PORTID)
#DIM PORTID
#DIM SHIP_ID
#DIM SELECTED

$INPUT_LOOP
PRINTL ◆装備更新
FOR SHIP_ID, 0, MAX_SHIP
	SIF SHIP_COUNTRY:SHIP_ID != PLAYER_COUNTRY
		CONTINUE
	SIF SHIP_POSITION:SHIP_ID != 星系座標ID:PORTID
		CONTINUE
	PRINTBUTTON @"[{SHIP_ID, 3, RIGHT}] %SHIP_NAME:SHIP_ID, 30, LEFT%", SHIP_ID
	PRINTL
NEXT
PRINTBUTTON "[9999]戻る", 9999
INPUT
SHIP_ID = RESULT
IF RESULT >= 0 && RESULT < MAX_SHIP && SHIP_COUNTRY:RESULT == PLAYER_COUNTRY && SHIP_POSITION:RESULT == 星系座標ID:PORTID
	$INPUT_LOOP2
	PRINTBUTTON @"[0] 名称変更 %SHIP_NAME:SHIP_ID%", 0
	PRINTL
	CALL GET_MAIN_WEAPON(SHIP_EQUIP:主砲:SHIP_ID)
	PRINTBUTTON @"[1] 主砲変更 %EQUIP_NAME%", 1
	PRINTL
	CALL GET_SUB_WEAPON(SHIP_EQUIP:副砲:SHIP_ID)
	PRINTBUTTON @"[2] 副砲変更 %EQUIP_NAME%", 2
	PRINTL
	CALL GET_SIELD(SHIP_EQUIP:シールド:SHIP_ID)
	PRINTBUTTON @"[3] シールド変更 %EQUIP_NAME%", 3
	PRINTL
	CALL GET_ARMOR(SHIP_EQUIP:装甲:SHIP_ID)
	PRINTBUTTON @"[4] 装甲変更 %EQUIP_NAME%", 4
	PRINTL
	CALL GET_ENGIN(SHIP_EQUIP:エンジン:SHIP_ID)
	PRINTBUTTON @"[5] エンジン変更 %EQUIP_NAME%", 5
	PRINTL
	CALL GET_COMPUTER(SHIP_EQUIP:AI:SHIP_ID)
	PRINTBUTTON @"[6] AI変更 %EQUIP_NAME%", 6
	PRINTL
	CALL GET_SENSOR(SHIP_EQUIP:センサー:SHIP_ID)
	PRINTBUTTON @"[7] センサー変更 %EQUIP_NAME%", 7
	PRINTL
	CALL GET_OPTION(SHIP_EQUIP:オプション:SHIP_ID)
	PRINTBUTTON @"[8] オプション %EQUIP_NAME%", 8
	PRINTL
	PRINTBUTTON "[9] 戻る", 9
ELSEIF RESULT == 9999
	RETURN
ELSE
	GOTO INPUT_LOOP
ENDIF
INPUT
SELECTCASE RESULT
	CASE 0
		CALL CHANGE_SHIP_NAME(SHIP_ID)
		GOTO INPUT_LOOP2
	CASE 1
		CALL EQUIP_UPDATE_0(SHIP_ID, STAR_OWNER:PORTID)
		GOTO INPUT_LOOP2
	CASE 2
		CALL EQUIP_UPDATE_1(SHIP_ID, STAR_OWNER:PORTID)
		GOTO INPUT_LOOP2
	CASE 3
		CALL EQUIP_UPDATE_2(SHIP_ID, STAR_OWNER:PORTID)
		GOTO INPUT_LOOP2
	CASE 4
		CALL EQUIP_UPDATE_3(SHIP_ID, STAR_OWNER:PORTID)
		GOTO INPUT_LOOP2
	CASE 5
		CALL EQUIP_UPDATE_4(SHIP_ID, STAR_OWNER:PORTID)
		GOTO INPUT_LOOP2
	CASE 6
		CALL EQUIP_UPDATE_5(SHIP_ID, STAR_OWNER:PORTID)
		GOTO INPUT_LOOP2
	CASE 7
		CALL EQUIP_UPDATE_6(SHIP_ID, STAR_OWNER:PORTID)
		GOTO INPUT_LOOP2
	CASE 8
		CALL EQUIP_UPDATE_7(SHIP_ID, STAR_OWNER:PORTID)
		GOTO INPUT_LOOP2
	CASE 9
		GOTO INPUT_LOOP
	CASEELSE
		GOTO INPUT_LOOP2
ENDSELECT

;-------------------------------------------------
; クエスト
;-------------------------------------------------
@QUEST_MENU
CALL QUEST_LIST

;-------------------------------------------------
; 入港可能かチェック
;-------------------------------------------------
@CHECK_ARRIVAL(SHIP_ID)
#FUNCTION
#DIM SHIP_ID
SIF SHIP_ID < 0 || SHIP_ID >= MAX_SHIP
	RETURNF 0
FOR LOCAL, 0, 星系数
	IF 星系座標ID:LOCAL == SHIP_POSITION:SHIP_ID
		; TODO 敵対チェックを入れる
		RETURNF 1
	ENDIF
NEXT
RETURNF 0

;-------------------------------------------------
; ターンエンド時にドックにいる艦船は回復
;-------------------------------------------------
@DOCK_REPAIR
#DIM SHIP_ID
#DIM STAR_ID
#DIM COUNTRY_ID
#DIM REPAIR_COST, MAX_COUNTRY
#DIM CONST REPAIR = 500

VARSET REPAIR_COST, 0

FOR SHIP_ID, 0, MAX_SHIP
    SIF SHIP_MISSION != 待機
        CONTINUE
    FOR STAR_ID, 0, 星系数
        SIF 星系座標ID:STAR_ID != SHIP_POSITION:SHIP_ID
            CONTINUE
        ; 敵対勢力の基地にいる場合は回復しない
        SIF REL_LIKE:(STAR_OWNER:STAR_ID):(SHIP_COUNTRY:SHIP_ID) < -500
			CONTINUE
		IF IS_NEED_REPAIR(SHIP_ID)
			IF REMOVE_RESOURCE(星系座標ID:STAR_ID, STAR_OWNER:STAR_ID, 装甲合金, 1) == 0
				REPAIR_COST:(SHIP_COUNTRY:SHIP_ID) += REPAIR(SHIP_ID)
				SIELD_DAMAGE:SHIP_ID = LIMIT(SIELD_DAMAGE:SHIP_ID - REPAIR, 0, 999999)
				SHIP_DAMAGE:SHIP_ID = LIMIT(SHIP_DAMAGE:SHIP_ID - REPAIR, 0, 999999)
			ENDIF
		ENDIF
    NEXT
NEXT

FOR COUNTRY_ID, 1, MAX_COUNTRY
    IF COUNTRY_ID == PLAYER_COUNTRY && REPAIR_COST:COUNTRY_ID > 0
        PRINTFORML 艦船修理コストとして$%TOSTR((REPAIR_COST:COUNTRY_ID), "###,###,###")%払いました
        MONEY -= REPAIR_COST:PLAYER_COUNTRY
    ELSEIF REPAIR_COST:COUNTRY_ID > 0
        MONEY:COUNTRY_ID -= REPAIR_COST:COUNTRY_ID
    ENDIF
NEXT

;-------------------------------------------------
; ターンエンド
;-------------------------------------------------
@SLG_TURN_END(IS_TIME_PROGRESS)
#DIM IS_TIME_PROGRESS
#DIM POS_X
#DIM POS_Y
#DIM ITER
#DIM STAR_ID

CALL UPDATE_CHARACTER_POSITION

; 星ごとの民需生産と消費
FOR STAR_ID, 0, 星系数
	CALL PRICATE_PRODUCTION(STAR_ID)
	CALL PRIVATE_DEMAND(STAR_ID)
NEXT

CALL SLG_AI

FOR POS_X, 0, MAP_WIDTH
	FOR POS_Y, 0, MAP_HEIGHT
		CHECKED_MAP:POS_X:POS_Y = 0
	NEXT
NEXT
PRINTFORML AI試行中……
; シールドの自動回復
FOR ITER, 0, MAX_SHIP
	SIF SHIP_COUNTRY:ITER > 0
		CALL AUTO_RECOVER_SIELD(ITER)
NEXT
; DOCKにいる艦船の回復
CALL DOCK_REPAIR
IF IS_TIME_PROGRESS == 1
	TIME ++
	IF TIME % 2 == 0
		DAY ++
		TIME = 0
	ENDIF
ENDIF
RETURN

;-------------------------------------------------
; MAPID2PORTID
;-------------------------------------------------
@MAPID2PORTID(MAPID)
#DIM MAPID

FOR LOCAL, 0, 星系数
	SIF 星系座標ID:LOCAL == MAPID
		RETURN LOCAL
NEXT

;-------------------------------------------------
; キャラクターの現在位置更新
;-------------------------------------------------
@UPDATE_CHARACTER_POSITION
#DIM ID

; キャラクターの移動
FOR ID, 0, CHARANUM
	IF BOARDING_SHIP_ID:ID != -1
		CHARACTER_POSITION:ID = SHIP_POSITION:(BOARDING_SHIP_ID)
	ELSEIF RIDING_WITH:ID != -1
		CHARACTER_POSITION:ID = SHIP_POSITION:(RIDING_WITH:ID)
	ENDIF
NEXT

SIF TARGET != -1 && CHARACTER_POSITION:0 != CHARACTER_POSITION:TARGET
	TARGET = -1