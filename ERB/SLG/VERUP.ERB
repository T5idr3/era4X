;バージョンアップ処理
;FLAG:10がセーブされたバージョン
@VERSION_UP()
#DIM SHIP_ID
#DIM ITEM_ID
#DIM STAR_ID
#DIM PORT_ID
#DIM ITEM_NUM
SIF FLAG:10 >= GAMEBASE_VERSION
    RETURN 0

CALL DRAW_TEXT_RAIN

IF FLAG:10 < 1413
    PRINTL アイテムの移行中……

    CALL INIT_CARGO()

    FOR SHIP_ID, 0, MAX_SHIP
        FOR ITEM_ID, 0, 60
        ;ITEM_LENGTHは後で変わる予定なので、数字で60としておく
        SIF CARGO_CONTENTS:SHIP_ID:ITEM_ID == 0
            CONTINUE
        ITEM_NUM = CARGO_CONTENTS:SHIP_ID:ITEM_ID
        CALL LOAD_TO_SHIP(SHIP_ID, ITEM_ID, ITEM_NUM)
        IF RESULT < ITEM_NUM
            LOCAL = RESULT
            CALL GET_ITEM_NAME(ITEM_ID)
            PRINTFORML {SHIP_ID}%RESULTS%を{LOCAL}個積み残しました
        ENDIF
        NEXT
    NEXT

    FOR STAR_ID, 0, 星系数
        FOR LOCAL, 0, STAR_PORT_SLOT_NUM
            SIF STAR_PORT_SLOT:STAR_ID:LOCAL != コンテナ
                CONTINUE           
            FOR ITEM_ID, 0, 60
                CALL STORE_ITEM_TO_STAR_PORT(STAR_ID, ITEM_ID, STAR_PORT_CONTAINER:STAR_ID:LOCAL:ITEM_ID)
            NEXT
        NEXT
    NEXT

    FOR PORT_ID, 0, MAX_PORT
        FOR LOCAL, 0, STAR_PORT_SLOT_NUM
            SIF PORT_SLOT:PORT_ID:LOCAL != コンテナ
                CONTINUE
            FOR ITEM_ID, 0, 60
                CALL STORE_ITEM_TO_PORT(PORT_ID, ITEM_ID, PORT_CONTAINER:PORT_ID:LOCAL:ITEM_ID)
            NEXT
        NEXT
    NEXT

ENDIF


FLAG:10 = 1413

PRINTFORMW ver.{FLAG:10}に更新しました。

RETURN 0

@DRAW_TEXT_RAIN
#DIM X_SIZE = 110
#DIM Y_SIZE = 30
#DIM CONST DROPS_MAX_LENGTH = 50
#DIM COUNT_X
#DIM COUNT_Y
#DIM TEXT_MATRIX, 110, 30    ; X_SIZE, Y_SIZE
#DIM DROPS, 110              ; X_SIZE
#DIM PRE_LINECOUNT
#DIMS C_DROPS = "ｱ", "ｲ", "ｳ", "ｴ", "ｵ", "ｶ", "ｷ", "ｸ", "ｹ", "ｺ", "ｻ", "ｼ", "ｽ", "ｾ", "ｿ", "ﾀ", "ﾁ", "ﾂ", "ﾃ", "ﾄ"
#DIM C_DROP_NUM
#DIM C_COLOR

C_DROP_NUM = VARSIZE("C_DROPS")


CLEARLINE LINECOUNT

PRINTW LOADING...
PRINTDATAW
    DATA カストディアン・プロトコルよりアップデートを受信しました
    DATA ケアテイカー・ネットワークよりアップデートを受信しました
    DATA コンティンジェンシー・プロジェクトよりアップデートを受信しました
ENDDATA
PRINTL
PRE_LINECOUNT = LINECOUNT

$INPUT_LOOP
FOR LOCAL, 0, VARSIZE("DROPS")
    SIF DROPS:LOCAL == 0 && RAND:50 == 0
        DROPS:LOCAL = 1
NEXT

FOR LOCAL, 0, VARSIZE("DROPS")
    SIF DROPS:LOCAL > 0
        DROPS:LOCAL += 1
    SIF DROPS:LOCAL > DROPS_MAX_LENGTH
        DROPS:LOCAL = 0
NEXT

FOR COUNT_X, 0, X_SIZE
    FOR COUNT_Y, 0, Y_SIZE
        TEXT_MATRIX:COUNT_X:COUNT_Y = DROPS:COUNT_X - COUNT_Y
        SIF TEXT_MATRIX:COUNT_X:COUNT_Y < 0
            TEXT_MATRIX:COUNT_X:COUNT_Y = DROPS_MAX_LENGTH
    NEXT
NEXT

FOR COUNT_Y, 1, Y_SIZE
    FOR COUNT_X, 0, X_SIZE
        C_COLOR = MAX(0, 255 - 10 * TEXT_MATRIX:COUNT_X:COUNT_Y)
        SETCOLOR 0, C_COLOR, 0
        PRINTFORM %C_DROPS:(RAND(C_DROP_NUM))%
    NEXT
    PRINTL
NEXT

RESETCOLOR
PRINTL [0] アップデート

TINPUT 100, 99, 0
IF RESULT == 99
    CLEARLINE (LINECOUNT - PRE_LINECOUNT)
    GOTO INPUT_LOOP
ELSE
    CLEARLINE (LINECOUNT - PRE_LINECOUNT)
ENDIF