;-----------------------------------
; サブウェポン
;-----------------------------------
@GET_SUB_WEAPON(ID)
#DIM ID

SELECTCASE ID
	CASE 1
		DAMAGE = 1
		FIRE_RATE = 10
		ANTI_AIR = 10
		HIT_RATE = 90
		FIRE_RANGE = 1
		SIZE = 1
		EQUIP_NAME = TIER1対空ガトリング
		PRICE = 10000
	CASE 2
		DAMAGE = 1
		FIRE_RATE = 10
		ANTI_AIR = 15
		HIT_RATE = 80
		FIRE_RANGE = 1
		SIZE = 1
		EQUIP_NAME = TIER2対空ガトリング
		PRICE = 20000
	CASE 3
		DAMAGE = 5
		FIRE_RATE = 10
		ANTI_AIR = 40
		HIT_RATE = 80
		FIRE_RANGE = 1
		SIZE = 1
		EQUIP_NAME = TIER3対空ガトリング
		PRICE = 40000
	CASE 4
		DAMAGE = 100
		FIRE_RATE = 10
		ANTI_AIR = 10
		HIT_RATE = 80
		FIRE_RANGE = 1
		SIZE = 4
		EQUIP_NAME = TIER1汎用ホーミングレーザー
		PRICE = 10000
	CASEELSE
		EQUIP_NAME = ---
ENDSELECT

;-----------------------------------
; サブウェポンの説明
;-----------------------------------
@GET_SUB_WEAPON_DESCRIPTION(ID)
#DIM ID
CALL GET_SUB_WEAPON(ID)
SELECTCASE ID
	CASE 1 TO 4
		PRINTFORML %EQUIP_NAME%
		PRINTFORML ダメージ  : {DAMAGE}
		PRINTFORML 射撃レート: {FIRE_RATE}発/ターン
		PRINTFORML 対空攻撃力: {ANTI_AIR}
		PRINTFORML 命中率    : {HIT_RATE}
		PRINTFORML 有効距離  : {FIRE_RANGE}
		PRINTFORML サイズ    : {SIZE}
	CASEELSE
		PRINTFORML = ---
ENDSELECT

;-----------------------------------
; サブウェポンの資材
;-----------------------------------
@GET_SUB_WEAPON_RESOURCE(USE_RESOURCE, ID)
#DIM REF USE_RESOURCE
#DIM ID

SELECTCASE ID
	CASE 1
		USE_RESOURCE:武装合金 = 2
		USE_RESOURCE:武装制御機器 = 1
	CASE 2
		USE_RESOURCE:武装合金 = 4
		USE_RESOURCE:武装制御機器 = 2
	CASE 3
		USE_RESOURCE:武装合金 = 8
		USE_RESOURCE:武装制御機器 = 4
	CASE 4
		USE_RESOURCE:武装合金 = 1
		USE_RESOURCE:耐熱合金 = 1
		USE_RESOURCE:武装制御機器 = 1
ENDSELECT

;-----------------------------------
; サブウェポンの換装
;-----------------------------------
@CHANGE_SUB_WEAPON(SHIP_ID, SALE_COUNTRY, EQUIP_ID)
#DIM SHIP_ID
#DIM SALE_COUNTRY
#DIM EQUIP_ID
#DIM USE_RESOURCE, ITEM_LENGTH
#DIM ITER
#DIM RESOURCE_COUNT
#DIM LACK_RESOURCE, ITEM_LENGTH
#DIM CAN_BUY

CAN_BUY = 1

CALL GET_SUB_WEAPON_RESOURCE(USE_RESOURCE, EQUIP_ID)
FOR ITER, 0, ITEM_LENGTH
	SIF USE_RESOURCE:ITER == 0
		CONTINUE
	RESOURCE_COUNT = GET_RESOURCE_NUM(SHIP_POSITION:SHIP_ID, SALE_COUNTRY, ITER)
	IF USE_RESOURCE:ITER > RESOURCE_COUNT
		LACK_RESOURCE:ITER += USE_RESOURCE:ITER - RESOURCE_COUNT
		CAN_BUY = 0
	ENDIF
NEXT

IF CAN_BUY == 0
	IF SHIP_COUNTRY:SHIP_ID == PLAYER_COUNTRY
		SETCOLOR 0XFF0000
		PRINTL [CAUTION] 資源不足で装備を購入できませんでした
		RESETCOLOR
	ENDIF
	RETURN 0
ENDIF

FOR ITER, 0, ITEM_LENGTH
	SIF USE_RESOURCE:ITER == 0
		CONTINUE
	RESULT = REMOVE_RESOURCE(SHIP_COUNTRY:SHIP_ID, SALE_COUNTRY, ITER, USE_RESOURCE:ITER)
	SHIP_EQUIP:副砲:SHIP_ID = EQUIP_ID
NEXT
RETURN 1
