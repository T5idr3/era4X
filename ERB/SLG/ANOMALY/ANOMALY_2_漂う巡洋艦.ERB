;--------------------------
; 漂う巡洋艦
;--------------------------
@INIT_ANOMALY_2
#DIM TASK_COMPLETE
#DIM SECTOR_ID
#DIM POS_X
#DIM POS_Y
; 遺棄された貨物船
WHILE TASK_COMPLETE == 0
	SECTOR_ID = RAND:MAX_SECTOR
	POS_X = RAND:MAP_WIDTH
	POS_Y = RAND:MAP_HEIGHT
	SIF MAP:SECTOR_ID:POS_X:POS_Y != 明瞭
		CONTINUE
	MAP:SECTOR_ID:POS_X:POS_Y = ANOMALY
	ANOMALY_MAP:SECTOR_ID:POS_X:POS_Y = 漂う巡洋艦
	CALL POS2ID(SECTOR_ID, POS_X, POS_Y)
	ANOMALY_POSITIONID:2 = RESULT
	TASK_COMPLETE = 1
	PRINTFORML セクター: {SECTOR_ID}
	PRINTFORML {ANOMALY_POSITIONID:2}
WEND

;--------------------------
; ANOMALY本体
;--------------------------
@ANOMALY_BODY_漂う巡洋艦
#DIM SECTOR_ID
#DIM X_POS
#DIM Y_POS

PRINTFORML 宙に無人と思わしき巡洋艦が漂っている

IF SHIP_EQUIP:オプション:0:(BOARDING_SHIP_ID:MASTER) == 1
	FINDELEMENT RIDING_WITH, BOARDING_SHIP_ID
	IF RESULT != -1
		PRINTFORML 移乗ポッドで乗り込むことができそうだ
		CALL ASK_YN("乗り込みますか？")
		SIF RESULT == 0
			RETURN
		CALL SELECT_RIDE_CHARA
		SIF RESULT == 0
			RETURN
		CALL START_ANOMALY_2(RESULT)
		;アノマリーの初期化
		CALL ID2POS(ANOMALY_POSITIONID:2)
		SECTOR_ID = RESULT:0
		X_POS = RESULT:1
		Y_POS = RESULT:2
		ANOMALY_MAP:SECTOR_ID:X_POS:Y_POS = %""%
		MAP:SECTOR_ID:X_POS:Y_POS = 明瞭
	ENDIF
ENDIF
RETURN

@SELECT_RIDE_CHARA
#DIM ID

$INPUT_LOOP
PRINTL 誰で移乗しますか?
FOR ID, 1, CHARANUM
	IF RIDING_WITH:ID == BOARDING_SHIP_ID
		PRINTBUTTON @"[{ID, 2, RIGHT}] %CALLNAME:ID%", ID
		PRINTL
	ENDIF
NEXT
PRINTBUTTON "[999] やっぱり止める", 999

INPUT
SIF RESULT == 999
	RETURN 0
IF RESULT > 0 && RESULT < CHARANUM && RIDING_WITH:RESULT == BOARDING_SHIP_ID
	RETURN RESULT
ENDIF
GOTO INPUT_LOOP

@START_ANOMALY_2(ID)
#DIM ID

DRAWLINE
PRINTFORML 人気のない巡洋艦に乗り込んだ%CALLNAME:ID%
PRINTL 暗闇に包まれた辺りを見回すと、配線が壁や天井に縦横無尽に配置されている
PRINTL その幾つかがレーザー銃により無作為に切断された跡があり、戦闘が行われたことを物語っている
LOCALS = %CALLNAME:ID%はどうする？
CALL ASK_SELECT(LOCALS, "この区画を調査する", "他の区画を調査しにいく")
SIF RESULT == 1
	CALL ANOMALY2_1(ID)
SIF RESULT == 2
	CALL ANOMALY2_2(ID)

@ANOMALY2_1(ID)
#DIM ID

DRAWLINE
PRINTL この区画は動力室のようだ
PRINTL 特筆すべきは、移乗ポッドが開けたのとは別の穴が壁に開いていたことだ
PRINTL エネルギー兵器か強酸で溶かされたような穴で、切り口がドロドロになって固まっている
PRINTL 特筆すべきは、切断された配線の切り口とは全く違う形をしていることだ
PRINTL 別種の武装を持つ勢力同士が接敵したようだ
PRINTW しかし、なぜか壁に穴を開けている勢力の攻撃痕が一切見つからなかったのが気がかりだ……
LOCALS = %CALLNAME:ID%はどうする？
CALL ASK_SELECT(LOCALS, "徹底的に調査する", "次の区画を調査しにいく")
SIF RESULT == 1
	CALL ANOMALY2_3(ID)
SIF RESULT == 2
	CALL ANOMALY2_2(ID)

@ANOMALY2_2(ID)
#DIM ID

DRAWLINE
PRINTL 閉鎖された区画のロックをハンドレーザーで切り開いて先に進むと、そこは倉庫兼居住区画だった
PRINTL 引き出しが全てひっくり返され、床に溶けた鉄の跡が残っている
PRINTL 引き出しの中身と思わしき物は一切なく、襲撃者の徹底ぶりを感じさせる
LOCALS = %CALLNAME:ID%はどうする？
CALL ASK_SELECT(LOCALS, "この区画を調査する", "次の区画を調査しにいく")
SIF RESULT == 1
	CALL ANOMALY2_4(ID)
SIF RESULT == 2
	CALL ANOMALY2_5(ID)

@ANOMALY2_3(ID)
#DIM ID

DRAWLINE
PRINTFORML 徹底的に辺りを調査した%CALLNAME:ID%は、床がピカピカに磨き上げられていることに気がついた
PRINTL 清掃したてで放置されていると言われても納得してしまうほどだ
PRINTFORML そこまで考えて、%CALLNAME:ID%は襲撃者の正体に気がついた
PRINTL 宇宙の掃除屋と言われるスライムなら、強酸を吐き、汚れを取り込んでエネルギーに変換することができるはずだ
PRINTL ならばこの艦にめぼしい物はもう残っていないだろう……
ANOMALY_PROGRESS:2 = 1
LOCALS = %CALLNAME:ID%はどうする？
LOCALS:1 = %CALLNAME:MASTER%に報告に戻る
CALL ASK_SELECT(LOCALS, "一応探索する", LOCALS:1)
IF RESULT == 1
	PRINTL ここまで来て手ぶらで帰るのも惜しい
	PRINTFORML %CALLNAME:ID%は先に進むことにした
	CALL ANOMALY2_2(ID)
ELSEIF RESULT == 2
	PRINTL 残念だがここにめぼしい物は残っていないだろう
	PRINTFORML %CALLNAME:ID%はそう判断し、%CALLNAME:MASTER%のもとへと帰って行った
	RETURN
ENDIF

@ANOMALY2_4(ID)
#DIM ID

DRAWLINE
PRINTL 辺りを調べると、指輪やネックレスの宝石らしき鉱石類が無造作に部屋の隅に捨てられているのを見つけた
PRINTL すぐ側には、台座部分だったであろう貴金属類がドロドロに溶けて床に張り付いている
PRINTFORML これは売れば小銭になるだろう。%CALLNAME:ID%はそれらをポケットにしまうと先に進んだ
MONEY += 5000
PRINTW 所持金 +$5,000
CALL ANOMALY2_5(ID)

@ANOMALY2_5(ID)
#DIM ID

DRAWLINE
PRINTL コックピット区画にたどり着いた
PRINTL まるで建造仕立てのようなピカピカに磨き上げられた計器類
PRINTL そして無残に食い破られた窓ガラスの破片が散らばっている
CALL ASK_SELECT("さて、どうする？", "これだけ綺麗なら動くかも？動力室に戻って電源を入れてみる", "一旦戻って状況を報告する")
SIF RESULT == 1
	CALL ANOMALY2_6(ID)
SIF RESULT == 2
	CALL ANOMALY2_7(ID)

@ANOMALY2_6(ID)
#DIM ID

DRAWLINE
PRINTL 動力室に戻り、それらしいレバーをおろしてみた
PRINTL 途端にバコンと大きな音がしたものの、何も起こらない
PRINTL もしかしたら致命的なダメージを船に与えてしまったかもしれない
PRINTFORMW %CALLNAME:ID%は何も見なかったことにして、%CALLNAME:MASTER%の元に戻ることにした
RETURN

@ANOMALY2_7(ID)
#DIM ID

DRAWLINE
PRINTFORML %CALLNAME:MASTER%は戻ってきた%CALLNAME:ID%から話を聞くと、艦に備え付けの簡易修復用のロボットで船の修復を試みることにした
PRINTW ……
PRINTFORMW 数時間後、あらかたの配線の損傷を修復すると、%CALLNAME:ID%は動力室の電源レバーを降ろす
PRINTW 途端に辺りが明るくなり、艦の電源が回復した!
PRINTW 自然と早まる足取りでコックピットに行くと、復活した艦の管理AIが話しかけてきた
PRINTL 「警告。損傷により空気が艦外へ流出しています。そのため……」
PRINTW ....
PRINTL どうやらこの艦のコントロールを入手することに成功したようだ
PRINTFORML 損傷した巡洋艦が%CALLNAME:MASTER%のものになりました
CALL INIT_PRESET_SHIP(PLAYER_COUNTRY, ANOMALY_POSITIONID:2, 汎用巡洋艦, "損傷した巡洋艦", 汎用巡洋艦)
PRINTW ただし外壁の損傷を回復するために、かなりのお金がかかりそうだ……
BOARDING_SHIP_ID:ID = RESULT
RIDING_WITH:ID = -1
SHIP_DAMAGE:RESULT = 4900

RETURN