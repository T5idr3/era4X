;--------------------------
; 艦艇による移動 MOVE
; 移動処理
; 戻り値 0: 未着, 1: 到着
;--------------------------
@MOVE_SHIP(X_POS, Y_POS, SHIP_ID)
#DIM POSID
#DIM SHIP_ID
#DIM X_POS
#DIM Y_POS
#DIM CURRENT_X_POS
#DIM CURRENT_Y_POS
#DIM X_DIFF
#DIM Y_DIFF
#DIM SURPLUS
#DIM 移動量 = 1
#DIM MOVE

;移動量 = CALL GET_SHIP_MOVE(SHIP_ID)

; 引数のエラーチェック
IF SHIP_ID < 0 || SHIP_ID >= MAX_SHIP
    PRINTFORML 異常なSHIP_ID: {SHIP_ID}が確認されました
    THROW
ENDIF
IF X_POS < 0 || X_POS >= MAP_WIDTH || Y_POS < 0 || Y_POS >= MAP_HEIGHT
    PRINTFORML %SHIP_NAME:SHIP_ID%が座標X: {X_POS}, Y: {Y_POS}へ移動しようとしました。
    THROW 上記の以上挙動が確認されました
ENDIF
CURRENT_X_POS = SHIP_POSITION:SHIP_ID/100
CURRENT_Y_POS = SHIP_POSITION:SHIP_ID%100
SIF CURRENT_X_POS == X_POS && CURRENT_Y_POS == Y_POS
    RETURN 1
FOR MOVE, 0, 移動量
    ; X座標を合わせに行く
    X_DIFF = X_POS - CURRENT_X_POS
    IF X_DIFF > 0
        CALL POS2ID(CURRENT_X_POS + 1, CURRENT_Y_POS)
    ELSEIF X_DIFF < 0
        CALL POS2ID(CURRENT_X_POS - 1, CURRENT_Y_POS)
    ENDIF
    Y_DIFF = Y_POS - CURRENT_Y_POS
    IF Y_DIFF > 0
        CALL POS2ID(CURRENT_X_POS, CURRENT_Y_POS + 1)
    ELSEIF Y_DIFF < 0
        CALL POS2ID(CURRENT_X_POS, CURRENT_Y_POS - 1)
    ENDIF
    SHIP_POSITION:SHIP_ID = RESULT
    SIF CURRENT_X_POS == X_POS && CURRENT_Y_POS == Y_POS
        RETURN 1
NEXT
RETURN 0

; 特定方向への移動
@SHIP_MOVE_FOR(VEC, SHIP_ID)
#DIM VEC
#DIM SHIP_ID
#DIM CURRENT_X_POS
#DIM CURRENT_Y_POS
#DIM 移動量 = 1

CALL ID2POS(SHIP_POSITION:SHIP_ID)
CURRENT_X_POS = RESULT:1
CURRENT_Y_POS = RESULT:2

SELECTCASE VEC
    CASE 8
        IF CURRENT_Y_POS <= 0
            RETURN 0
        ELSEIF CURRENT_Y_POS - 移動量 < 0
            CALL POS2ID(CURRENT_X_POS, 0)
        ELSE
            CALL POS2ID(CURRENT_X_POS, CURRENT_Y_POS - 移動量)
        ENDIF
    CASE 6
        IF CURRENT_X_POS >= MAP_WIDTH - 1
            RETURN 0
        ELSEIF CURRENT_X_POS + 移動量 >= MAP_WIDTH - 1
            CALL POS2ID(MAP_WIDTH - 1, CURRENT_Y_POS)
        ELSE
            CALL POS2ID(CURRENT_X_POS + 移動量, CURRENT_Y_POS)
        ENDIF
    CASE 2
        IF CURRENT_Y_POS >= MAP_HEIGHT - 1
            RETURN 0
        ELSEIF CURRENT_Y_POS + 移動量 >= MAP_HEIGHT - 1
            CALL POS2ID(CURRENT_X_POS, MAP_HEIGHT - 1)
        ELSE
            CALL POS2ID(CURRENT_X_POS, CURRENT_Y_POS + 移動量)
        ENDIF
    CASE 4
        IF CURRENT_X_POS <= 0
            RETURN 0
        ELSEIF CURRENT_X_POS - 移動量 < 0
            CALL POS2ID(0, CURRENT_Y_POS)
        ELSE
            CALL POS2ID(CURRENT_X_POS - 移動量, CURRENT_Y_POS)
        ENDIF
ENDSELECT
SHIP_POSITION:SHIP_ID = RESULT
RETURN 1

;--------------------------
; 周囲の明瞭化
;--------------------------
@CLEAR_MAP(X, Y, RANGE)
#DIM RANGE
FOR LOCAL:0, -RANGE, RANGE + 1
	FOR LOCAL:1, -RANGE, RANGE + 1
		CLEARMAP:LIMIT(X + LOCAL:0, 0, MAP_WIDTH - 1):LIMIT(Y + LOCAL:1, 0, MAP_HEIGHT - 1) = 1
	NEXT
NEXT
