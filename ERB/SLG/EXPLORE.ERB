;--------------------------
; 艦艇による移動 MOVE
; 移動処理
; 戻り値 0: 未着, 1: 到着
;--------------------------
@MOVE_SHIP(POSID, SHIP_ID)
#DIM POSID
#DIM SHIP_ID
#DIM SECTOR_ID
#DIM X_POS
#DIM Y_POS
#DIM CURRENT_X_POS
#DIM CURRENT_Y_POS
#DIM X_DIFF
#DIM Y_DIFF
#DIM SURPLUS
#DIM 移動量
#DIM MOVE
#DIM CHARA_ID

CALL ID2POS(POSID)
SECTOR_ID = RESULT:0
X_POS = RESULT:1
Y_POS = RESULT:2

CALL GET_ENGIN(SHIP_EQUIP:エンジン:SHIP_ID)
移動量 = SPEED

; 引数のエラーチェック
IF SHIP_ID < 0 || SHIP_ID >= MAX_SHIP
    PRINTFORML 異常なSHIP_ID: {SHIP_ID}が確認されました
    THROW
ENDIF
IF X_POS < 0 || X_POS >= MAP_WIDTH || Y_POS < 0 || Y_POS >= MAP_HEIGHT
    PRINTFORML %SHIP_NAME:SHIP_ID%が座標X: {X_POS}, Y: {Y_POS}へ移動しようとしました。
    THROW 上記の以上挙動が確認されました
ENDIF
FOR MOVE, 0, 移動量
    CURRENT_X_POS = (SHIP_POSITION:SHIP_ID % 10000) / 100
    CURRENT_Y_POS = (SHIP_POSITION:SHIP_ID % 10000) % 100
    SIF CURRENT_X_POS == X_POS && CURRENT_Y_POS == Y_POS
        RETURN 1
    ; X座標を合わせに行く
    X_DIFF = X_POS - CURRENT_X_POS
    IF X_DIFF > 0
        CALL POS2ID(SECTOR_ID, CURRENT_X_POS + 1, CURRENT_Y_POS)
    ELSEIF X_DIFF < 0
        CALL POS2ID(SECTOR_ID, CURRENT_X_POS - 1, CURRENT_Y_POS)
    ENDIF
    Y_DIFF = Y_POS - CURRENT_Y_POS
    IF Y_DIFF > 0
        CALL POS2ID(SECTOR_ID, CURRENT_X_POS, CURRENT_Y_POS + 1)
    ELSEIF Y_DIFF < 0
        CALL POS2ID(SECTOR_ID, CURRENT_X_POS, CURRENT_Y_POS - 1)
    ENDIF
    SHIP_POSITION:SHIP_ID = RESULT
    IF SHIP_COUNTRY:SHIP_ID == PLAYER_COUNTRY
        FOR CHARA_ID, 0, CHARANUM
            SIF BOARDING_SHIP_ID:CHARA_ID != SHIP_ID
                CONTINUE
            CALL ID2POS(SHIP_POSITION:SHIP_ID)
            CALL CLEAR_MAP(SHIP_POSITION:SHIP_ID, SHIP_EQUIP:センサー:(BOARDING_SHIP_ID:CHARA_ID))
            BREAK
        NEXT
    ENDIF
    SIF CURRENT_X_POS == X_POS && CURRENT_Y_POS == Y_POS
        RETURN 1
NEXT
RETURN 0

; 特定方向への移動
@SHIP_MOVE_FOR(VEC, SHIP_ID, IS_SLOW)
#DIM VEC
#DIM SHIP_ID
#DIM CURRENT_SECTOR_ID
#DIM CURRENT_X_POS
#DIM CURRENT_Y_POS
#DIM 移動量
#DIM IS_SLOW
#DIM POSID
#DIM CHARA_ID

IF IS_SLOW == 0
    CALL GET_ENGIN(SHIP_EQUIP:エンジン:SHIP_ID)
    移動量 = SPEED
ELSE
    移動量 = 1
ENDIF
IS_SLOW = 0
CALL ID2POS(SHIP_POSITION:SHIP_ID)
CURRENT_SECTOR_ID = RESULT:0
CURRENT_X_POS = RESULT:1
CURRENT_Y_POS = RESULT:2

SELECTCASE VEC
    CASE 8
        IF CURRENT_Y_POS <= 0
            RETURN 0
        ELSEIF CURRENT_Y_POS - 移動量 < 0
            CALL POS2ID(CURRENT_SECTOR_ID, CURRENT_X_POS, 0)
        ELSE
            CALL POS2ID(CURRENT_SECTOR_ID, CURRENT_X_POS, CURRENT_Y_POS - 移動量)
        ENDIF
    CASE 6
        IF CURRENT_X_POS >= MAP_WIDTH - 1
            RETURN 0
        ELSEIF CURRENT_X_POS + 移動量 >= MAP_WIDTH - 1
            CALL POS2ID(CURRENT_SECTOR_ID, MAP_WIDTH - 1, CURRENT_Y_POS)
        ELSE
            CALL POS2ID(CURRENT_SECTOR_ID, CURRENT_X_POS + 移動量, CURRENT_Y_POS)
        ENDIF
    CASE 2
        IF CURRENT_Y_POS >= MAP_HEIGHT - 1
            RETURN 0
        ELSEIF CURRENT_Y_POS + 移動量 >= MAP_HEIGHT - 1
            CALL POS2ID(CURRENT_SECTOR_ID, CURRENT_X_POS, MAP_HEIGHT - 1)
        ELSE
            CALL POS2ID(CURRENT_SECTOR_ID, CURRENT_X_POS, CURRENT_Y_POS + 移動量)
        ENDIF
    CASE 4
        IF CURRENT_X_POS <= 0
            RETURN 0
        ELSEIF CURRENT_X_POS - 移動量 < 0
            CALL POS2ID(CURRENT_SECTOR_ID, 0, CURRENT_Y_POS)
        ELSE
            CALL POS2ID(CURRENT_SECTOR_ID, CURRENT_X_POS - 移動量, CURRENT_Y_POS)
        ENDIF
ENDSELECT
POSID = RESULT
IF SHIP_COUNTRY:SHIP_ID == PLAYER_COUNTRY
FOR CHARA_ID, 0, CHARANUM
    SIF BOARDING_SHIP_ID:CHARA_ID != SHIP_ID
        CONTINUE
    CALL ID2POS(RESULT)
    CALL CLEAR_MAP(POSID, SHIP_EQUIP:センサー:(BOARDING_SHIP_ID:CHARA_ID))
    BREAK
NEXT
ENDIF
SHIP_POSITION:SHIP_ID = POSID
RETURN 1

;--------------------------
; 周囲の明瞭化
;--------------------------
@CLEAR_MAP(POSID, RANGE)
#DIM POSID
#DIM RANGE
#DIM SECTOR_ID
#DIM X_POS
#DIM Y_POS
CALL ID2POS(POSID)
SECTOR_ID = RESULT:0
X_POS = RESULT:1
Y_POS = RESULT:2
FOR LOCAL:0, -RANGE, RANGE + 1
	FOR LOCAL:1, -RANGE, RANGE + 1
		CLEARMAP:SECTOR_ID:LIMIT(X_POS + LOCAL:0, 0, MAP_WIDTH - 1):LIMIT(Y_POS + LOCAL:1, 0, MAP_HEIGHT - 1) = 1
	NEXT
NEXT
