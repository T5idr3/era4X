;---------------------------
; 敵襲イベント
;---------------------------
@IS_HAPPEN_SLG_EVENT_1
#DIM PORT_ID
#DIM SHIP_ID
#DIM TARGET_PORT
#DIM IS_RAID

; あなたが自分の宇宙港を持っている
TARGET_PORT = -1
FOR PORT_ID, 0, MAX_PORT
	IF PORT_OWNER:PORT_ID == PLAYER_COUNTRY
		TARGET_PORT = PORT_ID
		BREAK
	ENDIF
NEXT
SIF TARGET_PORT == -1
	RETURN 0

IS_RAID = 0
; 戦闘艦を持っている&&1/1000の確率
FOR SHIP_ID, 0, MAX_SHIP
	SIF SHIP_COUNTRY:SHIP_ID != PLAYER_COUNTRY
		CONTINUE
	SIF SHIP_CATEGORY:SHIP_ID == 調査船 || SHIP_CATEGORY:SHIP_ID == 輸送船 || SHIP_CATEGORY:SHIP_ID == 作業船
		CONTINUE
	SIF SHIP_POSITION:SHIP_ID != PORT_POSITION_ID:TARGET_PORT
		CONTINUE
	SIF RAND:2000 != 0
		CONTINUE
	IS_RAID = 1
NEXT
RETURN IS_RAID

@SLG_EVENT_MAIN_1
#DIM ITER
#DIM PORT_ID
#DIM TARGET_PORT
#DIM SECTION_ID
#DIM FROM

FOR PORT_ID, 0, MAX_PORT
	IF PORT_OWNER:PORT_ID == PLAYER_COUNTRY
		TARGET_PORT = PORT_ID
		BREAK
	ENDIF
NEXT
CALL ID2POS(PORT_POSITION_ID:TARGET_PORT)
SECTION_ID = RESULT:0

IF SECTION_ID > 9
	CALL POS2ID(SECTION_ID - 10, 7, 13)
	FROM = RESULT
ELSE
	CALL POS2ID(SECTION_ID + 10, 7, 1)
	FROM = RESULT
ENDIF

FOR ITER, 0, 3
	CALL INIT_PRESET_SHIP(10, FROM, 汎用コルベット, "海賊船", 汎用コルベット)
	SHIP_MISSION:RESULT = 移動
	MOVING_POS:RESULT = PORT_POSITION_ID:TARGET_PORT
NEXT

PRINTFORML {FROM}
SETCOLOR 0XFF0000
	PRINTFORMW [警報] セクション＃{SECTION_ID}にある%CALLNAME:MASTER%の宇宙港に向けて、海賊が襲撃に向かいました
RESETCOLOR
