;----------------------------------
; インフォメーション
;----------------------------------
@INFORMATION_INDEX
#DIM CURRENT_TAB
#DIM CONST TOP = 0
#DIM CONST RESOURCE = 1
#DIM CONST ARMY = 2
#DIM CONST HISTORY = 3
#DIM PAGE
#DIM COUNTRY_PAGE
#DIM PORT_COUNT

CURRENT_TAB = TOP
PAGE = 1
COUNTRY_PAGE = 0

FINDELEMENT PORT_POSITION_ID, -1
PORT_COUNT = RESULT

$INPUT_LOOP
PRINTL
PRINTL ◆インフォメーション
CALL SHOW_INFORMATION_TAB(CURRENT_TAB)

IF CURRENT_TAB == TOP
    CALL SHOW_INFORMATION_TOP(PAGE)
ELSEIF CURRENT_TAB == RESOURCE
    CALL SHOW_INFORMATION_RESOURCE(PAGE, COUNTRY_PAGE)
ELSEIF CURRENT_TAB == ARMY
    CALL SHOW_INFORMATION_ARMY(PAGE, COUNTRY_PAGE)
ELSEIF CURRENT_TAB == HISTORY
    CALL SHOW_INFORMATION_HISTORY(PAGE)
ENDIF
INPUT

IF RESULT == TOP
    IF CURRENT_TAB != TOP
        PAGE = 1
        COUNTRY_PAGE = 0
    ENDIF
    CURRENT_TAB = TOP
ELSEIF RESULT == RESOURCE
    IF CURRENT_TAB != TOP
        PAGE = 1
        COUNTRY_PAGE = 0
    ENDIF
    CURRENT_TAB = RESOURCE
ELSEIF RESULT == ARMY
    IF CURRENT_TAB != TOP
        PAGE = 1
        COUNTRY_PAGE = 0
    ENDIF
    CURRENT_TAB = ARMY
ELSEIF RESULT == HISTORY
    IF CURRENT_TAB != HISTORY
        PAGE = 1
        COUNTRY_PAGE = 0
    ENDIF
    CURRENT_TAB = HISTORY
ELSEIF RESULT == 9
    RETURN
ELSEIF RESULT == 101
    PAGE = LIMIT(PAGE - 1, 1, MAX_COUNTRY - 1)
ELSEIF RESULT == 102
    PAGE = LIMIT(PAGE + 1, 1, MAX_COUNTRY - 1)
ELSEIF RESULT == 201
    COUNTRY_PAGE = LIMIT(COUNTRY_PAGE - 1, 0, 星系数 - 5)
ELSEIF RESULT == 202
    COUNTRY_PAGE = LIMIT(COUNTRY_PAGE + 1, 0, 星系数 - 5)
ENDIF
GOTO INPUT_LOOP

@SHOW_INFORMATION_TAB(CURRENT_TAB)
#DIM CURRENT_TAB
#DIM CONST TOP = 0
#DIM CONST RESOURCE = 1
#DIM CONST ARMY = 2
#DIM CONST HISTORY = 3

PRINTL ┌──────────────────────┐
PRINT │
SIF CURRENT_TAB == TOP
    SETCOLOR 0X00FF00
PRINTBUTTON " トップ ", TOP
RESETCOLOR
PRINT │
SIF CURRENT_TAB == RESOURCE
    SETCOLOR 0X00FF00
PRINTBUTTON " リソース ", RESOURCE
RESETCOLOR
PRINT │
SIF CURRENT_TAB == ARMY
    SETCOLOR 0X00FF00
PRINTBUTTON " 軍事力 ", ARMY
RESETCOLOR
PRINT │
SIF CURRENT_TAB == HISTORY
    SETCOLOR 0X00FF00
PRINTBUTTON " 歴史 ", HISTORY
RESETCOLOR
PRINT │
PRINTBUTTON "戻る", 9
PRINTL │
DRAWLINE

; PAGER
@SHOW_INFORMATION_PAGER(PAGE)
#DIM PAGE

PRINTL
SETCOLOR 0X666666
PRINTFORML ---------------------------------------------------------------------------------------
RESETCOLOR
PRINTFORM %"確認する国を変更", 30, RIGHT%
IF PAGE > 1
    PRINTBUTTON "[<]", 101
ELSE
    PRINT    
ENDIF
SIF PAGE < MAX_COUNTRY - 1
    PRINTBUTTON "[>]", 102
PRINTL

; TOPは国庫と保持星と好感度表がまとめられている
@SHOW_INFORMATION_TOP(PAGE)
#DIM STAR_ID
#DIM STAR_COUNTER
#DIM PORT_ID
#DIM PORT_COUNTER
#DIM REF PAGE

PRINTFORML  %COUNTRY_NAME:PAGE%情報
IF PAGE == PLAYER_COUNTRY
    LOCALS = %TOSTR(MONEY, "$###,###,###,###")%
    SIF MONEY == 0
        LOCALS = %"$0"%
ELSE
    LOCALS = %TOSTR(MONEY:PAGE, "$##,###,###,###")%
    SIF MONEY:PAGE == 0
        LOCALS = %"$0"%
ENDIF
PRINTL
PRINTL  ◆国庫
PRINTFORML   %LOCALS%
PRINTL
PRINTL  ◆所有星系 
STAR_COUNTER = 0
FOR STAR_ID, 0, 星系数
    SIF STAR_OWNER:STAR_ID != PAGE
        CONTINUE
    STAR_COUNTER ++
    CALL ID2POS(星系座標ID:STAR_ID)
    PRINTFORML    %STAR_NAME:STAR_ID% (セクター＃{RESULT})
NEXT
SIF STAR_COUNTER == 0
    PRINTL  なし
PRINTL
PRINTL  ◆所有独立宇宙港 
PORT_COUNTER = 0
FOR PORT_ID, 0, MAX_PORT
    SIF PORT_OWNER:PORT_ID != PAGE
        CONTINUE
    PORT_COUNTER ++
    CALL ID2POS(PORT_POSITION_ID)
    PRINTFORML    宇宙港＃{PORT_ID} (X: {RESULT:1}}, Y: {RESULT:2})
NEXT
SIF PORT_COUNTER == 0
    PRINTL  なし
CALL SHOW_INFORMATION_PAGER(PAGE)
DRAWLINE

; リソース表示
@SHOW_INFORMATION_RESOURCE(PAGE, COUNTRY_PAGE)
#DIM REF PAGE
#DIM REF COUNTRY_PAGE
#DIM ITEM_ID
#DIM PORT_ID
#DIM STAR_ID

PRINTFORML  %COUNTRY_NAME:PAGE% リソース 情報
PRINTL
PRINTFORM %"", 20, LEFT%
FOR STAR_ID, COUNTRY_PAGE, COUNTRY_PAGE + 5
    PRINTFORM %STAR_NAME:STAR_ID, 16, RIGHT%
NEXT
PRINTL
FOR ITEM_ID, 0, ITEM_LENGTH
    LOCALS = %GET_RESOURCE_NAME(ITEM_ID)%
    SIF LOCALS == NO_ITEM
        CONTINUE
    PRINTFORM %LOCALS, 20, LEFT%
    FOR STAR_ID, COUNTRY_PAGE, COUNTRY_PAGE + 5
        PRINTFORM {GET_RESOURCE_NUM(星系座標ID:STAR_ID, PAGE, ITEM_ID), 16, RIGHT}
    NEXT
    PRINTL
NEXT
CALL SHOW_INFORMATION_PAGER(PAGE)
CALL SHOW_INFORMATION_COUNTRY_PAGER(COUNTRY_PAGE)
DRAWLINE

; 星のページ送り
@SHOW_INFORMATION_COUNTRY_PAGER(PAGE)
#DIM PAGE

SETCOLOR 0X666666
PRINTFORML ---------------------------------------------------------------------------------------
RESETCOLOR
PRINTFORM %"確認する星を変更", 30, RIGHT%
IF PAGE > 0
    PRINTBUTTON "[<]", 201
ELSE
    PRINT    
ENDIF
SIF PAGE < 星系数 - 5
    PRINTBUTTON "[>]", 202
PRINTL

@SHOW_INFORMATION_ARMY(PAGE, COUNTRY_PAGE)
#DIM REF PAGE
#DIM REF COUNTRY_PAGE
#DIM STAR_ID
#DIM CURRENT_SHIP_TYPE
#DIM SHIP_ID
#DIM COUNTER

PRINTFORML  %COUNTRY_NAME:PAGE% 軍事情報
PRINTL
PRINTFORM %"", 20, LEFT%
FOR STAR_ID, COUNTRY_PAGE, COUNTRY_PAGE + 5
    PRINTFORM %STAR_NAME:STAR_ID, 16, RIGHT%
NEXT
PRINTL
FOR CURRENT_SHIP_TYPE, 0, SHIP_TYPE_COUNT
    SELECTCASE CURRENT_SHIP_TYPE
        CASE 偵察艇
            PRINTFORM %"偵察艇", 20, LEFT%
        CASE コルベット
            PRINTFORM %"コルベット", 20, LEFT%
        CASE フリゲート
            PRINTFORM %"フリゲート", 20, LEFT%
        CASE 駆逐艦
            PRINTFORM %"駆逐艦", 20, LEFT%
        CASE 巡洋艦
            PRINTFORM %"巡洋艦", 20, LEFT%
        CASE 戦艦
            PRINTFORM %"戦艦", 20, LEFT%
        CASE 空母
            PRINTFORM %"空母", 20, LEFT%
        CASE 輸送船
            PRINTFORM %"輸送船", 20, LEFT%
        CASE 作業船
            PRINTFORM %"作業船", 20, LEFT%
    ENDSELECT
    FOR STAR_ID, COUNTRY_PAGE, COUNTRY_PAGE + 5
        COUNTER = 0
        FOR SHIP_ID, 0, MAX_SHIP
            SIF SHIP_TYPE:SHIP_ID != CURRENT_SHIP_TYPE
                CONTINUE
            SIF SHIP_COUNTRY:SHIP_ID != PAGE
                CONTINUE
            SIF SHIP_POSITION:SHIP_ID != 星系座標ID:STAR_ID
                CONTINUE
            COUNTER ++
        NEXT
        SIF COUNTER == 0
            SETCOLOR 0X666666
        PRINTFORM {COUNTER, 16, RIGHT}
        RESETCOLOR
    NEXT
    PRINTL
    PRINTL
NEXT
PRINTL
CALL SHOW_INFORMATION_PAGER(PAGE)
CALL SHOW_INFORMATION_COUNTRY_PAGER(COUNTRY_PAGE)
DRAWLINE

; 歴史の表示
@SHOW_INFORMATION_HISTORY(PAGE)
#DIM REF PAGE

PRINTFORML  %COUNTRY_NAME:PAGE% 歴史情報
PRINTL
SELECTCASE PAGE
    CASE 1
        PRINTL プレイヤーの築き上げた文明
    CASE 2
        PRINTL 民主主義国家との地上戦争に負けた文明が脱出し築き上げた文明
        PRINTL 男子は成人と認められる16歳から3年間の軍役につくことが義務付けられている
        PRINTL 民主主義国家への報復を国是としており、錦の旗のもと高い税率を維持している
        PRINTL 貧困率が高く軍役の義務が終わっても軍属を続けるものも多い
        PRINTL そのため軍人が他の勢力よりも多く、質よりも量のドクトリンを採用している
        PRINTL ドクトリンで必要な物資を補うため、海賊ともやり取りしていると噂が立っている
        PRINTL
        PRINTL 女子の貧困率も高く、奴隷として売られる者や売春婦になるものが他国よりも圧倒的に多い
        PRINTL 
        PRINTL 犯罪率は高い国だが、春を買うために訪れる者も少なくない
    CASE 3
        PRINTL 企業の連合が国家運営を行っている文明
        PRINTL 物質主義で格差が大きいが、貧困層からの成り上がりストーリーが好きな国民性のためか皆ハングリー精神にあふれている
        PRINTL 軍に関しても軍事産業の副産物に過ぎず、ドクトリン研究などは他国より遅れている
        PRINTL その割に国民性からか軍の士気も高く、海賊討伐などの任務を率先して行っている
        PRINTL 
        PRINTL 成り上がりのための博打に負け、鉱山労働や売春をさせられる者も一定数いる
    CASE 4
        PRINTL 社会主義国との地上戦争に勝利し初期宇宙時代を制覇した文明
        PRINTL だが現在は軍事力で社会主義国に追いつかれており、急いで軍拡を行っている
        PRINTL 国政の方針から軍務などを科すことができないため、志願兵のみで軍を保っている
        PRINTL そのため少数精鋭のドクトリンを採用しており、特に空母運用に力を入れている
        PRINTL 国民の格差が少ないことで有名で、犯罪率も引い
        PRINTL 個人主義の国民性のため、生産性が低いのがネック
    CASE 5
        PRINTL 数々の宗教が奇跡的に統合され出来上がった統一宗教を国教とする文明
        PRINTL 宗教家が国家運営を行っており、「神の右手」に住むものとして神のために労働することを是としている
        PRINTL 機械文明とは犬猿の仲で、頻繁に紛争を起こしている
        PRINTL 居住惑星には珍しい軟体生物がたくさん住んでおり、神の使途としてあがめられている
        PRINTL だが密輸が横行しており、他国で拷問や調教に使われる
        PRINTL
        PRINTL 貞操を成人まで守ることを国教で厳命されているうえ妻以外との姦通を禁じられており、性産業が全くない珍しい文明でもある
    CASE 6
        PRINTL 機械により国の運営が行われている文明
        PRINTL 極度の物質主義で精神主義のアレス宗教国家とは犬猿の仲
        PRINTL マザーと呼ばれるスーパーコンピュータ配下にピラミッド構造で機械が配置されており、マザーの命令を遵守し実行する
        PRINTL 基本的に人間とは良好な関係を築いており、人間のために働く人工知能搭載ロボットも多数存在する
        PRINTL 他の国と違い人工知能AIに国民としての権利が付与されているが、マザーの支配下であるため好き勝手動くことはない
        PRINTL
        PRINTL 高性能なセクサロイドも作られており、銀河中の男のあこがれとして性産業の主力になっている
    CASEELSE
        PRINTL 不明の文明
ENDSELECT


CALL SHOW_INFORMATION_PAGER(PAGE)
DRAWLINE