;-----------------------------------
;装備の説明(集約用)
;-----------------------------------
@EQUIP_STATUS_DESCRIPTION(EQUIP_TYPE, EQUIP_ID)
#DIM EQUIP_ID
#DIM EQUIP_TYPE
#DIM STATUS, STATUS_LENGTH
#DIM WEAPON_TYPE_NUM
#DIM CONST HEADWORD_DIGIT = 12

VARSET STATUS, 0
;ステータスと名前を呼び出す
TRYCALLFORM GET_%EQUIP_CATEGORY_NAME_EN:EQUIP_TYPE%_STATUS_{EQUIP_ID}(STATUS)
TRYCALLFORM GET_%EQUIP_CATEGORY_NAME_EN:EQUIP_TYPE%_NAME_{EQUIP_ID}
PRINTFORML %RESULTS%
TRYCALLFORM GET_%EQUIP_CATEGORY_NAME_EN:EQUIP_TYPE%_DESCRIPTION_F_{EQUIP_ID}

SELECTCASE EQUIP_TYPE
	CASE 主砲
		WEAPON_TYPE_NUM = STATUS:WEAPON_TYPE
		PRINTFORML  %"ダメージ", HEADWORD_DIGIT,LEFT%: {STATUS:DAMAGE}
		PRINTFORML  %"射撃レート", HEADWORD_DIGIT,LEFT%: {STATUS:FIRE_RATE}発/ターン
		PRINTFORML  %"命中率", HEADWORD_DIGIT,LEFT%: {STATUS:HIT_RATE}
		PRINTFORML  %"有効距離", HEADWORD_DIGIT,LEFT%: {STATUS:FIRE_RANGE}
		PRINTFORML  %"サイズ", HEADWORD_DIGIT,LEFT%: {STATUS:SIZE}
		IF STATUS:WEAPON_TYPE == 艦載機
			PRINTFORML  %"耐久", HEADWORD_DIGIT,LEFT%: {STATUS:AIR_SHIP_HP}
			PRINTFORML  %"空対空攻撃力", HEADWORD_DIGIT,LEFT%: {STATUS:ANTI_AIR}
			PRINTFORML  %"搭載数", HEADWORD_DIGIT,LEFT%: {STATUS:AIR_SHIP_COUNT}
		ENDIF
		PRINTFORML  %"属性", HEADWORD_DIGIT,LEFT%: %WEAPON_TYPE_NAME:WEAPON_TYPE_NUM%
		PRINTL
		IF STATUS:WEAPON_TYPE == 艦載機
			PRINTFORML  %"最大対艦DPT", HEADWORD_DIGIT,LEFT%:{STATUS:DAMAGE * STATUS:FIRE_RATE * STATUS:AIR_SHIP_COUNT}
			PRINTFORML  %"実効対艦DPT", HEADWORD_DIGIT,LEFT%:{STATUS:DAMAGE * STATUS:FIRE_RATE * STATUS:HIT_RATE * STATUS:AIR_SHIP_COUNT / 100}
			PRINTFORML  %"最大対空DPT", HEADWORD_DIGIT,LEFT%:{STATUS:ANTI_AIR * STATUS:FIRE_RATE * STATUS:AIR_SHIP_COUNT}
			PRINTFORML  %"実効対空DPT", HEADWORD_DIGIT,LEFT%:{STATUS:ANTI_AIR * STATUS:FIRE_RATE * STATUS:HIT_RATE * STATUS:AIR_SHIP_COUNT / 100}
		ELSE
			PRINTFORML  %"最大DPT", HEADWORD_DIGIT,LEFT%:{STATUS:DAMAGE * STATUS:FIRE_RATE}
			PRINTFORML  %"実効DPT", HEADWORD_DIGIT,LEFT%:{STATUS:DAMAGE * STATUS:FIRE_RATE * STATUS:HIT_RATE / 100}
		ENDIF
	CASE 副砲
		WEAPON_TYPE_NUM = STATUS:WEAPON_TYPE
		PRINTFORML  %"ダメージ", HEADWORD_DIGIT,LEFT%: {STATUS:DAMAGE}
		PRINTFORML  %"射撃レート", HEADWORD_DIGIT,LEFT%: {STATUS:FIRE_RATE}発/ターン
		PRINTFORML  %"対空攻撃力", HEADWORD_DIGIT,LEFT%: {STATUS:ANTI_AIR}
		PRINTFORML  %"命中率", HEADWORD_DIGIT,LEFT%: {STATUS:HIT_RATE}
		PRINTFORML  %"有効距離", HEADWORD_DIGIT,LEFT%: {STATUS:FIRE_RANGE}
		PRINTFORML  %"サイズ", HEADWORD_DIGIT,LEFT%: {STATUS:SIZE}
		PRINTFORML  %"属性", HEADWORD_DIGIT,LEFT%: %WEAPON_TYPE_NAME:WEAPON_TYPE_NUM%
		PRINTL
		PRINTFORML  %"最大対艦DPT", HEADWORD_DIGIT,LEFT%:{STATUS:DAMAGE * STATUS:FIRE_RATE}
		PRINTFORML  %"実効対艦DPT", HEADWORD_DIGIT,LEFT%:{STATUS:DAMAGE * STATUS:FIRE_RATE * STATUS:HIT_RATE / 100}
		PRINTFORML  %"最大対空DPT", HEADWORD_DIGIT,LEFT%:{STATUS:ANTI_AIR * STATUS:FIRE_RATE}
		PRINTFORML  %"実効対空DPT", HEADWORD_DIGIT,LEFT%:{STATUS:ANTI_AIR * STATUS:FIRE_RATE * STATUS:HIT_RATE / 100}
	CASE シールド
		PRINTFORML  %"シールド", HEADWORD_DIGIT,LEFT%: {STATUS:SIELD}
		PRINTFORML  %"シールド回復", HEADWORD_DIGIT,LEFT%: {STATUS:SIELD_RECOVER}/ターン
		PRINTFORML  %"サイズ", HEADWORD_DIGIT,LEFT%: {STATUS:SIZE}
	CASE 装甲
		PRINTFORML  %"装甲", HEADWORD_DIGIT,LEFT%: {STATUS:ARMOR}
		SIF STATUS:AVOID != 0
			 PRINTFORML  %"回避", HEADWORD_DIGIT,LEFT%: {STATUS:AVOID}
		PRINTFORML  %"サイズ", HEADWORD_DIGIT,LEFT%: {STATUS:SIZE}
	CASE エンジン
		PRINTFORML  %"スピード", HEADWORD_DIGIT,LEFT%: {STATUS:SPEED}
		PRINTFORML  %"回避", HEADWORD_DIGIT,LEFT%: {STATUS:AVOID}
		PRINTFORML  %"サイズ", HEADWORD_DIGIT,LEFT%: {STATUS:SIZE}
	CASE AI
		PRINTFORML  %"回避", HEADWORD_DIGIT,LEFT%: {STATUS:AVOID}
	CASE センサー
		PRINTFORML  %"センサー範囲", HEADWORD_DIGIT,LEFT%: {STATUS:SENSOR}
	CASE オプション
		SIF STATUS:SIELD != 0
			PRINTFORML  %"シールド", HEADWORD_DIGIT,LEFT%: {STATUS:SIELD}
		SIF STATUS:SIELD_RECOVER != 0
			PRINTFORML  %"シールド回復", HEADWORD_DIGIT,LEFT%: {STATUS:SIELD_RECOVER}/ターン
		SIF STATUS:ARMOR != 0
			PRINTFORML  %"装甲", HEADWORD_DIGIT,LEFT%: {STATUS:ARMOR}
		SIF STATUS:SPEED != 0
			PRINTFORML  %"スピード", HEADWORD_DIGIT,LEFT%: {STATUS:SPEED}
		SIF STATUS:AVOID != 0
			PRINTFORML  %"回避", HEADWORD_DIGIT,LEFT%: {STATUS:AVOID}
		SIF STATUS:SENSOR != 0
			PRINTFORML  %"センサー範囲", HEADWORD_DIGIT,LEFT%: {STATUS:SENSOR}
	;CASEELSE
ENDSELECT

