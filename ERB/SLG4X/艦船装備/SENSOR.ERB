;-----------------------------------
; センサーの換装
;-----------------------------------
@CHANGE_SENSOR(SHIP_ID, SALE_COUNTRY, EQUIP_ID)
#DIM SHIP_ID
#DIM SALE_COUNTRY
#DIM EQUIP_ID
#DIM USE_RESOURCE, ITEM_LENGTH
#DIM ITER
#DIM RESOURCE_COUNT
#DIM LACK_RESOURCE, ITEM_LENGTH
#DIM CAN_BUY
#DIM DYNAMIC _SHIP_STATUS, STATUS_LENGTH

CAN_BUY = 1
IF SHIP_COUNTRY:SHIP_ID != SALE_COUNTRY
	TRYCALLFORM GET_SENSOR_STATUS_{EQUIP_ID}(_SHIP_STATUS)
	IF _SHIP_STATUS:PRICE > MONEY:(SHIP_COUNTRY:SHIP_ID == 1 ? 0 # SHIP_COUNTRY:SHIP_ID)
		IF SHIP_COUNTRY:SHIP_ID == 国家ID_プレイヤー
			SETCOLOR 0XFF0000
			PRINTL [CAUTION] 資金不足で装備を購入できませんでした
			RESETCOLOR
		ENDIF
		RETURN 0
	ENDIF
ENDIF

TRYCALLFORM GET_SENSOR_RESOURCE_{EQUIP_ID}(USE_RESOURCE)
FOR ITER, 0, ITEM_LENGTH
	SIF USE_RESOURCE:ITER == 0
		CONTINUE
	RESOURCE_COUNT = GET_RESOURCE_NUM(SHIP_POSITION:SHIP_ID, SALE_COUNTRY, ITER)
	IF USE_RESOURCE:ITER > RESOURCE_COUNT
		LACK_RESOURCE:ITER += USE_RESOURCE:ITER - RESOURCE_COUNT
		CAN_BUY = 0
	ENDIF
NEXT

IF CAN_BUY == 0
	IF SHIP_COUNTRY:SHIP_ID == 国家ID_プレイヤー
		SETCOLOR 0XFF0000
		PRINTL [CAUTION] 資源不足で装備を購入できませんでした
		RESETCOLOR
	ENDIF
	RETURN 0
ENDIF

FOR ITER, 0, ITEM_LENGTH
	SIF USE_RESOURCE:ITER == 0
		CONTINUE
	RESULT = REMOVE_RESOURCE(SHIP_POSITION:SHIP_ID, SALE_COUNTRY, ITER, USE_RESOURCE:ITER)
	SHIP_EQUIP:センサー:0:SHIP_ID = EQUIP_ID
NEXT
IF SHIP_COUNTRY:SHIP_ID == 国家ID_プレイヤー
	MONEY -= _SHIP_STATUS:PRICE
ELSE
	MONEY:(SHIP_COUNTRY:SHIP_ID) -= _SHIP_STATUS:PRICE
ENDIF
RETURN 1