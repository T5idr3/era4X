;--------------------------
;メスガキ分からせ・闘争か逃走か
;--------------------------
;FIGHT-OR-FLIGHT:闘争か逃走。つまり戦ってもっと徹底的に分からせるか、逃げさせて分からせられた姿を晒し者にするか。
@MESUGAKI_WAKARASE_FIGHT_OR_FLIGHT()
$INPUT_LOOP
PRINTL 
PRINTBUTTON "[0] とっ捕まえてここからが本当の分からせの時間だ！", 0
PRINTL 
PRINTBUTTON "[1] 他のメスガキにも分からされた事を伝えさせる方が効果的では？", 1
INPUT
IF RESULT == 0
	RETURN 0
ELSEIF RESULT == 1
	RETURN 1
ELSE
	GOTO INPUT_LOOP
ENDIF


;--------------------------
;メスガキ分からせ・白兵戦パート
;--------------------------
@MESUGAKI_WAKARASE_CLOSE_COMBAT(MESUGAKI_LEADER_ID, MESUGAKI_CREW_ID, HEADCOUNT)
#DIM MESUGAKI_LEADER_ID
#DIM MESUGAKI_CREW_ID
#DIM HEADCOUNT
#DIM ADD_COUNT
#DIM JUDGE

;白兵戦参加メンバーの初期化
CALL CC_INIT_MEMBER()

;戦力選択(アノニム系向けのそれを流用)
$CQC_LOOP
CALL QUEST_ANONYM_SQUAD_CHOICE(6)
FINDELEMENT ANONYM_CQC_SELECTED, 1
IF RESULT == -1
	$INPUT_LOOP
	PRINTFORML 白兵戦を止めますか？
	PRINTBUTTON "[0] はい", 0
	PRINTL 
	PRINTBUTTON "[1] いいえ", 1
	INPUT
	IF RESULT == 0
		RETURN 0
	ELSEIF RESULT == 1
		GOTO CQC_LOOP
	ELSE
		GOTO INPUT_LOOP
	ENDIF
ENDIF

;メスガキ戦力登録
CALL CC_ADDCHARA("DEFENDER", MESUGAKI_LEADER_ID)

IF MESUGAKI_CREW_ID != -1
	CALL CC_ADDCHARA("DEFENDER", MESUGAKI_CREW_ID)
ELSE
	CALL CC_ADDMOB("DEFENDER", "メスガキ副官", 1700, 1700, 40)
ENDIF

FOR ADD_COUNT, 2, HEADCOUNT
	CALL CC_ADDMOB("DEFENDER", "メスガキ船員", 1500, 1500, 30)
NEXT
FOR ADD_COUNT, 0, CHARANUM
	SIF ANONYM_CQC_SELECTED:ADD_COUNT
		CALL CC_ADDCHARA("ATTACKER", ADD_COUNT)
NEXT

CALL CLOSE_COMBAT()
JUDGE = RESULT
;抽出した戦力を船に詰めなおす(アノニム系向けのそれを流用)
CALL REBOARDING_SHIP

;CLOSE_COMBAT()の戻り値は1 = 攻撃側勝利, 2 = 防御側勝利
IF JUDGE == 1
	RETURN 1
ELSE
	RETURN 0
ENDIF