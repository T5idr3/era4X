;--------------------------
;メスガキ確認・取得処理本体
;--------------------------
@MESUGAKI_CHOICE_BODY(MESUGAKI_LEADER, MESUGAKI_LEADER_ID, MESUGAKI_CREW, MESUGAKI_CREW_ID)
#DIM MESUGAKI_LEADER
#DIM MESUGAKI_LEADER_ID
#DIM MESUGAKI_CREW
#DIM MESUGAKI_CREW_ID
#DIM RANDOM_CREDIT

DRAWLINE
PRINTL 逃げ出そうとしたメスガキ共を捕縛し、持ち出そうとしていた物品なども押収した。
PRINTL 物品はもちろん、宇宙海賊であったメスガキももはや人権も何もないただの戦利品だ。
PRINTL 
PRINTL 捕えたメスガキを全て自分の物にするという訳には行かないが、1人くらいであれば気に入ったメスガキを自分の物にしてもいいし、興味がそそられないのなら適当に小銭にしてもいい。
SIF MESUGAKI_CREW == 5 && CAN_ADD_RANDOM_CHARA()
	PRINTL あるいはメスガキ共の所有物とされていた人物に声を掛ければ、快く加入してくれるだろう。
PRINTL

$MESUGAKI_CHOICE_LOOP
TRYCALLFORM MESUGAKI_PIRATES_AFTER_BATTLE_CHOICE_LEADER_{MESUGAKI_LEADER}
SIF MESUGAKI_CREW_ID != -1 || (MESUGAKI_CREW == 5 && CAN_ADD_RANDOM_CHARA())
	PRINTL 
	TRYCALLFORM MESUGAKI_PIRATES_AFTER_BATTLE_CHOICE_CREW_{MESUGAKI_CREW}
PRINTL 
PRINTL 
PRINTBUTTON "[999] 特に惹かれるものはない",999
INPUT

IF RESULT == 0
	;リーダーを取得
	DRAWLINE
	TRYCALLFORM MESUGAKI_PIRATES_BEFORE_CHOICE_LEADER_{MESUGAKI_LEADER}(IS_MALE(MESUGAKI_LEADER_ID))
	CALL MESUGAKI_CHOICE_SUB(MESUGAKI_LEADER_ID, 0)
	SIF RESULT == 0
		GOTO MESUGAKI_CHOICE_LOOP
	;リーダーを取得したらクルーが居るならクルーを消す
	SIF MESUGAKI_CREW_ID != -1
		CALL DELETE_CHARA(MESUGAKI_CREW_ID)
ELSEIF RESULT == 1 && (MESUGAKI_CREW_ID != -1 || (MESUGAKI_CREW == 5 && CAN_ADD_RANDOM_CHARA()))
	;クルーを取得
	;クルーが未作成で・非戦闘要員だった場合には先にクルーを作って取得
	IF MESUGAKI_CREW == 5 && MESUGAKI_CREW_ID == -1
		CALL MESUGAKI_PIRATES_CHARA_GENERATE(MESUGAKI_CREW, 0)
		MESUGAKI_CREW_ID = RESULT
		DRAWLINE
		TRYCALLFORM MESUGAKI_PIRATES_BEFORE_CHOICE_CREW_{MESUGAKI_CREW}(IS_MALE(MESUGAKI_CREW_ID))
		CALL MESUGAKI_CHOICE_SUB(MESUGAKI_CREW_ID, 1)
	ELSE
		DRAWLINE
		TRYCALLFORM MESUGAKI_PIRATES_BEFORE_CHOICE_CREW_{MESUGAKI_CREW}(IS_MALE(MESUGAKI_CREW_ID))
		;クルーが非海賊だったら捕虜ではなく加入処理にする
		IF MESUGAKI_CREW == 5
			CALL MESUGAKI_CHOICE_SUB(MESUGAKI_CREW_ID, 1)

		ELSE
			CALL MESUGAKI_CHOICE_SUB(MESUGAKI_CREW_ID, 0)
		ENDIF
	ENDIF
	SIF RESULT == 0
		GOTO MESUGAKI_CHOICE_LOOP
	;クルーを取得したらリーダーを消す
	CALL DELETE_CHARA(MESUGAKI_LEADER_ID)
ELSEIF RESULT == 999
	$MESUGAKI_CHOICE_END_LOOP
	PRINTL 人物を取得しない事にしますか？
	PRINTBUTTON "[0] はい",0
	PRINTBUTTON "[1] いいえ",1
	INPUT

	IF RESULT == 0
		;取得せず終わる
		PRINTL 特に興味も湧かなかったので適切に処理する様に伝えて立ち去る事にした。
		RANDOM_CREDIT = RAND:501 + 250
		PRINTFORMW 戦利品は{RANDOM_CREDIT}$になった。
		MONEY += RANDOM_CREDIT

		;リーダーと(居るなら)クルーを消す
		; より大きいIDを持つ方のキャラから消去する
		IF MESUGAKI_LEADER_ID < MESUGAKI_CREW_ID
			CALL DELETE_CHARA(MESUGAKI_CREW_ID)
			CALL DELETE_CHARA(MESUGAKI_LEADER_ID)
		ELSE
			CALL DELETE_CHARA(MESUGAKI_LEADER_ID)
			SIF MESUGAKI_CREW_ID != -1
				CALL DELETE_CHARA(MESUGAKI_CREW_ID)
		ENDIF
	ELSEIF RESULT == 1
		GOTO MESUGAKI_CHOICE_LOOP
	ELSE
		GOTO MESUGAKI_CHOICE_END_LOOP
	ENDIF
ELSE
	GOTO MESUGAKI_CHOICE_LOOP
ENDIF

;--------------------------
;メスガキ確認用キャラ情報表示(名前だけ変えて汎用関数に入れても?)
;--------------------------
@MESUGAKI_CHOICE_INFO(CHOICE_ID)
#DIM CHOICE_ID

FLAG:能力表示モード = 0
CALL SINGLE_DRAWLINE
CALL SHOW_INFO(CHOICE_ID)
CALL SINGLE_DRAWLINE

;--------------------------
;メスガキ確認・取得処理選択肢(+取得処理呼び出し)
;--------------------------
@MESUGAKI_CHOICE_SUB(CHOICE_ID, JOIN_TYPE)
#DIM CHOICE_ID
#DIM JOIN_TYPE

$MESUGAKI_CHOICE_SUB_LOOP
CALL MESUGAKI_CHOICE_INFO(CHOICE_ID)

IF JOIN_TYPE == 1
	PRINTBUTTON "[0] 仲間に迎える",0
ELSE
	PRINTBUTTON "[0] 自分の物にする",0
ENDIF
PRINTS "     "
PRINTBUTTON "[1] 考え直す",1
INPUT

IF RESULT == 0
	IF JOIN_TYPE == 1
		CALL CHANGE_COUNTRY(CHOICE_ID, CFLAG:MASTER:所属, 1)
	ELSE
		CALL CAPTURE(CHOICE_ID, CFLAG:MASTER:所属)
	ENDIF
	RETURN 1
ELSEIF RESULT == 1
	RETURN 0
ELSE
	GOTO MESUGAKI_CHOICE_SUB_LOOP
ENDIF
	