;--------------------------
; !!収容所強襲と 38を好きな番号に変える！!
; 収容所強襲
; ここにクエストの説明
;--------------------------
;--------------------------
; クエスト受注可能か
;--------------------------
@QUEST_ACCEPTABLE_38
SIF QUEST_COMPLETE_DATE:38 + 50 > DAY
	RETURN 0
RETURN QUEST_PROGRESS:34 >= 3

;--------------------------
; クエストの名前を返却
;--------------------------
@QUEST_NAME_38
SETCOLOR 0X00FF00
RESULTS = 収容所強襲
RETURN

;--------------------------
; クエスト本体
;--------------------------
@QUEST_BODY_38
#DIM SELECTED, MAX_CHARA_NUM
#DIM FIRST_LINE
#DIM SELECTED_COUNT
PRINTFORML 各地の政治犯収容所には協賛してくれる仲間がたくさんいるだろう
PRINTFORMW 多少の危険は承知で彼らの解放作戦を行うべきかもしれない
PRINTL
PRINTFORML しかしアイザック達の抱える傭兵は隠密作戦には向かない
PRINTFORMW %CALLNAME:MASTER%の抱える私兵のみでミッションを行うべきだ
PRINTL
DRAWLINE


FIRST_LINE = LINECOUNT
PRINTL メンバーを選択してください(最大6名)
SETCOLOR 0X666666
CUSTOMDRAWLINE =
RESETCOLOR
$INPUT_LOOP
SELECTED_COUNT = 0
FOR LOCAL, 0, CHARANUM
	SIF CFLAG:LOCAL:所属 != CFLAG:MASTER:所属
		CONTINUE
	SIF SELECTED:LOCAL
		SELECTED_COUNT ++
NEXT
FOR LOCAL, 0, CHARANUM
	SIF CFLAG:LOCAL:所属 != CFLAG:MASTER:所属
		CONTINUE
	IF SELECTED:LOCAL
		SETCOLOR 0X00FF00
		PRINTBUTTON @"[{LOCAL}] %CALLNAME:LOCAL% - 白兵: {ABL:LOCAL:白兵}", LOCAL
		PRINTL
	ELSEIF !SELECTED:LOCAL && SELECTED_COUNT < 6
		PRINTBUTTON @"[{LOCAL}] %CALLNAME:LOCAL% - 白兵: {ABL:LOCAL:白兵}", LOCAL
		PRINTL
	ELSE
		PRINTFORML [{LOCAL}] %CALLNAME:LOCAL% - 白兵: {ABL:LOCAL:白兵}
	ENDIF
	RESETCOLOR
NEXT
SETCOLOR 0X666666
CUSTOMDRAWLINE =
RESETCOLOR
FINDELEMENT SELECTED, 1
IF RESULT == -1
	PRINT [決定]
ELSE
	PRINTBUTTON "[決定]", CHARANUM
ENDIF
PRINT 　
PRINTBUTTON "[キャンセル]", CHARANUM + 1
INPUT
IF RESULT == CHARANUM
	CALL CAMP_ASSULT_38(SELECTED)
	RETURN
ENDIF
SIF RESULT == CHARANUM + 1
	RETURN
IF RESULT < 0 || RESULT >= CHARANUM || CFLAG:RESULT:所属 != CFLAG:MASTER:所属
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO INPUT_LOOP
ENDIF
IF SELECTED:RESULT
	SELECTED:RESULT = 0
ELSEIF SELECTED_COUNT < 6
	SELECTED:RESULT = 1
ENDIF
CLEARLINE LINECOUNT - FIRST_LINE
GOTO INPUT_LOOP


@CAMP_ASSULT_38(SELECTED)
#DIM REF SELECTED, 0

PRINTFORML 気休め程度の光学迷彩搭載の歩兵装備で身を固めた%CALLNAME:MASTER%達は、HMDのセンサーで収容所の穴を探す
PRINTFORML 周囲を電流有刺鉄線で囲われた収容所は、入り口以外穴がなさそうだ
PRINTFORMW ご丁寧にも、ステルス航空機による降下作戦の対策に、上空にもセンサーが張り巡らされているのだ
PRINTL
PRINTFORML 入り口には、6人の兵士が哨戒している
PRINTFORML この兵士たちを倒すのが一番の近道のようだ
PRINTFORMW そう結論付けた%CALLNAME:MASTER%は通信でブリーフィングをした後、静かに基地へと近づいて行った
PRINTW ………………
PRINTW …………
PRINTW ……

;白兵戦参加メンバーの初期化
CALL CC_INIT_MEMBER()
;攻撃側キャラクターの設定(最大6名)
FOR LOCAL, 0, CHARANUM
	SIF SELECTED:LOCAL
		CALL CC_ADDCHARA("ATTACKER", LOCAL)
NEXT
CALL CC_ADDMOB("DEFENDER", "警備兵1", 2000, 2000, 55)
CALL CC_ADDMOB("DEFENDER", "警備兵2", 2000, 2000, 55)
CALL CC_ADDMOB("DEFENDER", "警備兵3", 2000, 2000, 55)
CALL CC_ADDMOB("DEFENDER", "警備兵4", 2000, 2000, 60)
CALL CC_ADDMOB("DEFENDER", "警備兵5", 2000, 2000, 60)
CALL CC_ADDMOB("DEFENDER", "分隊長", 2000, 2000, 70)
;白兵戦イベント呼出
CALL CLOSE_COMBAT()
SIF RESULT == 2
	CALL GAMEOVER_38

PRINTFORML 作戦成功だ
PRINTFORMW 静かに敵兵を片付けた%CALLNAME:MASTER%達は、次にセキュリティルームに突入し警備部隊の排除に当たった
PRINTW ………………
PRINTW …………
PRINTW ……
;白兵戦参加メンバーの初期化
CALL CC_INIT_MEMBER()
;攻撃側キャラクターの設定(最大6名)
FOR LOCAL, 0, CHARANUM
	SIF SELECTED:LOCAL
		CALL CC_ADDCHARA("ATTACKER", LOCAL)
NEXT
CALL CC_ADDMOB("DEFENDER", "通信兵1", 2000, 2000, 40)
CALL CC_ADDMOB("DEFENDER", "通信兵2", 2000, 2000, 40)
CALL CC_ADDMOB("DEFENDER", "衛兵3", 2000, 2000, 45)
CALL CC_ADDMOB("DEFENDER", "衛兵4", 2000, 2000, 45)
CALL CC_ADDMOB("DEFENDER", "警備兵1", 2000, 2000, 60)
CALL CC_ADDMOB("DEFENDER", "指揮官", 2000, 2000, 70)
;白兵戦イベント呼出
CALL CLOSE_COMBAT()
SIF RESULT == 2
	CALL GAMEOVER_38
PRINTFORML 作戦成功だ
PRINTFORMW セキュリティルームを制圧した%CALLNAME:MASTER%達は、全てのセキュリティをオフにし、密かに収容者達を救助することに成功した
PRINTL
SETCOLOR 0X666666
CUSTOMDRAWLINE =
RESETCOLOR
PRINTFORML 支持率が1\%上がった
PRINTFORML 戦闘員が200人加わった
PRINTFORML 非戦闘員が300人加わった
PRINTFORMW 研究者が1人加わった
SETCOLOR 0X666666
CUSTOMDRAWLINE =
RESETCOLOR
R支持率 += 1
SIF R支持率 > 100
	R支持率 = 100
SIF R支持率 + G支持率 > 100
	G支持率 = 100 - R支持率
R戦闘員 += 200
R非戦闘員 += 300
R研究者 += 1
G戦闘員 -= 12
SIF G戦闘員 < 0
	G戦闘員 = 0

QUEST_COMPLETE_DATE:38 = DAY

@GAMEOVER_38
PRINTFORML 作戦失敗
PRINTFORML %CALLNAME:MASTER%達はその場で銃殺された
PRINTFORML GAME OVER
$INPUT_LOOP
INPUT
CLEARLINE 1
GOTO INPUT_LOOP
