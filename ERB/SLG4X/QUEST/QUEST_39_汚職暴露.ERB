;--------------------------
; !!汚職暴露と 39を好きな番号に変える！!
; 汚職暴露
; ここにクエストの説明
;--------------------------


;--------------------------
; クエスト受注可能か
;--------------------------
@QUEST_ACCEPTABLE_39
SIF QUEST_COMPLETE_DATE:39 + 100 > DAY
	RETURN 0
CALL GET_STAR_ID_FROM_POS(CHARACTER_POSITION)
SIF RESULT != 2
	RETURN 0
RETURN QUEST_PROGRESS:34 >= 3
;--------------------------
; クエストの名前を返却
;--------------------------
@QUEST_NAME_39
SETCOLOR 0X00FF00
RESULTS = 汚職暴露
RETURN


;--------------------------
; クエスト本体
;--------------------------
@QUEST_BODY_39
#DIM SELECTED, MAX_CHARA_NUM
#DIM FIRST_LINE
#DIM SELECTED_COUNT

PRINTFORML 汚職を暴露することで体制側の支持率を下げる作戦を思いついた%CALLNAME:MASTER%
PRINTFORML 汚職で有名な政治家の目星はついている
PRINTFORMW その秘書官を誘拐し尋問することで、汚職の証拠を入手し暴露しよう
PRINTL
PRINTFORML 誘拐作戦に参加するメンバーは少数精鋭がいいだろう
PRINTFORMW さて、誰にするか
PRINTW ………………
PRINTW …………
PRINTW ……
DRAWLINE
FIRST_LINE = LINECOUNT
PRINTL メンバーを選択してください(最大6名)
SETCOLOR 0X666666
CUSTOMDRAWLINE =
RESETCOLOR
$INPUT_LOOP
SELECTED_COUNT = 0
FOR LOCAL, 0, CHARANUM
	SIF CFLAG:LOCAL:所属 != CFLAG:MASTER:所属
		CONTINUE
	SIF SELECTED:LOCAL
		SELECTED_COUNT ++
NEXT
FOR LOCAL, 0, CHARANUM
	SIF CFLAG:LOCAL:所属 != CFLAG:MASTER:所属
		CONTINUE
	IF SELECTED:LOCAL
		SETCOLOR 0X00FF00
		PRINTBUTTON @"[{LOCAL}] %CALLNAME:LOCAL% - 白兵: {ABL:LOCAL:白兵}", LOCAL
		PRINTL
	ELSEIF !SELECTED:LOCAL && SELECTED_COUNT < 6
		PRINTBUTTON @"[{LOCAL}] %CALLNAME:LOCAL% - 白兵: {ABL:LOCAL:白兵}", LOCAL
		PRINTL
	ELSE
		PRINTFORML [{LOCAL}] %CALLNAME:LOCAL% - 白兵: {ABL:LOCAL:白兵}
	ENDIF
	RESETCOLOR
NEXT
SETCOLOR 0X666666
CUSTOMDRAWLINE =
RESETCOLOR
FINDELEMENT SELECTED, 1
IF RESULT == -1
	PRINT [決定]
ELSE
	PRINTBUTTON "[決定]", CHARANUM
ENDIF
PRINT 　
PRINTBUTTON "[キャンセル]", CHARANUM + 1
INPUT
IF RESULT == CHARANUM
	CALL KIDNAPPING_39(SELECTED)
	RETURN
ENDIF
SIF RESULT == CHARANUM + 1
	RETURN
IF RESULT < 0 || RESULT >= CHARANUM || CFLAG:RESULT:所属 != CFLAG:MASTER:所属
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO INPUT_LOOP
ENDIF
IF SELECTED:RESULT
	SELECTED:RESULT = 0
ELSEIF SELECTED_COUNT < 6
	SELECTED:RESULT = 1
ENDIF
CLEARLINE LINECOUNT - FIRST_LINE
GOTO INPUT_LOOP

@KIDNAPPING_39(SELECTED)
#DIM REF SELECTED, 0

PRINTFORML 秘書官が自宅である警備員付きの立派な集合住宅に帰る夜半
PRINTFORMW 護衛を兼任する運転手が家の前に車をつけたタイミングで、%CALLNAME:MASTER%達は突撃する算段をとっていた
PRINTL 
PRINTFORML そして定刻
PRINTFORML 何も知らず、いつも通り秘書官が帰宅してきた
PRINTFORMW 光学迷彩で周囲に隠れていた%CALLNAME:MASTER%達は、秘書官に向け静かに突撃した
PRINTW ………………
PRINTW …………
PRINTW ……

;白兵戦参加メンバーの初期化
CALL CC_INIT_MEMBER()
;攻撃側キャラクターの設定(最大6名)
FOR LOCAL, 0, CHARANUM
	SIF SELECTED:LOCAL
		CALL CC_ADDCHARA("ATTACKER", LOCAL)
NEXT
CALL CC_ADDMOB("DEFENDER", "運転手", 1200, 1200, 15)
CALL CC_ADDMOB("DEFENDER", "警備員", 1000, 1000, 10)
;白兵戦イベント呼出
CALL CLOSE_COMBAT()
SIF RESULT == 2
	CALL GAMEOVER_38
CALL CC_ADD_COMBAT_EXP(5)
PRINTFORML 運転手と警備員を無力した%CALLNAME:MASTER%は、腰が抜けて動けないでいる秘書官を薬で昏睡させ、かついでその場を後にした
PRINTFORML その後、悲惨な目にあった秘書官は秘密を洗いざらい吐き、それをもとに証拠を集めて政治家の汚職を暴露した
PRINTFORMW 政治家自身はすぐに蜥蜴の尻尾切りにあったが、確実に政権への支持率は下がっただろう
PRINTL
PRINTFORMW 銀河人民統一連邦への支持率 -5\%
GB_支持率:2 -= 5
SIF GB_支持率:2 < 0
	GB_支持率:2 = 0
QUEST_COMPLETE_DATE:39 = DAY