;--------------------------
; !!商船護衛と 22を好きな番号に変える！!
; 商船護衛
; ここにクエストの説明
;--------------------------


;--------------------------
; クエスト受注可能か
;--------------------------
@QUEST_ACCEPTABLE_22
CALL GET_STAR_ID_FROM_POS(CHARACTER_POSITION)
SIF STAR_OWNER:RESULT != 国家ID_企業連合
  RETURN 0
SIF QUEST_COMPLETE_DATE:22 + 30 > DAY && QUEST_PROGRESS:22 == 0
  RETURN 0
RETURN 1

;--------------------------
; クエストの名前を返却
;--------------------------
@QUEST_NAME_22
RESULTS = 商船護衛
RETURN


;--------------------------
; クエスト本体
;--------------------------
@QUEST_BODY_22
#DIM ITER
#DIM SHIP_ID
#DIM DESTROYED
ITER = 0
DESTROYED = 0

FOR SHIP_ID, 0, MAX_SHIP
  SIF SHIP_COUNTRY:SHIP_ID == 国家ID_プレイヤー && SHIP_CATEGORY:SHIP_ID == コルベット && SHIP_POSITION:SHIP_ID == CHARACTER_POSITION
    ITER ++
NEXT

IF QUEST_PROGRESS:22 == 0
  PRINTFORML 企業連合の評議会所属企業が護衛艦を募集している
  PRINTFORML コルベットを3隻募集しているいるようだ
  IF ITER >= 3
    PRINTFORML 応募しますか?
    CALL ASK_YN
    IF RESULT == 0
      CALL SELECT_RENTAL_CORVETTE(3)
      PRINTL 指定したコルベットを貸し出すことにした
      PRINTW 30ターン後に戻ってくるようだ
      QUEST_PROGRESS:22 = 1
      QUEST_COMPLETE_DATE:22 = DAY + 30
      RETURN 1
    ELSE
      PRINTW やっぱりやめることにした
      RETURN 0
    ENDIF
  ELSE
    PRINTW 貸し出せるコルベットがここにいない
    RETURN 0
  ENDIF
ELSE
  IF QUEST_COMPLETE_DATE:22 <= DAY
    IF SHIP_COUNTRY:(RENTAL_SHIP:0) != 国家ID_企業連合 || SHIP_CATEGORY:(RENTAL_SHIP:0) != コルベット
      DESTROYED ++
    ELSE
      SHIP_COUNTRY:(RENTAL_SHIP:0) = 国家ID_プレイヤー
    ENDIF
    IF SHIP_COUNTRY:(RENTAL_SHIP:1) != 国家ID_企業連合 || SHIP_CATEGORY:(RENTAL_SHIP:1) != コルベット
      DESTROYED ++
    ELSE
      SHIP_COUNTRY:(RENTAL_SHIP:1) = 国家ID_プレイヤー
    ENDIF
    IF SHIP_COUNTRY:(RENTAL_SHIP:2) != 国家ID_企業連合 || SHIP_CATEGORY:(RENTAL_SHIP:2) != コルベット
      DESTROYED ++
    ELSE
      SHIP_COUNTRY:(RENTAL_SHIP:2) = 国家ID_プレイヤー
    ENDIF
    PRINTL ミッションが完了した
    IF DESTROYED > 0
      PRINTFORML しかし貸し出したうち、{DESTROYED}隻のコルベットが撃破されてしまったようだ...
    ELSE
      PRINTFORML 貸し出したコルベットが戻ってきた
      PRINTL 無事ミッションをこなせたようだ
      PRINTW また、評議会所属の企業の依頼をこなしたことで企業連合からの好感度が上がったようだ
    ENDIF
    MONEY += 100000
    REL_LIKE_COUNTRY:国家ID_企業連合:国家ID_プレイヤー = LIMIT(REL_LIKE_COUNTRY:国家ID_企業連合:国家ID_プレイヤー + 10 , -1000, 1000)
    QUEST_PROGRESS:22 = 0
  ELSE
    PRINTL まだコルベットの返却日は先だ
    PRINTW 気長に待つことにしよう
  ENDIF
ENDIF

@SELECT_RENTAL_CORVETTE(NUM)
#DIM NUM
#DIM SHIP_ID
VARSET RENTAL_SHIP, -1

$INPUT_LOOP
FOR SHIP_ID, 0, MAX_SHIP
  SIF SHIP_COUNTRY:SHIP_ID != 国家ID_プレイヤー || SHIP_CATEGORY:SHIP_ID != コルベット || SHIP_POSITION:SHIP_ID != CHARACTER_POSITION
    CONTINUE
  IF RENTAL_SHIP:0 == SHIP_ID || RENTAL_SHIP:1 == SHIP_ID || RENTAL_SHIP:2 == SHIP_ID
    SETCOLOR 0XFFFFFF
  ELSE
    SETCOLOR 0X666666
  ENDIF
  PRINTBUTTON @"[{SHIP_ID}] %SHIP_NAME:SHIP_ID%", SHIP_ID
  PRINTL 
  RESETCOLOR
NEXT
FINDELEMENT RENTAL_SHIP, -1
IF RESULT == -1
  PRINTBUTTON "[9999]決定", 9999
ELSE
  SETCOLOR 0X666666
  PRINT [9999]決定
ENDIF
INPUT
IF RESULT >= 0 && RESULT < MAX_SHIP
  IF SHIP_COUNTRY:RESULT == 国家ID_プレイヤー && SHIP_CATEGORY:RESULT == コルベット && SHIP_POSITION:RESULT == CHARACTER_POSITION
    IF RENTAL_SHIP:0 != RESULT && RENTAL_SHIP:1 != RESULT && RENTAL_SHIP:2 != RESULT
      IF RENTAL_SHIP:0 == -1
        RENTAL_SHIP:0 = RESULT
      ELSEIF RENTAL_SHIP:1 == -1
        RENTAL_SHIP:1 = RESULT
      ELSEIF RENTAL_SHIP:2 == -1
        RENTAL_SHIP:2 = RESULT
      ENDIF
    ELSE
      IF RENTAL_SHIP:0 == RESULT
        RENTAL_SHIP:0 = -1
      ELSEIF RENTAL_SHIP:1 == RESULT
        RENTAL_SHIP:1 = -1
      ELSEIF RENTAL_SHIP:2 == RESULT
        RENTAL_SHIP:2 = -1
      ENDIF
    ENDIF
  ENDIF
ELSEIF RESULT == 9999 && RENTAL_SHIP:0 != -1 && RENTAL_SHIP:1 != -1 && RENTAL_SHIP:2 != -1
  SHIP_COUNTRY:(RENTAL_SHIP:0) = 国家ID_企業連合
  SHIP_COUNTRY:(RENTAL_SHIP:1) = 国家ID_企業連合
  SHIP_COUNTRY:(RENTAL_SHIP:2) = 国家ID_企業連合
  RETURN
ENDIF
GOTO INPUT_LOOP
