

;--------------------------
; クエスト受注可能か
;--------------------------
@QUEST_ACCEPTABLE_82
RETURN 1

;--------------------------
; クエストの名前を返却
;--------------------------
@QUEST_NAME_82
RESULTS = 宇宙怪獣の群れ討伐依頼
RETURN

;--------------------------
; クエスト本体
;--------------------------
@QUEST_BODY_82
#DIM SECTOR_ID
#DIM IS_COMPLETE
#DIM SHIP_ID
#DIM COUNTRY_ID
#DIM POSID

IF QUEST_PROGRESS:82 == 0
	PRINTFORML 「お前、戦闘艦を持ってるか？」
	PRINTFORML %CALLNAME:MASTER%は軍人らしき男に話しかけられた
	$INPUT_LOOP
	PRINTBUTTON "[0] はい", 0
	PRINTBUTTON "[1] いいえ", 1
	INPUT
	IF RESULT == 0
		PRINTFORML 「ちょうどいい。もしお前が自分の艦隊に自信があるなら、このセクターに沸いた宇宙怪獣の群れを撃滅して欲しい
		PRINTFORMW 討伐してくれたら、$100,000,000と、この国における名声を贈ろう
		PRINTFORMW 本当に強い連中だから、戦う前に戦力を調査しておいたほうがいい。もし無理なら、諦めてくれて構わない。本当なら正規軍が出ばる相手だからな」
		PRINTFORMW %CALLNAME:MASTER%は軍人らしき男に宇宙怪獣の群れ討伐のミッションを依頼された
		QUEST_PROGRESS:82 = 1
		CALL ID2POS(CHARACTER_POSITION)
		SECTOR_ID = RESULT:0
		CALL POS2ID(SECTOR_ID, 10, 10)
		POSID = RESULT
		FOR SHIP_ID, 0, 30
			CALL INIT_PRESET_SHIP(10, POSID, 宇宙ししゃも, "宇宙ししゃも", 宇宙ししゃも)
		NEXT
		FOR SHIP_ID, 0, 20
			CALL INIT_PRESET_SHIP(10, POSID,宇宙巨大スライム, "宇宙巨大スライム", 宇宙巨大スライム)
		NEXT
		FOR SHIP_ID, 0, 10
			CALL INIT_PRESET_SHIP(10, POSID, 宇宙たこ, "宇宙たこ", 宇宙たこ)
		NEXT
		FOR SHIP_ID, 0, 5
			CALL INIT_PRESET_SHIP(10, POSID, 宇宙いか, "宇宙いか", 宇宙いか)
		NEXT
		FOR SHIP_ID, 0, 2
			CALL INIT_PRESET_SHIP(10, POSID, 宇宙かに, "宇宙かに", 宇宙かに)
		NEXT
		FOR SHIP_ID, 0, 1
			CALL INIT_PRESET_SHIP(10, POSID, 宇宙ひらめ, "宇宙ひらめ", 宇宙ひらめ)
		NEXT
	ELSEIF RESULT == 1
		PRINTFORMW 「そうか、なら聞かなかったことにしてくれ」
	ELSE
		GOTO INPUT_LOOP
	ENDIF
ELSEIF QUEST_PROGRESS:82 == 1
	IS_COMPLETE = 1
	FOR SHIP_ID, 0, MAX_SHIP
		SIF SHIP_COUNTRY:SHIP_ID != 10
			CONTINUE
		CALL ID2POS(CHARACTER_POSITION)
		SECTOR_ID = RESULT:0
		CALL ID2POS(SHIP_POSITION:SHIP_ID)
		SIF SECTOR_ID != RESULT:0
			CONTINUE
			IS_COMPLETE = 0
	NEXT
	IF IS_COMPLETE == 0
		PRINTFORMW 「頼んだぞ。無理なら正規軍が動くまで待っててくれ」
	ELSE
		PRINTFORML 「宇宙怪獣の群れを討伐したんだな！素晴らしい指揮と軍隊だ。
		PRINTFORML これが約束の報酬だ。君の名声はこの国に広く知れ渡るだろう」
		PRINTFORMW 男は%CALLNAME:MASTER%に報酬の$100,000,000を手渡すと、恭しく敬礼して見せた
		MONEY += 100000000
		FOR COUNTRY_ID, 0, 星系数
			IF CHARACTER_POSITION == 星系座標ID:COUNTRY_ID
				REL_LIKE_COUNTRY:(STAR_OWNER:COUNTRY_ID):国家ID_プレイヤー += 100
				BREAK;
			ENDIF
		NEXT
		QUEST_PROGRESS:82 = 0
	ENDIF
ENDIF

