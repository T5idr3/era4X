;--------------------------
; クエストリスト
;--------------------------
@QUEST_LIST(LEFT_COLUMNS, LEFT_COLUMNS_BUTTON_VALUE)
#DIMS REF LEFT_COLUMNS
#DIM REF LEFT_COLUMNS_BUTTON_VALUE
#DIM ITER
#DIM LINE_ITER
#DIM LEFT_COLUMN_SIZE

$INPUT_LOOP

FINDELEMENT LEFT_COLUMNS_BUTTON_VALUE, NONE_LINE
LEFT_COLUMN_SIZE = RESULT

LINE_ITER = 0
CALL PRINT_LEFT_COLUMN(LEFT_COLUMNS, LEFT_COLUMNS_BUTTON_VALUE, LINE_ITER)
PRINTL 　クエスト
LINE_ITER ++

CALL PRINT_LEFT_COLUMN(LEFT_COLUMNS, LEFT_COLUMNS_BUTTON_VALUE, LINE_ITER)
CALL DRAW_RIGHT_COLUMN_GRAY_LINE
LINE_ITER ++

FOR ITER, 0, QUEST_LENGTH
	RESULT = 0
	TRYCALLFORM QUEST_ACCEPTABLE_{ITER}
	IF RESULT == 1
		CALL PRINT_LEFT_COLUMN(LEFT_COLUMNS, LEFT_COLUMNS_BUTTON_VALUE, LINE_ITER)
		RESULTS = 
		TRYCALLFORM QUEST_NAME_{ITER}
		PRINTBUTTON @"　%RESULTS%", ITER +  100
		PRINTL
		LINE_ITER ++
	ENDIF
NEXT

; 左カラムが右絡むより長かった場合の処理
FOR LOCAL, LINE_ITER, LEFT_COLUMN_SIZE
	CALL PRINT_LEFT_COLUMN(LEFT_COLUMNS, LEFT_COLUMNS_BUTTON_VALUE, LINE_ITER)
	PRINTL
	LINE_ITER ++
NEXT
PRINTFORM %"", LEFT_COLUMN_LENGTH, LEFT%
CALL DRAW_RIGHT_COLUMN_GRAY_LINE

INPUT
;RESULTの値をLOCAL:0に退避
LOCAL:0 = RESULT

IF LOCAL:0 >= 0 && LOCAL:0 < LEFT_COLUMN_SIZE
	RETURN LOCAL:0
ENDIF

LOCAL:0 -= 100
IF LOCAL:0 >= 0 && LOCAL:0 < QUEST_LENGTH
	TRYCALLFORM QUEST_DESCRIPTION_{LOCAL:0}
	PRINTFORML このクエストを受領しますか？
	CALL ASK_YN
	IF RESULT == 0 
		PRINTL
		DRAWLINE
		TRYCALLFORM QUEST_BODY_{LOCAL:0}
	ENDIF
	PRINTL
	DRAWLINE
ENDIF

GOTO INPUT_LOOP