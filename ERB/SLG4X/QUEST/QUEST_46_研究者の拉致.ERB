;--------------------------
; !!研究者の拉致と 46を好きな番号に変える！!
; 研究者の拉致
; ここにクエストの説明
;--------------------------


;--------------------------
; クエスト受注可能か
;--------------------------
@QUEST_ACCEPTABLE_46
RETURN QUEST_PROGRESS:46 == 0 && QUEST_PROGRESS:34 >= 3

;--------------------------
; クエストの名前を返却
;--------------------------
@QUEST_NAME_46
SETCOLOR 0X00FF00
RESULTS = 研究者の拉致
RETURN


;--------------------------
; マップでの表示を設定する
; @QUEST_SET_TILE_  : マップ上対象地点にマークを出す。
; @QUEST_MAP_INFO_  : 右下小窓にメッセージを出す
; 引数１： ARG:0 SS46YY　座標データ　マップ上でクリックされた座標。表示条件分岐に。
;--------------------------
;;;マップ画面上での表示。 隠したかったら関数コメントアウトか RETURN 0
;;;色を替えたかったらSETCOLORしておけば後の処理で色反映します
@QUEST_SET_TILE_46 (ARG:0 )
	RESULTS '= "？"
	SETCOLOR 0xFFFF00
RETURN 1

;;;右下小窓に出す。隠したければ関数コメントアウトか RETURN 0
@QUEST_MAP_INFO_46 (ARG:0 )
		RESULTS:0 '= ""
		RESULTS:1 '= "クエスト対象エリア"
		RESULTS:2 '= ""
		RESULTS:3 '= ""
		RESULTS:4 '= ""
RETURN 1

;-----------------------
; マップ上からQUEST_BODYを実行する処理
; @QUEST_SHOW_COMMAND_x : ここで指定した文字列のコマンドを現地で表示する
; 　　　　　　　　　　 ：(調査する||捜索する||採集する||待ち伏せする||挑戦する)といったフレーバー
; @QUEST_PLACE_CHECK_x ： 設定マスを踏んだら呼び出される。不要ならば消すか無処理で返す
;--------------------------
@QUEST_SHOW_COMMAND_46 ()
	RESULTS '= "クエストを行う"
RETURN 1

@QUEST_REACH_PLACE_46 ()
; CALL QUEST_BODY_46
RETURN

;--------------------------
; クエスト本体
;--------------------------
@QUEST_BODY_46
#DIM SELECTED, MAX_CHARA_NUM
#DIM FIRST_LINE
#DIM SELECTED_COUNT

PRINTFORML 銀河人民統一連邦の研究者を拉致し、我方の研究に従事させる作戦を考案した%CALLNAME:MASTER%
PRINTFORML 輸送船で研究所に乗り付け、警備を排除、研究者を拉致する作戦だ
LOCAL:1 = 0
FOR LOCAL, 0, MAX_SHIP
	SIF SHIP_COUNTRY:LOCAL == 国家ID_プレイヤー && SHIP_CATEGORY:LOCAL == 輸送船 && SHIP_POSITION:LOCAL == CHARACTER_POSITION
		LOCAL:1 ++
NEXT
IF !LOCAL:1
	PRINTFORML しかしこの宙域に輸送船がいない。輸送船をこの宙域に用意しよう
	RETURN
ENDIF
PRINTFORML うまく行けば研究者を大量に入手することができるが、ミッションの難易度は高いだろう
PRINTFORML 実施しますか？
CALL ASK_YN
IF RESULT
	PRINTFORML いや、難易度が高すぎる。今回は見送ろう……
	RETURN
ENDIF
DRAWLINE


FIRST_LINE = LINECOUNT
PRINTL メンバーを選択してください(最大6名)
SETCOLOR 0X666666
CUSTOMDRAWLINE =
RESETCOLOR
$INPUT_LOOP
SELECTED_COUNT = 0
FOR LOCAL, 0, CHARANUM
	SIF CFLAG:LOCAL:所属 != CFLAG:MASTER:所属
		CONTINUE
	SIF SELECTED:LOCAL
		SELECTED_COUNT ++
NEXT
FOR LOCAL, 0, CHARANUM
	SIF CFLAG:LOCAL:所属 != CFLAG:MASTER:所属
		CONTINUE
	IF SELECTED:LOCAL
		SETCOLOR 0X00FF00
		PRINTBUTTON @"[{LOCAL}] %CALLNAME:LOCAL% - 白兵: {ABL:LOCAL:白兵}", LOCAL
		PRINTL
	ELSEIF !SELECTED:LOCAL && SELECTED_COUNT < 6
		PRINTBUTTON @"[{LOCAL}] %CALLNAME:LOCAL% - 白兵: {ABL:LOCAL:白兵}", LOCAL
		PRINTL
	ELSE
		PRINTFORML [{LOCAL}] %CALLNAME:LOCAL% - 白兵: {ABL:LOCAL:白兵}
	ENDIF
	RESETCOLOR
NEXT
SETCOLOR 0X666666
CUSTOMDRAWLINE =
RESETCOLOR
FINDELEMENT SELECTED, 1
IF RESULT == -1
	PRINT [決定]
ELSE
	PRINTBUTTON "[決定]", CHARANUM
ENDIF
PRINT 　
PRINTBUTTON "[キャンセル]", CHARANUM + 1
INPUT
IF RESULT == CHARANUM
	CALL LAB_ASSULT_46(SELECTED)
	RETURN
ENDIF
SIF RESULT == CHARANUM + 1
	RETURN
IF RESULT < 0 || RESULT >= CHARANUM || CFLAG:RESULT:所属 != CFLAG:MASTER:所属
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO INPUT_LOOP
ENDIF
IF SELECTED:RESULT
	SELECTED:RESULT = 0
ELSEIF SELECTED_COUNT < 6
	SELECTED:RESULT = 1
ENDIF
CLEARLINE LINECOUNT - FIRST_LINE
GOTO INPUT_LOOP

@LAB_ASSULT_46(SELECTED)
#DIM REF SELECTED, 0

PRINTFORMW 輸送船で乗り付けた%CALLNAME:MASTER%達は早速警備兵のお出迎えにあった

;白兵戦参加メンバーの初期化
CALL CC_INIT()
;攻撃側キャラクターの設定(最大6名)
FOR LOCAL, 0, CHARANUM
	SIF SELECTED:LOCAL
		CALL CC_ADDCHARA("ATTACKER", LOCAL)
NEXT
CALL CC_ADDMOB("DEFENDER", "警備兵1", 1000, 1000, 45)
CALL CC_ADDMOB("DEFENDER", "警備兵2", 1000, 1000, 45)
CALL CC_ADDMOB("DEFENDER", "警備兵3", 1000, 1000, 45)
CALL CC_ADDMOB("DEFENDER", "警備兵4", 1200, 1200, 50)
CALL CC_ADDMOB("DEFENDER", "警備兵5", 1200, 1200, 50)
CALL CC_ADDMOB("DEFENDER", "分隊長", 1800, 1800, 60)
;連戦フラグオン
CALL CC_SET_FLAG(LINE_CHAN, "1")
;白兵戦イベント呼出
CALL CLOSE_COMBAT()
SIF RESULT == 2
	CALL GAMEOVER_46
CALL CC_ADD_COMBAT_EXP(15)

PRINTFORMW 第一波を片付けたところで、第二波がやってきた！
PRINTW ………………
PRINTW …………
PRINTW ……
;白兵戦参加メンバーの初期化
CALL CC_INIT()
CALL CC_ADDMOB("DEFENDER", "警備兵1", 1000, 1000, 45)
CALL CC_ADDMOB("DEFENDER", "警備兵2", 1000, 1000, 45)
CALL CC_ADDMOB("DEFENDER", "警備兵3", 1000, 1000, 45)
CALL CC_ADDMOB("DEFENDER", "警備兵4", 1200, 1200, 50)
CALL CC_ADDMOB("DEFENDER", "警備兵5", 1200, 1200, 50)
CALL CC_ADDMOB("DEFENDER", "分隊長", 1800, 1800, 60)
;連戦フラグオン
CALL CC_SET_FLAG(LINE_CHAN, "1")
;白兵戦イベント呼出
CALL CLOSE_COMBAT()
SIF RESULT == 2
	CALL GAMEOVER_46
CALL CC_ADD_COMBAT_EXP(15)
PRINTFORML あらかたの警備員は倒した
PRINTFORMW %CALLNAME:MASTER%達は怯える研究員達をまとめて輸送船に連行する
PRINTL
PRINTFORML しかし時間がかかったせいで、近場の警官達が集まってきてしまった……
PRINTFORMW 連戦で疲労している体を奮起し、%CALLNAME:MASTER%達は武器を構えた
;白兵戦参加メンバーの初期化
CALL CC_INIT()
CALL CC_ADDMOB("DEFENDER", "警官1", 1000, 1000, 40)
CALL CC_ADDMOB("DEFENDER", "警官2", 1000, 1000, 40)
CALL CC_ADDMOB("DEFENDER", "警官3", 1000, 1000, 40)
CALL CC_ADDMOB("DEFENDER", "警官4", 1000, 1000, 40)
;先制攻撃フラグオン
CALL CC_SET_FLAG(FIRST_STRIKE, "1")
;白兵戦イベント呼出
CALL CLOSE_COMBAT()
SIF RESULT == 2
	CALL GAMEOVER_46
CALL CC_ADD_COMBAT_EXP(10)
PRINTFORML なんとか警官達を退けることに成功した
PRINTFORMW %CALLNAME:MASTER%達はこれ以上の増援がくる前に、その場を後にした
PRINTW ………………
PRINTW …………
PRINTW ……
PRINTL
SETCOLOR 0X666666
CUSTOMDRAWLINE =
RESETCOLOR
PRINTFORMW 研究者が50人加わった
SETCOLOR 0X666666
CUSTOMDRAWLINE =
RESETCOLOR
R研究者 += 50
QUEST_PROGRESS:46 = 1

@GAMEOVER_46
PRINTFORML 作戦失敗
PRINTFORML %CALLNAME:MASTER%達はその場で銃殺された
PRINTFORML GAME OVER
$INPUT_LOOP
INPUT
CLEARLINE 1
GOTO INPUT_LOOP