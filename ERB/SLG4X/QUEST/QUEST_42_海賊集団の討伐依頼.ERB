;--------------------------
; !!クエスト名と 42を好きな番号に変える！!
; 海賊の討伐
; 近場に沸いた海賊を討伐する
;--------------------------

;--------------------------
; クエスト受注可能か
;--------------------------
@QUEST_ACCEPTABLE_42
SIF QUEST_PROGRESS:42 < 1
	RETURN 0
RETURN 1

;--------------------------
; クエストの名前を返却
;--------------------------
@QUEST_NAME_42
RESULTS = 海賊集団の討伐依頼
RETURN

;--------------------------
; クエスト本体
;--------------------------
@QUEST_BODY_42
#DIM SECTOR_ID
#DIM IS_COMPLETE
#DIM SHIP_ID
#DIM POSID

IF QUEST_PROGRESS:42 == 1
	PRINTFORMW %CALLNAME:MASTER%が軍部を訪ねると、皇国の高級軍人らしき男が現れた。
	PRINTFORML 「ああ、丁度良いところに来てくれた。彼女から聞いたかも知れないが、少し、頼みたいことがあってね」
	PRINTFORML 口にされるのは静流から聞いた通りの内容であった。
	PRINTFORMW リーダーに率いられた海賊集団の被害に悩まされているらしい。
	PRINTFORML
	PRINTFORML 護衛のない輸送船狙いではなく、船団狙いの中規模集団。
	PRINTFORML 軍の識別コードを読まれて逃げられるばかりで、随分と手を焼いているらしい。
	PRINTFORMW 是非に君の力を借りたかったのだと彼は告げる。
	PRINTFORML
	PRINTFORML 「ただ、どうにも敵は船団を襲うためのグループ。普通の海賊より規模が大きい」
	PRINTFORMW 確認されているのはコルベット十隻、フリゲート一隻。中々の相手である。
	PRINTFORML 「危険もあるだろう。戦力に不安があれば断ってくれて構わない」
	PRINTFORMW 報酬は$750,000を出してくれるらしいが……。
	PRINTFORML
	PRINTFORMW 「どうだろう？」
	$INPUT_LOOP
	PRINTBUTTON "[0] はい", 0
	PRINTBUTTON "[1] いいえ", 1
	INPUT
	IF RESULT == 0
		PRINTFORMW 「それは良かった。襲撃地点の座標を送っておく」
		PRINTFORMW 「$750,000の報奨を用意しておこう、武運を祈る」
		PRINTFORMW 海賊集団の討伐任務を引き受けた。
		QUEST_PROGRESS:42 = 2
		CALL ID2POS(CHARACTER_POSITION)
		SECTOR_ID = RESULT:0
		CALL POS2ID(SECTOR_ID, 7, 3)
		POSID = RESULT
		FOR SHIP_ID, 0, 10
			CALL INIT_PRESET_SHIP(10, POSID, 汎用コルベット, "海賊コルベット", 汎用コルベット)
		NEXT
		FOR SHIP_ID, 0, 1
			CALL INIT_PRESET_SHIP(10, POSID, 汎用フリゲート, "海賊フリゲート", 汎用フリゲート)
		NEXT
		
	ELSEIF RESULT == 1
		PRINTFORMW 「まぁ、危険な依頼だ。リスクとリターンを考えるのは当然のこと……君がそういうなら仕方ない」
		PRINTFORMW 男は残念そうに言って嘆息した。
		PRINTFORMW 「元々我々の仕事。仕方ない……色々と考えてみよう」
		PRINTFORML 
		PRINTFORMW 後日、皇国の特務艦隊に海賊集団は壊滅させられたらしい。
		QUEST_PROGRESS:42 = 0
	ELSE
		GOTO INPUT_LOOP
	ENDIF
ELSE
	IS_COMPLETE = 1
	CALL ID2POS(CHARACTER_POSITION)
	SECTOR_ID = RESULT:0
	CALL POS2ID(SECTOR_ID, 7, 3)
	SIF SHIP_POSITION:(QUEST_FLAG_SHIP_ID:42) == RESULT:0 && SHIP_COUNTRY:(QUEST_FLAG_SHIP_ID:42) == 国家ID_一般海賊
		IS_COMPLETE = 0
	IF IS_COMPLETE == 0
		PRINTFORMW 「賊の討伐は任せたぞ」
	ELSE
		PRINTFORMW 「討伐に成功したか。では報酬を送金しておくとしよう」
		PRINTFORMW 「かくして連中も、父祖たちと同じく時の流れに飲み込まれた。敵ながら哀れなものよ」
		PRINTFORMW 海賊集団の討伐に成功したことで、報酬$750,000を手に入れた。
		MONEY += 750000
		MONEY:国家ID_豊秋津皇国 -= 750000
		QUEST_PROGRESS:42 = 0
		REL_LIKE_COUNTRY:国家ID_豊秋津皇国:国家ID_プレイヤー = LIMIT(REL_LIKE_COUNTRY:国家ID_豊秋津皇国:国家ID_プレイヤー + 75, -1000, 1000)
	ENDIF
ENDIF
