;--------------------------
; 遺棄された貨物船
;--------------------------
@INIT_ANOMALY_0
#DIM TASK_COMPLETE
#DIM SECTOR_ID
#DIM POS_X
#DIM POS_Y
; 遺棄された貨物船
WHILE TASK_COMPLETE == 0
	SECTOR_ID = RAND:MAX_SECTOR
	POS_X = RAND:(MAP_WIDTH)
	POS_Y = RAND:(MAP_HEIGHT)
	SIF MAP:SECTOR_ID:POS_X:POS_Y != 明瞭
		CONTINUE
	MAP:SECTOR_ID:POS_X:POS_Y = ANOMALY
	ANOMALY_MAP:SECTOR_ID:POS_X:POS_Y = 遺棄された貨物船
	CALL POS2ID(SECTOR_ID, POS_X, POS_Y)
	ANOMALY_POSITIONID:0 = RESULT
	TASK_COMPLETE = 1
WEND

;--------------------------
; ANOMALY本体
;--------------------------
@ANOMALY_BODY_遺棄された貨物船
#DIM ITER, 2
#DIM SHIP_ID

SELECTCASE ANOMALY_PROGRESS:0
	CASE 0
		PRINTFORML 宙域にポツンと古いタイプの輸送船が漂っている。
		PRINTFORML 何者かに攻撃されたのか、装甲がボロボロだ。
		PRINTFORML 貨物部分に空いた人工的な人型サイズの穴を見るに、接舷されて乗り込み攻撃をされたようだ。
		PRINTFORML だがエンジンとコントロール部分は比較的無傷で、乗り込めばまだ使えるようだ。
		PRINTFORMW 同乗者入りの移乗ポットで乗り込めれば自分のものにできそうだ。
		PRINTL
		ANOMALY_PROGRESS:0 = 1
	CASE 1
		PRINTFORML 自分のものにできそうなボロボロな輸送船が浮かんでいる……
ENDSELECT
FOR ITER, 0, MAX_SLOT
	IF SHIP_EQUIP:オプション:ITER:(BOARDING_SHIP_ID:MASTER) == 1
		RESULT = 1
		BREAK
	ENDIF
NEXT
IF RESULT == 1
	FINDELEMENT RIDING_WITH, BOARDING_SHIP_ID:MASTER
	SIF RESULT == -1
		RETURN
	CALL RIDE_ADANDONMENT_SHIP(ITER)
ENDIF

;--------------------------
; ANOMALY本体
;--------------------------
@RIDE_ADANDONMENT_SHIP(ITER)
#DIM ITER
#DIM SECTOR_ID
#DIM X_POS
#DIM Y_POS

PRINTFORML %CALLNAME:ITER%を移乗コンテナで遺棄船に射出した。
PRINTFORML どうやらうまくいったようだ。
PRINTFORMW %CALLNAME:MASTER%は新しい輸送船を手に入れた。
PRINTFORMW ただしかなりボロボロなので、修理には数万$かかるだろう。

CALL INIT_PRESET_SHIP(国家ID_プレイヤー, ANOMALY_POSITIONID:0, 汎用輸送船, "ぼろぼろの輸送船", 汎用輸送船)
SHIP_DAMAGE:RESULT = 250

ANOMALY_PROGRESS:0 = 2
CALL ID2POS(ANOMALY_POSITIONID:0)
SECTOR_ID = RESULT:0
X_POS = RESULT:1
Y_POS = RESULT:2
ANOMALY_MAP:SECTOR_ID:X_POS:Y_POS = %""%
MAP:SECTOR_ID:X_POS:Y_POS = 明瞭

