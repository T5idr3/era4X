;--------------------------
; 遺棄された貨物船
;--------------------------
@INIT_ANOMALY_1
#DIM TASK_COMPLETE
#DIM SECTOR_ID
#DIM POS_X
#DIM POS_Y
; 遺棄された貨物船
WHILE TASK_COMPLETE == 0
	SECTOR_ID = RAND:MAX_SECTOR
	POS_X = 7
	POS_Y = 7
	SIF MAP:SECTOR_ID:POS_X:POS_Y != 明瞭
		CONTINUE
	MAP:SECTOR_ID:POS_X:POS_Y = ANOMALY
	ANOMALY_MAP:SECTOR_ID:POS_X:POS_Y = 遺跡探索1
	CALL POS2ID(SECTOR_ID, POS_X, POS_Y)
	ANOMALY_POSITIONID:1 = RESULT
	TASK_COMPLETE = 1
WEND

;--------------------------
; ANOMALY本体
;--------------------------
@ANOMALY_BODY_遺跡探索1
#DIM ITER
#DIM SHIP_ID

PRINTFORML かつて繁栄を極めていたと思わしき荒廃した星に%CALLNAME:MASTER%はたどり着いた
IF SHIP_CATEGORY:BOARDING_SHIP_ID == 調査船
	PRINTFORMW %CALLNAME:MASTER%は早速調査船のセンサーを使って、めぼしいものを探しながら着陸軌道に乗っかった
	PRINTFORMW ……
	PRINTFORML たどり着いたのは遺跡だった
	PRINTFORML 極めて原始的な警備ロボットの残骸が転がっている。どうやら守るべき物がある施設だったようだ
	PRINTFORMW さて、どうするか
	$INPUT_LOOP
	PRINTBUTTON "[0] 探索する", 0
	PRINTL
	PRINTBUTTON "[1] 引き返す", 1
	INPUT
	IF RESULT == 0
		CALL ANOMALY_EXPLORE_1
	ELSEIF RESULT == 1
		RETURN
	ELSE
		GOTO INPUT_LOOP
	ENDIF
ELSE
	PRINTFORMW しかし調査船でないとめぼしい物がある地域に着地することは不可能だろう……
ENDIF

@ANOMALY_EXPLORE_1
#DIM OXYGEN
OXYGEN = 10
IF ANOMALY_PROGRESS:1 == 0
	PRINTL それは実にチグハグな遺跡だった
	PRINTL 外見こそ超古代の遺跡というのにふさわしい石のレンガで作りあげられているのだが、内装は近代に流行ったグリッドオイルとプラスチックを混ぜて作られたポップでチープな家具ばかりなのだ
	PRINTL まるで遺跡に近代の人間が引っ越してきたようである
	$INPUT_LOOP
	IF OXYGEN == 0
		PRINTL [時間切れ] そろそろ酸素がきれそうだ
		PRINTFORMW %CALLNAME:MASTER%は急いで調査船に戻ることにした
		RETURN
	ENDIF
	PRINTL さて、どうする？
	PRINTBUTTON "[0] 辺りを調査する", 0
	PRINTL
	PRINTBUTTON "[1] 奥にに進む", 1
	INPUT
	IF RESULT == 0
		OXYGEN -= 1
		IF RAND:3 != 0
			PRINTL [判定失敗] しばらく周りを調べたが、何も見つからなかった
			PRINTFORMW 残り酸素: {OXYGEN}
			GOTO INPUT_LOOP
		ELSE
			PRINTFORML [判定成功] %CALLNAME:MASTER%はチープな引き出しの中に、コレクターがいる古い型のプラモデルが箱のまま残っているのを見つけた
			PRINTFORML %CALLNAME:MASTER%はプラモデルを手に、調査船に戻ることにした
			PRINTFORMW [探索成功] 所持金 + $2,000
			MONEY += 2000
			ANOMALY_PROGRESS:1 = 1
			RETURN
		ENDIF
	ELSEIF RESULT == 1
		OXYGEN -= 1
		PRINTFORML %CALLNAME:MASTER%はしばらく辺りを見回した後、奥に進むことにした
		PRINTFORMW 残り酸素: {OXYGEN}
		CALL ANOMALY_EXPLORE_1_2(OXYGEN)
	ELSE
		GOTO INPUT_LOOP
	ENDIF
ELSE
	PRINTL 調査済みのエリアなのでとっとと通り過ぎることにした
	PRINTFORMW 残り酸素: {OXYGEN}
	CALL ANOMALY_EXPLORE_1_2(OXYGEN)
ENDIF


@ANOMALY_EXPLORE_1_2(OXYGEN)
#DIM OXYGEN

IF ANOMALY_PROGRESS:1 == 1
	PRINTL 階段をのぼると、そこにはショーケースが多数設置された商業スペースか博物館のような場所だった
	PRINTL ショーケースのガラスに積もったホコリを払うと、中には何かの骨が展示されていた
	PRINTFORML 一階の民家のような内装とまるで違う内装に、%CALLNAME:MASTER%は困惑を抑えきれなかった
	$INPUT_LOOP
	IF OXYGEN == 0
		PRINTL [時間切れ] そろそろ酸素がきれそうだ
		PRINTFORMW %CALLNAME:MASTER%は急いで調査船に戻ることにした
		RETURN
	ENDIF
	PRINTL さて、どうする？
	PRINTBUTTON "[0] 辺りを調査する", 0
	PRINTL
	PRINTBUTTON "[1] 調査船に戻る", 1
	INPUT
	IF RESULT == 0
		OXYGEN -= 1
		IF RAND:4 != 0
			PRINTL [判定失敗] しばらく周りを調べたが、めぼしいものは見つからなかった
			PRINTFORMW 残り酸素: {OXYGEN}
			GOTO INPUT_LOOP
		ELSE
			PRINTFORML [判定成功] %CALLNAME:MASTER%はケースの一つに、宝飾品が飾られているのを見つけた。しかも現代でも名の知れている企業の初期モデルの宝飾品だ
			PRINTFORML しかし鍵がかかっていて開けることができない。近くにあった石材でガラスを叩き割ろうとしたが、傷一つつかなかった……
			PRINTFORML [探索成功] 進捗 + 1
			PRINTFORMW 残り酸素: {OXYGEN}
			CALL ANOMALY_EXPLORE_1_3(OXYGEN)
		ENDIF
	ELSEIF RESULT == 1
		OXYGEN -= 1
		PRINTFORMW %CALLNAME:MASTER%はしばらく辺りを見回した後、調査船に戻ることにした
		RETURN
	ELSE
		GOTO INPUT_LOOP
	ENDIF
ELSE
	CALL ANOMALY_EXPLORE_1_3(OXYGEN)
ENDIF

@ANOMALY_EXPLORE_1_3(OXYGEN)
#DIM OXYGEN
#DIM SECTOR_ID
#DIM X_POS
#DIM Y_POS

PRINTFORML 宝飾品の飾られたショーケースの鍵を探すことにした%CALLNAME:MASTER%は、この小さな遺跡の隅々までを調べることにした
$INPUT_LOOP
IF OXYGEN == 0
	PRINTL [時間切れ] そろそろ酸素がきれそうだ
	PRINTFORMW %CALLNAME:MASTER%は急いで調査船に戻ることにした
	RETURN
ENDIF
PRINTBUTTON "[0] 探索する", 0
PRINTL
PRINTBUTTON "[1] 調査船に戻る", 1
INPUT
IF RESULT == 0
	IF RAND:5 != 0
		PRINTL [判定失敗] 何も見つからなかった
		OXYGEN -= 1
		PRINTFORMW 残り酸素: {OXYGEN}
		GOTO INPUT_LOOP
	ELSE
		PRINTFORML [判定成功] %CALLNAME:MASTER%は一階の引き出しから、鍵束を見つけ出した
		PRINTL 早速それを持って二階に行き、それらしき鍵を挿していくと、見事そのうちの一本が挿さり、鍵を開けることに成功した
		PRINTFORML %CALLNAME:MASTER%は年代物の宝飾品を手に入れた。これひとつで普通の人なら一生平穏に暮らしていけることだろう
		PRINTW [探索成功] 所持金 + $4,500,000
		MONEY += 4500000
		ANOMALY_PROGRESS:1 = 3
		;アノマリーの初期化
		CALL ID2POS(ANOMALY_POSITIONID:1)
		SECTOR_ID = RESULT:0
		X_POS = RESULT:1
		Y_POS = RESULT:2
		ANOMALY_MAP:SECTOR_ID:X_POS:Y_POS = %""%
		MAP:SECTOR_ID:X_POS:Y_POS = 明瞭
		RETURN
	ENDIF
ELSEIF RESULT == 1
	PRINTFORMW %CALLNAME:MASTER%は調査船に戻ることにした
ELSE
	GOTO INPUT_LOOP
ENDIF