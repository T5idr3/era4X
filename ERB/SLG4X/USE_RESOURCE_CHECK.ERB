@USE_RESOURCE_CHECK(PORT_POSID, OWNER, ORDERBY, OBJECT_ID ,USE_RESOURCE, NO_MESSAGE)
#DIM PORT_POSID
#DIM OWNER
#DIM ORDERBY
#DIM OBJECT_ID
#DIM ITEM_ID
#DIM REF USE_RESOURCE
#DIM RESOURCE_NUM
#DIM CHECK_CAN
#DIM NO_MESSAGE

CHECK_CAN = 1
; 呼び出したのがプレイヤー以外か、表示処理不要フラグがある場合にはSKIPDISPで表示を飛ばす
IF ORDERBY != 国家ID_プレイヤー || NO_MESSAGE == 1
	SKIPDISP 1
ENDIF
PRINTL
PRINTL 必要資材 / 宇宙港の貯蔵
; 必要資材が足りているか
FOR ITEM_ID, 0, ITEM_LENGTH
	RESOURCE_NUM = GET_RESOURCE_NUM(PORT_POSID, OWNER, ITEM_ID)
	; 必要資材が0個よりも多い素材だったら足りてるか確認をする
	IF USE_RESOURCE:ITEM_ID > 0
		CALL GET_ITEM_NAME(ITEM_ID)
		PRINTFORML %@"%RESULTS%",20, LEFT% {USE_RESOURCE:ITEM_ID, 12, RIGHT} / {RESOURCE_NUM, 7, LEFT}個
		; 資材不足時には不足数を表示しつつ作成不可フラグをを立てる
		IF RESOURCE_NUM < USE_RESOURCE:ITEM_ID
			PRINTFORML %"・・・",51, RIGHT% {USE_RESOURCE:ITEM_ID - RESOURCE_NUM}個不足
			CHECK_CAN = 0
		ENDIF
	ENDIF
NEXT

SKIPDISP 0

; 作成の可不可判定を返す。(CAN_BUILD・CAN_BUYに返すつもり)
IF CHECK_CAN == 0
	RETURN 0
ELSE
	RETURN 1
ENDIF