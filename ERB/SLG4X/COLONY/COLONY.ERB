;テスト用コロニーのエントランス
@COLONY_ENTRY
CALL COLONY_MANAGEMENT(0)

@COLONY_MANAGEMENT(_ID)
#DIM _ID
#DIM DYNAMIC _SECTION
#DIM DYNAMIC _INPUT_VALUE

; 新規コロニーの場合
SIF COLONY_POP:_ID == 0
    CALL INIT_COLONY(_ID)

WHILE _INPUT_VALUE != 999
    CALL UPDATE_COLONY(_ID)
    CALL SHOW_COLONY_HEADER(_ID)
    ; 現在開放されているセクションを表示
    CALL SHOW_SECTION_TABS(_ID, _SECTION)
    CALL SHOW_BUILT_FACILITY_LIST(_ID, _SECTION)
    PRINTL
    PRINTL [999] 戻る
    INPUT
    _INPUT_VALUE = RESULT
    IF INRANGE(_INPUT_VALUE, 0, MAX_COLONY_SECTION - 1) && COLONY_RELEASED_SECTION:_ID:_INPUT_VALUE
        _SECTION = _INPUT_VALUE
    ELSEIF INRANGE(_INPUT_VALUE, MAX_COLONY_SECTION, MAX_COLONY_BLOCK + MAX_COLONY_SECTION - 1)
        CALL CHANGE_FACILITY(_ID, _SECTION, _INPUT_VALUE - MAX_COLONY_SECTION)
    ENDIF
WEND

; 新規コロニーの設定
@INIT_COLONY(_ID)
#DIM _ID
PRINTL コロニーの初期POPを奴隷商に頼んで用意してもらいますか？（今は無料）
CALL ASK_YN()
SIF RESULT == 1
    RETURN
PRINTL 奴隷商がどこからともなく膨大な貧民を連れてやってきた
PRINTL 彼らは新天地での暮らしを夢見て目を輝かせている
PRINTL 5POP手に入れた
COLONY_POP:_ID = 5
COLONY_RELEASED_SECTION:_ID:0 = 1
COLONY_TAX_RATE:_ID = 10
COLONY_OWNER:_ID = 国家ID_プレイヤー

; 現在所持しているリソースを表示
@SHOW_COLONY_HEADER(_ID)
#DIM _ID

DRAWLINE
PRINTL コロニー情報
DRAWLINE
PRINTL
PRINTFORML [無職POP数]{COLONY_POP:_ID}
PRINTFORML [税収]{COLONY_RESOURCES:_ID:COLONY_MONEY} [税率]{COLONY_TAX_RATE:_ID}\%
FOR LOCAL, 0, COLONY_MAX_RESOURCE
    SIF LOCAL == COLONY_MONEY
        CONTINUE
    PRINTFORM [%COLONY_RESOURCES_NAME:LOCAL, 6, LEFT%] 需要:{COLONY_DEMAND:_ID:LOCAL, 3, RIGHT} 供給:{COLONY_RESOURCES:_ID:LOCAL, 3, RIGHT} 計:
    SIF COLONY_DEMAND:_ID:LOCAL > COLONY_RESOURCES:_ID:LOCAL
        SETCOLOR 0XFF9999
    PRINTFORM {(COLONY_RESOURCES:_ID:LOCAL - COLONY_DEMAND:_ID:LOCAL), 3, RIGHT}
    RESETCOLOR
    PRINTL
NEXT
PRINTL

; リソースの更新を行う
@UPDATE_COLONY(_ID)
#DIM _ID
#DIM _SECTION_ID
#DIM _COLONY_WORKING_POP_NUM
#DIM _COLONY_TOTAL_POP_NUM
#DIM _DIFF_VAL
#DIM DYNAMIC _HOUSE_COUNT

VARSET COLONY_RESOURCES
VARSET COLONY_DEMAND

CALL GET_POP_NUM
_COLONY_WORKING_POP_NUM = RESULT
_COLONY_TOTAL_POP_NUM = COLONY_POP:_ID + _COLONY_WORKING_POP_NUM

FOR LOCAL, 0, COLONY_MAX_RESOURCE
    SELECTCASE LOCAL
        CASE COLONY_FOOD
            COLONY_DEMAND:_ID:LOCAL = _COLONY_TOTAL_POP_NUM / COLONY_NEED_FOOD_RATE
            FOR _SECTION_ID, 0, MAX_COLONY_SECTION
                CALL FACILITY_COUNT(_ID, _SECTION_ID, COLONY_FOOD_FACTORY)
                COLONY_RESOURCES:_ID:LOCAL += RESULT
            NEXT
        CASE COLONY_MONEY
            ;税収 = 労働人口 * 税率 * 基本税額
            COLONY_RESOURCES:_ID:LOCAL = _COLONY_WORKING_POP_NUM * COLONY_TAX_RATE:_ID * COLONY_PAYMENT_TAX_RATE
        CASE COLONY_SECURITY
            COLONY_DEMAND:_ID:LOCAL = _COLONY_TOTAL_POP_NUM / COLONY_NEED_SECURITY_RATE
            FOR _SECTION_ID, 0, MAX_COLONY_SECTION
                CALL FACILITY_COUNT(_ID, _SECTION_ID, COLONY_RED_LIGHT_DISTRICT)
                COLONY_DEMAND:_ID:LOCAL += RESULT
                CALL FACILITY_COUNT(_ID, _SECTION_ID, COLONY_POLICE)
                COLONY_RESOURCES:_ID:LOCAL += RESULT
                CALL FACILITY_COUNT(_ID, _SECTION_ID, COLONY_COURT)
                COLONY_RESOURCES:_ID:LOCAL += RESULT * 3
            NEXT
        CASE COLONY_HOSPIS
            FOR _SECTION_ID, 0, MAX_COLONY_SECTION
                CALL FACILITY_COUNT(_ID, _SECTION_ID, COLONY_EMBASSY)
                COLONY_RESOURCES:_ID:LOCAL += RESULT
            NEXT
        CASE COLONY_GROUND_FORCES
            FOR _SECTION_ID, 0, MAX_COLONY_SECTION
                CALL FACILITY_COUNT(_ID, _SECTION_ID, COLONY_STRONGHOLD)
                COLONY_RESOURCES:_ID:LOCAL += RESULT
            NEXT
        CASE COLONY_ENERGY
            COLONY_DEMAND:_ID:LOCAL = _COLONY_TOTAL_POP_NUM / COLONY_NEED_ENERGY_RATE
            FOR _SECTION_ID, 0, MAX_COLONY_SECTION
                CALL FACILITY_COUNT(_ID, _SECTION_ID, COLONY_POWER_PLANT)
                COLONY_RESOURCES:_ID:LOCAL += RESULT
                CALL FACILITY_COUNT(_ID, _SECTION_ID, COLONY_COMMERICAL)
                COLONY_DEMAND:_ID:LOCAL += RESULT
                CALL FACILITY_COUNT(_ID, _SECTION_ID, COLONY_MINE)
                COLONY_DEMAND:_ID:LOCAL += RESULT
                CALL FACILITY_COUNT(_ID, _SECTION_ID, COLONY_OIL_PLANT)
                COLONY_DEMAND:_ID:LOCAL += RESULT
                CALL FACILITY_COUNT(_ID, _SECTION_ID, COLONY_GAS_PLANT)
                COLONY_DEMAND:_ID:LOCAL += RESULT
            NEXT
        CASE COLONY_POP_SATISFACTION
            COLONY_DEMAND:_ID:LOCAL = _COLONY_TOTAL_POP_NUM / COLONY_SATISFACTION_RATE
            FOR _SECTION_ID, 0, MAX_COLONY_SECTION
                CALL FACILITY_COUNT(_ID, _SECTION_ID, COLONY_RED_LIGHT_DISTRICT)
                COLONY_RESOURCES:_ID:LOCAL += RESULT * 3
            NEXT
            ;エネルギー需要を満たしていないと不満
            _DIFF_VAL = COLONY_RESOURCES:_ID:COLONY_ENERGY - COLONY_DEMAND:_ID:COLONY_ENERGY
            SIF _DIFF_VAL < 0
                COLONY_DEMAND:_ID:LOCAL += _DIFF_VAL
            ;治安により増減
            _DIFF_VAL = COLONY_RESOURCES:_ID:COLONY_SECURITY - COLONY_DEMAND:_ID:COLONY_SECURITY
            IF _DIFF_VAL > 0
                COLONY_RESOURCES:_ID:LOCAL += _DIFF_VAL / COLONY_SATISFACTION_SECURITY_RATE
            ELSEIF _DIFF_VAL < 0
                COLONY_DEMAND:_ID:LOCAL += _DIFF_VAL / COLONY_SATISFACTION_SECURITY_RATE
            ENDIF
            ;家の有無により増減
            FOR _SECTION_ID, 0, MAX_COLONY_SECTION
                CALL FACILITY_COUNT(_ID, _SECTION_ID, COLONY_RESIDENCE)
                _HOUSE_COUNT += RESULT * 3
                CALL FACILITY_COUNT(_ID, _SECTION_ID, COLONY_MANSION)
                _HOUSE_COUNT += RESULT
                COLONY_RESOURCES:_ID:LOCAL += RESULT
            NEXT
            SIF _HOUSE_COUNT < _COLONY_TOTAL_POP_NUM / COLONY_NEED_HOUSE_RATE
                COLONY_DEMAND:_ID:LOCAL += (_COLONY_TOTAL_POP_NUM / COLONY_NEED_HOUSE_RATE) -  _HOUSE_COUNT
            SIF _DIFF_VAL < 0
                COLONY_DEMAND:_ID:LOCAL += _DIFF_VAL
            ;税額により増減
            IF COLONY_TAX_RATE:_ID > COLONY_COMMON_TAX_RATE
                COLONY_DEMAND:_ID:LOCAL += (COLONY_TAX_RATE:_ID - COLONY_COMMON_TAX_RATE) / COLONY_TAX_REACTIVE_RATE
            ELSE
                COLONY_RESOURCES:_ID:LOCAL += (COLONY_COMMON_TAX_RATE - COLONY_TAX_RATE:_ID) / COLONY_TAX_REACTIVE_RATE
            ENDIF
        CASE COLONY_POP_HEALTH
            COLONY_DEMAND:_ID:LOCAL = _COLONY_TOTAL_POP_NUM / COLONY_NEED_HEALTH_RATE
            FOR _SECTION_ID, 0, MAX_COLONY_SECTION
                CALL FACILITY_COUNT(_ID, _SECTION_ID, COLONY_HOSPITAL)
                COLONY_RESOURCES:_ID:LOCAL += RESULT
            NEXT
    ENDSELECT
NEXT


; 現在開放されているセクションをタブとして表示
@SHOW_SECTION_TABS(_ID, _SECTION)
#DIM _ID
#DIM _SECTION

FOR LOCAL, 0, MAX_COLONY_SECTION
    SIF COLONY_RELEASED_SECTION:_ID:LOCAL == 0
        CONTINUE
    SIF _SECTION == LOCAL
        SETCOLOR 0X9999FF
    PRINTBUTTON @"[セクション{LOCAL + 1}]", LOCAL
    RESETCOLOR
    SIF LOCAL % 4 == 3
        PRINTL
NEXT
PRINTL

; 現在設定しているセクションの一覧を表示
@SHOW_BUILT_FACILITY_LIST(_COLONY_ID, _SECTION_ID)
#DIM _COLONY_ID
#DIM _SECTION_ID

FOR LOCAL, 0, MAX_COLONY_BLOCK
    PRINTBUTTON @"[BLOCK - {LOCAL + 1}] %FACILITIES_NAME:(COLONY_FACILITIES:_COLONY_ID:_SECTION_ID:LOCAL)%", LOCAL + MAX_COLONY_SECTION
    PRINTL
NEXT


; 建造物変更
@CHANGE_FACILITY(_COLONY_ID, _SECTION_ID, _BLOCK_ID)
#DIM _COLONY_ID
#DIM _SECTION_ID
#DIM _BLOCK_ID
#DIM _CURRENT_FACILITY
#DIM _INPUT_VALUE

_CURRENT_FACILITY = COLONY_FACILITIES:_COLONY_ID:_SECTION_ID:_BLOCK_ID

DRAWLINE
PRINTL ■ 建造物設定変更
DRAWLINE
PRINTL
PRINTFORML 現在このブロックに建てられている建物: %FACILITIES_NAME:_CURRENT_FACILITY%
PRINTL 効果:
CALL SHOW_FACILITY_DESCRIPTION(_CURRENT_FACILITY)
IF _CURRENT_FACILITY != COLONY_NON_FACILITY || COLONY_POP:_COLONY_ID > 0
    CALL SHOW_FACILITY_LIST
ELSE
    PRINTW POPが不足しているため新しい建造物を建てられません
    RETURN
ENDIF
INPUT
_INPUT_VALUE = RESULT
IF INRANGE(_INPUT_VALUE, COLONY_STRONGHOLD, VARSIZE("FACILITIES_NAME") - 1)
    ;前提条件がある建物の前提条件チェック
    SELECTCASE _INPUT_VALUE
        CASE COLONY_COURT
            CALL FACILITY_COUNT(_COLONY_ID, _SECTION_ID, COLONY_POLICE)
            IF RESULT == 0
                PRINTW 裁判所の建設には警察署が同セクション内に建設されている必要があります
                RETURN
            ENDIF
    ENDSELECT
    ;建設
    SIF COLONY_FACILITIES:_COLONY_ID:_SECTION_ID:_BLOCK_ID == COLONY_NON_FACILITY
        COLONY_POP:_COLONY_ID -= 1
    COLONY_FACILITIES:_COLONY_ID:_SECTION_ID:_BLOCK_ID = _INPUT_VALUE
ENDIF

; 建造物の説明を表示
@SHOW_FACILITY_DESCRIPTION(_FACILITY)
#DIM _FACILITY

SELECTCASE _FACILITY
    CASE COLONY_NON_FACILITY
        PRINTL なし
    CASE COLONY_STRONGHOLD
        PRINTL 1POPが地上軍として陸戦時に使えるようになる
    CASE COLONY_POWER_PLANT
        PRINTL インフラで使用される電力を生産する
    CASE COLONY_FOOD_FACTORY
        PRINTL POPが消費する食料を生産する
    CASE COLONY_RESIDENCE
        PRINTL POPが住む住居を提供する
    CASE COLONY_MANSION
        PRINTL POPが住む住居を提供する。通常の住居より満足度が高い
    CASE COLONY_HOSPITAL
        PRINTL POPの健康を上昇させる
    CASE COLONY_POLICE
        PRINTL 治安が上昇する
    CASE COLONY_COURT
        PRINTL 治安が大幅に上昇する。建造には警察署が必要。
    CASE COLONY_EMBASSY
        PRINTL 国家間の交流が行える。大使館一つに付き、一つの国との好感度改善が実施可能
    CASE COLONY_COMMERICAL
        PRINTL POPの満足度を大幅に上げるが、余剰電力を必要とする
    CASE COLONY_RED_LIGHT_DISTRICT
        PRINTL POPの満足度を大幅に上げるが、治安が低下する
    CASE COLONY_MINE
        PRINTL 宇宙を漂う岩石、あるいは地上の鉱物エリアから鉱物を生産する。余剰電力を必要とする
    CASE COLONY_OIL_PLANT
        PRINTL 合成オイルを生成する。余剰電力を必要とする
    CASE COLONY_GAS_PLANT
        PRINTL ガスを生成する。余剰電力を必要とする
    CASE COLONY_IMMIGRATION_OFFICE
        PRINTL ターン毎のPOP増加量を増す
ENDSELECT

;選択可能な建物一覧を表示
@SHOW_FACILITY_LIST
PRINTL
FOR LOCAL, COLONY_STRONGHOLD, VARSIZE("FACILITIES_NAME")
    PRINTBUTTON @"[{LOCAL}] %FACILITIES_NAME:LOCAL%", LOCAL
    PRINTL
    CALL SHOW_FACILITY_DESCRIPTION(LOCAL)
    PRINTL
NEXT

;建築物の存在判定
@FACILITY_COUNT(_COLONY_ID, _SECTION_ID, _FACILITY)
#DIM _COLONY_ID
#DIM _SECTION_ID
#DIM _FACILITY
#DIM DYNAMIC _COUNT

FOR LOCAL, 0, MAX_COLONY_BLOCK
    IF COLONY_FACILITIES:_COLONY_ID:_SECTION_ID:LOCAL == _FACILITY
        _COUNT ++
        CONTINUE
    ENDIF
NEXT
RETURN _COUNT

;総POP数取得
@GET_POP_NUM(_ID)
#DIM _ID
#DIM _SECTION_ID
#DIM _BLOCK_ID
#DIM DYNAMIC _COUNT

FOR _SECTION_ID, 0, MAX_COLONY_SECTION
    SIF COLONY_RELEASED_SECTION:_ID:_SECTION_ID == 0
        CONTINUE
    FOR _BLOCK_ID, 0, MAX_COLONY_BLOCK
        SIF COLONY_FACILITIES:_ID:_SECTION_ID:_BLOCK_ID == COLONY_NON_FACILITY
            CONTINUE
        _COUNT ++
    NEXT
NEXT

RETURN _COUNT

@TURNEND_COLONY
#DIM _COLONY_ID

FOR _COLONY_ID, 0, MAX_COLONY
    ;未開発のコロニーはスキップ
    SIF COLONY_OWNER:_COLONY_ID == 0
        CONTINUE
    CALL UPDATE_COLONY(_COLONY_ID)

    ;POP増加
    COLONY_NEXT_POP:_COLONY_ID += COLONY_RESOURCES:_COLONY_ID:COLONY_POP_HEALTH - COLONY_DEMAND:_COLONY_ID:COLONY_POP_HEALTH
    COLONY_NEXT_POP:_COLONY_ID += COLONY_RESOURCES:_COLONY_ID:COLONY_POP_SATISFACTION - COLONY_DEMAND:_COLONY_ID:COLONY_POP_SATISFACTION

    FOR LOCAL, 0, MAX_COLONY_SECTION
        CALL FACILITY_COUNT(_COLONY_ID, LOCAL, COLONY_IMMIGRATION_OFFICE)
        COLONY_NEXT_POP:_COLONY_ID += RESULT * 20
    NEXT
    IF COLONY_NEXT_POP >= NEW_POP_RATE
        COLONY_POP:_COLONY_ID += COLONY_NEXT_POP:_COLONY_ID / NEW_POP_RATE
        COLONY_NEXT_POP:_COLONY_ID = COLONY_NEXT_POP:_COLONY_ID % NEW_POP_RATE
    ENDIF

    ;SECTION開放
    CALL GET_POP_NUM(_COLONY_ID)
    FOR LOCAL, 0, RESULT / 10
        SIF COLONY_RELEASED_SECTION:_COLONY_ID:LOCAL == 0
            COLONY_RELEASED_SECTION:_COLONY_ID:LOCAL = 1
    NEXT

    ;税金
    IF COLONY_OWNER:_COLONY_ID == 国家ID_プレイヤー
        MONEY += COLONY_RESOURCES:_COLONY_ID:COLONY_MONEY- COLONY_DEMAND:_COLONY_ID:COLONY_MONEY
        DRAWLINE
        PRINTFORML コロニーからの税収: %TOSTR(COLONY_RESOURCES:_COLONY_ID:COLONY_MONEY- COLONY_DEMAND:_COLONY_ID:COLONY_MONEY, "$###,##0")%
    ELSE
        MONEY:(COLONY_OWNER:_COLONY_ID) += COLONY_RESOURCES:_COLONY_ID:COLONY_MONEY- COLONY_DEMAND:_COLONY_ID:COLONY_MONEY
    ENDIF
NEXT