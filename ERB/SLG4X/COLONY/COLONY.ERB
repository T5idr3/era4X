;テスト用コロニーのエントランス
@COLONY_ENTRY
CALL COLONY_MANAGEMENT(0)

@COLONY_MANAGEMENT(_ID)
#DIM _ID
#DIM DYNAMIC _SECTION
#DIM DYNAMIC _INPUT_VALUE

; 新規コロニーの場合
SIF COLONY_POP:_ID == 0
    CALL INIT_COLONY(_ID)

WHILE _INPUT_VALUE != 999
    CALL SHOW_HEADER(_ID)
    ; 現在開放されているセクションを表示
    CALL SHOW_SECTION_TABS(_ID)
    CALL SHOW_BUILT_FACILITY_LIST(_ID, _SECTION)
    INPUT
    _INPUT_VALUE = RESULT
    IF INRANGE(_INPUT_VALUE, 0, MAX_COLONY_SECTION - 1) && COLONY_RELEASED_SECTION:_ID:_INPUT_VALUE
        _SECTION = _INPUT_VALUE
    ELSEIF INRANGE(_INPUT_VALUE, MAX_COLONY_SECTION, MAX_COLONY_BLOCK + MAX_COLONY_SECTION - 1)
        CALL CHANGE_FACILITY(_ID, _SECTION, _INPUT_VALUE - MAX_COLONY_SECTION)
    ENDIF
WEND

; 新規コロニーの設定
@INIT_COLONY(_ID, _SECTION_ID)
#DIM _ID
#DIM _SECTION_ID
PRINTL コロニーの初期POPを奴隷商に頼んで用意してもらいますか？（今は無料）
CALL ASK_YN()
SIF RESULT == 1
    RETURN
PRINTL 奴隷商がどこからともなく膨大な貧民を連れてやってきた
PRINTL 彼らは新天地での暮らしを夢見て目を輝かせている
PRINTL 5POP手に入れた
COLONY_POP:_ID = 5
COLONY_RELEASED_SECTION:_ID:_SECTION_ID = 1

; 現在所持しているリソースを表示
@SHOW_HEADER(_ID)
#DIM _ID

DRAWLINE
PRINTL コロニー情報
DRAWLINE
PRINTL
PRINTFORML 無職POP数 {COLONY_POP:_ID}
PRINTFORM 食料　: {COLONY_RESOURCES:_ID:COLONY_FOOD} 金　　: {COLONY_RESOURCES:_ID:COLONY_MONEY} 
PRINTFORML 治安　: {COLONY_RESOURCES:_ID:COLONY_FOOD} 国交　: {COLONY_RESOURCES:_ID:COLONY_MONEY}
PRINTFORM 民需　: {COLONY_RESOURCES:_ID:COLONY_FOOD} 地上軍: {COLONY_RESOURCES:_ID:COLONY_MONEY} 
PRINTFORML 満足度: {COLONY_RESOURCES:_ID:COLONY_FOOD} 健康　: {COLONY_RESOURCES:_ID:COLONY_MONEY}
PRINTL


; 現在開放されているセクションをタブとして表示
@SHOW_SECTION_TABS(_ID)
#DIM _ID

FOR LOCAL, 0, MAX_COLONY_SECTION
    SIF COLONY_RELEASED_SECTION:_ID:LOCAL == 0
        CONTINUE
    PRINTBUTTON @"[セクション{LOCAL + 1}]", LOCAL
    SIF LOCAL % 4 == 0
        PRINTL
NEXT

; 現在設定しているセクションの一覧を表示
@SHOW_BUILT_FACILITY_LIST(_COLONY_ID, _SECTION_ID)
#DIM _COLONY_ID
#DIM _SECTION_ID

FOR LOCAL, 0, MAX_COLONY_BLOCK
    PRINTBUTTON @"[BLOCK - {LOCAL + 1}]%FACILITIES_NAME:(COLONY_FACILITIES:_COLONY_ID:_SECTION_ID:LOCAL)%", LOCAL + MAX_COLONY_SECTION
    PRINTL
NEXT


; 建造物変更
@CHANGE_FACILITY(_COLONY_ID, _SECTION_ID, _BLOCK_ID)
#DIM _COLONY_ID
#DIM _SECTION_ID
#DIM _BLOCK_ID
#DIM _CURRENT_FACILITY
#DIM _INPUT_VALUE

_CURRENT_FACILITY = COLONY_FACILITIES:_COLONY_ID:_SECTION_ID:_BLOCK_ID

DRAWLINE
PRINTL ■ 建造物設定変更
DRAWLINE
PRINTL
PRINTFORML 現在このブロックに建てられている建物: %FACILITIES_NAME:_CURRENT_FACILITY%
PRINTL 効果:
CALL SHOW_FACILITY_DESCRIPTION(_CURRENT_FACILITY)
IF _CURRENT_FACILITY != 0 || COLONY_POP:_COLONY_ID > 0
    CALL SHOW_FACILITY_LIST
ELSE
    PRINTW POPが不足しているため新しい建造物を建てられません
    RETURN
ENDIF
INPUT
_INPUT_VALUE = RESULT
IF INRANGE(_INPUT_VALUE, COLONY_STRONGHOLD, VARSIZE("FACILITIES_NAME") - 1)
    COLONY_POP:_COLONY_ID -= 1
    COLONY_FACILITIES:_COLONY_ID:_SECTION_ID:_BLOCK_ID = _INPUT_VALUE
ENDIF

; 建造物の説明を表示
@SHOW_FACILITY_DESCRIPTION(_FACILITY)
#DIM _FACILITY

SELECTCASE _FACILITY
    CASE COLONY_NON_FACILITY
        PRINTL なし
    CASE COLONY_STRONGHOLD
        PRINTL 1POPが地上軍として陸戦時に使えるようになる
    CASE COLONY_POWER_PLANT
        PRINTL インフラで使用される電力を生産する
    CASE COLONY_FOOD_FACTORY
        PRINTL POPが消費する食料を生産する
    CASE COLONY_RESIDENCE
        PRINTL POPが住む住居を提供する
    CASE COLONY_MANSION
        PRINTL POPが住む住居を提供する。通常の住居より満足度が高い
    CASE COLONY_HOSPITAL
        PRINTL POPの健康を上昇させる
    CASE COLONY_CIVILIAN_INDUSTRIES
        PRINTL POPの満足度を上げる民需品を生成する
    CASE COLONY_POLICE
        PRINTL 治安が上昇する
    CASE COLONY_COURT
        PRINTL 治安が大幅に上昇する。建造には警察署が必要。
    CASE COLONY_EMBASSY
        PRINTL 国家間の交流が行える。大使館一つに付き、一つの国との好感度改善が実施可能
    CASE COLONY_COMMERICAL
        PRINTL POPの満足度を大幅に上げるが、余剰電力を必要とする
    CASE COLONY_RED_LIGHT_DISTRICT
        PRINTL POPの満足度を大幅に上げるが、治安が低下する
    CASE COLONY_MINE
        PRINTL 宇宙を漂う岩石、あるいは地上の鉱物エリアから鉱物を生産する。余剰電力を必要とする
    CASE COLONY_OIL_PLANT
        PRINTL 合成オイルを生成する。余剰電力を必要とする
    CASE COLONY_GAS_PLANT
        PRINTL ガスを生成する。余剰電力を必要とする
ENDSELECT

;選択可能な建物一覧を表示
@SHOW_FACILITY_LIST
PRINTL
FOR LOCAL, COLONY_STRONGHOLD, VARSIZE("FACILITIES_NAME")
    PRINTBUTTON @"[{LOCAL}] %FACILITIES_NAME:LOCAL%", LOCAL
    PRINTL
    CALL SHOW_FACILITY_DESCRIPTION(LOCAL)
    PRINTL
NEXT