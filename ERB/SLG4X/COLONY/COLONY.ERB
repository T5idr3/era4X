;テスト用コロニーのエントランス
@COLONY_ENTRY
CALL COLONY_MANAGEMENT(0)

@COLONY_MANAGEMENT(_ID)
#DIM _ID
#DIM DYNAMIC _SECTION
#DIM DYNAMIC _INPUT_VALUE
#DIM _IMMIGRATION_OFFICE_COUNT

; 新規コロニーの場合
SIF COLONY_POP:_ID == 0
    CALL INIT_COLONY(_ID)

WHILE _INPUT_VALUE != 999
    CALL UPDATE_COLONY(_ID)
    CALL SHOW_COLONY_HEADER(_ID)
    ; 現在開放されているセクションを表示
    CALL SHOW_SECTION_TABS(_ID, _SECTION)
    CALL SHOW_BUILT_FACILITY_LIST(_ID, _SECTION)
    PRINTL
    PRINTL [999] 戻る
    ; 移民局のカウント
    _IMMIGRATION_OFFICE_COUNT = 0
    FOR LOCAL, 0, MAX_COLONY_SECTION
        CALL FACILITY_COUNT(_ID, LOCAL, COLONY_IMMIGRATION_OFFICE)
        _IMMIGRATION_OFFICE_COUNT += RESULT
    NEXT
    INPUT
    _INPUT_VALUE = RESULT
    IF INRANGE(_INPUT_VALUE, 0, MAX_COLONY_SECTION - 1) && COLONY_RELEASED_SECTION:_ID:_INPUT_VALUE
        _SECTION = _INPUT_VALUE
    ELSEIF INRANGE(_INPUT_VALUE, MAX_COLONY_SECTION, MAX_COLONY_BLOCK + MAX_COLONY_SECTION - 1)
        CALL CHANGE_FACILITY(_ID, _SECTION, _INPUT_VALUE - MAX_COLONY_SECTION)
    ELSEIF RESULT == 996
        CALL COLONY_TAX_SETTING(_ID)
    ELSEIF RESULT == 997 && COLONY_RESOURCES:_ID:COLONY_HOSPIS > 0
        CALL DIPLOMACY_SETTING(_ID)
    ELSEIF RESULT == 998 && MONEY >= COLONY_POP_PRICE
        MONEY -= COLONY_POP_PRICE
        COLONY_POP:_ID ++
    ENDIF
WEND

; 新規コロニーの設定
@INIT_COLONY(_ID)
#DIM _ID
PRINTL コロニーの初期POPを奴隷商に頼んで用意してもらいますか？（今は無料）
CALL ASK_YN()
SIF RESULT == 1
    RETURN
PRINTL 奴隷商がどこからともなく膨大な貧民を連れてやってきた
PRINTL 彼らは新天地での暮らしを夢見て目を輝かせている
PRINTL 5POP手に入れた
COLONY_POP:_ID = 5
COLONY_RELEASED_SECTION:_ID:0 = 1
COLONY_TAX_RATE:_ID = 10
COLONY_OWNER:_ID = 国家ID_プレイヤー

; 現在所持しているリソースを表示
@SHOW_COLONY_HEADER(_ID)
#DIM _ID
#DIM _SECTION_ID
#DIM DYNAMIC _IMMIGRATION_OFFICE_COUNT
#DIM DYNAMIC _EMBASSY_COUNT

DRAWLINE
PRINTL コロニー情報
DRAWLINE
PRINTL
PRINTFORML [無職POP数]{COLONY_POP:_ID}
PRINTFORML [税収]{COLONY_RESOURCES:_ID:COLONY_MONEY} [税率]{COLONY_TAX_RATE:_ID}\%
FOR LOCAL, 0, COLONY_MAX_RESOURCE
    SIF LOCAL == COLONY_MONEY
        CONTINUE
    PRINTFORM [%COLONY_RESOURCES_NAME:LOCAL, 6, LEFT%] 需要:{COLONY_DEMAND:_ID:LOCAL, 3, RIGHT} 供給:{COLONY_RESOURCES:_ID:LOCAL, 3, RIGHT} 計:
    SIF COLONY_DEMAND:_ID:LOCAL > COLONY_RESOURCES:_ID:LOCAL
        SETCOLOR 0XFF9999
    PRINTFORM {(COLONY_RESOURCES:_ID:LOCAL - COLONY_DEMAND:_ID:LOCAL), 3, RIGHT}
    RESETCOLOR
    PRINTL
NEXT
FOR _SECTION_ID, 0, MAX_COLONY_SECTION
    CALL FACILITY_COUNT(_ID, _SECTION_ID, COLONY_IMMIGRATION_OFFICE)
    _IMMIGRATION_OFFICE_COUNT += RESULT
NEXT
PRINTL [996] 税率設定
SIF COLONY_RESOURCES:_ID:COLONY_HOSPIS > 0
    PRINTL [997] 外交設定
SIF _IMMIGRATION_OFFICE_COUNT > 0 && MONEY > COLONY_POP_PRICE
    PRINTFORML [998] 10Mで1POP購入 (所持金:{MONEY})
PRINTL

; リソースの更新を行う
@UPDATE_COLONY(_ID)
#DIM _ID
#DIM _SECTION_ID
#DIM _COLONY_WORKING_POP_NUM
#DIM _COLONY_TOTAL_POP_NUM
#DIM _DIFF_VAL
#DIM DYNAMIC _HOUSE_COUNT
#DIM DYNAMIC _HOSPIS_COUNT

VARSET COLONY_RESOURCES
VARSET COLONY_DEMAND

CALL GET_POP_NUM(_ID)
_COLONY_WORKING_POP_NUM = RESULT
_COLONY_TOTAL_POP_NUM = COLONY_POP:_ID + _COLONY_WORKING_POP_NUM
;SECTION開放
CALL UPDATE_RELEASED_SECTION(_COLONY_TOTAL_POP_NUM, _ID)

FOR LOCAL, 0, COLONY_MAX_RESOURCE
    SELECTCASE LOCAL
        CASE COLONY_FOOD
            COLONY_DEMAND:_ID:LOCAL = _COLONY_TOTAL_POP_NUM / COLONY_NEED_FOOD_RATE
            FOR _SECTION_ID, 0, MAX_COLONY_SECTION
                CALL FACILITY_COUNT(_ID, _SECTION_ID, COLONY_FOOD_FACTORY)
                COLONY_RESOURCES:_ID:LOCAL += RESULT * 2
            NEXT
        CASE COLONY_MONEY
            ;税収 = 労働人口 * 税率 * 基本税額
            COLONY_RESOURCES:_ID:LOCAL = _COLONY_WORKING_POP_NUM * COLONY_TAX_RATE:_ID * COLONY_PAYMENT_TAX_RATE
            FOR _SECTION_ID, 0, MAX_COLONY_SECTION
                CALL FACILITY_COUNT(_ID, _SECTION_ID, COLONY_COMMERICAL)
                COLONY_RESOURCES:_ID:LOCAL += RESULT * COLONY_COMMERICAL_TAX
            NEXT
        CASE COLONY_SECURITY
            COLONY_DEMAND:_ID:LOCAL = _COLONY_TOTAL_POP_NUM / COLONY_NEED_SECURITY_RATE
            FOR _SECTION_ID, 0, MAX_COLONY_SECTION
                CALL FACILITY_COUNT(_ID, _SECTION_ID, COLONY_RED_LIGHT_DISTRICT)
                COLONY_DEMAND:_ID:LOCAL += RESULT
                CALL FACILITY_COUNT(_ID, _SECTION_ID, COLONY_POLICE)
                COLONY_RESOURCES:_ID:LOCAL += RESULT
                CALL FACILITY_COUNT(_ID, _SECTION_ID, COLONY_COURT)
                COLONY_RESOURCES:_ID:LOCAL += RESULT * 3
            NEXT
        CASE COLONY_HOSPIS
            FOR _SECTION_ID, 0, MAX_COLONY_SECTION
                CALL FACILITY_COUNT(_ID, _SECTION_ID, COLONY_EMBASSY)
                COLONY_RESOURCES:_ID:LOCAL += RESULT
            NEXT
            FOR LOCAL:1, 1, MAX_COUNTRY
                SIF COLONY_DIPLOMACY_SETTING:_ID:(LOCAL:1) != 0
                    _HOSPIS_COUNT ++
            NEXT
            COLONY_DEMAND:_ID:LOCAL = _HOSPIS_COUNT
        CASE COLONY_GROUND_FORCES
            FOR _SECTION_ID, 0, MAX_COLONY_SECTION
                CALL FACILITY_COUNT(_ID, _SECTION_ID, COLONY_STRONGHOLD)
                COLONY_RESOURCES:_ID:LOCAL += RESULT
            NEXT
        CASE COLONY_ENERGY
            COLONY_DEMAND:_ID:LOCAL = _COLONY_TOTAL_POP_NUM / COLONY_NEED_ENERGY_RATE
            FOR _SECTION_ID, 0, MAX_COLONY_SECTION
                CALL FACILITY_COUNT(_ID, _SECTION_ID, COLONY_POWER_PLANT)
                COLONY_RESOURCES:_ID:LOCAL += RESULT * 2
                CALL FACILITY_COUNT(_ID, _SECTION_ID, COLONY_COMMERICAL)
                COLONY_DEMAND:_ID:LOCAL += RESULT
                CALL FACILITY_COUNT(_ID, _SECTION_ID, COLONY_MINE)
                COLONY_DEMAND:_ID:LOCAL += RESULT * 2
                CALL FACILITY_COUNT(_ID, _SECTION_ID, COLONY_OIL_PLANT)
                COLONY_DEMAND:_ID:LOCAL += RESULT * 2
                CALL FACILITY_COUNT(_ID, _SECTION_ID, COLONY_GAS_PLANT)
                COLONY_DEMAND:_ID:LOCAL += RESULT * 2
            NEXT
        CASE COLONY_POP_SATISFACTION
            COLONY_DEMAND:_ID:LOCAL = _COLONY_TOTAL_POP_NUM / COLONY_SATISFACTION_RATE
            FOR _SECTION_ID, 0, MAX_COLONY_SECTION
                CALL FACILITY_COUNT(_ID, _SECTION_ID, COLONY_RED_LIGHT_DISTRICT)
                COLONY_RESOURCES:_ID:LOCAL += RESULT * 3
            NEXT
            FOR _SECTION_ID, 0, MAX_COLONY_SECTION
                CALL FACILITY_COUNT(_ID, _SECTION_ID, COLONY_COMMERICAL)
                COLONY_RESOURCES:_ID:LOCAL += RESULT * 2
            NEXT
            ;エネルギー需要を満たしていないと不満
            _DIFF_VAL = COLONY_RESOURCES:_ID:COLONY_ENERGY - COLONY_DEMAND:_ID:COLONY_ENERGY
            SIF _DIFF_VAL < 0
                COLONY_DEMAND:_ID:LOCAL += _DIFF_VAL
            ;治安により増減
            _DIFF_VAL = COLONY_RESOURCES:_ID:COLONY_SECURITY - COLONY_DEMAND:_ID:COLONY_SECURITY
            IF _DIFF_VAL > 0
                COLONY_RESOURCES:_ID:LOCAL += _DIFF_VAL / COLONY_SATISFACTION_SECURITY_RATE
            ELSEIF _DIFF_VAL < 0
                COLONY_DEMAND:_ID:LOCAL += _DIFF_VAL / COLONY_SATISFACTION_SECURITY_RATE
            ENDIF
            ;家の有無により増減
            FOR _SECTION_ID, 0, MAX_COLONY_SECTION
                CALL FACILITY_COUNT(_ID, _SECTION_ID, COLONY_RESIDENCE)
                _HOUSE_COUNT += RESULT * 3
                CALL FACILITY_COUNT(_ID, _SECTION_ID, COLONY_MANSION)
                _HOUSE_COUNT += RESULT
                COLONY_RESOURCES:_ID:LOCAL += RESULT
            NEXT
            SIF _HOUSE_COUNT < _COLONY_TOTAL_POP_NUM / COLONY_NEED_HOUSE_RATE
                COLONY_DEMAND:_ID:LOCAL += (_COLONY_TOTAL_POP_NUM / COLONY_NEED_HOUSE_RATE) -  _HOUSE_COUNT
            SIF _DIFF_VAL < 0
                COLONY_DEMAND:_ID:LOCAL += _DIFF_VAL
            ;税額により増減
            IF COLONY_TAX_RATE:_ID > COLONY_COMMON_TAX_RATE
                COLONY_DEMAND:_ID:LOCAL += (COLONY_TAX_RATE:_ID - COLONY_COMMON_TAX_RATE) / COLONY_TAX_REACTIVE_RATE
            ELSE
                COLONY_RESOURCES:_ID:LOCAL += (COLONY_COMMON_TAX_RATE - COLONY_TAX_RATE:_ID) / COLONY_TAX_REACTIVE_RATE
            ENDIF
        CASE COLONY_POP_HEALTH
            COLONY_DEMAND:_ID:LOCAL = _COLONY_TOTAL_POP_NUM / COLONY_NEED_HEALTH_RATE
            FOR _SECTION_ID, 0, MAX_COLONY_SECTION
                CALL FACILITY_COUNT(_ID, _SECTION_ID, COLONY_HOSPITAL)
                COLONY_RESOURCES:_ID:LOCAL += RESULT * 2
            NEXT
    ENDSELECT
NEXT

; 現在開放されているセクションをタブとして表示
@SHOW_SECTION_TABS(_ID, _SECTION)
#DIM _ID
#DIM _SECTION

FOR LOCAL, 0, MAX_COLONY_SECTION
    SIF COLONY_RELEASED_SECTION:_ID:LOCAL == 0
        CONTINUE
    SIF _SECTION == LOCAL
        SETCOLOR 0X9999FF
    PRINTBUTTON @"[セクション{LOCAL + 1}]", LOCAL
    RESETCOLOR
    SIF LOCAL % 4 == 3
        PRINTL
NEXT
PRINTL

; 現在設定しているセクションの一覧を表示
@SHOW_BUILT_FACILITY_LIST(_COLONY_ID, _SECTION_ID)
#DIM _COLONY_ID
#DIM _SECTION_ID

FOR LOCAL, 0, MAX_COLONY_BLOCK
    PRINTBUTTON @"[BLOCK - {LOCAL + 1}] %FACILITIES_NAME:(COLONY_FACILITIES:_COLONY_ID:_SECTION_ID:LOCAL)%", LOCAL + MAX_COLONY_SECTION
    PRINTL
NEXT


; 建造物変更
@CHANGE_FACILITY(_COLONY_ID, _SECTION_ID, _BLOCK_ID)
#DIM _COLONY_ID
#DIM _SECTION_ID
#DIM _BLOCK_ID
#DIM _CURRENT_FACILITY
#DIM _INPUT_VALUE

_CURRENT_FACILITY = COLONY_FACILITIES:_COLONY_ID:_SECTION_ID:_BLOCK_ID

DRAWLINE
PRINTL ■ 建造物設定変更
PRINTFORML 　1建造物につき\$100Mかかります(所持金: %TOSTR(MONEY, "$###,##0")%)
DRAWLINE
PRINTL
PRINTFORML 現在このブロックに建てられている建物: %FACILITIES_NAME:_CURRENT_FACILITY%
PRINTL 効果:
CALL SHOW_FACILITY_DESCRIPTION(_CURRENT_FACILITY)
IF (_CURRENT_FACILITY != COLONY_NON_FACILITY || COLONY_POP:_COLONY_ID > 0) && MONEY >= COLONY_BUILD_PRICE
    CALL SHOW_FACILITY_LIST
ELSE
    PRINTW POPあるいは資金が不足しているため新しい建造物を建てられません
    RETURN
ENDIF
INPUT
_INPUT_VALUE = RESULT
IF INRANGE(_INPUT_VALUE, COLONY_STRONGHOLD, VARSIZE("FACILITIES_NAME") - 1)
    ;前提条件がある建物の前提条件チェック
    SELECTCASE _INPUT_VALUE
        CASE COLONY_COURT
            CALL FACILITY_COUNT(_COLONY_ID, _SECTION_ID, COLONY_POLICE)
            IF RESULT == 0
                PRINTW 裁判所の建設には警察署が同セクション内に建設されている必要があります
                RETURN
            ENDIF
    ENDSELECT
    ;建設
    SIF COLONY_FACILITIES:_COLONY_ID:_SECTION_ID:_BLOCK_ID == COLONY_NON_FACILITY
        COLONY_POP:_COLONY_ID -= 1
    COLONY_FACILITIES:_COLONY_ID:_SECTION_ID:_BLOCK_ID = _INPUT_VALUE
    MONEY -= COLONY_BUILD_PRICE
ENDIF

; 建造物の説明を表示
@SHOW_FACILITY_DESCRIPTION(_FACILITY)
#DIM _FACILITY

SELECTCASE _FACILITY
    CASE COLONY_NON_FACILITY
        PRINTL なし
    CASE COLONY_STRONGHOLD
        PRINTL 1POPが地上軍として陸戦時に使えるようになる
    CASE COLONY_POWER_PLANT
        PRINTL インフラで使用される電力を生産する
    CASE COLONY_FOOD_FACTORY
        PRINTL POPが消費する食料を生産する
    CASE COLONY_RESIDENCE
        PRINTL POPが住む住居を提供する
    CASE COLONY_MANSION
        PRINTL POPが住む住居を提供する。通常の住居より満足度が高い
    CASE COLONY_HOSPITAL
        PRINTL POPの健康を上昇させる
    CASE COLONY_POLICE
        PRINTL 治安が上昇する
    CASE COLONY_COURT
        PRINTL 治安が大幅に上昇する。建造には警察署が必要。
    CASE COLONY_EMBASSY
        PRINTL 国家間の交流が行える。大使館一つに付き、一つの国との好感度改善が実施可能
    CASE COLONY_COMMERICAL
        PRINTL POPの満足度上昇と税収を得るが、余剰電力を必要とする
    CASE COLONY_RED_LIGHT_DISTRICT
        PRINTL POPの満足度を大幅に上げるが、治安が低下する
    CASE COLONY_MINE
        PRINTL 宇宙を漂う岩石、あるいは地上の鉱物エリアから鉱物を生産する。余剰電力を必要とする
    CASE COLONY_OIL_PLANT
        PRINTL 合成オイルを生成する。余剰電力を必要とする
    CASE COLONY_GAS_PLANT
        PRINTL ガスを生成する。余剰電力を必要とする
    CASE COLONY_IMMIGRATION_OFFICE
        PRINTL ターン毎のPOP増加量を増す。移民の購入もできる
ENDSELECT

;選択可能な建物一覧を表示
@SHOW_FACILITY_LIST
PRINTL
FOR LOCAL, COLONY_STRONGHOLD, VARSIZE("FACILITIES_NAME")
    PRINTBUTTON @"[{LOCAL}] %FACILITIES_NAME:LOCAL, 14, LEFT%", LOCAL
    PRINT ：
    CALL SHOW_FACILITY_DESCRIPTION(LOCAL)
    PRINTL
NEXT

;建築物の存在判定
@FACILITY_COUNT(_COLONY_ID, _SECTION_ID, _FACILITY)
#DIM _COLONY_ID
#DIM _SECTION_ID
#DIM _FACILITY
#DIM DYNAMIC _COUNT

FOR LOCAL, 0, MAX_COLONY_BLOCK
    IF COLONY_FACILITIES:_COLONY_ID:_SECTION_ID:LOCAL == _FACILITY
        _COUNT ++
        CONTINUE
    ENDIF
NEXT
RETURN _COUNT

;総POP数取得
@GET_POP_NUM(_ID)
#DIM _ID
#DIM _SECTION_ID
#DIM _BLOCK_ID
#DIM DYNAMIC _COUNT

FOR _SECTION_ID, 0, MAX_COLONY_SECTION
    SIF COLONY_RELEASED_SECTION:_ID:_SECTION_ID == 0
        CONTINUE
    FOR _BLOCK_ID, 0, MAX_COLONY_BLOCK
        SIF COLONY_FACILITIES:_ID:_SECTION_ID:_BLOCK_ID == COLONY_NON_FACILITY
            CONTINUE
        _COUNT ++
    NEXT
NEXT

RETURN _COUNT

@TURNEND_COLONY
#DIM _COLONY_ID

FOR _COLONY_ID, 0, MAX_COLONY
    ;未開発のコロニーはスキップ
    SIF COLONY_OWNER:_COLONY_ID == 0
        CONTINUE
    CALL UPDATE_COLONY(_COLONY_ID)

    ;POP増加
    COLONY_NEXT_POP:_COLONY_ID += COLONY_RESOURCES:_COLONY_ID:COLONY_POP_HEALTH - COLONY_DEMAND:_COLONY_ID:COLONY_POP_HEALTH
    COLONY_NEXT_POP:_COLONY_ID += COLONY_RESOURCES:_COLONY_ID:COLONY_POP_SATISFACTION - COLONY_DEMAND:_COLONY_ID:COLONY_POP_SATISFACTION

    FOR LOCAL, 0, MAX_COLONY_SECTION
        CALL FACILITY_COUNT(_COLONY_ID, LOCAL, COLONY_IMMIGRATION_OFFICE)
        COLONY_NEXT_POP:_COLONY_ID += RESULT * 20
    NEXT
    IF COLONY_NEXT_POP >= NEW_POP_RATE
        COLONY_POP:_COLONY_ID += COLONY_NEXT_POP:_COLONY_ID / NEW_POP_RATE
        COLONY_NEXT_POP:_COLONY_ID = COLONY_NEXT_POP:_COLONY_ID % NEW_POP_RATE
    ENDIF

    ;SECTION開放
    CALL UPDATE_RELEASED_SECTION(COLONY_POP:_COLONY_ID + RESULT, _COLONY_ID)

    ;税金
    CALL COLONY_TAX_COLLECTION(_COLONY_ID)

    ;コロニーの算出物取得
    CALL COLONY_RESOURCE_GATHERING(_COLONY_ID)

    ;国交
    CALL COLONY_DIPLOMACY(_COLONY_ID)
NEXT

;コロニーシップ建造時の処理
@BUILD_COLONY_SHIP(_SHIP_ID)
#DIM _SHIP_ID
#DIM _POP

FOR LOCAL, 0, MAX_COLONY
    SIF COLONY_OWNER:LOCAL != 0
        CONTINUE
    $INPUT_LOOP
    PRINTFORML 1POP \$10M で 5POP まで購入できます。何POP購入しますか？
    IF MONEY < COLONY_POP_PRICE
        PRINTFORMW 金額不足でPOPを購入できませんでした。POPが用意できないため処理はキャンセルされます
        RETURN 0
    ENDIF
    FOR _POP, 1, 6
        SIF MONEY < _POP * COLONY_POP_PRICE
            BREAK
        PRINTBUTTON @"[{_POP}] {_POP}POP", _POP
        PRINTL
    NEXT
    PRINTL [9] キャンセル
    INPUT
    IF RESULT == 9
        PRINTFORMW 処理はキャンセルされます
    ELSEIF RESULT > 0 && RESULT < 6 && MONEY >= COLONY_POP * RESULT
        COLONY_POP:LOCAL = RESULT
        MONEY -= COLONY_POP * RESULT
        PRINTFORMW {RESULT}POP購入しました
    ELSE
        GOTO INPUT_LOOP
    ENDIF
    COLONY_SHIP_ID:LOCAL = _SHIP_ID
    COLONY_OWNER:LOCAL = SHIP_COUNTRY:_SHIP_ID
    COLONY_RELEASED_SECTION:LOCAL:0 = 1
    COLONY_TAX_RATE:LOCAL = 10
    RETURN 1
NEXT

@UPDATE_RELEASED_SECTION(_POP_NUM, _COLONY_ID)
#DIM _POP_NUM
#DIM _COLONY_ID
FOR LOCAL, 0, MIN(1 + (_POP_NUM / COLONY_NEED_POP_RELEASE_SECTION), MAX_COLONY_SECTION)
    SIF COLONY_RELEASED_SECTION:_COLONY_ID:LOCAL == 0
        COLONY_RELEASED_SECTION:_COLONY_ID:LOCAL = 1
NEXT

; コロニー船の上にいるか
@ON_THE_COLONY(_POSID)
#DIM _POSID
FOR LOCAL, 0, MAX_COLONY
    SIF COLONY_OWNER:LOCAL != 国家ID_プレイヤー
        CONTINUE
    IF INRANGE(COLONY_BELONGING_STAR:LOCAL, 0, MAX_STAR - 1) && STAR_POSITION_ID:(COLONY_BELONGING_STAR:LOCAL) == _POSID
        RETURN 1
    ELSEIF SHIP_POSITION:(COLONY_SHIP_ID:LOCAL) == _POSID
        RETURN 2
    ENDIF
NEXT
RETURN 0

;現在地のコロニーリスト表示
@SELECT_ON_COLONIES(_POSID, _MESSAGE = "編集するコロニーを選んでください")
#DIM _POSID
#DIMS _MESSAGE
#DIM _INPUT_VALUE

PRINTL
DRAWLINE
PRINTFORML %_MESSAGE%
DRAWLINE
PRINTL
FOR LOCAL, 0, MAX_COLONY
    SIF COLONY_OWNER:LOCAL != 国家ID_プレイヤー
        CONTINUE
    IF INRANGE(COLONY_BELONGING_STAR:LOCAL, 0, MAX_STAR - 1) && STAR_POSITION_ID:(COLONY_BELONGING_STAR:LOCAL) == _POSID
        PRINTBUTTON @"[{LOCAL}] %STAR_NAME:LOCAL% 所属コロニー", LOCAL
        PRINTL
    ELSEIF SHIP_POSITION:(COLONY_SHIP_ID:LOCAL) == _POSID
        PRINTBUTTON @"[{LOCAL}] %SHIP_NAME:(COLONY_SHIP_ID:LOCAL)% コロニー", LOCAL
        PRINTL
    ENDIF
NEXT

INPUT
_INPUT_VALUE = RESULT
IF INRANGE(COLONY_BELONGING_STAR:_INPUT_VALUE, 0, MAX_STAR - 1) && STAR_POSITION_ID:(COLONY_BELONGING_STAR:_INPUT_VALUE) == _POSID
    RETURN _INPUT_VALUE
ELSEIF SHIP_POSITION:(COLONY_SHIP_ID:_INPUT_VALUE) == _POSID
    RETURN _INPUT_VALUE
ENDIF
RESTART

; 税収
@COLONY_TAX_COLLECTION(_COLONY_ID)
#DIM _COLONY_ID
#DIM DYNAMIC _TAX_RATE
#DIM _COLONY_TOTAL_POP_NUM
#DIM _COLONY_PAYMENT_TAX

_TAX_RATE += (COLONY_RESOURCES:_COLONY_ID:COLONY_POP_SATISFACTION - COLONY_DEMAND:_COLONY_ID:COLONY_POP_SATISFACTION)
_TAX_RATE += (COLONY_RESOURCES:_COLONY_ID:COLONY_POP_HEALTH - COLONY_DEMAND:_COLONY_ID:COLONY_POP_HEALTH)
SIF _TAX_RATE < 0
    _TAX_RATE = POWER(_TAX_RATE, 2) * -1
CALL GET_POP_NUM(_COLONY_ID)
_COLONY_TOTAL_POP_NUM = COLONY_POP:_COLONY_ID + RESULT
COLONY_RESOURCES:_COLONY_ID:COLONY_MONEY = COLONY_RESOURCES:_COLONY_ID:COLONY_MONEY + (_TAX_RATE * _COLONY_TOTAL_POP_NUM * COLONY_PAYMENT_TAX_RATE)

IF COLONY_OWNER:_COLONY_ID == 国家ID_プレイヤー
    MONEY += COLONY_RESOURCES:_COLONY_ID:COLONY_MONEY - COLONY_DEMAND:_COLONY_ID:COLONY_MONEY
    DRAWLINE
    IF (COLONY_RESOURCES:_COLONY_ID:COLONY_MONEY) > 0
        PRINTFORML コロニーからの税収: %TOSTR(COLONY_RESOURCES:_COLONY_ID:COLONY_MONEY - COLONY_DEMAND:_COLONY_ID:COLONY_MONEY, "$###,##0")%
    ELSEIF COLONY_RESOURCES:_COLONY_ID:COLONY_MONEY
        PRINTFORML コロニーの維持費として: %TOSTR(COLONY_RESOURCES:_COLONY_ID:COLONY_MONEY - COLONY_DEMAND:_COLONY_ID:COLONY_MONEY, "$###,##0")%
    ENDIF
ELSE
    MONEY:(COLONY_OWNER:_COLONY_ID) += COLONY_RESOURCES:_COLONY_ID:COLONY_MONEY - COLONY_DEMAND:_COLONY_ID:COLONY_MONEY
ENDIF

; 資源収集
@COLONY_RESOURCE_GATHERING(_COLONY_ID)
#DIM _COLONY_ID
#DIM _SECTION_ID
#DIM DYNAMIC _MINE_COUNT
#DIM DYNAMIC _OIL_PLANT_COUNT
#DIM DYNAMIC _GAS_PLANT_COUNT
#DIM DYNAMIC _ENERGY

_ENERGY = COLONY_RESOURCES:_COLONY_ID:COLONY_ENERGY - COLONY_DEMAND:_COLONY_ID:COLONY_ENERGY 

FOR _SECTION_ID, 0, MAX_COLONY_SECTION
    CALL FACILITY_COUNT(_COLONY_ID, _SECTION_ID, COLONY_MINE)
    _MINE_COUNT += RESULT
    CALL FACILITY_COUNT(_COLONY_ID, _SECTION_ID, COLONY_OIL_PLANT)
    _OIL_PLANT_COUNT += RESULT
    CALL FACILITY_COUNT(_COLONY_ID, _SECTION_ID, COLONY_GAS_PLANT)
    _GAS_PLANT_COUNT += RESULT
NEXT
IF _ENERGY < 0
    IF COLONY_OWNER:_COLONY_ID == 国家ID_プレイヤー && (_MINE_COUNT || _OIL_PLANT_COUNT || _GAS_PLANT_COUNT) 
        PRINTFORML ※電力不足で稼働できなかったコロニー上の施設があります
    ENDIF
    RETURN
ENDIF

;鉱石
COLONY_CONTAINER:_COLONY_ID:赤原石 += _MINE_COUNT * GET_RESOURCE_RAIT()
COLONY_CONTAINER:_COLONY_ID:青原石 += _MINE_COUNT * GET_RESOURCE_RAIT()
COLONY_CONTAINER:_COLONY_ID:貴金属鉱石 += _MINE_COUNT * GET_RESOURCE_RAIT()

;オイル
COLONY_CONTAINER:_COLONY_ID:エウスガス += _OIL_PLANT_COUNT * GET_RESOURCE_RAIT()
COLONY_CONTAINER:_COLONY_ID:ビクニーガス += _OIL_PLANT_COUNT * GET_RESOURCE_RAIT()
COLONY_CONTAINER:_COLONY_ID:ゼスタガス += _OIL_PLANT_COUNT * GET_RESOURCE_RAIT()

;ガス
COLONY_CONTAINER:_COLONY_ID:チェルニーオイル += _GAS_PLANT_COUNT * GET_RESOURCE_RAIT()
COLONY_CONTAINER:_COLONY_ID:グリッドオイル += _GAS_PLANT_COUNT * GET_RESOURCE_RAIT()
COLONY_CONTAINER:_COLONY_ID:ジェルオイル += _GAS_PLANT_COUNT * GET_RESOURCE_RAIT()

;資源徴収
@COLONY_COLLECT_RESOURCES(_COLONY_ID)
#DIM _COLONY_ID
#DIM DYNAMIC _CONTAINER_SHIP_ITEM, ITEM_LENGTH
#DIM _COLONY_POSITION
#DIM _TRANSPORTATION_AMOUNT
#DIM _INPUT_VALUE

IF COLONY_BELONGING_STAR:_COLONY_ID != -1
    _COLONY_POSITION = STAR_POSITION_ID:(COLONY_BELONGING_STAR:_COLONY_ID)
ELSE
    _COLONY_POSITION = SHIP_POSITION:(COLONY_SHIP_ID:_COLONY_ID)
ENDIF


DRAWLINE
PRINTL ■資源徴収
DRAWLINE
LOCALS = %"資源", 18, LEFT%
PRINTFORM %LOCALS%
FOR LOCAL, 0, MAX_STAR
    SIF STAR_OWNER:LOCAL != COLONY_OWNER:_COLONY_ID
        CONTINUE
    SIF STAR_POSITION_ID:LOCAL != _COLONY_POSITION
        CONTINUE
    PRINTFORML %STAR_NAME:LOCAL% 星系港コンテナ {GET_MAX_STAR_PORT_CARGO_CAPACITY(LOCAL) - GET_TOTAL_ITEM_NUM_IN_STAR_PORT(LOCAL), 7, RIGHT}
NEXT
FOR LOCAL, 0, MAX_PORT
    SIF PORT_OWNER:LOCAL != COLONY_OWNER:_COLONY_ID
        CONTINUE
    SIF PORT_POSITION_ID:LOCAL != _COLONY_POSITION
        CONTINUE
    PRINTFORML %STAR_NAME:LOCAL% 宇宙港コンテナ {GET_MAX_PORT_CARGO_CAPACITY(LOCAL) - GET_TOTAL_ITEM_NUM_IN_PORT(LOCAL), 7, RIGHT}
NEXT
FOR LOCAL, 0, MAX_SHIP
    SIF SHIP_COUNTRY:LOCAL != COLONY_OWNER:_COLONY_ID
        CONTINUE
    SIF SHIP_POSITION:LOCAL != _COLONY_POSITION
        CONTINUE
    CALL GET_VACANT_CARGO_SIZE(LOCAL)
    PRINTFORML %SHIP_NAME:LOCAL%コンテナ {RESULT, 5, RIGHT}
NEXT

FOR LOCAL, 0, 9
    TRYCALLFORM GET_ITEM_NAME_{LOCAL}
    PRINTFORM %RESULTS, 18%
    PRINTFORM {_CONTAINER_SHIP_ITEM:LOCAL, 6, RIGHT} 
    PRINTBUTTON "[<<<<] ", LOCAL * 10 + 0
    PRINTBUTTON "[<<<] ", LOCAL * 10 + 1
    PRINTBUTTON "[<<] ", LOCAL * 10 + 2
    PRINTBUTTON "[<] ", LOCAL * 10 + 3
    PRINTBUTTON "[>] ", LOCAL * 10 + 4
    PRINTBUTTON "[>>] ", LOCAL * 10 + 5
    PRINTBUTTON "[>>>] ", LOCAL * 10 + 6
    PRINTBUTTON "[>>>>] ", LOCAL * 10 + 7
    PRINTFORML {COLONY_CONTAINER:_COLONY_ID:LOCAL, 10, RIGHT} 
NEXT
PRINTL [999] 戻る

INPUT
_INPUT_VALUE = RESULT
IF _INPUT_VALUE % 10 == 0 && GROUPMATCH(_INPUT_VALUE / 10, 0, 8)
    IF COLONY_CONTAINER:_COLONY_ID:(_INPUT_VALUE/10) > 1000
        _TRANSPORTATION_AMOUNT = 1000
    ELSE
        _TRANSPORTATION_AMOUNT = COLONY_CONTAINER:_COLONY_ID:(_INPUT_VALUE/10)
    ENDIF
ELSEIF _INPUT_VALUE % 10 == 1 && GROUPMATCH(_INPUT_VALUE / 10, 0, 8)
    IF COLONY_CONTAINER:_COLONY_ID:(_INPUT_VALUE/10) > 100
        _TRANSPORTATION_AMOUNT = 100
    ELSE
        _TRANSPORTATION_AMOUNT = COLONY_CONTAINER:_COLONY_ID:(_INPUT_VALUE/10)
    ENDIF
ELSEIF _INPUT_VALUE % 10 == 2 && GROUPMATCH(_INPUT_VALUE / 10, 0, 8)
    IF COLONY_CONTAINER:_COLONY_ID:(_INPUT_VALUE/10) > 10
        _TRANSPORTATION_AMOUNT = 10
    ELSE
        _TRANSPORTATION_AMOUNT = COLONY_CONTAINER:_COLONY_ID:(_INPUT_VALUE/10)
    ENDIF
ELSEIF _INPUT_VALUE % 10 == 3 && GROUPMATCH(_INPUT_VALUE / 10, 0, 8)
    IF COLONY_CONTAINER:_COLONY_ID:(_INPUT_VALUE/10) > 1
        _TRANSPORTATION_AMOUNT = 1
    ELSE
        _TRANSPORTATION_AMOUNT = 0
    ENDIF
ELSEIF _INPUT_VALUE % 10 == 4 && GROUPMATCH(_INPUT_VALUE / 10, 0, 8)
    IF GET_RESOURCE_NUM(_COLONY_POSITION, COLONY_OWNER:_COLONY_ID, _INPUT_VALUE / 10) > 1
        _TRANSPORTATION_AMOUNT = -1
    ELSE
        _TRANSPORTATION_AMOUNT = 0
    ENDIF
ELSEIF _INPUT_VALUE % 10 == 5 && GROUPMATCH(_INPUT_VALUE / 10, 0, 8)
    _TRANSPORTATION_AMOUNT = GET_RESOURCE_NUM(_COLONY_POSITION, COLONY_OWNER:_COLONY_ID, _INPUT_VALUE / 10)
    SIF _TRANSPORTATION_AMOUNT > 10
        _TRANSPORTATION_AMOUNT = -10
ELSEIF _INPUT_VALUE % 10 == 6 && GROUPMATCH(_INPUT_VALUE / 10, 0, 8)
    _TRANSPORTATION_AMOUNT = GET_RESOURCE_NUM(_COLONY_POSITION, COLONY_OWNER:_COLONY_ID, _INPUT_VALUE / 10)
    SIF _TRANSPORTATION_AMOUNT > 100
        _TRANSPORTATION_AMOUNT = -100
ELSEIF _INPUT_VALUE % 10 == 7 && GROUPMATCH(_INPUT_VALUE / 10, 0, 8)
    _TRANSPORTATION_AMOUNT = GET_RESOURCE_NUM(_COLONY_POSITION, COLONY_OWNER:_COLONY_ID, _INPUT_VALUE / 10)
    SIF _TRANSPORTATION_AMOUNT > 1000
        _TRANSPORTATION_AMOUNT = -1000
ELSEIF _INPUT_VALUE == 999
    RETURN
ENDIF
IF _TRANSPORTATION_AMOUNT > 0
    COLONY_CONTAINER:_COLONY_ID:(_INPUT_VALUE/10) -= _TRANSPORTATION_AMOUNT
    CALL ADD_RESOURCE(_COLONY_POSITION, COLONY_OWNER:_COLONY_ID, _INPUT_VALUE / 10, _TRANSPORTATION_AMOUNT)
    SIF RESULT
        COLONY_CONTAINER:_COLONY_ID:(_INPUT_VALUE/10) += RESULT
ELSEIF _TRANSPORTATION_AMOUNT < 0
    COLONY_CONTAINER:_COLONY_ID:(_INPUT_VALUE/10) += ABS(_TRANSPORTATION_AMOUNT)
    LOCAL = REMOVE_RESOURCE(_COLONY_POSITION, COLONY_OWNER:_COLONY_ID, _INPUT_VALUE/ 10, ABS(_TRANSPORTATION_AMOUNT))
    COLONY_CONTAINER:_COLONY_ID:(_INPUT_VALUE/10) -= LOCAL
ENDIF
RESTART

;GET_RESOURCE_RAIT
@GET_RESOURCE_RAIT
#FUNCTION
LOCAL = GATHERING_RESOURCE_RAIT
SIF IS_COMPLETE_TECH:6
    LOCAL += 5
SIF IS_COMPLETE_TECH:19
    LOCAL += 5
SIF IS_COMPLETE_TECH:25
    LOCAL += 5
RETURNF LOCAL

; コロニーの税金設定
@COLONY_TAX_SETTING(_ID)
#DIM _ID

PRINTL 現在の設定
PRINTFORML [税収]{COLONY_RESOURCES:_ID:COLONY_MONEY} [税率]{COLONY_TAX_RATE:_ID}\%
PRINTL
PRINTL 新しい税率を入力してください(0~100)
PRINTL [999] 戻る
INPUT
IF RESULT >= 0 && RESULT <= 100
    COLONY_TAX_RATE:_ID = RESULT
    CALL UPDATE_COLONY(_ID)
ELSEIF RESULT == 999
    RETURN
ENDIF
RESTART


; 外交設定
@DIPLOMACY_SETTING(_ID)
#DIM _ID
#DIM _HOSPIS_COUNT
#DIM _INPUT_VALUE

_HOSPIS_COUNT = COLONY_RESOURCES:_ID:COLONY_HOSPIS - COLONY_DEMAND:_ID:COLONY_HOSPIS

FOR LOCAL, 1, MAX_COUNTRY
    SIF GROUPMATCH(LOCAL, 国家ID_プレイヤー, 国家ID_一般海賊, 国家ID_レジスタンス, 国家ID_慈悲の宙族団, 国家ID_赤の傭兵集団)
        CONTINUE
    PRINTBUTTON @"[{LOCAL}] %COUNTRY_NAME:LOCAL, 20, LEFT% 目標外交値: {COLONY_DIPLOMACY_SETTING:_ID:LOCAL}", LOCAL
    PRINT 　
    PRINTBUTTON "[リセット]", LOCAL * MAX_COUNTRY
    PRINTL
NEXT
PRINTL [999] 戻る

INPUT
_INPUT_VALUE = RESULT
IF _INPUT_VALUE == 999
    RETURN
ELSEIF _INPUT_VALUE > 0 && _INPUT_VALUE < MAX_COUNTRY
    IF COLONY_DIPLOMACY_SETTING:_ID:_INPUT_VALUE == 0 && _HOSPIS_COUNT <= 0
        PRINTW 大使館を複数立てることで複数の国家との国交を開くことができます
        RESTART
    ENDIF
    $INPUT_LOOP
    PRINTL 目標外交値を入力してください
    INPUT
    IF RESULT != 0 && INRANGE(RESULT, -1000, 1000)
        COLONY_DIPLOMACY_SETTING:_ID:_INPUT_VALUE = RESULT
        CALL UPDATE_COLONY(_ID)
    ELSE
        CLEARLINE 1
        GOTO INPUT_LOOP
    ENDIF
ELSEIF INRANGE(_INPUT_VALUE/MAX_COUNTRY, 1, MAX_COUNTRY - 1)
    COLONY_DIPLOMACY_SETTING:_ID:(_INPUT_VALUE/MAX_COUNTRY) = 0
    CALL UPDATE_COLONY(_ID)
ENDIF
RESTART

@COLONY_DIPLOMACY(_ID)
#DIM _ID

FOR LOCAL, 0, MAX_COUNTRY
    IF COLONY_DIPLOMACY_SETTING:_ID:LOCAL > 0
        CALL ADD_REL_LIKE_COUNTRY(LOCAL, COLONY_OWNER:_ID, 1)
    ELSEIF COLONY_DIPLOMACY_SETTING:_ID:LOCAL < 0
        CALL ADD_REL_LIKE_COUNTRY(LOCAL, COLONY_OWNER:_ID, -1)
    ENDIF
NEXT
