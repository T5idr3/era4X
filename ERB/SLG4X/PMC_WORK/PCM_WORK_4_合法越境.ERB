;----------------------------
; 合法越境
;----------------------------

;----------------------------
; 受注可能か
;----------------------------
@PMC_QUEST_ACCEPTABLE_4
CALL GET_STAR_ID_FROM_POS(CHARACTER_POSITION)
RETURN RESULT == 7 && STAR_OWNER:RESULT == 国家ID_民主主義

;----------------------------
; クエスト名
;----------------------------
@PMC_QUEST_NAME_4
RESULTS = 合法越境(難易度4~)
RETURN

;----------------------------
; クエスト本体
;----------------------------
@PMC_QUEST_BODY_4
#DIM _BATTLE_UNIT_ID
#DIM DYNAMIC _RETIER
#DIM DYNAMIC _SCORE
#DIM DYNAMIC _LINE_CHAN
#DIMS _DIC, MAX_PMC_UNIT, MAX_PMC_UNIT_MEMBER
#DIM _EXP

FOR LOCAL, 0, MAX_PMC_UNIT
  VARSET _DIC:LOCAL:0, "-1"
NEXT

PRINTFORML ブリーフィングを受けますか？
CALL ASK_YN
SIF !RESULT
  CALL PMC_QUEST_BRIEFING_4
CALL PMC_EDIT_UNIT

PRINTFORML 現場は二つのマフィアとPMCの三つ巴と化していた
PRINTFORML 銃声と悲鳴が満ち溢れており、とてもじゃないが商業地区だとは思えない
PRINTFORMW %CALLNAME:MASTER%達は準備を整えると、乗り気のしない戦場へと足を踏み入れた
PRINTL ………………
PRINTL …………
PRINTL ……
CALL SELECT_PMC_BATTLE_UNIT
_BATTLE_UNIT_ID = RESULT
IF _BATTLE_UNIT_ID == -1
  PRINTFORMW 戦闘に参加できるユニットがいない
  PRINTFORMW ユニットを編成してから再度クエストを受けよう
  RETURN
ENDIF

WHILE !_RETIER
  ;白兵戦参加メンバーの初期化
  CALL CC_INIT()
  ;前の戦いの記録が残っていたら上書き
  IF _DIC:_BATTLE_UNIT_ID:0 != "-1" && _LINE_CHAN == 1
    FOR LOCAL, 0, FINDELEMENT(CC_ATTACKER_LIST, "-1")
      CC_ATTACKER_LIST:LOCAL = %DIC_SET(CC_ATTACKER_LIST:LOCAL, "体力", DIC_GET(_DIC:_BATTLE_UNIT_ID:LOCAL, "体力"))%
      CC_ATTACKER_LIST:LOCAL = %DIC_SET(CC_ATTACKER_LIST:LOCAL, "状態", "待機")%
    NEXT
  ELSE
    FOR LOCAL, 0, CHARANUM
      SIF UNIT_ID:LOCAL == _BATTLE_UNIT_ID
        CALL CC_ADDCHARA("ATTACKER", LOCAL)
    NEXT
    IF _DIC:_BATTLE_UNIT_ID:0 != "-1"
      FOR LOCAL, 0, FINDELEMENT(CC_ATTACKER_LIST, "-1")
        CC_ATTACKER_LIST:LOCAL = %DIC_SET(CC_ATTACKER_LIST:LOCAL, "体力", DIC_GET(_DIC:_BATTLE_UNIT_ID:LOCAL, "体力"))%
        CC_ATTACKER_LIST:LOCAL = %DIC_SET(CC_ATTACKER_LIST:LOCAL, "状態", "待機")%
      NEXT
    ENDIF 
  ENDIF
  SELECTCASE RAND:3
    CASE 1
      CALL CC_ADDMOB("DEFENDER", "マフィア1", 1500, 1500, 55)
      CALL CC_ADDMOB("DEFENDER", "マフィア2", 1500, 1500, 55)
      CALL CC_ADDMOB("DEFENDER", "マフィア3", 1500, 1500, 55)
      CALL CC_ADDMOB("DEFENDER", "マフィア4", 1500, 1500, 55)
      CALL CC_ADDMOB("DEFENDER", "エルヴィンMK11思考戦車1", 8000, 8000, 90)
      CALL CC_ADDMOB("DEFENDER", "エルヴィンMK11思考戦車2", 8000, 8000, 90)
      _EXP = 15
      _SCORE += POWER(55, 2) * 4 + POWER(90 + 80, 2) * 2
    CASE 2
      CALL CC_ADDMOB("DEFENDER", "マフィア1", 1500, 1500, 55)
      CALL CC_ADDMOB("DEFENDER", "マフィア2", 1500, 1500, 55)
      CALL CC_ADDMOB("DEFENDER", "マフィア3", 1500, 1500, 55)
      CALL CC_ADDMOB("DEFENDER", "マフィア4", 1500, 1500, 55)
      CALL CC_ADDMOB("DEFENDER", "エルヴィン多目的メック1", 5000, 5000, 90)
      CALL CC_ADDMOB("DEFENDER", "エルヴィン多目的メック2", 5000, 5000, 90)
      _EXP = 15
      _SCORE += POWER(55, 2) * 4 + POWER(90 + 50, 2) * 2
    CASE 1
      CALL CC_ADDMOB("DEFENDER", "マフィア1", 1500, 1500, 55)
      CALL CC_ADDMOB("DEFENDER", "マフィア2", 1500, 1500, 55)
      CALL CC_ADDMOB("DEFENDER", "マフィア3", 1500, 1500, 55)
      CALL CC_ADDMOB("DEFENDER", "マフィア4", 1500, 1500, 55)
      CALL CC_ADDMOB("DEFENDER", "陸軍2型アームスーツ1", 5000, 5000, 70)
      CALL CC_ADDMOB("DEFENDER", "陸軍2型アームスーツ2", 5000, 5000, 70)
      _EXP = 15
      _SCORE += POWER(55, 2) * 4 + POWER(50 + 70, 2) * 2
  ENDSELECT
  ;白兵戦イベント呼出
  CALL CLOSE_COMBAT()
  SIF RESULT == 2
    CALL GAMEOVER_46
  CALL CC_ADD_COMBAT_EXP(_EXP)
  FOR LOCAL, 0, FINDELEMENT(CC_ATTACKER_LIST, "-1")
    _DIC:_BATTLE_UNIT_ID:LOCAL = %CC_ATTACKER_LIST:LOCAL%
  NEXT

  DRAWLINE
  PRINTBUTTON "[連戦]", 0
  PRINTBUTTON "[チーム変更]", 1
  PRINTBUTTON "[撤退]", 3
  PRINTL
  DRAWLINE
  INPUT
  _LINE_CHAN = 0
  IF RESULT == 0
    ;連戦フラグオン
    CALL CC_SET_FLAG(LINE_CHAN, "1")
    _LINE_CHAN = 1
  ELSEIF RESULT == 1
    CALL SELECT_PMC_BATTLE_UNIT
    _BATTLE_UNIT_ID = RESULT
  ELSE
    _RETIER = 1
  ENDIF
WEND
DRAWLINE
IF _SCORE == 0
  LOCALS = $0
ELSE
  LOCALS = %TOSTR(_SCORE, "$###,###")%
ENDIF
PRINTFORMW 報酬として%LOCALS%手に入れた。
MONEY += _SCORE
DAY ++

;----------------------------
; クエスト説明
;----------------------------
@PMC_QUEST_BRIEFING_4
SETCOLOR 0X666666
CUSTOMDRAWLINE ///
RESETCOLOR
PRINTFORML フロンティアはUSFが入植した巨大惑星だ
PRINTFORML 住む場所には困らない広さなものの、高重力化による建設ノウハウの確立が遅れ開発が遅れている星でもある
PRINTFORMW その間にマフィアの土地利権を求めた抗争が激化し、建物を建てるときは国より先に現地マフィアに話をつけるのが慣例になっているところまである
PRINTL
PRINTFORML 件の大型商業ビルは厄介なことに二つのマフィアの縄張りを跨いでおり、利権の独占をもくろむ二つのマフィアの抗争が以前にもまして激化
PRINTFORML メックや思考戦車まで持ち出して争いだしたことで現地警察では手がつけられなくなり、PMCが投入されることになった
PRINTFORMW 依頼主が地方政府のため、ミッションの難易度の割りに報酬が少ないので、危ないと思ったらすぐに引いたほうがいいだろう
PRINTL
PRINTFORMW 具体的なミッションは両マフィアの損耗。戦闘車両を撃破し、可能な限りの人員を削ることでなし崩し的に土地利権の独占を諦めさせることが目的だ
SETCOLOR 0X666666
CUSTOMDRAWLINE ///
RESETCOLOR


