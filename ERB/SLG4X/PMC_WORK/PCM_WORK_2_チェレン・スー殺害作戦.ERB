;----------------------------
; チェレン・スー殺害作戦
;----------------------------

;----------------------------
; 受注可能か
;----------------------------
@PMC_QUEST_ACCEPTABLE_2
CALL GET_STAR_ID_FROM_POS(CHARACTER_POSITION)
RETURN RESULT == 6 && STAR_OWNER:6 == 国家ID_民主主義 && PMC_QUEST_PROGRESS:2 == 0

;----------------------------
; クエスト名
;----------------------------
@PMC_QUEST_NAME_2
RESULTS = チェレン・スー殺害作戦(難易度1~良心の呵責に注意)
RETURN

;----------------------------
; クエスト本体
;----------------------------
@PMC_QUEST_BODY_2
#DIM _BATTLE_UNIT_ID
#DIM DYNAMIC _RETIER
#DIM DYNAMIC _TURN
#DIM DYNAMIC _LINE_CHAN
#DIMS _DIC, MAX_PMC_UNIT, MAX_PMC_UNIT_MEMBER
#DIM _EXP

FOR LOCAL, 0, MAX_PMC_UNIT
  VARSET _DIC:LOCAL:0, "-1"
NEXT

PRINTFORML ブリーフィングを受けますか？
CALL ASK_YN
SIF !RESULT
  CALL PMC_QUEST_BRIEFING_2
CALL PMC_EDIT_UNIT

PRINTFORML 現場は阿鼻叫喚の地獄だった
PRINTFORML 電脳をハックされた民間人が別の民間人を襲い、止めに入った警官まで電脳を犯され暴走する有様だ
PRINTFORMW もはや誰が敵なのかわからない状況の中、送られてくるチェレンの居場所だけを頼りに%CALLNAME:MASTER%の部隊は移動を開始した
PRINTL ………………
PRINTL …………
PRINTL ……
CALL SELECT_PMC_BATTLE_UNIT
_BATTLE_UNIT_ID = RESULT
IF _BATTLE_UNIT_ID == -1
  PRINTFORMW 戦闘に参加できるユニットがいない
  PRINTFORMW ユニットを編成してから再度クエストを受けよう
  RETURN
ENDIF

WHILE !_RETIER
  ;白兵戦参加メンバーの初期化
  CALL CC_INIT()
  ;前の戦いの記録が残っていたら上書き
  IF _DIC:_BATTLE_UNIT_ID:0 != "-1" && _LINE_CHAN == 1
    FOR LOCAL, 0, FINDELEMENT(CC_ATTACKER_LIST, "-1")
      CC_ATTACKER_LIST:LOCAL = %DIC_SET(CC_ATTACKER_LIST:LOCAL, "体力", DIC_GET(_DIC:_BATTLE_UNIT_ID:LOCAL, "体力"))%
      CC_ATTACKER_LIST:LOCAL = %DIC_SET(CC_ATTACKER_LIST:LOCAL, "状態", "待機")%
    NEXT
  ELSE
    FOR LOCAL, 0, CHARANUM
      SIF UNIT_ID:LOCAL == _BATTLE_UNIT_ID
        CALL CC_ADDCHARA("ATTACKER", LOCAL)
    NEXT
    IF _DIC:_BATTLE_UNIT_ID:0 != "-1"
      FOR LOCAL, 0, FINDELEMENT(CC_ATTACKER_LIST, "-1")
        CC_ATTACKER_LIST:LOCAL = %DIC_SET(CC_ATTACKER_LIST:LOCAL, "体力", DIC_GET(_DIC:_BATTLE_UNIT_ID:LOCAL, "体力"))%
        CC_ATTACKER_LIST:LOCAL = %DIC_SET(CC_ATTACKER_LIST:LOCAL, "状態", "待機")%
      NEXT
    ENDIF 
  ENDIF
  IF _TURN == 0
    CALL CC_ADDMOB("DEFENDER", "操られた民間人1", 500, 500, 10)
    CALL CC_ADDMOB("DEFENDER", "操られた民間人2", 500, 500, 10)
    CALL CC_ADDMOB("DEFENDER", "操られた民間人3", 500, 500, 10)
    CALL CC_ADDMOB("DEFENDER", "操られた民間人4", 500, 500, 10)
    CALL CC_ADDMOB("DEFENDER", "操られた民間人5", 500, 500, 10)
    CALL CC_ADDMOB("DEFENDER", "操られた民間人6", 500, 500, 10)
    _EXP = 1
    _TURN = 1
  ELSEIF _TURN == 1
    CALL CC_ADDMOB("DEFENDER", "リモート死体1", 1000, 1000, 20)
    CALL CC_ADDMOB("DEFENDER", "リモート死体2", 1000, 1000, 20)
    CALL CC_ADDMOB("DEFENDER", "リモート死体3", 1000, 1000, 20)
    CALL CC_ADDMOB("DEFENDER", "リモート死体4", 1000, 1000, 20)
    CALL CC_ADDMOB("DEFENDER", "リモート死体5", 1000, 1000, 20)
    CALL CC_ADDMOB("DEFENDER", "リモート死体6", 1000, 1000, 20)
    _EXP = 2
    _TURN = 2
  ELSEIF _TURN == 2
    CALL CC_ADDMOB("DEFENDER", "操られた警官1", 1500, 1500, 45)
    CALL CC_ADDMOB("DEFENDER", "操られた警官2", 1500, 1500, 45)
    CALL CC_ADDMOB("DEFENDER", "操られた警官3", 1500, 1500, 45)
    CALL CC_ADDMOB("DEFENDER", "操られた警官4", 1500, 1500, 45)
    CALL CC_ADDMOB("DEFENDER", "操られた警官5", 1500, 1500, 45)
    CALL CC_ADDMOB("DEFENDER", "操られた警官6", 1500, 1500, 45)
    _EXP = 4
    _TURN = 3
  ELSEIF _TURN == 3
    PRINTFORMW 逃げ続けるチェレンを追い続け、ついにチェレンと思わしき人物を発見した！
    CALL CC_ADDMOB("DEFENDER", "操られた民間人1", 500, 500, 10)
    CALL CC_ADDMOB("DEFENDER", "操られた民間人2", 500, 500, 10)
    CALL CC_ADDMOB("DEFENDER", "操られた警官1", 1500, 1500, 45)
    CALL CC_ADDMOB("DEFENDER", "操られた警官2", 1500, 1500, 45)
    CALL CC_ADDMOB("DEFENDER", "チェレン・スー", 2000, 2000, 50)
    CALL CC_ADDMOB("DEFENDER", "操られた思考戦車", 4000, 4000, 90)
    _EXP = 12
    _TURN = 4
  ENDIF
  ;白兵戦イベント呼出
  CALL CLOSE_COMBAT()
  SIF RESULT == 2
    CALL GAMEOVER_46
  CALL CC_ADD_COMBAT_EXP(_EXP)
  FOR LOCAL, 0, FINDELEMENT(CC_ATTACKER_LIST, "-1")
    _DIC:_BATTLE_UNIT_ID:LOCAL = %CC_ATTACKER_LIST:LOCAL%
  NEXT
  SIF _TURN == 4
    BREAK

  DRAWLINE
  PRINTBUTTON "[連戦]", 0
  PRINTBUTTON "[チーム変更]", 1
  PRINTBUTTON "[撤退]", 3
  PRINTL
  DRAWLINE
  INPUT
  _LINE_CHAN = 0
  IF RESULT == 0
    ;連戦フラグオン
    CALL CC_SET_FLAG(LINE_CHAN, "1")
    _LINE_CHAN = 1
  ELSEIF RESULT == 1
    CALL SELECT_PMC_BATTLE_UNIT
    _BATTLE_UNIT_ID = RESULT
  ELSE
    _RETIER = 1
  ENDIF
WEND
DRAWLINE
IF _TURN != 4
  PRINTFORMW 報酬はもらえなかった……
ELSE
  PRINTFORMW チェレンはどこにでもいそうな三十路ほどの男だった。義体だが、肉体強化は図っていなかったようだ
  PRINTFORMW %CALLNAME:MASTER%の部隊は死体と化したチェレンを警察本部まで届けた
  PRINTFORMW 報酬として$1,000,000手に入れたが、なんとも胸糞悪いミッションだった……
  PMC_QUEST_PROGRESS:2 = 1
ENDIF
MONEY += 1000000
DAY ++

;----------------------------
; クエスト説明
;----------------------------
@PMC_QUEST_BRIEFING_2
SETCOLOR 0X666666
CUSTOMDRAWLINE ///
RESETCOLOR
PRINTFORML USFのエデンで暗躍する電脳犯罪者、チェレン・スーの名前を知らない警察・軍関係者は存在しない
PRINTFORML 特級の電脳犯罪を易々とこなす奴は、機械文明が送り出したガイノイドなのではないかと噂されているほどだ
PRINTFORMW その犯罪をあげれば枚挙はないが、エデン１厳重なエデン・中央銀行から$1の銀行強盗をやってのけた事件が特に有名だろう
PRINTL
PRINTFORML それだけ聞くと愉快犯に聞こえるが、暗殺から電脳乗っ取り、企業恐喝や株価の不正操作など凶悪犯罪にも積極的に手を出す悪人だ
PRINTFORML そんなチェレンの居場所がついに発見された。別件でしょっぴかれた闇医者がチェレンの治療をしたと告白し、そこから居場所を特定するに至ったのだ
PRINTFORMW チェレンは現在電脳ハックとリモート死体を使い追手を巻いており、さらなる戦力投入のためPMCの投入が決められた
PRINTL
PRINTFORML チェレンの身柄を生死を問わず捕獲せよ。捕獲の際の不慮の事故には目を瞑る。それが今回のミッション概要だ
PRINTFORMW チェレンが人間の盾を使う以上、最悪の状況は想定しないといけないだろう
SETCOLOR 0X666666
CUSTOMDRAWLINE ///
RESETCOLOR


