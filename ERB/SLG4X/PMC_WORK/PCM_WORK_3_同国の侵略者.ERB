;----------------------------
; 同国の侵略者
;----------------------------

;----------------------------
; 受注可能か
;----------------------------
@PMC_QUEST_ACCEPTABLE_3
CALL GET_STAR_ID_FROM_POS(CHARACTER_POSITION)
RETURN RESULT == 6 && STAR_OWNER:6 == 国家ID_民主主義

;----------------------------
; クエスト名
;----------------------------
@PMC_QUEST_NAME_3
RESULTS = 同国の侵略者(難易度1~)
RETURN

;----------------------------
; クエスト本体
;----------------------------
@PMC_QUEST_BODY_3
#DIM _BATTLE_UNIT_ID
#DIM DYNAMIC _RETIER
#DIM DYNAMIC _SCORE
#DIM DYNAMIC _LINE_CHAN
#DIMS _DIC, MAX_PMC_UNIT, MAX_PMC_UNIT_MEMBER
#DIM _EXP

FOR LOCAL, 0, MAX_PMC_UNIT
  VARSET _DIC:LOCAL:0, "-1"
NEXT

PRINTFORML ブリーフィングを受けますか？
CALL ASK_YN
SIF !RESULT
  CALL PMC_QUEST_BRIEFING_3
CALL PMC_EDIT_UNIT

PRINTFORML そこは博物館で飾られているような古代の銃が活躍する戦場だった
PRINTFORML バナナマガジンの刺さった実弾銃を構えた暴徒達が、幾重にも構築されたバリケード目掛けて鉛玉を撃ちまくっている
PRINTFORMW %CALLNAME:MASTER%達のミッションは、タイムスリップしたようなこの戦場で、一人でも多くの暴徒を撃滅することだ
PRINTL ………………
PRINTL …………
PRINTL ……
CALL SELECT_PMC_BATTLE_UNIT
_BATTLE_UNIT_ID = RESULT
IF _BATTLE_UNIT_ID == -1
  PRINTFORMW 戦闘に参加できるユニットがいない
  PRINTFORMW ユニットを編成してから再度クエストを受けよう
  RETURN
ENDIF

WHILE !_RETIER
  ;白兵戦参加メンバーの初期化
  CALL CC_INIT()
  ;前の戦いの記録が残っていたら上書き
  IF _DIC:_BATTLE_UNIT_ID:0 != "-1" && _LINE_CHAN == 1
    FOR LOCAL, 0, FINDELEMENT(CC_DEFENDER_LIST, "-1")
      CC_DEFENDER_LIST:LOCAL = %DIC_SET(CC_DEFENDER_LIST:LOCAL, "体力", DIC_GET(_DIC:_BATTLE_UNIT_ID:LOCAL, "体力"))%
      CC_DEFENDER_LIST:LOCAL = %DIC_SET(CC_DEFENDER_LIST:LOCAL, "状態", "待機")%
    NEXT
  ELSE
    FOR LOCAL, 0, CHARANUM
      SIF UNIT_ID:LOCAL == _BATTLE_UNIT_ID
        CALL CC_ADDCHARA("DEFENDER", LOCAL)
    NEXT
    IF _DIC:_BATTLE_UNIT_ID:0 != "-1"
      FOR LOCAL, 0, FINDELEMENT(CC_DEFENDER_LIST, "-1")
        CC_DEFENDER_LIST:LOCAL = %DIC_SET(CC_DEFENDER_LIST:LOCAL, "体力", DIC_GET(_DIC:_BATTLE_UNIT_ID:LOCAL, "体力"))%
        CC_DEFENDER_LIST:LOCAL = %DIC_SET(CC_DEFENDER_LIST:LOCAL, "状態", "待機")%
      NEXT
    ENDIF 
  ENDIF
  CALL CC_ADDMOB("ATTACKER", "暴徒1", 1500, 1500, 50)
  CALL CC_ADDMOB("ATTACKER", "暴徒2", 1500, 1500, 50)
  CALL CC_ADDMOB("ATTACKER", "暴徒3", 1500, 1500, 50)
  CALL CC_ADDMOB("ATTACKER", "暴徒4", 1500, 1500, 50)
  CALL CC_ADDMOB("ATTACKER", "暴徒5", 1500, 1500, 50)
  CALL CC_ADDMOB("ATTACKER", "暴徒6", 1500, 1500, 50)
  _SCORE += POWER(50, 2) * 6
  _EXP = 10
  ;白兵戦イベント呼出
  CALL CLOSE_COMBAT()
  SIF RESULT == 1
    CALL GAMEOVER_46
  CALL CC_ADD_COMBAT_EXP(_EXP)
  FOR LOCAL, 0, FINDELEMENT(CC_DEFENDER_LIST, "-1")
    _DIC:_BATTLE_UNIT_ID:LOCAL = %CC_DEFENDER_LIST:LOCAL%
  NEXT

  DRAWLINE
  PRINTBUTTON "[連戦]", 0
  PRINTBUTTON "[チーム変更]", 1
  PRINTBUTTON "[撤退]", 3
  PRINTL
  DRAWLINE
  INPUT
  _LINE_CHAN = 0
  IF RESULT == 0
    ;連戦フラグオン
    CALL CC_SET_FLAG(LINE_CHAN, "2")
    _LINE_CHAN = 1
  ELSEIF RESULT == 1
    CALL SELECT_PMC_BATTLE_UNIT
    _BATTLE_UNIT_ID = RESULT
  ELSE
    _RETIER = 1
  ENDIF
WEND
DRAWLINE
IF _SCORE == 0
  LOCALS = $0
ELSE
  LOCALS = %TOSTR(_SCORE, "$###,###")%
ENDIF
PRINTFORMW 報酬として%LOCALS%手に入れた。
MONEY += _SCORE
DAY ++

;----------------------------
; クエスト説明
;----------------------------
@PMC_QUEST_BRIEFING_3
SETCOLOR 0X666666
CUSTOMDRAWLINE ///
RESETCOLOR
PRINTFORML エデンの第八都市、それが今回の現場だ
PRINTFORML エデンは初めてUSFが入植した惑星で、比較的小型の惑星だ
PRINTFORMW そのため土地が限られているものの、USF初の領土拡大を過大宣伝したかったUSFは主星と同じく第十都市までの大都市を設立した
PRINTL
PRINTFORML その過程で限られた土地のどこに大都市を立てるかという問題が発生し、第八都市は一度採用が見送られた離島に建てられることになった
PRINTFORML しかし当時その土地を開発し、土着心を持っていた開拓団の子孫と、新たに流入してきた主星の開発団が衝突
PRINTFORMW 結果最初からいた開拓団を武力制圧し、主星の開発団が第八都市を建てることに成功した
PRINTL
PRINTFORML だが当時の怨恨は現在まで続いており、さらに近年の開拓団子孫と開発団子孫の貧富の差が拡大し、対立が悪化
PRINTFORML 暴徒と化した開拓団子孫が第八都市に襲撃をかける自体に陥った
PRINTFORMW 今回のミッションは開拓団子孫の第八都市に対する襲撃の防衛だ
PRINTL
PRINTFORML なお、正規軍は今回の暴動に乗じて第八都市内で便乗して行われる可能性のある犯罪の防止のため都市内の警備にあたるため、実質防衛はPMC頼りになる
PRINTFORMW 余談だが、主星同様の宇宙軍保有を要求されているエデンは、少ない領土から徴収される税金の大半を宇宙軍に当てているため、地上軍の数が圧倒的に足りていないのだ……
SETCOLOR 0X666666
CUSTOMDRAWLINE ///
RESETCOLOR


