;------------------------------
; MESSAGE_QUE.ERB
;------------------------------
;------------------------------
; メッセージの挿入
; 戻り値: 数値 - ユニークID
;------------------------------
@MESSAGE_PUSH(TITLE, BODY, IS_DAILY = -1, DAILY_ID = -1)
#DIMS TITLE
#DIMS BODY
#DIM IS_DAILY
#DIM DAILY_ID

ARRAYSHIFT MQ_STATUS, 1, MQ_UNREAD
ARRAYSHIFT MQ_TITLE, 1, TITLE
ARRAYSHIFT MQ_BODY, 1, BODY
ARRAYSHIFT MQ_UID, 1, MQ_NEXT_UID
MQ_NEXT_UID ++
RETURN MQ_UID:0

;------------------------------
; メッセージキューの表示
;------------------------------
@PRINT_MESSAGE_QUE
#DIM PAGE
#DIM CURRENT_MESSAGE
#DIM PAGE_FIRST

$PAGE_VIEW_LOOP
PAGE_FIRST = PAGE * MQ_PAGE_PER_VIEW
PRINTL
CUSTOMDRAWLINE =
FOR CURRENT_MESSAGE, PAGE_FIRST, PAGE_FIRST + MQ_PAGE_PER_VIEW
    SIF MQ_STATUS:CURRENT_MESSAGE == MQ_UNDEF
        BREAK
    SIF MQ_STATUS:CURRENT_MESSAGE == MQ_UNREAD
        SETCOLOR 0XFFFF00
    STRLENFORM %MQ_TITLE:CURRENT_MESSAGE%
    PRINTBUTTON @"[{CURRENT_MESSAGE, 3, RIGHT}] %MQ_TITLE:CURRENT_MESSAGE%", CURRENT_MESSAGE
    SIF MQ_STATUS:CURRENT_MESSAGE == MQ_UNREAD
        RESETCOLOR
    PRINTL
NEXT
DRAWLINE
LOCALS = [前のページ]
IF PAGE == 0
    SETCOLOR 0X666666
    PRINTFORM %LOCALS, 16, LEFT%
    RESETCOLOR
ELSE
    PRINTBUTTON @"%LOCALS, 16, LEFT%", MESSAGE_QUE_SIZE
ENDIF
LOCALS = [次のページ]
IF MQ_STATUS:((PAGE + 1) * MQ_PAGE_PER_VIEW) == MQ_UNDEF
    SETCOLOR 0X666666
    PRINTFORM %LOCALS, 16, LEFT%
    RESETCOLOR
ELSE
    PRINTBUTTON @"%LOCALS, 16, LEFT%", MESSAGE_QUE_SIZE + 1
ENDIF
LOCALS = [キャンセル]
PRINTBUTTON @"%LOCALS%", MESSAGE_QUE_SIZE + 2
PRINTL
DRAWLINE
$INPUT_LOOP
INPUT
IF RESULT < 0 || RESULT > (MESSAGE_QUE_SIZE + 2)
    CLEARLINE 1
    GOTO INPUT_LOOP
ELSEIF RESULT == MESSAGE_QUE_SIZE
    IF PAGE == 0
        CLEARLINE 1
        GOTO INPUT_LOOP
    ELSE
        PAGE --
        GOTO PAGE_VIEW_LOOP
    ENDIF
ELSEIF RESULT == MESSAGE_QUE_SIZE + 1
    IF MQ_STATUS:((PAGE * MQ_PAGE_PER_VIEW) + 1) == MQ_UNDEF
        CLEARLINE 1
        GOTO INPUT_LOOP
    ELSE
        PAGE ++
        GOTO PAGE_VIEW_LOOP
    ENDIF
ELSEIF RESULT == MESSAGE_QUE_SIZE + 2
    RETURN
ELSEIF MQ_STATUS:RESULT == MQ_UNDEF
    CLEARLINE 1
    GOTO INPUT_LOOP
ELSE
    CALL SHOW_MESSAGE_QUE(RESULT)
    GOTO PAGE_VIEW_LOOP
ENDIF

;------------------------------
; メッセージの詳細表示
;------------------------------
@SHOW_MESSAGE_QUE(MESSAGE_ID)
#DIM MESSAGE_ID

SIF MESSAGE_ID < 0 || MESSAGE_ID >= MESSAGE_QUE_SIZE
    RETURN 0
SIF MQ_STATUS:MESSAGE_ID == MQ_UNDEF
    RETURN 0
IF MQ_IS_DAILY:MESSAGE_ID && MQ_STATUS:MESSAGE_ID == MQ_UNREAD
    TRYCALLFORM SD_EVENT_{MQ_DAILY_ID:MESSAGE_ID}
    MQ_STATUS:MESSAGE_ID = MQ_READ
    RETURN
ENDIF

PRINTL
CUSTOMDRAWLINE =
PRINTFORML %MQ_TITLE:MESSAGE_ID%
DRAWLINE
PRINTFORML %MQ_BODY:MESSAGE_ID%
DRAWLINE
MQ_STATUS:MESSAGE_ID = MQ_READ
PRINTBUTTON @"[完了]", 0
$INPUT_LOOP
INPUT
SIF RESULT == 0
    RETURN
CLEARLINE 1
GOTO INPUT_LOOP

;------------------------------
; メッセージキューへのリンク表示
;------------------------------
@SHOW_MESSAGE_QUE_LINK(LINK_ID = 1000)
#DIM LINK_ID
#DIMS CONST MQ_LINK_MES = "報告なし", "新着報告あり", "新着報告なし"
SIF MQ_STATUS:0 == MQ_UNDEF
    RETURN
LOCALS = %MQ_LINK_MES:(MQ_STATUS:0)%
PRINTL
CUSTOMDRAWLINE =
IF MQ_STATUS:0 == MQ_UNREAD
    LOCALS = [!] %LOCALS% - %MQ_TITLE:0%
    SETCOLOR 0XFFFF33
ENDIF
PRINTBUTTON @"%LOCALS%", LINK_ID
SIF MQ_STATUS:0 == MQ_UNREAD
    RESETCOLOR
PRINTL
DRAWLINE

