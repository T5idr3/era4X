;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;@SHOW_SHIP_WINDOW_INFO(SHIP_ID)
;;; 	引数１	SHIP_ID		表示する船
;;;	船情報を表示するウィンドウを展開する
;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
@SHOW_SHIP_WINDOW_INFO(SHIP_ID)
#DIM SHIP_ID
#DIM dynamic OPWnd , 4
#DIM WndCOUNT			;ウィンドウ群展開に使用
#DIM Wnd7_LINES

;;お約束 WINDOW未生成の場合作成する
CALL SETUP_WINDOW

;;内容作成　window5 船情報概略
CALL CREATE_WINDOW_SHIPINFO_MAIN(SHIP_ID)

;;内容作成　window6 船情報細目
;CALL CREATE_WINDOW_SHIPINFO_SUB(SHIP_ID)

;;内容作成　window7 船情報積荷
CALL CREATE_WINDOW_SHIPINFO_CARGO(SHIP_ID)
	Wnd7_LINES = RESULT
	
;;画面への表示を実行
DRAWLINE

OPWnd = 5
WndCOUNT = 1

;OPWnd = 6, 5
IF Wnd7_LINES > 0
	OPWnd:WndCOUNT = 7
	WndCOUNT ++
	IF Wnd7_LINES >= 5
		CALL WndMgr_GetRectangle( 7 )	;;枠情報の収集
		CALL WndMgr_Rectangle( 7, RESULT:0, RESULT:1, RESULT:2, Wnd7_LINES + 2, RESULT:6 )	;枠の高さ再設定
	ENDIF
ENDIF

;;;指定windowの表示を実行
CALL WndMgr_Display( WndCOUNT, OPWnd, 0 )

	CALL WndMgr_DestroyWindow( 7 )
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;@CREATE_WINDOW_SHIPINFO_MAIN(SHIP_ID)
;;;　引数１：表示する船ID
;;; window5 船情報概略
;;; あちこち出すイメージでなるべく小型化。宙域画面で出せるほどになれば・・・
;;; 表示項目は練る必要があります
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
@CREATE_WINDOW_SHIPINFO_MAIN(SHIP_ID)
#DIM windowID = 5
#DIM SHIP_ID
#DIM SLOT_ID
#DIM STATUS, STATUS_LENGTH
#DIM DYNAMIC SHIP_STATUS, SHIP_STATUS_LENGTH
#DIM HP
#DIM MAX_HP
#DIM SP
#DIM MAX_SP
#DIM CARGO
#DIM MAX_CARGO
#DIM 色,2
#DIMS MESSAGE,2

;ウィンドウ初期化　メッセージ残るため
CALL WndMgr_ResetWindow(windowID)

TRYCALLFORM GET_SHIP_STATUS_{SHIP_MODEL:SHIP_ID}(SHIP_STATUS)

;;耐久
CALL GET_SHIP_HP(SHIP_ID)
HP = RESULT
CALL GET_SHIP_TOTAL_HP(SHIP_ID)
MAX_HP = RESULT
IF HP >= MAX_HP
	色:0 = 0x00FF00		;緑
ELSEIF HP > 0
	色:0 = 0xADFF2F		;黄緑 greenyello
ELSE
	色:0 = 0xFF0000		;赤
ENDIF

;;シールド
CALL GET_SHIP_TOTAL_SIELD(SHIP_ID)
MAX_SP = RESULT
CALL GET_SHIP_SIELD(SHIP_ID)
SP = RESULT
IF SP >= MAX_SP
	色:1 = 0x00FFFF		;CYAN
ELSEIF SP > 0
	色:1 = 0x87CEFA		;LIGHTSKYBLUE
ELSE
	色:1 = 0xFFFF00		;YELLOW
ENDIF

;;カーゴ　要るかな・・？
FOR SLOT_ID, 0, SHIP_STATUS:SHIP_OPTION_SLOT
	TRYCALLFORM GET_OPTION_EQUIP_EFFECT_{SHIP_EQUIP:オプション:SLOT_ID:SHIP_ID}(SHIP_STATUS)
NEXT
MAX_CARGO = SHIP_STATUS:SHIP_CAPACITY
CALL SUM_CARGO_SIZE(SHIP_ID)
CARGO = RESULT


;;;とりあえずダミータイトル
;;;STATUS 正常・ドッグ内・ﾊﾟﾄﾛｰﾙ中・採掘中・建造中・DESTROYed・・・？
LOCALS = %SHIP_NAME:SHIP_ID%
CALL WndMgr_WindowTitle( windowID , @"%LOCALS%" )
CALL WndMgr_TextSet( windowID, 0, @" 　%SHIP_CATEGORY_NAME:(SHIP_CATEGORY:SHIP_ID)%")
;;SHIP ACTION(STATUS) を入れるか


CALL WndMgr_TextSet( windowID, 1, @" 耐久 @C:{色:0}@{HP, 5, RIGHT} /{MAX_HP, 5, RIGHT}@/C@ ")
;;;カラーバー表示したいが、PRINTしちゃうとダメなのでどうしよう
BARSTR HP, MAX_HP , 5
MESSAGE:0 '= RESULTS:0
CALL WndMgr_TextAdd( windowID, 1, @"@C:{色:0}@%MESSAGE:0%@/C@")

CALL WndMgr_TextSet( windowID, 2, @"SHILD @C:{色:1}@{SP, 5, RIGHT} /{MAX_SP, 5, RIGHT}@/C@ ")
SIF MAX_SP
	BARSTR SP, MAX_SP , 5
MESSAGE:0 '= RESULTS:0
CALL WndMgr_TextAdd( windowID, 2, @"@C:{色:1}@%MESSAGE:0%@/C@")

SIF MAX_CARGO != 0
	CALL WndMgr_TextSet( windowID, 3, @" 積載 {CARGO, 5, RIGHT} /{MAX_CARGO, 5, RIGHT} ")
	
	
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;@CREATE_WINDOW_SHIPINFO_CARGO(TargetShip)
;;;　引数１：表示する船ID
;;; window7 船情報搭載貨物
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
@CREATE_WINDOW_SHIPINFO_CARGO(TargetShip)
#DIM windowID = 7
#DIM TargetShip
#DIM Compartment
#DIM Wnd7_LINES
#DIM ITEM_ID
#DIM ITEM_NUM

CALL WndMgr_ResetWindow(windowID)
CALL WndMgr_WindowTitle( windowID, @" IN CARGO " ) ;枠タイトル

Wnd7_LINES = 0
FOR Compartment, 0, ITEM_LENGTH
	CALL GET_CARGO_ITEM_INFO(TargetShip, Compartment)
	SIF RESULT:0 == -1 || RESULT:1 <= 0
		BREAK
	
	IF Wnd7_LINES >= 20
		CALL WndMgr_TEXTSET( windowID, (Wnd7_LINES -1 ), @" 省略されました" )
		BREAK
	ENDIF
	
	ITEM_ID = RESULT:0
	ITEM_NUM = RESULT:1
	CALL GET_ITEM_NAME(RESULT:0)
	CALL WndMgr_TextSet( windowID, Wnd7_LINES, @" [{ITEM_ID,2,RIGHT}] %RESULTS,20,LEFT% x {ITEM_NUM,4,LEFT}" 　)
	Wnd7_LINES ++
NEXT

RETURN Wnd7_LINES


;----------------------------
; 船の詳細WINDOW
; window6, 9 船情報概略
;----------------------------
@CREATE_WINDOW_SHIPINFO(SHIP_ID)
#DIM SHIP_ID
#DIM STATUS, STATUS_LENGTH
#DIM DYNAMIC SHIP_STATUS, SHIP_STATUS_LENGTH
#DIM HP
#DIM MAX_HP
#DIM SP
#DIM MAX_SP
#DIM CARGO
#DIM MAX_CARGO
#DIMS DYNAMIC WEAPON_NAME
#DIM DYNAMIC STATUS_WINDOW_ARRAY, 2
#DIM DYNAMIC LINE_COUNT
#DIM DYNAMIC SLOT_ID
#DIM DYNAMIC WEAPON_STATUS, STATUS_LENGTH
#DIM DYNAMIC SHIP_WEAPON_STATUS, STATUS_LENGTH
#DIM DYNAMIC DPS
#DIM DYNAMIC MIN_RANGE
#DIM DYNAMIC MAX_RANGE
#DIM DYNAMIC SHIP_WEAPON_STATUS_BUFFER, STATUS_LENGTH
#DIM DYNAMIC TOTAL_SIELD
#DIM DYNAMIC TOTAL_SIELD_RECOVER
#DIM DYNAMIC TOTAL_ARMOR
#DIM DYNAMIC SELECT_CATEGORY
#DIM DYNAMIC SELECT_SLOT_ID
#DIMS DYNAMIC SHIP_DESCRIPTION

#DIMS DYNAMIC SHIP_DESCRIPTION_SPLIT_LIST, 100
#DIMS DYNAMIC SHIP_DESCRIPTION_COLOR_LIST, 100
;100行もあれば足りると思いたい
#DIM LIST_LENGTH
#DIM 総文字数
#DIMS CONST 分割文 = "/仕切り/"




;ウィンドウ初期化　メッセージ残るため
CALL SETUP_WINDOW
CALL WndMgr_ResetWindow(SHIP_COMMON_STATUS_WINDOW_ID)

TRYCALLFORM GET_SHIP_STATUS_{SHIP_MODEL:SHIP_ID}(SHIP_STATUS)

LINE_COUNT = 1
CALL WndMgr_WindowTitle( SHIP_COMMON_STATUS_WINDOW_ID , "基本情報")
CALL WndMgr_TextSet( SHIP_COMMON_STATUS_WINDOW_ID, LINE_COUNT ++, "■ 登録情報")
CALL WndMgr_TextSet( SHIP_COMMON_STATUS_WINDOW_ID, LINE_COUNT ++, @"名前　　: %SHIP_NAME:SHIP_ID%")
CALL WndMgr_TextSet( SHIP_COMMON_STATUS_WINDOW_ID, LINE_COUNT ++, @"艦種　　: %SHIP_CATEGORY_NAME:(SHIP_CATEGORY:SHIP_ID)%")
CALL WndMgr_TextSet( SHIP_COMMON_STATUS_WINDOW_ID, LINE_COUNT ++, @"特殊分類: %SHIP_VARIANT_NAME:(SHIP_STATUS:SHIP_VARIANT)%")


; 概要
VARSET RESULTS
総文字数 = 0
TRYCALLFORM GET_SHIP_DESCRIPTION_F_{SHIP_MODEL:SHIP_ID}(3)

SPLIT RESULTS, 分割文, SHIP_DESCRIPTION_SPLIT_LIST
;最後に1つ後ろに何もない文が出力されているのでその分を引く
LIST_LENGTH = RESULT -1
CALL SHIP_DESCRIPTION_SPLIT_LIST_INIT(SHIP_DESCRIPTION_SPLIT_LIST, SHIP_DESCRIPTION_COLOR_LIST, LIST_LENGTH)

FOR COUNT, 0, LIST_LENGTH
	総文字数 += STRLENS(SHIP_DESCRIPTION_SPLIT_LIST:COUNT)
NEXT


SHIP_DESCRIPTION '= SHIP_DESCRIPTION_SPLIT_LIST:0
IF 総文字数 > 0
	LINE_COUNT ++
	CALL WndMgr_TextSet( SHIP_COMMON_STATUS_WINDOW_ID, LINE_COUNT ++, @"■ 概要")
	;説明文が長い場合はもっと見るボタン追加
	IF 総文字数 > 44 || LIST_LENGTH > 1
		CALL WndMgr_TextSet( SHIP_COMMON_STATUS_WINDOW_ID, LINE_COUNT ++, @"%SHIP_DESCRIPTION_COLOR_LIST:0%%SUBSTRING(SHIP_DESCRIPTION, 0, 44)%...@/C@")
		CALL WndMgr_TextSet( SHIP_COMMON_STATUS_WINDOW_ID, LINE_COUNT ++, @"@B:1001@[もっと見る]@/B@")
	ELSE
		CALL WndMgr_TextSet( SHIP_COMMON_STATUS_WINDOW_ID, LINE_COUNT ++, @"%SHIP_DESCRIPTION_COLOR_LIST:0%%SHIP_DESCRIPTION%@/C@")
	ENDIF
ENDIF

; 装備の情報
LINE_COUNT ++
CALL WndMgr_TextSet( SHIP_COMMON_STATUS_WINDOW_ID, LINE_COUNT ++, @"■ 武装情報")
FOR SLOT_ID, 0, SHIP_STATUS:SHIP_MAIN_WEAPON_SLOT
	TRYCALLFORM GET_MAIN_WEAPON_NAME_{SHIP_EQUIP:主砲:SLOT_ID:SHIP_ID}
	LOCALS = 主砲{SLOT_ID + 1}
	CALL WndMgr_TextSet( SHIP_COMMON_STATUS_WINDOW_ID, LINE_COUNT ++, @"%LOCALS, 12, LEFT%@B:{SLOT_ID}@[%RESULTS%]@/B@")
NEXT
FOR SLOT_ID, 0, SHIP_STATUS:SHIP_SUB_WEAPON_SLOT
	TRYCALLFORM GET_SUB_WEAPON_NAME_{SHIP_EQUIP:副砲:SLOT_ID:SHIP_ID}
	LOCALS = 副砲{SLOT_ID + 1}
	CALL WndMgr_TextSet( SHIP_COMMON_STATUS_WINDOW_ID, LINE_COUNT ++, @"%LOCALS, 12, LEFT%@B:{SLOT_ID + MAX_SLOT * 副砲}@[%RESULTS%]@/B@")
NEXT
FOR SLOT_ID, 0, SHIP_STATUS:SHIP_SIELD_SLOT
	TRYCALLFORM GET_SIELD_NAME_{SHIP_EQUIP:シールド:SLOT_ID:SHIP_ID}
	LOCALS = シールド{SLOT_ID + 1}
	CALL WndMgr_TextSet( SHIP_COMMON_STATUS_WINDOW_ID, LINE_COUNT ++, @"%LOCALS, 12, LEFT%@B:{SLOT_ID + MAX_SLOT * シールド}@[%RESULTS%]@/B@")
NEXT
FOR SLOT_ID, 0, SHIP_STATUS:SHIP_ARMOR_SLOT
	TRYCALLFORM GET_ARMOR_NAME_{SHIP_EQUIP:装甲:SLOT_ID:SHIP_ID}
	LOCALS = 装甲{SLOT_ID + 1}
	CALL WndMgr_TextSet( SHIP_COMMON_STATUS_WINDOW_ID, LINE_COUNT ++, @"%LOCALS, 12, LEFT%@B:{SLOT_ID + MAX_SLOT * 装甲}@[%RESULTS%]@/B@")
NEXT

TRYCALLFORM GET_ENGIN_NAME_{SHIP_EQUIP:エンジン:0:SHIP_ID}
LOCALS = エンジン
CALL WndMgr_TextSet( SHIP_COMMON_STATUS_WINDOW_ID, LINE_COUNT ++, @"%LOCALS, 12, LEFT%@B:{MAX_SLOT * エンジン}@[%RESULTS%]@/B@")

TRYCALLFORM GET_COMPUTER_NAME_{SHIP_EQUIP:AI:0:SHIP_ID}
LOCALS = 制御AI
CALL WndMgr_TextSet( SHIP_COMMON_STATUS_WINDOW_ID, LINE_COUNT ++, @"%LOCALS, 12, LEFT%@B:{MAX_SLOT * AI}@[%RESULTS%]@/B@")

TRYCALLFORM GET_SENSOR_NAME_{SHIP_EQUIP:センサー:0:SHIP_ID}
LOCALS = センサー
CALL WndMgr_TextSet( SHIP_COMMON_STATUS_WINDOW_ID, LINE_COUNT ++, @"%LOCALS, 12, LEFT%@B:{MAX_SLOT * センサー}@[%RESULTS%]@/B@")

FOR SLOT_ID, 0, SHIP_STATUS:SHIP_OPTION_SLOT
	TRYCALLFORM GET_OPTION_NAME_{SHIP_EQUIP:オプション:SLOT_ID:SHIP_ID}
	LOCALS = オプション{SLOT_ID + 1}
	CALL WndMgr_TextSet( SHIP_COMMON_STATUS_WINDOW_ID, LINE_COUNT ++, @"%LOCALS, 12, LEFT%@B:{SLOT_ID + MAX_SLOT * オプション}@[%RESULTS%]@/B@")
NEXT


;;耐久
CALL GET_SHIP_HP(SHIP_ID)
HP = RESULT
CALL GET_SHIP_TOTAL_HP(SHIP_ID)
MAX_HP = RESULT

;;シールド
CALL GET_SHIP_TOTAL_SIELD(SHIP_ID)
MAX_SP = RESULT
CALL GET_SHIP_SIELD(SHIP_ID)
SP = RESULT

LINE_COUNT ++
CALL WndMgr_TextSet( SHIP_COMMON_STATUS_WINDOW_ID, LINE_COUNT ++, @"■ 耐久情報")
LOCALS = 装甲
CALL WndMgr_TextSet( SHIP_COMMON_STATUS_WINDOW_ID, LINE_COUNT ++, @"%LOCALS, 12, LEFT% {HP}/{MAX_HP}")
LOCALS = シールド
CALL WndMgr_TextSet( SHIP_COMMON_STATUS_WINDOW_ID, LINE_COUNT ++, @"%LOCALS, 12, LEFT% {SP}/{MAX_SP}")
LINE_COUNT ++

;;カーゴキャパシティ
CALL GET_SHIP_CAPACITY(SHIP_ID)
MAX_CARGO = RESULT
IF MAX_CARGO > 0
	CALL WndMgr_TextSet( SHIP_COMMON_STATUS_WINDOW_ID, LINE_COUNT ++, @"■ 積載情報")
	LOCALS = 積載量
	CALL SUM_CARGO_SIZE(SHIP_ID)
	CALL WndMgr_TextSet( SHIP_COMMON_STATUS_WINDOW_ID, LINE_COUNT ++, @"%LOCALS, 12, LEFT% {RESULT}/{MAX_CARGO}")
	LINE_COUNT ++
ENDIF

CALL WndMgr_Rectangle( SHIP_COMMON_STATUS_WINDOW_ID, 2, 2, 53, LINE_COUNT + 2, 0X02)	;枠の高さ再設定

;;/_/_/_/_/_/_/_/_/_/_/_/_/
; 詳細情報
;;/_/_/_/_/_/_/_/_/_/_/_/_/
CALL WndMgr_ResetWindow(SHIP_DETAIL_WINDOW_ID)
CALL WndMgr_WindowTitle( SHIP_DETAIL_WINDOW_ID , "詳細情報")

LINE_COUNT = 1
;;/_/_/_/_/_/_/_/_/_/_/_/_/
; 艦艇スペック
;;/_/_/_/_/_/_/_/_/_/_/_/_/
CALL WndMgr_TextSet( SHIP_DETAIL_WINDOW_ID, LINE_COUNT ++, @"■艦艇情報")
LOCALS = 耐久
FOR LOCAL, 0, SHIP_STATUS:SHIP_ARMOR_SLOT
	TRYCALLFORM GET_ARMOR_STATUS_{SHIP_EQUIP:装甲:LOCAL:SHIP_ID}(WEAPON_STATUS)
	SHIP_WEAPON_STATUS:ARMOR += WEAPON_STATUS:ARMOR
NEXT
CALL WndMgr_TextSet( SHIP_DETAIL_WINDOW_ID, LINE_COUNT ++, @"%LOCALS, 12, LEFT% {MAX_HP} 基礎値:{SHIP_STATUS:SHIP_HP} + 追加装甲:{SHIP_WEAPON_STATUS:ARMOR}")

FOR LOCAL, 0, SHIP_STATUS:SHIP_SIELD_SLOT
	TRYCALLFORM GET_SIELD_STATUS_{SHIP_EQUIP:シールド:LOCAL:SHIP_ID}(WEAPON_STATUS)
	SHIP_WEAPON_STATUS:SIELD += WEAPON_STATUS:SIELD
NEXT
CALL WndMgr_TextSet( SHIP_DETAIL_WINDOW_ID, LINE_COUNT ++, @"%WEAPON_STATUS_NAME:SIELD, 12, LEFT% {SHIP_WEAPON_STATUS:SIELD}")

LOCALS = 回避
CALL GET_AVOID(SHIP_ID)
LOCAL = RESULT
CALL WndMgr_TextSet( SHIP_DETAIL_WINDOW_ID, LINE_COUNT ++, @"%LOCALS, 12, LEFT% {LOCAL} 基礎値:{SHIP_STATUS:SHIP_AVOID} + 装備補正:{LOCAL - SHIP_STATUS:SHIP_AVOID}")

LOCALS = 被CRT率
CALL GET_CRITICAL_RISK(SHIP_ID)
CALL WndMgr_TextSet( SHIP_DETAIL_WINDOW_ID, LINE_COUNT ++, @"%LOCALS, 12, LEFT% {RESULT / 10}.{RESULT % 10}")

LINE_COUNT ++

;;/_/_/_/_/_/_/_/_/_/_/_/_/
; ここから主砲
;;/_/_/_/_/_/_/_/_/_/_/_/_/
CALL WndMgr_TextSet( SHIP_DETAIL_WINDOW_ID, LINE_COUNT ++, @"■主砲")
LOCALS = 
LOCAL:1 = 0
VARSET SHIP_WEAPON_STATUS
DPS = 0
MIN_RANGE = 0
MIN_RANGE = 10
FOR LOCAL, 0, SHIP_STATUS:SHIP_MAIN_WEAPON_SLOT
	TRYCALLFORM GET_MAIN_WEAPON_STATUS_{SHIP_EQUIP:主砲:LOCAL:SHIP_ID}(WEAPON_STATUS)
	IF WEAPON_STATUS:WEAPON_TYPE == 射出装置
		CALL LAUNCHER_STATUS_CALC_EQUIP_ID(SHIP_EQUIP:主砲:LOCAL:SHIP_ID, SHIP_EQUIP:射出対象:LOCAL:SHIP_ID, WEAPON_STATUS)
		IF WEAPON_STATUS:DAMAGE == 0 || WEAPON_STATUS:FIRE_RATE <= 0 ||(WEAPON_STATUS:WEAPON_TYPE == 艦載機 && WEAPON_STATUS:AIR_SHIP_COUNT <= 0)
			; 攻撃能力を持たないデータであれば空きスロットと同様に扱う
			VARSET WEAPON_STATUS
			WEAPON_STATUS:WEAPON_TYPE = 射出装置
		ENDIF
	ENDIF
	SHIP_WEAPON_STATUS:DAMAGE += WEAPON_STATUS:DAMAGE * WEAPON_STATUS:FIRE_RATE
	DPS += WEAPON_STATUS:DAMAGE * WEAPON_STATUS:FIRE_RATE * WEAPON_STATUS:HIT_RATE / 100
	MIN_RANGE = MIN(MIN_RANGE, WEAPON_STATUS:FIRE_RANGE)
	MAX_RANGE = MAX(MAX_RANGE, WEAPON_STATUS:FIRE_RANGE)
	SETBIT LOCAL:1, WEAPON_STATUS:WEAPON_TYPE
NEXT
LOCALS = 最大ダメージ
CALL WndMgr_TextSet( SHIP_DETAIL_WINDOW_ID, LINE_COUNT ++, @"%LOCALS, 12, LEFT% {SHIP_WEAPON_STATUS:DAMAGE}")
LOCALS = 平均ダメージ
CALL WndMgr_TextSet( SHIP_DETAIL_WINDOW_ID, LINE_COUNT ++, @"%LOCALS, 12, LEFT% {DPS}")
LOCALS = 最短レンジ
CALL WndMgr_TextSet( SHIP_DETAIL_WINDOW_ID, LINE_COUNT ++, @"%LOCALS, 12, LEFT% {MIN_RANGE}")
LOCALS = 最長レンジ
CALL WndMgr_TextSet( SHIP_DETAIL_WINDOW_ID, LINE_COUNT ++, @"%LOCALS, 12, LEFT% {MAX_RANGE}")
LOCALS = タイプ
FOR LOCAL, 0, VARSIZE("WEAPON_TYPE_NAME")
	IF GETBIT(LOCAL:1, LOCAL)
		IF STRLENS(LOCALS) == 0
			LOCALS = %WEAPON_TYPE_NAME:LOCAL%
		ELSE
			LOCALS = %LOCALS%/%WEAPON_TYPE_NAME:LOCAL%
		ENDIF
	ENDIF
NEXT
CALL WndMgr_TextSet( SHIP_DETAIL_WINDOW_ID, LINE_COUNT ++, @"%WEAPON_STATUS_NAME:WEAPON_TYPE, 12, LEFT% %LOCALS%")
LINE_COUNT ++

;;/_/_/_/_/_/_/_/_/_/_/_/_/
; ここから副砲
;;/_/_/_/_/_/_/_/_/_/_/_/_/
CALL WndMgr_TextSet( SHIP_DETAIL_WINDOW_ID, LINE_COUNT ++, @"■副砲")
VARSET SHIP_WEAPON_STATUS
DPS = 0
MIN_RANGE = 0
MIN_RANGE = 10
FOR LOCAL, 0, SHIP_STATUS:SHIP_SUB_WEAPON_SLOT
	TRYCALLFORM GET_SUB_WEAPON_STATUS_{SHIP_EQUIP:副砲:LOCAL:SHIP_ID}(WEAPON_STATUS)
	SHIP_WEAPON_STATUS:DAMAGE += WEAPON_STATUS:DAMAGE * WEAPON_STATUS:FIRE_RATE
	DPS += WEAPON_STATUS:DAMAGE * WEAPON_STATUS:FIRE_RATE * WEAPON_STATUS:HIT_RATE / 100
	MIN_RANGE = MIN(MIN_RANGE, WEAPON_STATUS:FIRE_RANGE)
	MAX_RANGE = MAX(MAX_RANGE, WEAPON_STATUS:FIRE_RANGE)
	SHIP_WEAPON_STATUS:ANTI_AIR += WEAPON_STATUS:ANTI_AIR
NEXT
LOCALS = 最大ダメージ
CALL WndMgr_TextSet( SHIP_DETAIL_WINDOW_ID, LINE_COUNT ++, @"%LOCALS, 12, LEFT% {SHIP_WEAPON_STATUS:DAMAGE}")
LOCALS = 平均ダメージ
CALL WndMgr_TextSet( SHIP_DETAIL_WINDOW_ID, LINE_COUNT ++, @"%LOCALS, 12, LEFT% {DPS}")
LOCALS = 最短レンジ
CALL WndMgr_TextSet( SHIP_DETAIL_WINDOW_ID, LINE_COUNT ++, @"%LOCALS, 12, LEFT% {MIN_RANGE}")
LOCALS = 最長レンジ
CALL WndMgr_TextSet( SHIP_DETAIL_WINDOW_ID, LINE_COUNT ++, @"%LOCALS, 12, LEFT% {MAX_RANGE}")
CALL WndMgr_TextSet( SHIP_DETAIL_WINDOW_ID, LINE_COUNT ++, @"%WEAPON_STATUS_NAME:ANTI_AIR, 12, LEFT% {ANTI_AIR}")
LINE_COUNT ++


;;/_/_/_/_/_/_/_/_/_/_/_/_/
; ここからその他の装備
; ここからシールド
;;/_/_/_/_/_/_/_/_/_/_/_/_/
CALL WndMgr_TextSet( SHIP_DETAIL_WINDOW_ID, LINE_COUNT ++, @"■他")
VARSET SHIP_WEAPON_STATUS_BUFFER

LINE_COUNT ++
FOR LOCAL, 0, SHIP_STATUS:SHIP_SIELD_SLOT
	VARSET WEAPON_STATUS
	TRYCALLFORM GET_SIELD_STATUS_{SHIP_EQUIP:シールド:LOCAL:SHIP_ID}(WEAPON_STATUS)
	TOTAL_SIELD_RECOVER += WEAPON_STATUS:SIELD_RECOVER
NEXT
;;/_/_/_/_/_/_/_/_/_/_/_/_/
; ここからエンジン
;;/_/_/_/_/_/_/_/_/_/_/_/_/
VARSET WEAPON_STATUS
TRYCALLFORM GET_ENGIN_STATUS_{SHIP_EQUIP:エンジン:0:SHIP_ID}(WEAPON_STATUS)
FOR LOCAL, 0, STATUS_LENGTH
	SIF LOCAL == PRICE || WEAPON_STATUS:LOCAL == 0
		CONTINUE
	SHIP_WEAPON_STATUS_BUFFER:LOCAL += WEAPON_STATUS:LOCAL
NEXT

;;/_/_/_/_/_/_/_/_/_/_/_/_/
; ここからAI
;;/_/_/_/_/_/_/_/_/_/_/_/_/
VARSET WEAPON_STATUS
TRYCALLFORM GET_COMPUTER_STATUS_{SHIP_EQUIP:AI:0:SHIP_ID}(WEAPON_STATUS)
FOR LOCAL, 0, STATUS_LENGTH
	SIF LOCAL == PRICE || WEAPON_STATUS:LOCAL == 0
		CONTINUE
	SHIP_WEAPON_STATUS_BUFFER:LOCAL += WEAPON_STATUS:LOCAL
NEXT

;;/_/_/_/_/_/_/_/_/_/_/_/_/
; ここからセンサー
;;/_/_/_/_/_/_/_/_/_/_/_/_/
TRYCALLFORM GET_SENSOR_STATUS_{SHIP_EQUIP:センサー:0:SHIP_ID}(WEAPON_STATUS)
SHIP_WEAPON_STATUS_BUFFER:SENSOR += WEAPON_STATUS:SENSOR

;;/_/_/_/_/_/_/_/_/_/_/_/_/
; ここからオプション
;;/_/_/_/_/_/_/_/_/_/_/_/_/
FOR LOCAL, 0, SHIP_STATUS:SHIP_OPTION_SLOT
	VARSET WEAPON_STATUS
	TRYCALLFORM GET_OPTION_STATUS_{SHIP_EQUIP:オプション:LOCAL:SHIP_ID}(WEAPON_STATUS)
	FOR LOCAL:1, 0, STATUS_LENGTH
		SIF LOCAL:1 == PRICE || WEAPON_STATUS:(LOCAL:1) == 0
			CONTINUE
		SHIP_WEAPON_STATUS_BUFFER:(LOCAL:1) += WEAPON_STATUS:(LOCAL:1)
	NEXT
NEXT

;;/_/_/_/_/_/_/_/_/_/_/_/_/
; 表示して終了
;;/_/_/_/_/_/_/_/_/_/_/_/_/
FOR LOCAL, 0, STATUS_LENGTH
	SIF SHIP_WEAPON_STATUS_BUFFER:LOCAL == 0
		CONTINUE
	SELECTCASE LOCAL
		CASE FIRE_RATE
			LOCALS = 発射回数補正
			CALL WndMgr_TextSet( SHIP_DETAIL_WINDOW_ID, LINE_COUNT ++, @"%LOCALS, 12, LEFT% {SHIP_WEAPON_STATUS_BUFFER:LOCAL}")
		CASE HIT_RATE
			LOCALS = 命中率補正
			CALL WndMgr_TextSet( SHIP_DETAIL_WINDOW_ID, LINE_COUNT ++, @"%LOCALS, 12, LEFT% {SHIP_WEAPON_STATUS_BUFFER:LOCAL}")
		CASE SIELD_RECOVER, SPEED, SENSOR
			CALL WndMgr_TextSet( SHIP_DETAIL_WINDOW_ID, LINE_COUNT ++, @"%WEAPON_STATUS_NAME:LOCAL, 12, LEFT% {SHIP_WEAPON_STATUS_BUFFER:LOCAL}")
	ENDSELECT
NEXT

LINE_COUNT ++
CALL WndMgr_Rectangle( SHIP_DETAIL_WINDOW_ID, 56, 2, 53, LINE_COUNT + 2, 0X02)	;枠の高さ再設定

;;;指定windowの表示を実行
STATUS_WINDOW_ARRAY:0 = SHIP_COMMON_STATUS_WINDOW_ID
STATUS_WINDOW_ARRAY:1 = SHIP_DETAIL_WINDOW_ID
CALL WndMgr_Display(STATUS_WINDOW_COUNT, STATUS_WINDOW_ARRAY, 0)
PRINTL
PRINT 　
PRINTBUTTON @"[閉じる]", -1
PRINTL
INPUT
IF RESULT == 1001
	;CALL CREATE_SHIP_DESCRIPTION_WINDOW(SHIP_DESCRIPTION)
	CALL CREATE_SHIP_DESCRIPTION_WINDOW_LIST(SHIP_DESCRIPTION_SPLIT_LIST, SHIP_DESCRIPTION_COLOR_LIST, LIST_LENGTH)
	RESTART
ELSEIF RESULT >= 0 && RESULT < EQUIP_TYPE_LENGTH * MAX_SLOT
	VARSET WEAPON_NAME
	SELECT_CATEGORY = RESULT / MAX_SLOT
	SELECT_SLOT_ID = RESULT % MAX_SLOT
	SELECTCASE SELECT_CATEGORY
		CASE 主砲
			TRYCALLFORM GET_MAIN_WEAPON_NAME_{SHIP_EQUIP:主砲:SELECT_SLOT_ID:SHIP_ID}
			WEAPON_NAME = %RESULTS%
			TRYCALLFORM GET_MAIN_WEAPON_STATUS_{SHIP_EQUIP:主砲:SELECT_SLOT_ID:SHIP_ID}(WEAPON_STATUS)
			IF WEAPON_STATUS:WEAPON_TYPE == 射出装置
				CALL LAUNCHER_STATUS_CALC_EQUIP_ID(SHIP_EQUIP:主砲:SELECT_SLOT_ID:SHIP_ID, SHIP_EQUIP:射出対象:SELECT_SLOT_ID:SHIP_ID, WEAPON_STATUS)
				IF WEAPON_STATUS:DAMAGE == 0 || WEAPON_STATUS:FIRE_RATE <= 0 ||(WEAPON_STATUS:WEAPON_TYPE == 艦載機 && WEAPON_STATUS:AIR_SHIP_COUNT <= 0)
					; 攻撃能力を持たないデータであれば空きスロットと同様に扱う(属性だけ射出装置扱いに)
					VARSET WEAPON_STATUS
					WEAPON_STATUS:WEAPON_TYPE = 射出装置
				ELSE
					; 攻撃能力があるデータなら名前を入れる(全部入れたら長い(はみ出うる)ので射出装置の方は射出装置とだけする)
					TRYCALLFORM GET_PAYLOAD_NAME_{SHIP_EQUIP:射出対象:SELECT_SLOT_ID:SHIP_ID}
					WEAPON_NAME = 射出装置+%RESULTS%
				ENDIF
			ENDIF
		CASE 副砲
			TRYCALLFORM GET_SUB_WEAPON_NAME_{SHIP_EQUIP:副砲:SELECT_SLOT_ID:SHIP_ID}
			WEAPON_NAME = %RESULTS%
			TRYCALLFORM GET_SUB_WEAPON_STATUS_{SHIP_EQUIP:副砲:SELECT_SLOT_ID:SHIP_ID}(WEAPON_STATUS)
		CASE シールド
			TRYCALLFORM GET_SIELD_NAME_{SHIP_EQUIP:シールド:SELECT_SLOT_ID:SHIP_ID}
			WEAPON_NAME = %RESULTS%
			TRYCALLFORM GET_SIELD_STATUS_{SHIP_EQUIP:シールド:SELECT_SLOT_ID:SHIP_ID}(WEAPON_STATUS)
		CASE 装甲
			TRYCALLFORM GET_ARMOR_NAME_{SHIP_EQUIP:装甲:SELECT_SLOT_ID:SHIP_ID}
			WEAPON_NAME = %RESULTS%
			TRYCALLFORM GET_ARMOR_STATUS_{SHIP_EQUIP:装甲:SELECT_SLOT_ID:SHIP_ID}(WEAPON_STATUS)
		CASE エンジン
			TRYCALLFORM GET_ENGIN_NAME_{SHIP_EQUIP:エンジン:SELECT_SLOT_ID:SHIP_ID}
			WEAPON_NAME = %RESULTS%
			TRYCALLFORM GET_ENGIN_STATUS_{SHIP_EQUIP:エンジン:SELECT_SLOT_ID:SHIP_ID}(WEAPON_STATUS)
		CASE AI
			TRYCALLFORM GET_COMPUTER_NAME_{SHIP_EQUIP:AI:SELECT_SLOT_ID:SHIP_ID}
			WEAPON_NAME = %RESULTS%
			TRYCALLFORM GET_COMPUTER_STATUS_{SHIP_EQUIP:AI:SELECT_SLOT_ID:SHIP_ID}(WEAPON_STATUS)
		CASE センサー
			TRYCALLFORM GET_SENSOR_NAME_{SHIP_EQUIP:センサー:SELECT_SLOT_ID:SHIP_ID}
			WEAPON_NAME = %RESULTS%
			TRYCALLFORM GET_SENSOR_STATUS_{SHIP_EQUIP:センサー:SELECT_SLOT_ID:SHIP_ID}(WEAPON_STATUS)
		CASE オプション
			TRYCALLFORM GET_OPTION_NAME_{SHIP_EQUIP:オプション:SELECT_SLOT_ID:SHIP_ID}
			WEAPON_NAME = %RESULTS%
			TRYCALLFORM GET_OPTION_STATUS_{SHIP_EQUIP:オプション:SELECT_SLOT_ID:SHIP_ID}(WEAPON_STATUS)
	ENDSELECT
	IF STRLENS(WEAPON_NAME) > 0
		CALL CREATE_WEAPON_WINDOW(SELECT_CATEGORY, WEAPON_STATUS, 4 + LINE_COUNT, WEAPON_NAME, SHIP_ID, SELECT_SLOT_ID)
		RESTART
	ENDIF
ENDIF
CALL WndMgr_DestroyWindow( -1 )

;----------------------------
; 船の詳細WINDOWからの派生
; window10 武装詳細
;----------------------------
@CREATE_WEAPON_WINDOW(_WEAPON_CATEGORY, WEAPON_STATUS, Y_TOP = 0, WEAPON_TITLE, SHIP_ID, SLOT_ID)
#DIM DYNAMIC _WEAPON_CATEGORY
#DIM STATUS_WINDOW_ARRAY,3
#DIM REF WEAPON_STATUS
#DIM Y_TOP
#DIMS WEAPON_TITLE
#DIM DYNAMIC LINE_COUNT

#DIM SHIP_ID
#DIM SLOT_ID


STATUS_WINDOW_ARRAY:0 = SHIP_COMMON_STATUS_WINDOW_ID
STATUS_WINDOW_ARRAY:1 = SHIP_DETAIL_WINDOW_ID
STATUS_WINDOW_ARRAY:2 = SHIP_WEAPON_WINDOW_ID

CALL SETUP_WINDOW
CALL WndMgr_ResetWindow( SHIP_WEAPON_WINDOW_ID )

CALL WndMgr_WindowTitle( SHIP_WEAPON_WINDOW_ID , "武装詳細情報")
LINE_COUNT = 1
LOCALS = 名称
CALL WndMgr_TextSet( SHIP_WEAPON_WINDOW_ID, LINE_COUNT ++, @"%LOCALS, 12, LEFT% %WEAPON_TITLE%")
LINE_COUNT ++
IF GROUPMATCH(_WEAPON_CATEGORY, 主砲, 副砲)
	CALL WndMgr_TextSet( SHIP_WEAPON_WINDOW_ID, LINE_COUNT ++, @"%WEAPON_STATUS_NAME:WEAPON_TYPE, 12, LEFT% %WEAPON_TYPE_NAME:(WEAPON_STATUS:WEAPON_TYPE)%")
	CALL WndMgr_TextSet( SHIP_WEAPON_WINDOW_ID, LINE_COUNT ++, @"%WEAPON_STATUS_NAME:DAMAGE, 12, LEFT% {WEAPON_STATUS:DAMAGE}")
	CALL WndMgr_TextSet( SHIP_WEAPON_WINDOW_ID, LINE_COUNT ++, @"%WEAPON_STATUS_NAME:FIRE_RATE, 12, LEFT% {WEAPON_STATUS:FIRE_RATE}")
	CALL WndMgr_TextSet( SHIP_WEAPON_WINDOW_ID, LINE_COUNT ++, @"%WEAPON_STATUS_NAME:HIT_RATE, 12, LEFT% {WEAPON_STATUS:HIT_RATE}")
	CALL WndMgr_TextSet( SHIP_WEAPON_WINDOW_ID, LINE_COUNT ++, @"%WEAPON_STATUS_NAME:FIRE_RANGE, 12, LEFT% {WEAPON_STATUS:FIRE_RANGE}")
	IF GROUPMATCH(WEAPON_STATUS:WEAPON_TYPE, ミサイル, ミサイル_自爆)
		LOCALS = 射出物耐久
		CALL WndMgr_TextSet( SHIP_WEAPON_WINDOW_ID, LINE_COUNT ++, @"%LOCALS, 12, LEFT% {MISSILE_HP}")
		LOCALS = 射出物回避
		CALL WndMgr_TextSet( SHIP_WEAPON_WINDOW_ID, LINE_COUNT ++, @"%LOCALS, 12, LEFT% {MISSILE_AVOID}")
	ELSEIF GROUPMATCH(WEAPON_STATUS:WEAPON_TYPE, 魚雷, 魚雷_自爆)
		LOCALS = 射出物耐久
		CALL WndMgr_TextSet( SHIP_WEAPON_WINDOW_ID, LINE_COUNT ++, @"%LOCALS, 12, LEFT% {TORPED_HP}")
		LOCALS = 射出物回避
		CALL WndMgr_TextSet( SHIP_WEAPON_WINDOW_ID, LINE_COUNT ++, @"%LOCALS, 12, LEFT% {TORPED_AVOID}")
	ELSEIF GROUPMATCH(WEAPON_STATUS:WEAPON_TYPE, 艦載機, 艦載機_自爆) && _WEAPON_CATEGORY == 主砲
		LOCALS = 射出物耐久
		CALL WndMgr_TextSet( SHIP_WEAPON_WINDOW_ID, LINE_COUNT ++, @"%LOCALS, 12, LEFT% {WEAPON_STATUS:AIR_SHIP_HP}")
		LOCALS = 射出物回避
		;CALL WndMgr_TextSet( SHIP_WEAPON_WINDOW_ID, LINE_COUNT ++, @"%LOCALS, 12, LEFT% {WEAPON_STATUS:AVOID}")
		CALL WndMgr_TextSet( SHIP_WEAPON_WINDOW_ID, LINE_COUNT ++, @"%LOCALS, 12, LEFT% {AIR_SHIP_AVOID}")
		LOCALS = 航空機数
		CALL GET_JOIN_AIR_SHIP(SHIP_ID, SLOT_ID)
		CALL WndMgr_TextSet( SHIP_WEAPON_WINDOW_ID, LINE_COUNT ++, @"%LOCALS, 12, LEFT% {RESULT} / {WEAPON_STATUS:AIR_SHIP_COUNT}")
	ENDIF
ENDIF
IF _WEAPON_CATEGORY == 副砲
	CALL WndMgr_TextSet( SHIP_WEAPON_WINDOW_ID, LINE_COUNT ++, @"%WEAPON_STATUS_NAME:ANTI_AIR, 12, LEFT% {WEAPON_STATUS:ANTI_AIR}")
ENDIF
IF _WEAPON_CATEGORY == シールド
	CALL WndMgr_TextSet( SHIP_WEAPON_WINDOW_ID, LINE_COUNT ++, @"%WEAPON_STATUS_NAME:SIELD, 12, LEFT% {WEAPON_STATUS:SIELD}")
	CALL WndMgr_TextSet( SHIP_WEAPON_WINDOW_ID, LINE_COUNT ++, @"%WEAPON_STATUS_NAME:SIELD_RECOVER, 12, LEFT% {WEAPON_STATUS:SIELD_RECOVER}")
ENDIF
IF _WEAPON_CATEGORY == 装甲
	CALL WndMgr_TextSet( SHIP_WEAPON_WINDOW_ID, LINE_COUNT ++, @"%WEAPON_STATUS_NAME:ARMOR, 12, LEFT% {WEAPON_STATUS:ARMOR}")
ENDIF
IF _WEAPON_CATEGORY == エンジン
	CALL WndMgr_TextSet( SHIP_WEAPON_WINDOW_ID, LINE_COUNT ++, @"%WEAPON_STATUS_NAME:SPEED, 12, LEFT% {WEAPON_STATUS:SPEED}")
	CALL WndMgr_TextSet( SHIP_WEAPON_WINDOW_ID, LINE_COUNT ++, @"%WEAPON_STATUS_NAME:AVOID, 12, LEFT% {WEAPON_STATUS:AVOID}")
ENDIF
IF _WEAPON_CATEGORY == AI
	FOR LOCAL, 0, STATUS_LENGTH
		SIF WEAPON_STATUS:LOCAL && LOCAL != PRICE
			CALL WndMgr_TextSet( SHIP_WEAPON_WINDOW_ID, LINE_COUNT ++, @"%WEAPON_STATUS_NAME:LOCAL, 12, LEFT% {WEAPON_STATUS:AVOID}")
	NEXT
ENDIF
IF _WEAPON_CATEGORY == センサー
	CALL WndMgr_TextSet( SHIP_WEAPON_WINDOW_ID, LINE_COUNT ++, @"%WEAPON_STATUS_NAME:SENSOR, 12, LEFT% {WEAPON_STATUS:SENSOR}")
ENDIF
IF GROUPMATCH(_WEAPON_CATEGORY, 主砲, 副砲, シールド, 装甲, エンジン)
	LOCALS = 武装サイズ
	CALL WndMgr_TextSet( SHIP_WEAPON_WINDOW_ID, LINE_COUNT ++, @"%WEAPON_STATUS_NAME:SIZE, 12, LEFT% {WEAPON_STATUS:SIZE}")
ENDIF
LINE_COUNT ++

CALL WndMgr_Rectangle( SHIP_WEAPON_WINDOW_ID, 56, Y_TOP, 53, LINE_COUNT + 2, 0X02)	;枠の高さ再設定

CALL WndMgr_Display(3, STATUS_WINDOW_ARRAY, 0)
PRINTW 　CLICK OR ENTER KEY TO NEXT
RETURN

;----------------------------
; 艦船説明のもっと見るボタンを押した時
; window11 艦船説明
;----------------------------
@CREATE_SHIP_DESCRIPTION_WINDOW(SHIP_DESCRIPTION)
#DIMS SHIP_DESCRIPTION
#DIM STATUS_WINDOW_ARRAY, 3
#DIM DYNAMIC LINE_COUNT
#DIMS DYNAMIC WORD_BUFFER


STATUS_WINDOW_ARRAY:0 = SHIP_COMMON_STATUS_WINDOW_ID
STATUS_WINDOW_ARRAY:1 = SHIP_DETAIL_WINDOW_ID
STATUS_WINDOW_ARRAY:2 = SHIP_DESCRIPTION_WINDOW_ID

CALL SETUP_WINDOW
CALL WndMgr_ResetWindow( SHIP_DESCRIPTION_WINDOW_ID )

CALL WndMgr_WindowTitle( SHIP_DESCRIPTION_WINDOW_ID , "■ 概要")
LINE_COUNT = 1
WHILE 1
	IF STRLENS(SHIP_DESCRIPTION) > 46
		CALL WndMgr_TextSet( SHIP_DESCRIPTION_WINDOW_ID, LINE_COUNT ++, @"%SUBSTRING(SHIP_DESCRIPTION, 0, 46)%")
		SHIP_DESCRIPTION = %SUBSTRING(SHIP_DESCRIPTION, 46, -1)%
	ELSE
		CALL WndMgr_TextSet( SHIP_DESCRIPTION_WINDOW_ID, LINE_COUNT ++, @"%SHIP_DESCRIPTION%")
		BREAK
	ENDIF
WEND
LINE_COUNT ++

CALL WndMgr_Rectangle( SHIP_DESCRIPTION_WINDOW_ID, 2, 9, 53, LINE_COUNT + 2, 0X02)	;枠の高さ再設定

CALL WndMgr_Display(3, STATUS_WINDOW_ARRAY, 0)
PRINTW 　CLICK OR ENTER KEY TO NEXT
RETURN

;カラーコードと本文の分離
@SHIP_DESCRIPTION_SPLIT_LIST_INIT(SHIP_DESCRIPTION_SPLIT_LIST, SHIP_DESCRIPTION_COLOR_LIST, LIST_LENGTH)
#DIMS REF SHIP_DESCRIPTION_SPLIT_LIST
#DIMS REF SHIP_DESCRIPTION_COLOR_LIST

#DIM LIST_LENGTH

#DIMS COLOR_CODE_STR
#DIM COLOR_CODE_INT

FOR COUNT, 0, LIST_LENGTH
	; カラーコードを取得して本文から消す
	COLOR_CODE_STR '= DIC_GET(SHIP_DESCRIPTION_SPLIT_LIST:COUNT, "カラーコード")
	SHIP_DESCRIPTION_SPLIT_LIST:COUNT '= DIC_REMOVE(SHIP_DESCRIPTION_SPLIT_LIST:COUNT, "カラーコード")
	COLOR_CODE_INT = TOINT(COLOR_CODE_STR)
	IF COLOR_CODE_INT <= -1
		; ウィンドウの処理は-1でのリセット命令を理解できないので力業で疑似リセットする
		COLOR_CODE_INT = GETCOLOR()
	ENDIF
	COLOR_CODE_STR '= "0x" + TOSTR(COLOR_CODE_INT, "X6")
	;カラーコードをTagSetTextのタグ形式にしてリストに入れる
	SHIP_DESCRIPTION_COLOR_LIST:COUNT '= "@C:" + COLOR_CODE_STR + "@"

NEXT

RETURN

;----------------------------
; 艦船説明のもっと見るボタンを押した時(リスト化版)
; window11 艦船説明
;----------------------------
@CREATE_SHIP_DESCRIPTION_WINDOW_LIST(SHIP_DESCRIPTION_SPLIT_LIST, SHIP_DESCRIPTION_COLOR_LIST, LIST_LENGTH)
#DIMS SHIP_DESCRIPTION
#DIM STATUS_WINDOW_ARRAY, 3
#DIM DYNAMIC LINE_COUNT
#DIMS DYNAMIC WORD_BUFFER
#DIMS REF SHIP_DESCRIPTION_SPLIT_LIST
#DIMS REF SHIP_DESCRIPTION_COLOR_LIST
#DIM LIST_LENGTH


STATUS_WINDOW_ARRAY:0 = SHIP_COMMON_STATUS_WINDOW_ID
STATUS_WINDOW_ARRAY:1 = SHIP_DETAIL_WINDOW_ID
STATUS_WINDOW_ARRAY:2 = SHIP_DESCRIPTION_WINDOW_ID

CALL SETUP_WINDOW
CALL WndMgr_ResetWindow( SHIP_DESCRIPTION_WINDOW_ID )

CALL WndMgr_WindowTitle( SHIP_DESCRIPTION_WINDOW_ID , "■ 概要")
LINE_COUNT = 1


FOR COUNT, 0, LIST_LENGTH
	SHIP_DESCRIPTION '= SHIP_DESCRIPTION_SPLIT_LIST:COUNT
	WHILE 1
	;PRINTFORMW {LINE_COUNT}
		IF STRLENS(SHIP_DESCRIPTION) > 46
			CALL WndMgr_TextSet( SHIP_DESCRIPTION_WINDOW_ID, LINE_COUNT ++, @"%SHIP_DESCRIPTION_COLOR_LIST:COUNT%%SUBSTRING(SHIP_DESCRIPTION, 0, 46)%@/C@")
			SHIP_DESCRIPTION = %SUBSTRING(SHIP_DESCRIPTION, 46, -1)%
		ELSE
			CALL WndMgr_TextSet( SHIP_DESCRIPTION_WINDOW_ID, LINE_COUNT ++, @"%SHIP_DESCRIPTION_COLOR_LIST:COUNT%%SHIP_DESCRIPTION%@/C@")
			BREAK
		ENDIF
	WEND
	LINE_COUNT ++
NEXT

CALL WndMgr_Rectangle( SHIP_DESCRIPTION_WINDOW_ID, 2, 9, 53, LINE_COUNT + 2, 0X02)	;枠の高さ再設定

CALL WndMgr_Display(3, STATUS_WINDOW_ARRAY, 0)
PRINTW 　CLICK OR ENTER KEY TO NEXT
RETURN
