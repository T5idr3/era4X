;---------------------------------
; ドクトリン
;---------------------------------
@DOCTRINE_MENU
#DIM CONST _MODE_SELECT = 0
#DIM CONST _MODE_EDIT = 1
#DIM _EDIT_MODE
#DIM _EDIT_DOCTRINE_SET_ID

$INPUT_LOOP
IF _EDIT_MODE == _MODE_SELECT
	CALL SELECT_EDIT_DOCTRINE
	SIF RESULT:0 == 0
		RETURN
	_EDIT_DOCTRINE_SET_ID = RESULT:1
	_EDIT_MODE = _MODE_EDIT
ELSE
	CALL EDIT_DOCTRINE_SET(_EDIT_DOCTRINE_SET_ID)
	_EDIT_MODE = _MODE_SELECT
ENDIF
GOTO INPUT_LOOP


;---------------------------------
; ドクトリン選択
;---------------------------------
@SELECT_EDIT_DOCTRINE

PRINTL
CUSTOMDRAWLINE =
PRINTL 　コマンドオーダー選択
SETCOLOR 0X333333
DRAWLINE
RESETCOLOR
PRINTL 　　艦船に設定するコマンドオーダーを編集できます
PRINTL 　　艦船はコマンドオーダーにより行動の優先順を決定します
SETCOLOR 0X666666
DRAWLINE
RESETCOLOR
FOR LOCAL, 0, MAX_DOCTRINE_SET
	STRLENFORM %DOCTRINE_NAME:LOCAL:国家ID_プレイヤー%
	IF RESULT > 0
		PRINTBUTTON @"[{LOCAL, 2, RIGHT}] %DOCTRINE_NAME:LOCAL:国家ID_プレイヤー%", LOCAL
	ELSE
		PRINTBUTTON @"[{LOCAL, 2, RIGHT}] 未設定", LOCAL
	ENDIF
	PRINTL
NEXT
PRINTL
PRINT 　
PRINTBUTTON "[戻る]", MAX_DOCTRINE_SET
PRINTL
$INPUT_LOOP
INPUT
IF RESULT >= 0 && RESULT < MAX_DOCTRINE_SET
	RESULT:1 = RESULT:0
	RETURN 1
ELSEIF RESULT == MAX_DOCTRINE_SET
	RETURN 0
ELSE
	GOTO INPUT_LOOP
ENDIF

;---------------------------------
; ドクトリン編集詳細
;---------------------------------
@EDIT_DOCTRINE_SET(_SET_ID)
#DIM _SET_ID
#DIM _EDIT_ID
#DIM _ESCAPE_COMMAND_INDEX
#DIM _MOVE_COMMAND_INDEX
#DIM _ATTACK_COMMAND_INDEX
#DIM _CAUTION
#DIM _COMMAND_ORDER_ID


$INPUT_LOOP
_ESCAPE_COMMAND_INDEX = -1
_MOVE_COMMAND_INDEX = -1
_ATTACK_COMMAND_INDEX = -1
_CAUTION = -1
PRINTL
CUSTOMDRAWLINE =
PRINTL 　コマンドオーダー編集
SETCOLOR 0X333333
DRAWLINE
RESETCOLOR
PRINTFORML 　　優先度 高 0 ~ {MAX_ORDER-1} 低
SETCOLOR 0X666666
DRAWLINE
RESETCOLOR
PRINT 　名称: 
STRLENFORM %DOCTRINE_NAME:_SET_ID:国家ID_プレイヤー%
IF RESULT > 0
	PRINTBUTTON @"%DOCTRINE_NAME:_SET_ID:国家ID_プレイヤー%[編集]", -1
ELSE
	PRINTBUTTON "未設定[編集]", -1
ENDIF
PRINTL
DRAWLINE
; 推測されるオーダーの問題を報告
FOR LOCAL, 0, MAX_ORDER
	_COMMAND_ORDER_ID = DOCTRINE_COMPOSITION:_SET_ID:LOCAL:国家ID_プレイヤー
	SIF IN_ESCAPE_COMMAND(_COMMAND_ORDER_ID) > -1
		_ESCAPE_COMMAND_INDEX ++
	SIF IN_MOVE_COMMAND(_COMMAND_ORDER_ID) > -1
		_MOVE_COMMAND_INDEX ++
	SIF IN_ATTACK_COMMAND(_COMMAND_ORDER_ID) > -1
		_ATTACK_COMMAND_INDEX ++
NEXT
IF _ESCAPE_COMMAND_INDEX == -1
	SETCOLOR 0XFF9999
	PRINTL 　撤退条件が設定されていません。大破するまで撤退しません。
	RESETCOLOR
	_CAUTION ++
ENDIF
IF _MOVE_COMMAND_INDEX == -1
	SETCOLOR 0XFF9999
	PRINTL 　移動条件が設定されていません。そのため攻撃に参加できません。
	RESETCOLOR
	_CAUTION ++
ENDIF
IF _ATTACK_COMMAND_INDEX == -1
	SETCOLOR 0XFFFF99
	PRINTL 　攻撃条件が設定されていません。敵への攻撃はランダムに行われます。
	RESETCOLOR
	_CAUTION ++
ENDIF

SIF _CAUTION
	DRAWLINE
FOR LOCAL, 0, MAX_ORDER
	IF DOCTRINE_COMPOSITION:_SET_ID:LOCAL:国家ID_プレイヤー != 0
		PRINTBUTTON @"[{LOCAL}] %DOCTRINE_COMMAND:(DOCTRINE_COMPOSITION:_SET_ID:LOCAL:国家ID_プレイヤー)%", LOCAL
	ELSE
		PRINTBUTTON @"[{LOCAL}] 未設定", LOCAL
	ENDIF
	PRINTL
NEXT
PRINTL
PRINT 　
PRINTBUTTON "戻る", MAX_ORDER
PRINTL

INPUT
IF RESULT >= 0 && RESULT < MAX_ORDER
	_EDIT_ID = RESULT
	CALL SELECT_COMMAND
	SIF RESULT != -1
		DOCTRINE_COMPOSITION:_SET_ID:_EDIT_ID:国家ID_プレイヤー = RESULT
	GOTO INPUT_LOOP
ELSEIF RESULT == -1
	CALL UPDATE_DOCTRINE_NAME(_SET_ID)
; オーダーの間に未設定があった場合はその要素を詰める
ELSEIF RESULT == MAX_ORDER
	FOR LOCAL, 0, MAX_ORDER
		SIF DOCTRINE_COMPOSITION:_SET_ID:LOCAL:国家ID_プレイヤー > 0
			CONTINUE
		WHILE DOCTRINE_COMPOSITION:_SET_ID:(LOCAL):国家ID_プレイヤー == 0
			FOR LOCAL:1, LOCAL, MAX_ORDER - 1
				DOCTRINE_COMPOSITION:_SET_ID:(LOCAL:1):国家ID_プレイヤー = DOCTRINE_COMPOSITION:_SET_ID:(LOCAL:1 + 1):国家ID_プレイヤー
				DOCTRINE_COMPOSITION:_SET_ID:(LOCAL:1 + 1):国家ID_プレイヤー = -1
			NEXT
		WEND
		SIF DOCTRINE_COMPOSITION:_SET_ID:(LOCAL):国家ID_プレイヤー < 0
			DOCTRINE_COMPOSITION:_SET_ID:(LOCAL):国家ID_プレイヤー = 0
	NEXT
	RETURN
ELSE
	CLEARLINE 1
ENDIF
GOTO INPUT_LOOP

;---------------------------------
; コマンドリスト表示
;---------------------------------
@SELECT_COMMAND
#DIM _COMMAND_SIZE

VARSIZE DOCTRINE_COMMAND
_COMMAND_SIZE = RESULT
FOR LOCAL, 1, _COMMAND_SIZE
	PRINT 　
	PRINTBUTTON @"[{LOCAL, 2, RIGHT}] %DOCTRINE_COMMAND:LOCAL%", LOCAL
	PRINTL
NEXT
PRINT 　
PRINTBUTTON "[未設定を設定]", 0
PRINTL
PRINTL 　
PRINTBUTTON "[決定せずに戻る]", _COMMAND_SIZE
$INPUT_LOOP
INPUT
IF RESULT >= 0 && RESULT < _COMMAND_SIZE
	RETURN RESULT
ELSEIF GROUPMATCH(RESULT, 0, _COMMAND_SIZE)
	RETURN -1
ELSE
	CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF

;---------------------------------
; ドクトリン名編集
;---------------------------------
@UPDATE_DOCTRINE_NAME(_COMMAND_ORDER_ID)
#DIM _COMMAND_ORDER_ID
#DIMS _CURRENT_NAME
#DIMS _NEW_NAME

PRINTL
CUSTOMDRAWLINE =
PRINTL 　コマンドオーダーの名称編集
SETCOLOR 0X999999
DRAWLINE
RESETCOLOR
STRLENFORM %DOCTRINE_NAME:_COMMAND_ORDER_ID:国家ID_プレイヤー%
IF RESULT == 0
	_CURRENT_NAME = %DOCTRINE_NAME:_COMMAND_ORDER_ID:国家ID_プレイヤー%
ELSE
	_CURRENT_NAME = 未設定
ENDIF
$INPUT_LOOP
PRINTFORML 　現在の名称: %_CURRENT_NAME%
PRINTL
PRINTL 新しい名前を入力してください
PRINTL
INPUTS
_NEW_NAME = %RESULTS%
STRLENFORM %_NEW_NAME%
IF RESULT > 0 && RESULT < 128
	$INPUT_LOOP_2
	PRINTFORML [%_NEW_NAME%]でよろしいですか? 現在の名称: %_CURRENT_NAME%
	PRINTBUTTON "[0] 決定", 0
	PRINTBUTTON "[1] 再入力", 1
	PRINTBUTTON "[2] 戻る", 2
	INPUT
	IF RESULT == 0
		DOCTRINE_NAME:_COMMAND_ORDER_ID:国家ID_プレイヤー = %_NEW_NAME%
		RETURN
	ELSEIF RESULT == 1
		GOTO INPUT_LOOP
	ELSEIF RESULT == 2
		RETURN
	ELSE
		CLEARLINE 1
		GOTO INPUT_LOOP_2
	ENDIF
ENDIF
CLEARLINE 1
GOTO INPUT_LOOP

;---------------------------------
; 撤退条件が含まれているか
;---------------------------------
@IN_ESCAPE_COMMAND(_COMMAND_ID)
#FUNCTION
#DIM _COMMAND_ID

FINDELEMENT ESCAPE_COMMAND, _COMMAND_ID
RETURNF RESULT

;---------------------------------
; 移動条件が含まれているか
;---------------------------------
@IN_MOVE_COMMAND(_COMMAND_ID)
#FUNCTION
#DIM _COMMAND_ID

FINDELEMENT MOVE_COMMAND, _COMMAND_ID
RETURNF RESULT

;---------------------------------
; 攻撃条件が含まれているか
;---------------------------------
@IN_ATTACK_COMMAND(_COMMAND_ID)
#FUNCTION
#DIM _COMMAND_ID

FINDELEMENT ATTACK_COMMAND, _COMMAND_ID
RETURNF RESULT

;---------------------------------
; 艦船にドクトリンを指定
;---------------------------------
@SELECT_DOCTRINE(_SHIP_ID)
#DIM _SHIP_ID
#DIM _SETUPED_ORDER, MAX_DOCTRINE_SET
#DIM _SELECTED_MODE
#DIM _SELECTED_ORDER

$INPUT_LOOP

CUSTOMDRAWLINE =
PRINTL 　コマンドオーダーを指定
SETCOLOR 0X999999
DRAWLINE
RESETCOLOR

FOR LOCAL, 0, MAX_DOCTRINE_SET
	_SETUPED_ORDER:LOCAL = 0
	FOR LOCAL:1, 0, MAX_ORDER
		SIF DOCTRINE_COMPOSITION:LOCAL:(LOCAL:1):(SHIP_COUNTRY:_SHIP_ID) != -1
			_SETUPED_ORDER:LOCAL = 1
	NEXT
	STRLENFORM %DOCTRINE_NAME:LOCAL:(SHIP_COUNTRY:_SHIP_ID)%
	SIF RECIEVED_DOCTRINE:_SHIP_ID == LOCAL
		SETCOLOR 0X99FF99
	IF RESULT > 0
		PRINTFORM 　　%DOCTRINE_NAME:LOCAL:(SHIP_COUNTRY:_SHIP_ID)%　
	ELSE
		PRINTFORM 　　未設定　
	ENDIF
	SIF RECIEVED_DOCTRINE:_SHIP_ID == LOCAL
		RESETCOLOR
	PRINTBUTTON "[編集]", LOCAL
	PRINT 　
	IF _SETUPED_ORDER:LOCAL > 0 && RECIEVED_DOCTRINE:_SHIP_ID != LOCAL
		PRINTBUTTON "[決定]", LOCAL + MAX_DOCTRINE_SET
	ELSE
		PRINT 設定済み
	ENDIF
	PRINTL
NEXT
PRINT 　
PRINTBUTTON "[戻る]", MAX_DOCTRINE_SET * 2
INPUT
_SELECTED_MODE = RESULT / MAX_DOCTRINE_SET
_SELECTED_ORDER = RESULT % MAX_DOCTRINE_SET
IF _SELECTED_MODE == 0
	SIF _SETUPED_ORDER:_SELECTED_ORDER == 1
		CALL EDIT_DOCTRINE_SET(_SELECTED_ORDER)
ELSEIF _SELECTED_MODE == 1
	RECIEVED_DOCTRINE:_SHIP_ID = _SELECTED_ORDER
ELSEIF _SELECTED_MODE == 2
	RETURN -1
ENDIF
GOTO INPUT_LOOP
