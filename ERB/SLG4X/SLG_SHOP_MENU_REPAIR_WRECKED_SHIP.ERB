;--------------------------
; 大破艦修復メニュー
;--------------------------
@SLG_REPAIR_WRECKED_SHIP(PORT_POSID, OWNER, LEFT_COLUMNS, LEFT_COLUMNS_BUTTON_VALUE)
#DIM PORT_POSID
#DIM OWNER
#DIMS REF LEFT_COLUMNS
#DIM REF LEFT_COLUMNS_BUTTON_VALUE
#DIM LINE_ITER
#DIM LEFT_COLUMN_SIZE
#DIM 入力値
#DIM SHIP_STATUS, SHIP_STATUS_LENGTH
#DIM SHIP_ID
#DIM REPAIR_PRICE, MAX_SHIP
#DIM TOTAL_PRICE
#DIM REPAIR_PRICE_RATIO = 70
#DIM WRECKED_COUNT

FINDELEMENT LEFT_COLUMNS_BUTTON_VALUE, NONE_LINE
LEFT_COLUMN_SIZE = RESULT
$INPUT_LOOP
LINE_ITER = 0

CALL PRINT_LEFT_COLUMN(LEFT_COLUMNS, LEFT_COLUMNS_BUTTON_VALUE, LINE_ITER)
CALL DRAW_RIGHT_COLUMN_GRAY_LINE
LINE_ITER ++

CALL PRINT_LEFT_COLUMN(LEFT_COLUMNS, LEFT_COLUMNS_BUTTON_VALUE, LINE_ITER)
IF MONEY == 0
    LOCALS = $0
ELSE
    LOCALS = %TOSTR(MONEY, "$###,###,###,###")%
ENDIF
PRINTFORML 大破艦修復 宇宙港に停泊中の大破した艦を修復できます - 所持金: %LOCALS%
LINE_ITER ++

CALL PRINT_LEFT_COLUMN(LEFT_COLUMNS, LEFT_COLUMNS_BUTTON_VALUE, LINE_ITER)
CALL DRAW_RIGHT_COLUMN_GRAY_LINE
LINE_ITER ++



CALL PRINT_LEFT_COLUMN(LEFT_COLUMNS, LEFT_COLUMNS_BUTTON_VALUE, LINE_ITER)
PRINTBUTTON @"[全艦修復]", MAX_SHIP
PRINTL
LINE_ITER ++
; 艦船一覧
WRECKED_COUNT = 0
FOR LOCAL, 0, MAX_SHIP
    SIF SHIP_COUNTRY:LOCAL != 国家ID_プレイヤー
        CONTINUE
    SIF SHIP_POSITION:LOCAL != PORT_POSID
        CONTINUE
    SIF SHIP_CONDITION:LOCAL != 艦船状態_大破
        CONTINUE
    CALL PRINT_LEFT_COLUMN(LEFT_COLUMNS, LEFT_COLUMNS_BUTTON_VALUE, LINE_ITER)

    CALL GET_SHIP_PRICE(SHIP_MODEL:LOCAL)
    REPAIR_PRICE:LOCAL = RESULT
	;TIMES REPAIR_PRICE:LOCAL, REPAIR_PRICE_RATIO
    REPAIR_PRICE:LOCAL = REPAIR_PRICE:LOCAL * REPAIR_PRICE_RATIO / 100
    LOCALS = %TOSTR(REPAIR_PRICE:LOCAL, "$###,###,###,###")%
    PRINTBUTTON @"%SHIP_NAME:LOCAL, 30, LEFT% %LOCALS%", LOCAL + 100
    PRINTL
    LINE_ITER++
    WRECKED_COUNT++
NEXT

; 左カラムが右カラムより長かった場合の処理
FOR LOCAL, LINE_ITER, LEFT_COLUMN_SIZE
    CALL PRINT_LEFT_COLUMN(LEFT_COLUMNS, LEFT_COLUMNS_BUTTON_VALUE, LINE_ITER)
    PRINTL
    LINE_ITER ++
NEXT


INPUT
IF RESULT >= 0 && RESULT < LEFT_COLUMN_SIZE
    RETURN RESULT
ELSEIF RESULT == MAX_SHIP
    SIF WRECKED_COUNT == 0
        GOTO INPUT_LOOP
	;全艦修復
    TOTAL_PRICE = 0
    FOR LOCAL, 0, MAX_SHIP
        SIF SHIP_COUNTRY:LOCAL != 国家ID_プレイヤー
            CONTINUE
        SIF SHIP_POSITION:LOCAL != PORT_POSID
            CONTINUE
        SIF SHIP_CONDITION:LOCAL != 艦船状態_大破
            CONTINUE
        CALL GET_SHIP_PRICE(SHIP_MODEL:LOCAL)
        REPAIR_PRICE:LOCAL = RESULT
        REPAIR_PRICE:LOCAL = REPAIR_PRICE:LOCAL * REPAIR_PRICE_RATIO / 100
        TOTAL_PRICE += REPAIR_PRICE:LOCAL
    NEXT

    IF MONEY < TOTAL_PRICE
        PRINTL
        PRINTFORML 所持金が足りません
        PRINTL
        GOTO INPUT_LOOP
    ENDIF
    PRINTL
    PRINTFORML %"所持金：", 10, RIGHT% %TOSTR(MONEY, "$#,###,###,###,###"), 24, RIGHT%
    PRINTFORML %"　費用：", 10, RIGHT% %TOSTR(TOTAL_PRICE, "$#,###,###,###,###"), 24, RIGHT%
    PRINTFORML %"清算後：", 10, RIGHT% %TOSTR(MONEY - TOTAL_PRICE, "$#,###,###,###,###"), 24, RIGHT%
    PRINTFORML 全て修復しますか?
    CALL ASK_YN
    SIF RESULT == 1
        GOTO INPUT_LOOP

    FOR LOCAL, 0, MAX_SHIP
        SIF SHIP_COUNTRY:LOCAL != 国家ID_プレイヤー
            CONTINUE
        SIF SHIP_POSITION:LOCAL != PORT_POSID
            CONTINUE
        SIF SHIP_CONDITION:LOCAL != 艦船状態_大破
            CONTINUE
        SHIP_CONDITION:LOCAL = 0
        SHIP_NAME:LOCAL = %REPLACE(SHIP_NAME:LOCAL, "<大破>", "")%
    NEXT
    MONEY -= TOTAL_PRICE

    GOTO INPUT_LOOP
ENDIF



入力値 = RESULT - 100
IF 入力値 >= 0 && 入力値 < MAX_SHIP
    SIF SHIP_COUNTRY:入力値 != 国家ID_プレイヤー
        GOTO INPUT_LOOP
    SIF SHIP_POSITION:入力値 != PORT_POSID
        GOTO INPUT_LOOP
    SIF SHIP_CONDITION:入力値 != 艦船状態_大破
        GOTO INPUT_LOOP
    PRINTL
    PRINTFORML %SHIP_NAME:入力値%を修復しますか?
    CALL ASK_YN
    SIF RESULT == 1
        GOTO INPUT_LOOP
    IF MONEY < REPAIR_PRICE:入力値
        PRINTFORML 所持金が足りません
        GOTO INPUT_LOOP
    ENDIF
    SHIP_CONDITION:入力値 = 0
    SHIP_NAME:入力値 = %REPLACE(SHIP_NAME:入力値, "<大破>", "")%
    MONEY -= REPAIR_PRICE:入力値
    SIF OWNER != 国家ID_プレイヤー
        MONEY:OWNER += REPAIR_PRICE:入力値
    PRINTFORMW %SHIP_NAME:入力値%の修復が完了しました！
ENDIF
GOTO INPUT_LOOP
RETURN