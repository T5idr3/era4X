; TECHNOLOGY
@TECHNOLOGY_LAB()
#DIM PAGE
#DIM TECHNOLOGY_LIST, 7, MAX_TECH
#DIM TECHNOLOGY_COUNT, 7
#DIM MAX_TECH_COUNT

VARSET TECHNOLOGY_COUNT, 0
PAGE = 0

FOR LOCAL, 0, MAX_TECH
  TRYCCALLFORM TECHNOLOGY_TIER_{LOCAL}
    TECHNOLOGY_LIST:(RESULT - 1):(TECHNOLOGY_COUNT:(RESULT - 1)) = LOCAL
    TECHNOLOGY_COUNT:(RESULT - 1) ++
  CATCH
    BREAK
  ENDCATCH
NEXT
$INPUT_LOOP
DRAWLINE
IF CURRENT_RESEARCH_TECH != -1
  TRYCALLFORM TECHNOLOGY_NAME_{CURRENT_RESEARCH_TECH}
  PRINTFORM 現在研究中の研究は 
  SETCOLOR 0X00FF00
  PRINTFORM %RESULTS% 
  RESETCOLOR
  PRINTFORML です
ELSE
  PRINTFORML 現在研究中の研究はありません
ENDIF
DRAWLINE
MAX_TECH_COUNT = 0
FOR LOCAL, PAGE, PAGE + 3
  LOCALS = TIER{LOCAL + 1, 28, LEFT}
  PRINTFORM | %LOCALS% 
  ; 現在のページで最もたくさん研究要素があるTIERの要素数を取得
  SIF MAX_TECH_COUNT < TECHNOLOGY_COUNT:LOCAL
    MAX_TECH_COUNT = TECHNOLOGY_COUNT:LOCAL
NEXT
PRINTL |
DRAWLINE
FOR LOCAL, 0, MAX_TECH_COUNT
  PRINT | 
  FOR LOCAL:1, PAGE, PAGE + 3
    TRYCCALLFORM TECHNOLOGY_NAME_{TECHNOLOGY_LIST:(LOCAL:1):LOCAL}
      IF ((LOCAL:1 == 0 && LOCAL == 0 && TECHNOLOGY_LIST:(LOCAL:1):LOCAL == 0) || TECHNOLOGY_LIST:(LOCAL:1):LOCAL != 0 )
        IF TECHNOLOGY_LIST:(LOCAL:1):LOCAL == CURRENT_RESEARCH_TECH
          SETCOLOR 0X00FF00
        ELSEIF IS_COMPLETE_TECH:(TECHNOLOGY_LIST:(LOCAL:1):LOCAL)
          SETCOLOR 0XFFFF00
        ELSE
          TRYCALLFORM CAN_RESEARCH_TECHNOLOGY_{TECHNOLOGY_LIST:(LOCAL:1):LOCAL}
          SIF !RESULT
            SETCOLOR 0X666666
        ENDIF
        PRINTBUTTON @"[%RESULTS, 30, LEFT%]", TECHNOLOGY_LIST:(LOCAL:1):LOCAL
        RESETCOLOR
      ELSE
        PRINTFORM %"", 32, LEFT%
      ENDIF
    CATCH
      CONTINUE
    ENDCATCH
    PRINT  | 
  NEXT
  PRINTL 
NEXT
DRAWLINE
PRINT ランク切り替え
IF PAGE > 0
  PRINTBUTTON "[<]", MAX_TECH
ELSE
  SETCOLOR 0X666666
  PRINT [<]
  RESETCOLOR
ENDIF
IF PAGE < 6 - 2
  PRINTBUTTON "[>]", MAX_TECH + 1
ELSE
  SETCOLOR 0X666666
  PRINT [>]
  RESETCOLOR
ENDIF
PRINTL
PRINTBUTTON "[完了]", MAX_TECH + 2
PRINTL
DRAWLINE

INPUT
LOCAL = RESULT
IF RESULT == MAX_TECH
  PAGE --
ELSEIF RESULT == MAX_TECH + 1
  PAGE ++
ELSEIF RESULT == MAX_TECH + 2
  RETURN
ENDIF
TRYCCALLFORM TECHNOLOGY_NAME_{RESULT}
  CALL SHOW_TECH_DETAIL(LOCAL)
  GOTO INPUT_LOOP
CATCH
  GOTO INPUT_LOOP
ENDCATCH


@SHOW_TECH_DETAIL(ID)
#DIM ID
#DIM CAN_RESEARCH

CAN_RESEARCH = 0
DRAWLINE
TRYCALLFORM TECHNOLOGY_NAME_{ID}
PRINTFORML [%RESULTS%]
TRYCALLFORM TECHNOLOGY_DESCRIPTION_{ID}
TRYCALLFORM TECHNOLOGY_TIER_{ID}
PRINTFORML 研究ランク: {RESULT}
PRINTL
TRYCALLFORM CAN_RESEARCH_TECHNOLOGY_{ID}
IF RESULT
  IF IS_COMPLETE_TECH:ID
    PRINTFORML 研究進捗: 完了
  ELSE
    TRYCALLFORM TECHNOLOGY_TURN_{ID}
    PRINTFORML 研究進捗: {RESEARCH_TURN:ID}/{RESULT}ターン
    CAN_RESEARCH = 1
  ENDIF
ELSE
  PRINTFORML 研究進捗: 研究不可能 - 前提研究未完了
ENDIF
PRINT 前提研究: 
TRYCALLFORM BASE_TECHNOLOGY_{ID}
PRINTL
IF CAN_RESEARCH && ID != CURRENT_RESEARCH_TECH
  PRINTBUTTON "[研究する]", 1
ELSEIF CAN_RESEARCH
  PRINT [研究中です]
ELSE
  PRINT ---
ENDIF
PRINTL
DRAWLINE
PRINTBUTTON "[戻る]",9
INPUT
IF RESULT == 1
  TRYCALLFORM TECHNOLOGY_NAME_{ID}
  PRINTFORML [%RESULTS%]の研究を開始しますF
  CURRENT_RESEARCH_TECH = ID
ELSEIF RESULT == 9
  RETURN
ENDIF

@TURNEND_TECH
; 現在研究中の研究にターン消費の処理
SIF CURRENT_RESEARCH_TECH == -1
  RETURN
SIF !RELEASED_LAB
  RETURN
RESEARCH_TURN:CURRENT_RESEARCH_TECH ++
TRYCALLFORM TECHNOLOGY_TURN_{CURRENT_RESEARCH_TECH}
SIF RESEARCH_TURN:CURRENT_RESEARCH_TECH < RESULT
  RETURN
; 研究完了時の処理
TRYCALLFORM TECHNOLOGY_NAME_{CURRENT_RESEARCH_TECH}
SETCOLOR 0XFF0000
PRINTFORML [%RESULTS%]の研究が完了しました!
RESETCOLOR
IS_COMPLETE_TECH:CURRENT_RESEARCH_TECH = 1
CURRENT_RESEARCH_TECH = -1
