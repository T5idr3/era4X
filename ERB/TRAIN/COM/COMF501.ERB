;-------------------------------------------------
;シックスナイン
;愛撫・奉仕系派生コマンド
;-------------------------------------------------
@COM501
PRINTL シックスナイン
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
;共通処理
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0
	

;-------------------------------------------------
;実行決定
;-------------------------------------------------
STR:0 = シックスナイン
SELECTCOM = 501
CALL KOJO_MESSAGE_COM

;実行者が奴隷であるフラグ
TFLAG:28 = 1

;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
B = 1500

;ABL:調教者の技巧をみる
IF ABL:2 == 0
	TIMES B , 1.00
ELSEIF ABL:2 == 1
	TIMES B , 1.20
ELSEIF ABL:2 == 2
	TIMES B , 1.40
ELSEIF ABL:2 == 3
	TIMES B , 1.60
ELSEIF ABL:2 == 4
	TIMES B , 1.80
ELSE
	TIMES B , 2.00
ENDIF

;ABL:従順をみる
IF ABL:0 == 0
	TIMES B , 0.80
ELSEIF ABL:0 == 1
	TIMES B , 0.90
ELSEIF ABL:0 == 2
	TIMES B , 1.00
ELSEIF ABL:0 == 3
	TIMES B , 1.10
ELSEIF ABL:0 == 4
	TIMES B , 1.20
ELSE
	TIMES B , 1.30
ENDIF

;ABL:奉仕精神をみる
IF ABL:7 == 0
	TIMES B , 0.50
ELSEIF ABL:7 == 1
	TIMES B , 0.80
ELSEIF ABL:7 == 2
	TIMES B , 1.20
ELSEIF ABL:7 == 3
	TIMES B , 1.50
ELSEIF ABL:7 == 4
	TIMES B , 1.80
ELSE
	TIMES B , 2.40
ENDIF

;プレイヤーのABL:Ｃ感覚をみる
IF ABL:PLAYER:3 == 0
	TIMES B , 1.00
ELSEIF ABL:PLAYER:3 == 1
	TIMES B , 1.50
ELSEIF ABL:PLAYER:3 == 2
	TIMES B , 2.00
ELSEIF ABL:PLAYER:3 == 3
	TIMES B , 2.50
ELSEIF ABL:PLAYER:3 == 4
	TIMES B , 3.50
ELSE
	TIMES B , 5.00
ENDIF

;再装填処理中は射精ゲージは20分の1に
SIF CFLAG:PLAYER:24 > 0
	B /= 20


SIF TALENT:PLAYER:半人半妖 || TALENT:PLAYER:ふたなり || TALENT:PLAYER:オトコ
	BASE:PLAYER:2 += B

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:0 += 20
LOSEBASE:1 += 160

SOURCE:0 = 100
SOURCE:4 = 100
SOURCE:5 = 450
SOURCE:6 = 200
SOURCE:10 = 80
SOURCE:13 = 100
SOURCE:14 = 800
SOURCE:17 = 600
SOURCE:18 = 600

;上のほうで計算した汚れデータ
SOURCE:15 = Y*80 + 50

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:16 += 800

;舌を使う調教では、調教者の舌使いによって、快感のソース増加
SIF TALENT:PLAYER:舌使い
	TIMES SOURCE:0 , 1.50

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK

;射精時の処理
IF E >= 1
	;射精先タイプの設定
	TFLAG:703 = 4
	;射精先選択の可・不可
	TFLAG:704 = 1
	;汎用的な処理はこの関数で行う
	;（射精ゲージの再計算や射精経験の上昇と共通部分の表示）
	CALL SAMEN_SHOOT
ENDIF

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷の口⇔調教者のＶの汚れが移動（半人半妖、ふたなり、オトコはペニス）
IF TALENT:MASTER:半人半妖 || TALENT:MASTER:ふたなり || TALENT:MASTER:オトコ
	STAIN:0 |= STAIN:PLAYER:2
	STAIN:PLAYER:2 |= STAIN:0
ELSE
	STAIN:0 |= STAIN:PLAYER:3
	STAIN:PLAYER:3 |= STAIN:0
ENDIF
;奴隷のＶ⇔調教者の口の汚れが移動（半人半妖、ふたなり、オトコはペニス）
IF TALENT:半人半妖 || TALENT:ふたなり || TALENT:オトコ
	STAIN:2 |= STAIN:PLAYER:0
	STAIN:PLAYER:0 |= STAIN:2
ELSE
	STAIN:3 |= STAIN:PLAYER:0
	STAIN:PLAYER:0 |= STAIN:3
ENDIF

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;フェラ経験（半人半妖、ふたなり、オトコ限定）
IF TALENT:MASTER:半人半妖 || TALENT:MASTER:ふたなり || TALENT:MASTER:オトコ
	PRINTFORML %EXPNAME:22%＋１
	EXP:フェラ経験 += 1
ENDIF

;レズ経験
A = 8
B = NO:PLAYER + 899
IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	PRINTFORML %EXPNAME:40%+{A}
	EXP:レズ経験 += A
	CFLAG:B += A
ELSEIF TALENT:オトコ && TALENT:PLAYER:オトコ
	PRINTFORML %EXPNAME:41%+{A}
	EXP:ゲイ経験 += A
	CFLAG:B += A
ENDIF

;愛情経験
IF (TALENT:恋慕 == 1 || TALENT:親愛 == 1) && ASSIPLAY == 0
	PRINTS EXPNAME:23
	PRINTL +1
	EXP:愛情経験 += 1
ENDIF

;奴隷が猫舌持ちの場合は、調教者に嗜虐快楽経験を追加
IF TALENT:TARGET:猫舌
	EXP:嗜虐快楽経験 += 1
	PRINTFORML %EXPNAME:32%＋１
ENDIF

;調教者が猫舌持ちの場合は、調教者に嗜虐快楽経験を追加
IF TALENT:PLAYER:猫舌
	EXP:PLAYER:嗜虐快楽経験 += 1
	PRINTFORML %CALLNAME:PLAYER%の%EXPNAME:32%＋１
ENDIF


;依存度ベクトル
TFLAG:33 = 1

;依存度補正値(主人かつ恋慕、淫乱、服従で1。親愛、娼婦、隷属で2）
IF (TALENT:恋慕 || TALENT:淫乱 || TALENT:服従) && ASSIPLAY == 0
	TFLAG:30 += 1
ELSEIF (TALENT:親愛 || TALENT:娼婦 || TALENT:隷属) && ASSIPLAY == 0
	TFLAG:30 += 2
ENDIF

;奉仕快楽フラグ
TFLAG:100 = 1

RETURN 1

;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_501
A = 0
S = 0

;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

;ABL:欲望
IF ABL:1
	SIF S
		PRINT  + 
	A += ABL:1 * 2
	PRINTS ABLNAME:1
	PRINTV 'LV,ABL:1,'(,ABL:1 * 2,')
	S = 1
ENDIF
;ABL:奉仕精神
IF ABL:7
	SIF S
		PRINT  + 
	A += ABL:7 * 4
	PRINTS ABLNAME:7
	PRINTV 'LV,ABL:7,'(,ABL:7 * 4,')
	S = 1
ENDIF

IF MARK:1
	SIF S
		PRINT  + 
	A += MARK:1 * 2
	PRINTS MARKNAME:1
	PRINTV 'LV,MARK:1,'(,MARK:1 * 2,')
	S = 1
ENDIF


;PALAM:欲情
IF PALAM:6 < PALAMLV:1
	L = 0
ELSEIF PALAM:6 < PALAMLV:2
	L = 1
ELSEIF PALAM:6 < PALAMLV:3
	L = 2
ELSEIF PALAM:6 < PALAMLV:4
	L = 3
ELSEIF PALAM:6 < PALAMLV:5
	L = 4
ELSE
	L = 5
ENDIF
IF L
	SIF S
		PRINT  + 
	A += L * 2
	PRINTS PALAMNAME:6
	PRINTV 'LV,L,'(,L * 2,')
	S = 1
ENDIF

;恥じらい
IF TALENT:恥じらい
	PRINT  - 
	A -= 3
	PRINTS TALENTNAME:35
	PRINTV '(,3,')
	S = 1
ENDIF
;汚臭鈍感
IF TALENT:汚臭鈍感
	SIF S
		PRINT  + 
	A += 3
	PRINTS TALENTNAME:61
	PRINTV '(,3,')
	S = 1
ENDIF
;汚臭敏感
IF TALENT:汚臭敏感
	PRINT  - 
	A -= 3
	PRINTS TALENTNAME:62
	PRINTV '(,3,')
	S = 1
ENDIF
;献身的
IF TALENT:献身的
	SIF S
		PRINT  + 
	A += 6
	PRINTS TALENTNAME:63
	PRINTV '(,6,')
	S = 1
ENDIF
;快感に素直
IF TALENT:快感に素直
	SIF S
		PRINT  + 
	A += 3
	PRINTS TALENTNAME:70
	PRINTV '(,3,')
	S = 1
ENDIF
;快感の否定（淫乱、娼婦で無効）
IF TALENT:快感の否定 && (TALENT:淫乱 == 0 && TALENT:娼婦 == 0)
	PRINT  - 
	A -= 3
	PRINTS TALENTNAME:71
	PRINTV '(,3,')
	S = 1
ENDIF
;恋慕
IF TALENT:恋慕 && ASSIPLAY == 0
	SIF S
		PRINT  + 
	A += 5
	PRINTS TALENTNAME:3
	PRINTV '(,5,')
	S = 5
ENDIF
;親愛
IF TALENT:親愛 && ASSIPLAY == 0
	SIF S
		PRINT  + 
	A += 10
	PRINTS TALENTNAME:6
	PRINTV '(,10,')
	S = 10
ENDIF

;媚薬
IF TEQUIP:26
	SIF S
		PRINT  + 
	A += 8
	PRINTS ITEMNAME:41
	PRINTV '(,8,')
	S = 1
ENDIF

Y = 0
;愛液の汚れ
SIF STAIN:PLAYER:3 & 1
	Y += 1
;精液の汚れ
SIF STAIN:PLAYER:3 & 4
	Y += 3
;アナルの汚れ
SIF STAIN:PLAYER:3 & 8
	Y += 7
SIF STAIN:PLAYER:3 & 16
	Y += 15

SIF TALENT:汚臭鈍感
	Y /= 3
SIF TALENT:汚臭敏感
	Y *= 2

;汚れあり
IF Y
	PRINT  - 
	A -= Y
	;汚臭鈍感
	IF TALENT:汚臭鈍感
		PRINT 汚れあり、
		PRINTS TALENTNAME:61
	ELSEIF TALENT:汚臭敏感
		PRINT 汚れあり、
		PRINTS TALENTNAME:62
	ELSE
		PRINT 汚れあり
	ENDIF
	PRINTV '(,Y,')
	S = 1
ENDIF


;合計を表示(33以上で実行)
PRINT  = 
PRINTV A

V = 33
SIF A < V
	PRINT  < 
SIF A == V
	PRINT  = 
SIF A > V
	PRINT  > 
PRINT 実行値
PRINTV V

WAIT

;実行できない
SIF A < V
	RETURN 0

RETURN 1

