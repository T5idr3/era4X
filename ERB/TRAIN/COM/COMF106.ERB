;-------------------------------------------------
;縄
;SM系コマンド、レベル3
;-------------------------------------------------
@COM106
IF TEQUIP:25
	PRINTL 触手緊縛
	STR:0 = 触手緊縛
	SELECTCOM = 166
ELSE
	PRINTL 縄
	STR:0 = 縄
ENDIF
CALL KOJO_MESSAGE_COM

;着脱の処理
IF TEQUIP:19 == 0
	TEQUIP:19 = 1
	;触手緊縛は触手の汚れを追加
	IF TEQUIP:25
		STAIN:1 |= 32
		STAIN:5 |= 32
	ENDIF
ELSE
	TEQUIP:19 = 0
ENDIF

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:0 += 30
LOSEBASE:1 += 120

SOURCE:7 = 300
SOURCE:8 = 900
SOURCE:13 = 200
SOURCE:14 = 250
SOURCE:17 = 300
SOURCE:18 = 700

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;緊縛経験が高いと消費減少
IF EXP:緊縛経験 < EXPLV:3
	TIMES LOSEBASE:0 , 1.00
	TIMES LOSEBASE:1 , 1.00
ELSEIF EXP:緊縛経験 < EXPLV:4
	TIMES LOSEBASE:0 , 0.90
	TIMES LOSEBASE:1 , 0.90
ELSE
	TIMES LOSEBASE:0 , 0.70
	TIMES LOSEBASE:1 , 0.70
ENDIF

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
EXP:緊縛経験 += 5
PRINTL 緊縛経験＋５

A = 1
B = NO:PLAYER + 899
IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	PRINTFORML %EXPNAME:40%+{A}
	EXP:レズ経験 += A
	CFLAG:B += A
ELSEIF TALENT:オトコ && TALENT:PLAYER:オトコ
	PRINTFORML %EXPNAME:41%+{A}
	EXP:ゲイ経験 += A
	CFLAG:B += A
ENDIF

;触手召喚時、触手経験のリセットをする
SIF TEQUIP:25 == 1
	T = 0

;依存度ベクトル
TFLAG:33 = 3

;依存度補正値(服従で1、隷属で2）
IF TALENT:服従 && ASSIPLAY == 0
	TFLAG:30 += 1
ELSEIF TALENT:隷属 && ASSIPLAY == 0
	TFLAG:30 += 2
ENDIF


RETURN 1


;-------------------------------------------------
;装着継続の処理
;-------------------------------------------------
@EQUIP_COM106
IF TEQUIP:25
	PRINTL ＜触手緊縛中＞
ELSE
	PRINTL ＜緊縛中＞
ENDIF

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:0 += 15

SOURCE:7 += 150
SOURCE:8 += 450
SOURCE:13 += 100
SOURCE:14 += 125
SOURCE:17 += 150
SOURCE:18 += 350

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;緊縛経験が高いと消費減少
A = 60
IF EXP:緊縛経験 < EXPLV:3
	TIMES A , 1.00
ELSEIF EXP:緊縛経験 < EXPLV:4
	TIMES A , 0.90
ELSE
	TIMES A , 0.70
ENDIF
LOSEBASE:1 += A


;-------------------------------------------------
;経験の処理
;-------------------------------------------------
EXP:緊縛経験 += 1
PRINTL 緊縛経験＋１

;触手経験加算
SIF TEQUIP:25 == 1
	T += 1


RETURN 1

