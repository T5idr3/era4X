;-------------------------------------------------
;背面座位
;セックス系コマンド、レベル3
;-------------------------------------------------
@COM69
;-------------------------------------------------
;特殊派生処理
;-------------------------------------------------
;羞恥プレイ中、乱れ牡丹にランクアップ
SIF TEQUIP:30
 	JUMP COM504

PRINTL 背面座位
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
;共通処理
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0


;-------------------------------------------------
;実行決定
;-------------------------------------------------
STR:0 = 背面座位
CALL KOJO_MESSAGE_COM

;V経験を伴うコマンドのフラグ(処女膜再生対応)
TFLAG:19 = 1
SIF (TALENT:恋慕 || TALENT:親愛) && ASSIPLAY == 0 && EXP:Ｖ経験 == 0
	TFLAG:20 = 1

;処女喪失時のテキスト用処理
SIF TFLAG:60 && (PREVCOM == 60 || PREVCOM == 61 || PREVCOM == 66 || PREVCOM == 68 || PREVCOM == 69) && ((ASSIPLAY && TFLAG:50) || (ASSIPLAY == 0 && TFLAG:50 == 0)) && TFLAG:31
	TFLAG:31 = 2
;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
B = 1800

;ABL:調教者の技巧をみる
IF ABL:PLAYER:2 == 0
	TIMES B , 1.00
ELSEIF ABL:PLAYER:2 == 1
	TIMES B , 1.20
ELSEIF ABL:PLAYER:2 == 2
	TIMES B , 1.40
ELSEIF ABL:PLAYER:2 == 3
	TIMES B , 1.60
ELSEIF ABL:PLAYER:2 == 4
	TIMES B , 1.80
ELSE
	TIMES B , 2.00
ENDIF

;ABL:従順をみる
IF ABL:0 == 0
	TIMES B , 0.80
ELSEIF ABL:0 == 1
	TIMES B , 0.90
ELSEIF ABL:0 == 2
	TIMES B , 1.00
ELSEIF ABL:0 == 3
	TIMES B , 1.10
ELSEIF ABL:0 == 4
	TIMES B , 1.20
ELSE
	TIMES B , 1.30
ENDIF

;ABL:欲望をみる
IF ABL:1 == 0
	TIMES B , 1.00
ELSEIF ABL:1 == 1
	TIMES B , 1.10
ELSEIF ABL:1 == 2
	TIMES B , 1.20
ELSEIF ABL:1 == 3
	TIMES B , 1.30
ELSEIF ABL:1 == 4
	TIMES B , 1.40
ELSE
	TIMES B , 1.50
ENDIF

;PALAM:潤滑をみる
IF PALAM:4 < PALAMLV:1
	TIMES B , 0.60
ELSEIF PALAM:4 < PALAMLV:2
	TIMES B , 0.80
ELSEIF PALAM:4 < PALAMLV:3
	TIMES B , 1.00
ELSEIF PALAM:4 < PALAMLV:4
	TIMES B , 1.20
ELSEIF PALAM:4 >= PALAMLV:4
	TIMES B , 1.40
ENDIF

;プレイヤーのABL:Ｃ感覚をみる
IF ABL:PLAYER:3 == 0
	TIMES B , 1.00
ELSEIF ABL:PLAYER:3 == 1
	TIMES B , 1.50
ELSEIF ABL:PLAYER:3 == 2
	TIMES B , 2.00
ELSEIF ABL:PLAYER:3 == 3
	TIMES B , 2.50
ELSEIF ABL:PLAYER:3 == 4
	TIMES B , 3.50
ELSE
	TIMES B , 5.00
ENDIF

;EXP:Ｖ経験をみる
;処女だと増える
SIF EXP:Ｖ経験 < EXPLV:1
	TIMES B , 1.50

;再装填処理中は射精ゲージは20分の1に
SIF CFLAG:PLAYER:24 > 0
	B /= 20


SIF TALENT:PLAYER:半人半妖 || TALENT:PLAYER:ふたなり || TALENT:PLAYER:オトコ
	BASE:PLAYER:2 += B

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:0 += 50
LOSEBASE:1 += 100

SOURCE:0 = 350
SOURCE:1 = 350
SOURCE:4 = 350
SOURCE:7 = 400
SOURCE:9 = 500
SOURCE:13 = 200
SOURCE:17 = 500
SOURCE:18 = 1000

;上のほうで計算した汚れデータ
SOURCE:15 = Y*10 + 60

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:16 += 800

;貞操観念持ちで恋慕、親愛持ち以外には常に反発のソースを追加する（セックス系のみ）
SIF TALENT:貞操観念 && (TALENT:恋慕 == 0 && TALENT:親愛 == 0)
	SOURCE:18 += 1000

;処女だった場合は、苦痛のソースと反発のソースを追加する
IF TALENT:処女 == 1
	SOURCE:7 += 2000
	SOURCE:18 += 3000
ENDIF

;再生処女だった場合は、苦痛のソースと反発のソースを追加する
IF TALENT:再生処女 == 1
	SOURCE:7 += 500
	SOURCE:18 += 1000
ENDIF

;Vへの苦痛は先に処理しておく
;EXP:Ｖ経験をみる（処女の反感は別途処理済みなので省略）
IF EXP:Ｖ経験 < EXPLV:1
	TIMES SOURCE:7 , 2.50
ELSEIF EXP:Ｖ経験 < EXPLV:2
	TIMES SOURCE:7 , 0.80
	TIMES SOURCE:18 , 1.20
ELSEIF EXP:Ｖ経験 < EXPLV:3
	TIMES SOURCE:7 , 0.40
ELSEIF EXP:Ｖ経験 < EXPLV:4
	TIMES SOURCE:7 , 0.10
ELSEIF EXP:Ｖ経験 < EXPLV:5
	TIMES SOURCE:7 , 0.00
ELSEIF EXP:Ｖ経験 >= EXPLV:5
	TIMES SOURCE:7 , 0.00
ENDIF

;PALAM:潤滑をみる
IF PALAM:4 < PALAMLV:1
	TIMES SOURCE:7 , 1.20
	TIMES SOURCE:18 , 1.50
ELSEIF PALAM:4 < PALAMLV:2
	TIMES SOURCE:7 , 0.70
	TIMES SOURCE:18 , 1.20
ELSEIF PALAM:4 < PALAMLV:3
	TIMES SOURCE:7 , 0.40
ELSEIF PALAM:4 < PALAMLV:4
	TIMES SOURCE:7 , 0.10
ELSEIF PALAM:4 >= PALAMLV:4
	TIMES SOURCE:7 , 0.10
ENDIF

;中毒による中毒充足のソースは先に処理しておく
;セックス中毒を見る
IF ABL:18 == 1
	TIMES SOURCE:9 , 0.00
ELSEIF ABL:18 == 2
	TIMES SOURCE:9 , 0.30
ELSEIF ABL:18 == 3
	TIMES SOURCE:9 , 0.60
ELSEIF ABL:18 == 4
	TIMES SOURCE:9 , 0.90
ELSEIF ABL:18 == 5
	TIMES SOURCE:9 , 1.20
ELSEIF ABL:18 == 6
	TIMES SOURCE:9 , 1.50
ELSEIF ABL:18 == 7
	TIMES SOURCE:9 , 1.80
ELSEIF ABL:18 == 8
	TIMES SOURCE:9 , 2.30
ELSEIF ABL:18 == 9
	TIMES SOURCE:9 , 3.00
ELSEIF ABL:18 >= 10
	TIMES SOURCE:9 , 5.00
ENDIF

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK

;射精時の処理
IF E >= 1
	;射精先タイプの設定
	TFLAG:703 = 1
	;射精先選択の可・不可
	TFLAG:704 = 1
	;汎用的な処理はこの関数で行う
	;（射精ゲージの再計算や射精経験の上昇と共通部分の表示）
	CALL SAMEN_SHOOT
ENDIF

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;射精に関係なく行われる処理はここから
;膣内に破瓜の血の汚れを付加
SIF TALENT:処女 == 1 || TALENT:再生処女 == 1
	STAIN:6 |= 64
;奴隷の膣内⇔調教者のＰの汚れが移動
STAIN:6 |= STAIN:PLAYER:2
STAIN:PLAYER:2 |= STAIN:6

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;V経験
EXP:Ｖ経験 += 1
PRINTL Ｖ経験＋１

;性交経験
EXP:性交経験 += 1
PRINTS EXPNAME:4
PRINTL +1

;レズ経験
A = 4
B = NO:PLAYER + 899
IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	PRINTFORML %EXPNAME:40%+{A}
	EXP:レズ経験 += A
	CFLAG:B += A
ELSEIF TALENT:オトコ && TALENT:PLAYER:オトコ
	PRINTFORML %EXPNAME:41%+{A}
	EXP:ゲイ経験 += A
	CFLAG:B += A
ENDIF

;愛情経験
IF TALENT:恋慕 == 1 && ASSIPLAY == 0
	PRINTS EXPNAME:23
	PRINTL +2
	EXP:愛情経験 += 2
ELSEIF TALENT:親愛 == 1 && ASSIPLAY == 0
	PRINTS EXPNAME:23
	PRINTL +4
	EXP:愛情経験 += 4
ENDIF

;依存度ベクトル
TFLAG:33 = 1

;依存度補正値(恋慕で2、親愛で4）
IF TALENT:恋慕 && ASSIPLAY == 0
	TFLAG:30 += 2
ELSEIF TALENT:親愛 && ASSIPLAY == 0
	TFLAG:30 += 4
ENDIF


RETURN 1




;-------------------------------------------------
;固有の実行判定
;-------------------------------------------------
@COM_ORDER_69
;縄で縛られている場合は無視（縄が無い時のみ実行判定を行う）
IF TEQUIP:44 == 0
	A = 0
	S = 0
	;すべての命令に共通の要素を考慮
	;(従順が高いと命令に従いやすいなど)
	CALL COM_ORDER

	;ABL:欲望
	IF ABL:1
		SIF S
			PRINT  + 
		A += ABL:1 * 3
		PRINTS ABLNAME:1
		PRINTV 'LV,ABL:1,'(,ABL:1 * 3,')
		S = 1
	ENDIF
	;ABL:Ｖ感覚
	IF ABL:4
		SIF S
			PRINT  + 
		A += ABL:4 * 2
		PRINTS ABLNAME:4
		PRINTV 'LV,ABL:4,'(,ABL:4 * 2,')
		S = 1
	ENDIF
	;ABL:セックス中毒
	IF ABL:18
		SIF S
			PRINT  + 
		A += ABL:18 * 1
		PRINTS ABLNAME:18
		PRINTV 'LV,ABL:18,'(,ABL:18 * 1,')
		S = 1
	ENDIF
	;快楽刻印
	IF MARK:1
		SIF S
			PRINT  + 
		A += MARK:1 * 3
		PRINTS MARKNAME:1
		PRINTV 'LV,MARK:1,'(,MARK:1 * 3,')
		S = 1
	ENDIF
	;PALAM:潤滑（の不足）
	IF PALAM:4 < PALAMLV:3
		PRINT  - 
		A -= 5
		PRINT 潤滑不足
		PRINTV '(,5,')
		S = 1
	ENDIF
	;PALAM:欲情
	IF PALAM:6 < PALAMLV:1
		L = 0
	ELSEIF PALAM:6 < PALAMLV:2
		L = 1
	ELSEIF PALAM:6 < PALAMLV:3
		L = 2
	ELSEIF PALAM:6 < PALAMLV:4
		L = 3
	ELSEIF PALAM:6 < PALAMLV:5
		L = 4
	ELSE
		L = 5
	ENDIF
	IF L
		SIF S
			PRINT  + 
		A += L * 3
		PRINTS PALAMNAME:6
		PRINTV 'LV,L,'(,L * 3,')
		S = 1
	ENDIF
	;恥じらい
	IF TALENT:恥じらい
		PRINT  - 
		A -= 2
		PRINTS TALENTNAME:35
		PRINTV '(,2,')
		S = 1
	ENDIF
	;快感の否定
	IF TALENT:快感の否定
		PRINT  - 
		A -= 5
		PRINTS TALENTNAME:71
		PRINTV '(,5,')
		S = 1
	ENDIF
	;男嫌い（恋慕、親愛で無効）
	IF TALENT:男嫌い && TALENT:PLAYER:オトコ && ((TALENT:恋慕 == 0 && TALENT:親愛 == 0) || PLAYER != MASTER)
		PRINT  - 
		A -= 12
		PRINTS TALENTNAME:82
		PRINTV '(,12,')
		S = 1
	ENDIF
	;恋慕
	IF TALENT:恋慕 && ASSIPLAY == 0
		SIF S
			PRINT  + 
		A += 15
		PRINTS TALENTNAME:3
		PRINTV '(,15,')
		S = 15
	ENDIF
	;親愛
	IF TALENT:親愛 && ASSIPLAY == 0
		SIF S
			PRINT  + 
		A += 20
		PRINTS TALENTNAME:6
		PRINTV '(,20,')
		S = 20
	ENDIF
	;処女
	IF TALENT:処女
		PRINT  - 
		A -= 40
		PRINTS TALENTNAME:0
		PRINTV '(,40,')
		S = 1
	;V経験
	ELSEIF EXP:Ｖ経験 < EXPLV:2
		PRINT  - 
		A -= 15
		PRINTS EXPNAME:0
		PRINT 不足
		PRINTV '(,15,')
		S = 1
	ENDIF

	;調教者がふたなり
	IF TALENT:PLAYER:ふたなり
		SIF S
			PRINT  + 
		A += 4
		PRINTS TALENTNAME:139
		PRINTV '(,4,')
		S = 1
	ENDIF

	;媚薬
	IF TEQUIP:26
		SIF S
			PRINT  + 
		A += 6
		PRINTS ITEMNAME:41
		PRINTV '(,6,')
		S = 1
	ENDIF

	Y = 0
	;愛液の汚れ
	SIF STAIN:PLAYER:2 & 1
		Y += 1
	;精液の汚れ
	SIF STAIN:PLAYER:2 & 4
		Y += 3
	;アナルの汚れ
	SIF STAIN:PLAYER:2 & 8
		Y += 7
	SIF STAIN:PLAYER:2 & 16
		Y += 15

	SIF TALENT:汚臭鈍感
		Y /= 3
	SIF TALENT:汚臭敏感
		Y *= 2

	;汚れあり
	IF Y
		PRINT  - 
		A -= Y
		;汚臭鈍感
		IF TALENT:汚臭鈍感
			PRINT 汚れあり、
			PRINTS TALENTNAME:61
		ELSEIF TALENT:汚臭敏感
			PRINT 汚れあり、
			PRINTS TALENTNAME:62
		ELSE
			PRINT 汚れあり
		ENDIF
		PRINTV '(,Y,')
		S = 1
	ENDIF

	;合計を表示(42以上で実行)
	PRINT  = 
	PRINTV A

	V = 42
	SIF A < V
		PRINT  < 
	SIF A == V
		PRINT  = 
	SIF A > V
		PRINT  > 
	PRINT 実行値
	PRINTV V

	WAIT

	;実行できない
	SIF A < V
		RETURN 0
ENDIF
RETURN 1

