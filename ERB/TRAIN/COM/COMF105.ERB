;-------------------------------------------------
;アイマスク
;SM系コマンド、レベル1
;-------------------------------------------------
@COM105
PRINTL アイマスク
STR:0 = アイマスク
CALL KOJO_MESSAGE_COM

;着脱の処理
IF TEQUIP:18 == 0
	TEQUIP:18 = 1
ELSE
	TEQUIP:18 = 0
ENDIF

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:0 += 0
LOSEBASE:1 += 80

SOURCE:8 = 700
SOURCE:13 = 100
SOURCE:17 = 300
SOURCE:18 = 600

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;緊縛経験が高いと消費減少
IF EXP:緊縛経験 < EXPLV:3
	TIMES LOSEBASE:1 , 1.00
ELSEIF EXP:緊縛経験 < EXPLV:4
	TIMES LOSEBASE:1 , 0.90
ELSE
	TIMES LOSEBASE:1 , 0.70
ENDIF

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
EXP:緊縛経験 += 2
PRINTL 緊縛経験＋２

A = 1
B = NO:PLAYER + 899
IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	PRINTFORML %EXPNAME:40%+{A}
	EXP:レズ経験 += A
	CFLAG:B += A
ELSEIF TALENT:オトコ && TALENT:PLAYER:オトコ
	PRINTFORML %EXPNAME:41%+{A}
	EXP:ゲイ経験 += A
	CFLAG:B += A
ENDIF

;依存度ベクトル
TFLAG:33 = 3

;依存度補正値(服従で1、隷属で2）
IF TALENT:服従 && ASSIPLAY == 0
	TFLAG:30 += 1
ELSEIF TALENT:隷属 && ASSIPLAY == 0
	TFLAG:30 += 2
ENDIF


RETURN 1


;-------------------------------------------------
;装着継続の処理
;-------------------------------------------------
@EQUIP_COM105
PRINTL ＜アイマスク装着中＞

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
LOSEBASE:0 += 0
LOSEBASE:1 += 40

SOURCE:8 += 350
SOURCE:13 += 50
SOURCE:17 += 150
SOURCE:18 += 300

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;緊縛経験が高いと消費減少
A = 40
IF EXP:緊縛経験 < EXPLV:3
	TIMES A , 1.00
ELSEIF EXP:緊縛経験 < EXPLV:4
	TIMES A , 0.90
ELSE
	TIMES A , 0.70
ENDIF
LOSEBASE:1 += A

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
EXP:緊縛経験 += 1
PRINTL 緊縛経験＋１

RETURN 1

