;------------------
; 話術技能
;------------------
@ISABLUP22
#DIM LEVELUP_CONDITION
#DIM PARAM_COUNT
#DIM EXP_COUNT
;陥落はMAXLV10
IF ABL:22 >= 10
	RETURN 0
ELSEIF IS_FALL(TARGET) == 0 && ABL:22 == 5
	RETURN 0
ENDIF

CALL DECIDE_ABLUP22(LEVELUP_CONDITION, PARAM_COUNT, EXP_COUNT)
SIF LEVELUP_CONDITION == 0
	RETURN 1
RETURN 0

;話術技能
@ABLUP22
#DIM LEVELUP_CONDITION
#DIM PARAM_COUNT
#DIM EXP_COUNT
DRAWLINE
;恋慕、親愛、服従、隷属はMAXLV10
IF ABL:22 >= 10
	PRINTW 既にMAXです
	RETURN 0
ELSEIF IS_FALL(TARGET) == 0 && ABL:22 == 5
	PRINTW 既にMAXです
	RETURN 0
ENDIF

CALL DECIDE_ABLUP22(LEVELUP_CONDITION, PARAM_COUNT, EXP_COUNT)

PRINT [0] - 
PRINTS PALAMNAME:8
PRINT の珠×
PRINTV PARAM_COUNT
PRINT 、
PRINTS EXPNAME:65
PRINTV EXP_COUNT
PRINT 以上
PRINT ……
IF LEVELUP_CONDITION == 0
	PRINT ＯＫ
ELSE
	SIF LEVELUP_CONDITION & 1
		PRINT 珠不足 
	SIF LEVELUP_CONDITION & 2
		PRINT 経験不足 
ENDIF
PRINTL 

PRINTL [100] - やめる


INPUT
IF (RESULT < 0 || RESULT > 0) && RESULT != 100
	RESTART
ELSEIF LEVELUP_CONDITION != 0 && RESULT == 0
	PRINTL 条件を満たしていません
	RESTART
ELSEIF RESULT == 100
	RETURN 0
ENDIF

ABL:22 ++

IF RESULT == 0
	JUEL:8 -= PARAM_COUNT
ENDIF

PRINTFORMW %ABLNAME:22%のレベルが{ABL:22}になりました

;話術技能LVUP判定
@DECIDE_ABLUP22(LEVELUP_CONDITION, PARAM_COUNT, EXP_COUNT)
#DIM REF LEVELUP_CONDITION
#DIM REF PARAM_COUNT
#DIM REF EXP_COUNT
;条件別にＯＫかダメかを記録する
LEVELUP_CONDITION = 0

IF ABL:22 == 0
	PARAM_COUNT = 75
	EXP_COUNT = 5
ELSEIF ABL:22 == 1
	PARAM_COUNT = 500
	EXP_COUNT = 10
ELSEIF ABL:22 == 2
	PARAM_COUNT = 3000
	EXP_COUNT = 30
ELSEIF ABL:22 == 3
	PARAM_COUNT = 9000
	EXP_COUNT = 50
ELSEIF ABL:22 == 4
	PARAM_COUNT = 24000
	EXP_COUNT = 100
ELSEIF ABL:22 == 5
	PARAM_COUNT = 30000
	EXP_COUNT = 200
ELSEIF ABL:22 == 6
	PARAM_COUNT = 35000
	EXP_COUNT = 300
ELSEIF ABL:22 == 7
	PARAM_COUNT = 40000
	EXP_COUNT = 400
ELSEIF ABL:22 == 8
	PARAM_COUNT = 45000
	EXP_COUNT = 500
ELSEIF ABL:22 == 9
	PARAM_COUNT = 50000
	EXP_COUNT = 600
ENDIF

;恋慕
IF TALENT:恋慕
	TIMES PARAM_COUNT , 0.60
	TIMES EXP_COUNT , 0.60
ENDIF
;親愛
IF TALENT:親愛
	TIMES PARAM_COUNT , 0.40
	TIMES EXP_COUNT , 0.40
ENDIF
;一線越えない
IF TALENT:一線越えない && IS_FALL(TARGET)
	TIMES PARAM_COUNT , 2.00
	TIMES EXP_COUNT , 2.00
ENDIF

;習得の珠で上げる
SIF JUEL:8 < PARAM_COUNT
	LEVELUP_CONDITION |= 1
SIF EXP:話術経験 < EXP_COUNT
	LEVELUP_CONDITION |= 2

SIF LEVELUP_CONDITION == 0
	RETURN 1
RETURN 0

