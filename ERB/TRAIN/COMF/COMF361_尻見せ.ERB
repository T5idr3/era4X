;尻見せ

;-------------------------------------------------
;コマンド名称
;-------------------------------------------------
@COM_NAME361
RESULTS:0 = 尻を見せつける
RESULTS:1 = 尻を強調させられる
RESULTS:2 = 尻を強調してもらう
RESULTS:3 = 尻を見せつけられる

;-------------------------------------------------
;選択可否判定
;-------------------------------------------------
@COM_ABLE361
;共通部分
CALL COM_ABLE_COMMON(361)
SIF RESULT == 0
	RETURN 0
;プレイヤーとターゲットが1人以上
SIF MPLY_NUM <= 0 || MTAR_NUM <= 0
	RETURN 0
	
FOR LOCAL:0, 0, MPLY_NUM
	;口だのケツだの女性器だのが塞がってる状態での実行は不可とする
	;※日常フェラだけこの制限から逃れてしまうが、対象が一人ならまあ出来なくもないと見なそう
	SIF IS_V_HOLD(MPLY:(LOCAL:0))
		RETURN 0
	SIF IS_A_HOLD(MPLY:(LOCAL:0))
		RETURN 0
	SIF IS_M_HOLD(MPLY:(LOCAL:0))
		RETURN 0
	SIF IS_U_HOLD(MPLY:(LOCAL:0))
		RETURN 0
	;拘束された状態でエロポーズとれと。無茶言うなや！
	SIF IS_BIND(MPLY:(LOCAL:0))
		RETURN 0
	;顔面騎乗しがら尻を見せつけるのは無理でしょう
	SIF IS_RIDE(MPLY:(LOCAL:0))
		RETURN 0
	;顔面騎乗されながら、ならいけるな
	
	;他のポーズとは共存不可
	SIF SEARCH_EQUIP(360, MPLY:(LOCAL:0), -1) >= 0;胸見せ
		RETURN 0
	SIF SEARCH_EQUIP(362, MPLY:(LOCAL:0), -1) >= 0;股見せ
		RETURN 0
	
	;怪我なら不可
	SIF CFLAG:(MPLY:(LOCAL:0)):行動不能状態 == 3
		RETURN 0
NEXT

RETURN 1

;-------------------------------------------------
;メイン処理
;-------------------------------------------------
@COM361
;実行判定
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0
	
;ここからプレイヤー
LOCAL:0 = 0
LOCAL:8 = 0
LOCAL:9 = 0;プレイヤーの姿検知
FOR LOCAL:5, 0, MPLY_NUM
	LOCAL:6 = MPLY:(LOCAL:5)
	
	CALL COM_UNIQUE_MISE_PLAYER_KANRAKU_361(LOCAL:6,25)
	LOCAL:3 = RESULT
	CALL COM_UNIQUE_MISE_PLAYER_HANKAN_361(LOCAL:6,40)
	LOCAL:4 = RESULT
	CALL COM_UNIQUE_MISE_TARGET_SHASEIKAN_361(LOCAL:6,50)
	LOCAL:7 = RESULT

	SOURCE:(LOCAL:6):露出 = 300
	SOURCE:(LOCAL:6):逸脱 = 20
	SOURCE:(LOCAL:6):歓楽 = LOCAL:3
	SOURCE:(LOCAL:6):反感 = LOCAL:4
	
	;主導権に応じたソースの追加
	CALL ADD_SOURCE_INITIATIVE_N(LOCAL:6, 50, 50)
	
	EXP:(LOCAL:6):露出経験値 += 1
	
	
	LOCAL:0 += LOCAL:7
	
	;プレイヤーに男が混じってる検知
	SIF !IS_FEMALE(LOCAL:6)
		LOCAL:8 += 1
	
	;プレイヤーの姿検知
	CALL COM_UNIQUE_MISE_PLAYER_GET_SUGATACODE(LOCAL:6)
	LOCAL:9 = RESULT
NEXT


;ここからターゲット
FOR LOCAL:5, 0, MTAR_NUM
	LOCAL:6 = MTAR:(LOCAL:5)
	
	CALL COM_UNIQUE_MISE_TARGET_KANRAKU_361(LOCAL:6,25,LOCAL:9)
	LOCAL:1 = RESULT
	CALL COM_UNIQUE_MISE_TARGET_HANKAN_361(LOCAL:6,55,LOCAL:9)
	LOCAL:2 = RESULT
	
	;俺はホモじゃねえ！補正。ここまでが台無しになるレベルの激烈な補正
	;尻見せに男が混じってる＆両刀じゃない＆女じゃない
	IF !LOCAL:8 == 0 && !TALENT:(LOCAL:6):両刀 && !IS_FEMALE(LOCAL:6)
		TIMES LOCAL:1, 0.25
		TIMES LOCAL:2, 16.00
	ENDIF
	
	;まず感応に加算
	SOURCE:(LOCAL:6):感応 = LOCAL:0
	
	;ちんこがあったら射精感
	IF HAS_PENIS(LOCAL:6)
		;男が混じってないか
		IF LOCAL:8 == 0
			LOCAL:7 = PALAM:(LOCAL:6):射精 + LOCAL:0
			PALAM:(LOCAL:6):射精 = LOCAL:7
			;射精寸前だったら最後の一押し
			IF LOCAL:7 >= 10000
				SOURCE:(LOCAL:6):快Ｐ = 1
			ENDIF
		ELSE
			;混じってても両刀ならイケます
			IF TALENT:(LOCAL:6):両刀
				LOCAL:7 = PALAM:(LOCAL:6):射精 + LOCAL:0
				PALAM:(LOCAL:6):射精 = LOCAL:7
				;射精寸前だったら最後の一押し
				IF LOCAL:7 >= 10000
					SOURCE:(LOCAL:6):快Ｐ = 1
				ENDIF
			ENDIF
		ENDIF
	ENDIF
	
	SOURCE:(LOCAL:6):歓楽 = LOCAL:1
	SOURCE:(LOCAL:6):反感 = LOCAL:2
	
	;主導権に応じたソースの追加
	CALL ADD_SOURCE_INITIATIVE_N(LOCAL:6, 50, 50)
NEXT


;主導度変化基準値
TFLAG:49 = 2

;倒錯度変化基準値
TFLAG:50 = 1

RETURN 1

;-------------------------------------------------
;継続コマンドかどうかを設定
;-------------------------------------------------
@COM_IS_EQUIP361
RETURN 1

;-------------------------------------------------
;継続状態の処理
;-------------------------------------------------
@COM_EQUIP361(ARG:0)

LOCAL:3 = 0;射精感
LOCAL:4 = 0;男混じり検知
LOCAL:8 = 0;プレイヤーの姿検知
;●全プレイヤーについて判定
FOR LOCAL:0, 0, MEQUIP_PLAYER_NUM:(ARG:0)
	LOCAL:2 = MEQUIP_PLAYER:(ARG:0):(LOCAL:0)
	
	;ソース退避
	CALL PUTOUT_SOURCE(LOCAL:2)

	DOWNBASE:(LOCAL:2):体力 += 20
	
	SOURCE:(LOCAL:2):露出 = 300
	SOURCE:(LOCAL:2):逸脱 = 20
	CALL COM_UNIQUE_MISE_PLAYER_KANRAKU_361(LOCAL:2,20)
	SOURCE:(LOCAL:2):歓楽 = RESULT
	CALL COM_UNIQUE_MISE_PLAYER_HANKAN_361(LOCAL:2,32)
	SOURCE:(LOCAL:2):反感 = RESULT
	
	;主導権に応じたソースの追加
	CALL ADD_SOURCE_INITIATIVE_N(LOCAL:2, 50, 50)
	
	EXP:(LOCAL:2):露出経験値 += 1
	
	CALL COM_UNIQUE_MISE_TARGET_SHASEIKAN_361(LOCAL:2,30)
	LOCAL:3 += RESULT
	
	;プレイヤーに男が混じってる検知
	SIF !IS_FEMALE(LOCAL:2)
		LOCAL:4 += 1
		
	;プレイヤーの姿検知
	CALL COM_UNIQUE_MISE_PLAYER_GET_SUGATACODE(LOCAL:2)
	LOCAL:8 = RESULT
	
	;退避したソースを加算
	CALL SUM_SOURCE(LOCAL:2)
NEXT

;●全ターゲットについて判定
FOR LOCAL:0, 0, MEQUIP_TARGET_NUM:(ARG:0)
	LOCAL:1 = MEQUIP_TARGET:(ARG:0):(LOCAL:0)
	
	;ソース退避
	CALL PUTOUT_SOURCE(LOCAL:1)

	CALL COM_UNIQUE_MISE_TARGET_KANRAKU_361(LOCAL:1,20,LOCAL:8)
	LOCAL:5 = RESULT
	CALL COM_UNIQUE_MISE_TARGET_HANKAN_361(LOCAL:1,44,LOCAL:8)
	LOCAL:6 = RESULT
	
	;俺はホモじゃねえ！補正。ここまでが台無しになるレベルの激烈な補正
	;尻見せに男が混じってる＆両刀じゃない＆女じゃない
	IF !LOCAL:4 == 0 && !TALENT:(LOCAL:1):両刀 && !IS_FEMALE(LOCAL:1)
		TIMES LOCAL:5, 0.25
		TIMES LOCAL:6, 16.00
	ENDIF
	
	;まず感応に加算
	SOURCE:(LOCAL:1):感応 = LOCAL:3
	
	;ちんこがあったら射精感
	IF HAS_PENIS(LOCAL:1)
		;男が混じってないか
		IF LOCAL:4 == 0
			LOCAL:7 = PALAM:(LOCAL:1):射精 + LOCAL:3
			PALAM:(LOCAL:1):射精 = LOCAL:7
			;射精寸前だったら最後の一押し
			IF LOCAL:7 >= 10000
				SOURCE:(LOCAL:1):快Ｐ = 1
			ENDIF
		ELSE
			;混じってても両刀ならイケます
			IF TALENT:(LOCAL:1):両刀
				LOCAL:7 = PALAM:(LOCAL:1):射精 + LOCAL:3
				PALAM:(LOCAL:1):射精 = LOCAL:7
				;射精寸前だったら最後の一押し
				IF LOCAL:7 >= 10000
					SOURCE:(LOCAL:1):快Ｐ = 1
				ENDIF
			ENDIF
		ENDIF
	ENDIF
	
	SOURCE:(LOCAL:1):歓楽 = LOCAL:5
	SOURCE:(LOCAL:1):反感 = LOCAL:6
	
	;主導権に応じたソースの追加
	CALL ADD_SOURCE_INITIATIVE_N(LOCAL:1, 50, 50)
	
	;退避したソースを加算
	CALL SUM_SOURCE(LOCAL:1)
NEXT


;-------------------------------------------------
;継続中の表示
;-------------------------------------------------
@EQUIP_MESSAGE361(ARG:0)
RESULTS = %EQUIP_PLAYER_ANAME(ARG:0)%が尻強調ポーズ中

;-------------------------------------------------
;継続中の地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP361(ARG:0)
LOCALS:0 = 
IF GET_PLACENAME(TFLAG:54) == "遊泳場"
	LOCALS:0 = 水着姿の
ELSEIF GET_PLACENAME(TFLAG:54) == "温泉"
	LOCALS:0 = 裸の
ELSEIF SEARCH_EQUIP(720, ARG:0, -1) >= 0;撮影設備で水着姿
	LOCALS:0 = 水着姿の
ELSEIF SEARCH_EQUIP(721, ARG:0, -1) >= 0;撮影設備で裸
	LOCALS:0 = 裸の
ENDIF
PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%は%LOCALS:0%尻を強調したポーズをじっくり見せつけている…

;-------------------------------------------------
;継続を解除したときの地の文
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP361(ARG:0)

;-------------------------------------------------
;固有の実行判定(プレイヤー側)
;-------------------------------------------------
@COM_ORDER_PLAYER361(ARG:0)
CVARSET TCVAR, 24, -10000
CVARSET TCVAR, 25, 10000

TCVAR:(ARG:0):25 = 70

CALL COM_ORDER_BASE(ARG:0)

IF TALENT:(ARG:0):反抗的
	CALL COM_ORDER_ELEMENT(ARG:0, "反抗的", -5)
ENDIF
IF TALENT:(ARG:0):プライド高い
	CALL COM_ORDER_ELEMENT(ARG:0, "プライド高い", -15)
ENDIF
IF TALENT:(ARG:0):プライド低い
	CALL COM_ORDER_ELEMENT(ARG:0, "プライド低い", 5)
ENDIF
IF TALENT:(ARG:0):自制心
	CALL COM_ORDER_ELEMENT(ARG:0, "自制心", -15)
ENDIF
IF TALENT:(ARG:0):好奇心
	CALL COM_ORDER_ELEMENT(ARG:0, "好奇心", 10)
ENDIF
IF TALENT:(ARG:0):保守的
	CALL COM_ORDER_ELEMENT(ARG:0, "保守的", -10)
ENDIF
IF TALENT:(ARG:0):楽観的
	CALL COM_ORDER_ELEMENT(ARG:0, "楽観的", 5)
ENDIF
IF TALENT:(ARG:0):悲観的
	CALL COM_ORDER_ELEMENT(ARG:0, "悲観的", -10)
ENDIF
IF TALENT:(ARG:0):目立ちたがり
	CALL COM_ORDER_ELEMENT(ARG:0, "目立ちたがり", 15)
ENDIF
IF TALENT:(ARG:0):貞操観念
	CALL COM_ORDER_ELEMENT(ARG:0, "貞操観念", -15)
ENDIF
IF TALENT:(ARG:0):貞操無頓着
	CALL COM_ORDER_ELEMENT(ARG:0, "貞操無頓着", 10)
ENDIF
IF TALENT:(ARG:0):抑圧
	CALL COM_ORDER_ELEMENT(ARG:0, "抑圧", -10)
ENDIF
IF TALENT:(ARG:0):解放
	CALL COM_ORDER_ELEMENT(ARG:0, "解放", 10)
ENDIF
IF TALENT:(ARG:0):孤高
	CALL COM_ORDER_ELEMENT(ARG:0, "孤高", -(20 * KOKOU_RATE(ARG:0) / 100))
ENDIF
IF TALENT:(ARG:0):恥じらい
	CALL COM_ORDER_ELEMENT(ARG:0, "恥じらい", -20)
ENDIF
IF TALENT:(ARG:0):恥薄い
	CALL COM_ORDER_ELEMENT(ARG:0, "恥薄い", 10)
ENDIF
IF TALENT:(ARG:0):快感に素直
	CALL COM_ORDER_ELEMENT(ARG:0, "快感に素直", 5)
ENDIF
IF TALENT:(ARG:0):快感の否定
	CALL COM_ORDER_ELEMENT(ARG:0, "快感の否定", -35)
ENDIF
IF GETBIT(TALENT:(ARG:0):淫乱系, 素質_淫乱_淫乱)
	CALL COM_ORDER_ELEMENT(ARG:0, "淫乱", 20)
ENDIF
;ドストライク。これがある状態のヤツがこの程度を嫌がるわけがない
IF GETBIT(TALENT:(ARG:0):淫乱系, 素質_淫乱_露出狂)
	CALL COM_ORDER_ELEMENT(ARG:0, "露出狂", 100)
ENDIF

IF IS_SAMESEX(MTAR:0, ARG:0)
	CALL COM_ORDER_ELEMENT(ARG:0, "同性", 30)
ELSE
	IF TALENT:(ARG:0):一線越えない
		CALL COM_ORDER_ELEMENT(ARG:0, "一線越えない", -15)
	ENDIF
ENDIF

IF ABL:(ARG:0):性知識 == 0
	CALL COM_ORDER_ELEMENT(ARG:0, "性知識なし", 30)
ENDIF

IF ABL:(ARG:0):欲望 > 0
	CALL COM_ORDER_ELEMENT(ARG:0, "欲望Lv", ABL:(ARG:0):欲望 * 3)
ENDIF

IF ABL:(ARG:0):露出 > 0
	CALL COM_ORDER_ELEMENT(ARG:0, "露出Lv", ABL:(ARG:0):露出 * 5)
ENDIF

IF TALENT:(ARG:0):親愛
	CALL COM_ORDER_ELEMENT(ARG:0, "親愛", 25)
ELSEIF TALENT:(ARG:0):恋慕
	CALL COM_ORDER_ELEMENT(ARG:0, "恋慕", 10)
ENDIF

IF TALENT:(ARG:0):恋人
	CALL COM_ORDER_ELEMENT(ARG:0, "恋人", 20)
ELSEIF TALENT:(ARG:0):親友
	CALL COM_ORDER_ELEMENT(ARG:0, "親友", 20)
ENDIF

IF TALENT:(ARG:0):隷属
	CALL COM_ORDER_ELEMENT(ARG:0, "隷属", 25)
ELSEIF TALENT:(ARG:0):服従
	CALL COM_ORDER_ELEMENT(ARG:0, "服従", 10)
ENDIF

IF TALENT:(ARG:0):烙印
	CALL COM_ORDER_ELEMENT(ARG:0, "烙印", 20)
ENDIF

IF TALENT:(ARG:0):所有者
	CALL COM_ORDER_ELEMENT(ARG:0, "所有者", 25)
ELSEIF TALENT:(ARG:0):主人
	CALL COM_ORDER_ELEMENT(ARG:0, "主人", 10)
ENDIF

IF TALENT:(ARG:0):合意
	CALL COM_ORDER_ELEMENT(ARG:0, "合意", 15)
ENDIF

IF TALENT:(ARG:0):プライド高い
	LOCAL:0 = 4
ELSEIF TALENT:(ARG:0):プライド低い
	LOCAL:0 = 1
ELSE
	LOCAL:0 = 2
ENDIF
IF MARK:(ARG:0):既成事実 >= 1
	CALL COM_ORDER_ELEMENT(ARG:0, @"既成事実Lv{MARK:(ARG:0):既成事実}", MARK:(ARG:0):既成事実 * 3 * LOCAL:0)
ENDIF

IF TALENT:MASTER:魅惑
	CALL COM_ORDER_ELEMENT(ARG:0, "主人公の魅惑", 6)
ENDIF
IF TALENT:MASTER:謎の魅力
	CALL COM_ORDER_ELEMENT(ARG:0, "主人公の謎の魅力", 6)
ENDIF

IF PALAM:(ARG:0):酔い >= 150
	CALL COM_ORDER_ELEMENT(ARG:0, "酔い", PALAM:(ARG:0):酔い / 150)
ENDIF

IF GET_PLACENAME(TFLAG:54) == "遊泳場"
	CALL COM_ORDER_ELEMENT(ARG:0, "遊泳場", -10)
ENDIF

IF GET_PLACENAME(TFLAG:54) == "温泉"
	CALL COM_ORDER_ELEMENT(ARG:0, "温泉", -30)
ENDIF

;判定結果を表示
;PRINTFORMW  = {TCVAR:(ARG:0):24} %TOSTR_COMPARE(TCVAR:(ARG:0):24, TCVAR:(ARG:0):25)% 実行値{TCVAR:(ARG:0):25}

RETURN TCVAR:(ARG:0):24 - TCVAR:(ARG:0):25


;-------------------------------------------------
;地の文(前文)
;-------------------------------------------------
@COM_TEXT_BEFORE361

LOCALS:3 = 

LOCALS:0 = ポーズをとっ

IF MTAR_NUM == 1
	LOCALS:1 = %ANAME(MTAR:0)%
ELSEIF MTAR_NUM == 2
	LOCALS:1 = %ANAME(MTAR:0)%と%ANAME(MTAR:1)%
ELSE
	LOCALS:1 = %ANAME(MTAR:0)%たち
ENDIF

IF MPLY_NUM == 1
	LOCALS:2 = %ANAME(MPLY:0)%
ELSEIF MPLY_NUM == 2
	LOCALS:2 = %ANAME(MPLY:0)%と%ANAME(MPLY:1)%
ELSE
	LOCALS:2 = %ANAME(MPLY:0)%たち
ENDIF

IF GET_PLACENAME(TFLAG:54) == "遊泳場"
	LOCALS:3 = 水着姿の
ELSEIF GET_PLACENAME(TFLAG:54) == "温泉"
	LOCALS:3 = 裸の
ELSEIF SEARCH_EQUIP(720, MPLY:0, -1) >= 0;撮影設備で水着姿
	LOCALS:3 = 水着姿の
ELSEIF SEARCH_EQUIP(721, MPLY:0, -1) >= 0;撮影設備で裸
	LOCALS:3 = 裸の
ENDIF

SELECTCASE GET_COM_INITIATIVE()
	;プレイヤーに主導権
	CASE 0
		PRINTFORMW %LOCALS:2%は%LOCALS:1%に向かって、%LOCALS:3%尻をじっくり見せつけるような%LOCALS:0%た…
	;ターゲットに主導権
	CASE 1
		PRINTFORML %LOCALS:1%は、%LOCALS:3%尻を強調してほしいと%LOCALS:2%にリクエストした
		PRINTFORMW %LOCALS:2%は言われるまま、%LOCALS:1%の目の前で%LOCALS:0%た…
	;第三者に主導権
	CASEELSE
		PRINTFORML %ANAME(TFLAG:45)%は%LOCALS:2%に対して、%LOCALS:1%に%LOCALS:3%尻を強調するポーズを見せるように言った
		PRINTFORMW %LOCALS:2%は言われるまま、%LOCALS:1%の目の前で見せつけるように%LOCALS:0%た…
ENDSELECT

;-------------------------------------------------
;地の文(パラメータ・刻印変動後)
;-------------------------------------------------
@COM_TEXT_LAST361
;日常コマンドに共通の地の文
CALL COM_TEXT_LAST_LIFE

;-------------------------------------------------

;コマンド区分
;-------------------------------------------------

@COM_AVAILABLE_WHEN361
IF FLAG:調教モード != 調教_居住区
	RETURN コマンド_日常
ELSE
	RETURN コマンド_居住区
ENDIF


;-------------------------------------------------

;専用関数・見せコマンド時プレイヤー歓楽算出
;プレイヤーの露出、欲望に応じてプレイヤーに歓楽ソースが多く入る
;-------------------------------------------------
@COM_UNIQUE_MISE_PLAYER_KANRAKU_361(プレイヤ,基礎値)
#DIM プレイヤ
#DIM 基礎値

LOCAL:1 = 基礎値
SELECTCASE ABL:(プレイヤ):露出
	CASE 0
		TIMES LOCAL:1, 0.50
	CASE 1
		TIMES LOCAL:1, 1.00
	CASE 2
		TIMES LOCAL:1, 1.50
	CASE 3
		TIMES LOCAL:1, 2.00
	CASE 4
		TIMES LOCAL:1, 2.50
	CASEELSE
		LOCAL:1 = LOCAL:1 * ((ABL:(プレイヤ):露出 - 5) * 10 + 250) / 100
ENDSELECT

SELECTCASE ABL:(プレイヤ):欲望
	CASE 0
		TIMES LOCAL:1, 0.75
	CASE 1
		TIMES LOCAL:1, 1.00
	CASE 2
		TIMES LOCAL:1, 1.25
	CASE 3
		TIMES LOCAL:1, 1.50
	CASE 4
		TIMES LOCAL:1, 1.75
	CASEELSE
		LOCAL:1 = LOCAL:1 * ((ABL:(プレイヤ):欲望 - 5) * 10 + 175) / 100
ENDSELECT

IF TALENT:(プレイヤ):自制心
	TIMES LOCAL:1, 0.80
ENDIF
IF TALENT:(プレイヤ):好奇心
	TIMES LOCAL:1, 1.20
ENDIF
IF TALENT:(プレイヤ):保守的
	TIMES LOCAL:1, 0.70
ENDIF
IF TALENT:(プレイヤ):目立ちたがり
	TIMES LOCAL:1, 2.00
ENDIF
IF TALENT:(プレイヤ):貞操観念
	TIMES LOCAL:1, 0.50
ENDIF
IF TALENT:(プレイヤ):貞操無頓着
	TIMES LOCAL:1, 1.50
ENDIF
IF TALENT:(プレイヤ):抑圧
	TIMES LOCAL:1, 0.60
ENDIF
IF TALENT:(プレイヤ):解放
	TIMES LOCAL:1, 1.10
ENDIF
IF TALENT:(プレイヤ):孤高
	TIMES LOCAL:1, 0.25
ENDIF
IF TALENT:(プレイヤ):恥じらい
	TIMES LOCAL:1, 0.80
ENDIF
IF TALENT:(プレイヤ):恥薄い
	TIMES LOCAL:1, 1.20
ENDIF
IF TALENT:(プレイヤ):快感に素直
	TIMES LOCAL:1, 1.30
ENDIF
IF TALENT:(プレイヤ):快感の否定
	TIMES LOCAL:1, 0.25
ENDIF
IF GETBIT(TALENT:(プレイヤ):淫乱系, 素質_淫乱_淫乱)
	TIMES LOCAL:1, 2.00
ENDIF
IF GETBIT(TALENT:(プレイヤ):淫乱系, 素質_淫乱_露出狂)
	TIMES LOCAL:1, 4.00
ENDIF

CALL COM_UNIQUE_MISE_BASYO_HOSEI_361(LOCAL:1)
LOCAL:1 = RESULT
CALL COM_UNIQUE_MISE_PLAYER_SUGATA_HOSEI_361(プレイヤ,LOCAL:1)
LOCAL:1 = RESULT

RETURN LOCAL:1


;-------------------------------------------------

;専用関数・見せコマンド時プレイヤー反感算出
;プレイヤーの反感と、露出、欲望に応じた軽減対応
;露出と欲望が育っていけばいずれは嫌がらなくなる。個人差はデカイが
;-------------------------------------------------
@COM_UNIQUE_MISE_PLAYER_HANKAN_361(プレイヤ,基礎値)
#DIM プレイヤ
#DIM 基礎値

LOCAL:1 = 基礎値
SELECTCASE ABL:(プレイヤ):露出
	CASE 0
		TIMES LOCAL:1, 1.00
	CASE 1
		TIMES LOCAL:1, 0.90
	CASE 2
		TIMES LOCAL:1, 0.80
	CASE 3
		TIMES LOCAL:1, 0.70
	CASE 4
		TIMES LOCAL:1, 0.60
	CASEELSE
		LOCAL:1 = LOCAL:1 * (50 - ((ABL:(プレイヤ):露出 - 5) * 2)) / 100
ENDSELECT

SELECTCASE ABL:(プレイヤ):欲望
	CASE 0
		TIMES LOCAL:1, 1.00
	CASE 1
		TIMES LOCAL:1, 0.95
	CASE 2
		TIMES LOCAL:1, 0.90
	CASE 3
		TIMES LOCAL:1, 0.85
	CASE 4
		TIMES LOCAL:1, 0.80
	CASEELSE
		LOCAL:1 = LOCAL:1 * (75 - (ABL:(プレイヤ):欲望 - 5)) / 100
ENDSELECT

IF TALENT:(プレイヤ):自制心
	TIMES LOCAL:1, 1.30
ENDIF
IF TALENT:(プレイヤ):好奇心
	TIMES LOCAL:1, 0.90
ENDIF
IF TALENT:(プレイヤ):保守的
	TIMES LOCAL:1, 1.50
ENDIF
IF TALENT:(プレイヤ):目立ちたがり
	TIMES LOCAL:1, 0.50
ENDIF
IF TALENT:(プレイヤ):貞操観念
	TIMES LOCAL:1, 2.00
ENDIF
IF TALENT:(プレイヤ):貞操無頓着
	TIMES LOCAL:1, 0.70
ENDIF
IF TALENT:(プレイヤ):抑圧
	TIMES LOCAL:1, 1.80
ENDIF
IF TALENT:(プレイヤ):解放
	TIMES LOCAL:1, 0.90
ENDIF
IF TALENT:(プレイヤ):孤高
	TIMES LOCAL:1, 4.00
ENDIF
IF TALENT:(プレイヤ):恥じらい
	TIMES LOCAL:1, 1.30
ENDIF
IF TALENT:(プレイヤ):恥薄い
	TIMES LOCAL:1, 0.80
ENDIF
IF TALENT:(プレイヤ):快感に素直
	TIMES LOCAL:1, 0.70
ENDIF
IF TALENT:(プレイヤ):快感の否定
	TIMES LOCAL:1, 4.00
ENDIF
IF GETBIT(TALENT:(プレイヤ):淫乱系, 素質_淫乱_淫乱)
	TIMES LOCAL:1, 0.50
ENDIF
IF GETBIT(TALENT:(プレイヤ):淫乱系, 素質_淫乱_露出狂)
	TIMES LOCAL:1, 0.25
ENDIF

CALL COM_UNIQUE_MISE_BASYO_HOSEI_361(LOCAL:1)
LOCAL:1 = RESULT
CALL COM_UNIQUE_MISE_PLAYER_SUGATA_HOSEI_361(プレイヤ,LOCAL:1)
LOCAL:1 = RESULT

RETURN LOCAL:1


;-------------------------------------------------

;専用関数・見せコマンド時ターゲット射精感算出
;プレイヤーの露出、欲望によってターゲットのもよおす射精感が高くなる
;見せるだけで即射精とか相当ハイレベルなので、かなり低く抑えるべき
;-------------------------------------------------
@COM_UNIQUE_MISE_TARGET_SHASEIKAN_361(プレイヤ,基礎値)
#DIM プレイヤ
#DIM 基礎値

LOCAL:1 = 基礎値
SELECTCASE ABL:(プレイヤ):露出
	CASE 0
		TIMES LOCAL:1, 0.50
	CASE 1
		TIMES LOCAL:1, 1.00
	CASE 2
		TIMES LOCAL:1, 1.60
	CASE 3
		TIMES LOCAL:1, 2.20
	CASE 4
		TIMES LOCAL:1, 2.80
	CASEELSE
		LOCAL:1 = LOCAL:1 * ((ABL:(プレイヤ):露出 - 5) * 10 + 290) / 100
ENDSELECT

SELECTCASE ABL:(プレイヤ):欲望
	CASE 0
		TIMES LOCAL:1, 0.70
	CASE 1
		TIMES LOCAL:1, 1.00
	CASE 2
		TIMES LOCAL:1, 1.30
	CASE 3
		TIMES LOCAL:1, 1.60
	CASE 4
		TIMES LOCAL:1, 2.00
	CASEELSE
		LOCAL:1 = LOCAL:1 * ((ABL:(プレイヤ):欲望 - 5) * 10 + 210) / 100
ENDSELECT

CALL COM_UNIQUE_MISE_BASYO_HOSEI_361(LOCAL:1)
LOCAL:1 = RESULT
CALL COM_UNIQUE_MISE_PLAYER_SUGATA_HOSEI_361(プレイヤ,LOCAL:1)
LOCAL:1 = RESULT

RETURN LOCAL:1


;-------------------------------------------------

;専用関数・見せコマンド時対象歓楽算出
;対象の露出、欲望に応じて対象に歓楽ソースが多く入る
;-------------------------------------------------
@COM_UNIQUE_MISE_TARGET_KANRAKU_361(対象,基礎値,姿コード)
#DIM 対象
#DIM 基礎値
#DIM 姿コード

LOCAL:1 = 基礎値
SELECTCASE ABL:(対象):欲望
	CASE 0
		TIMES LOCAL:1, 0.50
	CASE 1
		TIMES LOCAL:1, 1.00
	CASE 2
		TIMES LOCAL:1, 1.50
	CASE 3
		TIMES LOCAL:1, 2.00
	CASE 4
		TIMES LOCAL:1, 2.50
	CASEELSE
		LOCAL:1 = LOCAL:1 * ((ABL:(対象):欲望 - 5) * 10 + 250) / 100
ENDSELECT

IF TALENT:(対象):自制心
	TIMES LOCAL:1, 0.80
ENDIF
IF TALENT:(対象):好奇心
	TIMES LOCAL:1, 1.20
ENDIF
IF TALENT:(対象):保守的
	TIMES LOCAL:1, 0.70
ENDIF
IF TALENT:(対象):貞操観念
	TIMES LOCAL:1, 0.50
ENDIF
IF TALENT:(対象):貞操無頓着
	TIMES LOCAL:1, 1.50
ENDIF
IF TALENT:(対象):抑圧
	TIMES LOCAL:1, 0.60
ENDIF
IF TALENT:(対象):解放
	TIMES LOCAL:1, 1.10
ENDIF
IF TALENT:(対象):孤高
	TIMES LOCAL:1, 0.25
ENDIF
IF TALENT:(対象):恥じらい
	TIMES LOCAL:1, 0.80
ENDIF
IF TALENT:(対象):恥薄い
	TIMES LOCAL:1, 1.20
ENDIF
IF TALENT:(対象):快感に素直
	TIMES LOCAL:1, 1.30
ENDIF
IF TALENT:(対象):快感の否定
	TIMES LOCAL:1, 0.25
ENDIF
IF GETBIT(TALENT:(対象):淫乱系, 素質_淫乱_淫乱)
	TIMES LOCAL:1, 2.00
ENDIF

CALL COM_UNIQUE_MISE_BASYO_HOSEI_361(LOCAL:1)
LOCAL:1 = RESULT
CALL COM_UNIQUE_MISE_TARGET_SUGATA_HOSEI_361(姿コード,LOCAL:1)
LOCAL:1 = RESULT

RETURN LOCAL:1


;-------------------------------------------------

;専用関数・見せコマンド時対象反感算出
;対象の反感と、欲望に応じた軽減対応
;欲望が育てばかなり軽減される…が、ダメなヤツはよっぽどガンバらないとダメ
;-------------------------------------------------
@COM_UNIQUE_MISE_TARGET_HANKAN_361(対象,基礎値,姿コード)
#DIM 対象
#DIM 基礎値
#DIM 姿コード

LOCAL:1 = 基礎値
SELECTCASE ABL:(対象):欲望
	CASE 0
		TIMES LOCAL:1, 1.00
	CASE 1
		TIMES LOCAL:1, 0.90
	CASE 2
		TIMES LOCAL:1, 0.80
	CASE 3
		TIMES LOCAL:1, 0.70
	CASE 4
		TIMES LOCAL:1, 0.60
	CASEELSE
		LOCAL:1 = LOCAL:1 * (50 - (ABL:(対象):欲望 - 5) * 2) / 100
ENDSELECT

IF TALENT:(対象):自制心
	TIMES LOCAL:1, 1.30
ENDIF
IF TALENT:(対象):好奇心
	TIMES LOCAL:1, 0.90
ENDIF
IF TALENT:(対象):保守的
	TIMES LOCAL:1, 1.50
ENDIF
IF TALENT:(対象):貞操観念
	TIMES LOCAL:1, 2.00
ENDIF
IF TALENT:(対象):貞操無頓着
	TIMES LOCAL:1, 0.70
ENDIF
IF TALENT:(対象):抑圧
	TIMES LOCAL:1, 1.80
ENDIF
IF TALENT:(対象):解放
	TIMES LOCAL:1, 0.90
ENDIF
IF TALENT:(対象):孤高
	TIMES LOCAL:1, 4.00
ENDIF
IF TALENT:(対象):恥じらい
	TIMES LOCAL:1, 1.30
ENDIF
IF TALENT:(対象):恥薄い
	TIMES LOCAL:1, 0.80
ENDIF
IF TALENT:(対象):快感に素直
	TIMES LOCAL:1, 0.70
ENDIF
IF TALENT:(対象):快感の否定
	TIMES LOCAL:1, 4.00
ENDIF
IF GETBIT(TALENT:(対象):淫乱系, 素質_淫乱_淫乱)
	TIMES LOCAL:1, 0.50
ENDIF

CALL COM_UNIQUE_MISE_BASYO_HOSEI_361(LOCAL:1)
LOCAL:1 = RESULT
CALL COM_UNIQUE_MISE_TARGET_SUGATA_HOSEI_361(姿コード,LOCAL:1)
LOCAL:1 = RESULT

RETURN LOCAL:1


;-------------------------------------------------

;専用関数・見せコマンド時の場所補正
;日常コマンドにおけるデートにのみ対応。居住区では基本的に何もしない
;-------------------------------------------------
@COM_UNIQUE_MISE_BASYO_HOSEI_361(対象の値)
#DIM 対象の値

LOCAL:1 = 対象の値
IF GET_PLACENAME(TFLAG:54) == "遊泳場"
	TIMES LOCAL:1, 1.50
ENDIF

;温泉。すっぽんぽん
IF GET_PLACENAME(TFLAG:54) == "温泉"
	TIMES LOCAL:1, 2.00
ENDIF

RETURN LOCAL:1


;-------------------------------------------------

;専用関数・見せコマンド時の姿補正
;居住区における撮影にのみ対応。日常では基本的に何もしない
;-------------------------------------------------
@COM_UNIQUE_MISE_PLAYER_SUGATA_HOSEI_361(対象,対象の値)
#DIM 対象
#DIM 対象の値

LOCAL:1 = 対象の値
IF SEARCH_EQUIP(720, 対象, -1) >= 0;撮影設備で水着姿
	TIMES LOCAL:1, 1.50
ENDIF

IF SEARCH_EQUIP(721, 対象, -1) >= 0;撮影設備で裸
	TIMES LOCAL:1, 2.00
ENDIF

RETURN LOCAL:1


;-------------------------------------------------

;専用関数・見せコマンド時の姿補正
;居住区における撮影にのみ対応。日常では基本的に何もしない
;-------------------------------------------------
@COM_UNIQUE_MISE_TARGET_SUGATA_HOSEI_361(姿コード,対象の値)
#DIM 姿コード
#DIM 対象の値

LOCAL:1 = 対象の値
IF 姿コード == 1;撮影設備で水着姿
	TIMES LOCAL:1, 1.50
ENDIF

IF 姿コード == 2;撮影設備で裸
	TIMES LOCAL:1, 2.00
ENDIF

RETURN LOCAL:1
