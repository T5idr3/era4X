;-------------------------------------------------
;キャラクターの行動定義
;-------------------------------------------------
@HO_行動定義_休憩(CHARA_ID)
#DIM CHARA_ID
#DIM  DYNAMIC CHARA_IDX
#DIM  DYNAMIC ROOM_IDX
#DIM  DYNAMIC 移動先ROOM_IDX
#DIMS DYNAMIC _LIST_移動先候補
#DIMS DYNAMIC 現在の行動 

;IDXを取得
CALL HO_GET_CHARAIDX(CHARA_ID)
CHARA_IDX = RESULT
;キャラが現在いる部屋を取得
CALL HO_GET_CHARADATA(CHARA_IDX, "ROOM_IDX")
ROOM_IDX = RESULT
CALL HO_GET_CHARADATA(CHARA_IDX, "ACTION")
現在の行動 = %RESULTS%

CALL HO_FIND_NEAREST_ROOM_BY_MODULE_USAGE("休憩", ROOM_IDX)
IF RESULT != -1
    移動先ROOM_IDX = RESULT
ELSE
    RETURN -1
ENDIF
CALL HO_GET_ROUTE(ROOM_IDX, 移動先ROOM_IDX)
SIF RESULT == -1
    RETURN -1
_LIST_移動先候補 = %RESULTS%
IF RESULT != 0
    CALL HO_STOP_USE_MODULE(CHARA_ID)
    CALL HO_SET_MOVELIST(CHARA_ID, _LIST_移動先候補)
ELSE
    CALL HO_STOP_USE_MODULE(CHARA_ID)
    CALL HO_TRY_USE_MODULE(CHARA_ID, "休憩")
    IF RESULT == 1
        CALL HO_SET_CHARADATA(CHARA_IDX, "ACTION", "休憩中")
        TCVAR:CHARA_ID:疲労度 = 0
        BASE:(CHARA_ID):体力 = LIMIT(BASE:(CHARA_ID):体力 + 100, BASE:(CHARA_ID):体力, MAXBASE:(CHARA_ID):体力)
        BASE:(CHARA_ID):気力 = LIMIT(BASE:(CHARA_ID):気力 + 50, BASE:(CHARA_ID):気力, MAXBASE:(CHARA_ID):気力)
        TCVAR:CHARA_ID:行動待機時間 = RAND(6, 12) * HO_単位時間
    ELSE
        CALL HO_FIND_NEAREST_ROOM_BY_MODULE_USAGE("休憩", ROOM_IDX, 1)
        IF RESULT != -1
            移動先ROOM_IDX = RESULT
            CALL HO_GET_ROUTE(ROOM_IDX, 移動先ROOM_IDX)
            _LIST_移動先候補 = %RESULTS%
            CALL HO_SET_MOVELIST(CHARA_ID, _LIST_移動先候補)
        ELSE
            RETURN -1
        ENDIF
    ENDIF
ENDIF

