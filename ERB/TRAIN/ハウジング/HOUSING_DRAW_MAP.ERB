;-------------------------------------------------
; FLAG:調教モード =8 居住区関連処理
;-------------------------------------------------
@HOUSING_DRAW_GUI
#DIM  DYNAMIC BTN_ID
#DIMS DYNAMIC ROOM_NAME
#DIM  DYNAMIC FCOLOR, 2

CUSTOMDRAWLINE ─
CALL TITLE_CATEGORY_GENERAL(@"%SHIP_NAME:(BOARDING_SHIP_ID:MASTER)%艦内居住区")
PRINTFORM {HO_TIME / 60}:%TOSTR(HO_TIME % 60 ,"00")%(GMT)
PRINTL
CALL HO_GET_ROOM_NAME(HO_MASTER現在地)
PRINTFORM 現在地:%RESULTS%
PRINTL
BTN_ID = HO_TOPボタンID
IF HO_DRAW_GUI_MODE == 0
    FCOLOR:0 = カラー_選択中
    FCOLOR:1 = カラー_白
ELSE
    FCOLOR:0 = カラー_白
    FCOLOR:1 = カラー_選択中
ENDIF
CALLF FuncTagSetText_Print(@"@C:{FCOLOR:0}@@B:{BTN_ID}@[MAP]@/B@@/C@  @C:{FCOLOR:1}@@B:{BTN_ID + 1}@[STATUS]@/B@@/C@", 5)
IF HO_DRAW_GUI_MODE == 0
    CALL HO_DRAW_MAP_WINDOW
ELSE
    CALL HO_DRAW_STATUS_WINDOW
ENDIF

@HO_DRAW_MAP_WINDOW
#DIM DYNAMIC WINDOW_ID
#DIM CONST WND_WIDTH  = 16
#DIM CONST WND_HEIGHT = 5
CALL WndMgr_DestroyWindow(-1)
FOR LOCAL, 0, HO_MAX_ROOMNUM
    CALL WndMgr_CreateWindow(WINDOW_ID, (LOCAL % 4 * WND_WIDTH), (LOCAL / 4 * WND_HEIGHT), WND_WIDTH, WND_HEIGHT, 2)
    CALL HO_SET_MAP_WINDOW_TEXT(WINDOW_ID, LOCAL)
    CALL HO_GET_ROOM_NAME(LOCAL)
    IF RESULTS == "EMPTY"
        CALL WndMgr_Attribute(WINDOW_ID, "Disabled", 1)
    ENDIF
    WINDOW_ID ++
NEXT
FOR LOCAL, 0, HO_MAX_ROOMNUM
    CALL HO_GET_PATH(LOCAL)
    IF LIST_INDEXOF(RESULTS, TOSTR(LOCAL + 1)) != -1 && LOCAL % 4 != 3
    ;横列の通路
        WINDOW_ID ++
        CALL WndMgr_CreateWindow(WINDOW_ID,(WND_WIDTH - 1) + (WND_WIDTH * (LOCAL % 4)), 1 + (LOCAL / 4 * WND_HEIGHT), 3, 3, 0)
        CALL WndMgr_TextSet(WINDOW_ID, 0, "￣")
        CALL WndMgr_TextSet(WINDOW_ID, 2, "＿")
    ENDIF
    CALL HO_GET_PATH(LOCAL)
    IF LIST_INDEXOF(RESULTS, TOSTR(LOCAL + 4)) != -1 && LOCAL / 4 != 3
    ;縦列の通路
        WINDOW_ID ++
        CALL WndMgr_CreateWindow(WINDOW_ID,(WND_WIDTH / 2 - 3) + (WND_WIDTH * (LOCAL % 4)), WND_HEIGHT - 1 + (LOCAL / 4 * WND_HEIGHT), 3, 2, 0)
        CALL WndMgr_TextSet(WINDOW_ID, 0, "┐  ┌")
        CALL WndMgr_TextSet(WINDOW_ID, 1, "┘  └")
    ENDIF
NEXT
CALL HO_DRAW_MODULE_WINDOW
CALL WndMgr_DisplayAll()

@HO_DRAW_MODULE_WINDOW
#DIM CONST WND_WIDTH  = 40
#DIM CONST WND_HEIGHT = 20
#DIM DYNAMIC WINDOW_ID
#DIM DYNAMIC L_CNT

#DIMS DYNAMIC ROOM_NAME
#DIMS DYNAMIC _LIST_MODULE_LIST
#DIM DYNAMIC MODULE_IDX
#DIM DYNAMIC MODULE_ID
#DIM DYNAMIC _ID
#DIM DYNAMIC CAPACITY
#DIM DYNAMIC USER_COUNT
#DIM DYNAMIC BTN_ID

CALL WndMgr_VacantId()
WINDOW_ID = RESULT

CALL HO_GET_ROOM_NAME(HO_MASTER現在地)
ROOM_NAME = %RESULTS%
CALL WndMgr_CreateWindow(WINDOW_ID, 68, 0, WND_WIDTH, WND_HEIGHT, 2)
CALL WndMgr_WindowTitle( WINDOW_ID, ROOM_NAME)
CALL HO_GET_MAPDATA(HO_有効マップID, HO_MASTER現在地, "MODULE_LIST")
_LIST_MODULE_LIST = %RESULTS%
FOR MODULE_IDX, 0, LIST_COUNT(_LIST_MODULE_LIST)
    BTN_ID = MODULE_IDX + HO_モジュールボタンID
    CALL HO_GET_MODULE_ID(HO_MASTER現在地, MODULE_IDX)
    MODULE_ID = RESULT
    ;クルー私室モジュール用の処理
    IF MODULE_ID == HO_CREW_PRIVATE_ROOM_ID
        CALL HO_DRAW_CREW_PRIVATE_ROOM(WINDOW_ID, HO_MASTER現在地, MODULE_IDX)
    ELSE
        CALL HO_GET_MODULE_DATA(MODULE_ID, "MODULE_NAME")
    ENDIF
    CALL WndMgr_TextSet(WINDOW_ID, L_CNT, @"@B:{BTN_ID}@◇ [%RESULTS%]@/B@")
    ;施錠ボタンに関する処理
    CALL HO_IS_MODULE_LOCKED(HO_MASTER現在地, MODULE_IDX)
    SIF RESULT == 1
        CALL WndMgr_TextAdd(WINDOW_ID, L_CNT, @" @C:{カラー_選択不可}@🔒@/C@")
    L_CNT ++
    CALL HO_STR_MODULE_CAPACITY_INDICATOR(MODULE_IDX)
    CALL WndMgr_TextSet(WINDOW_ID, L_CNT, @"%RESULTS%")
    L_CNT ++
NEXT
RETURN

;;クルー個室の固有描画処理
;前提としてクルー個室のMODULE_IDXはCHARA_IDXと対応している必要がある
@HO_DRAW_CREW_PRIVATE_ROOM(WINDOW_ID, ROOM_IDX, MODULE_IDX)
#DIM WINDOW_ID
#DIM ROOM_IDX
#DIM MODULE_IDX
#DIM DYNAMIC CHARA_IDX

VARSET RESULTS
CALL HO_GET_CHARADATA(MODULE_IDX, "CHARA_ID")
RESULTS '= @"%SNAME(RESULT)%の部屋"
RETURN

;モジュールの使用状況を可視化
@HO_STR_MODULE_CAPACITY_INDICATOR(MODULE_IDX)
#DIM MODULE_IDX
#DIM DYNAMIC CHARA_IDX
#DIM DYNAMIC USER_COUNT
#DIMS DYNAMIC STR_INDICATOR

VARSET RESULTS

CALL HO_GET_MASTER_DATA("MODULE_USAGE")
IF RESULT == MODULE_IDX
    USER_COUNT ++
    STR_INDICATOR += @"@C:{カラー_黄}@♙@/C@"
ENDIF
WHILE _DIC_HO_CHARALIST:(CHARA_IDX) != "-1"
    CALL HO_GET_CHARADATA(CHARA_IDX, "ROOM_IDX")
    IF RESULT != HO_MASTER現在地
        CHARA_IDX ++
        CONTINUE 
    ENDIF 
    CALL HO_GET_CHARADATA(CHARA_IDX, "MODULE_USAGE")
    IF MODULE_IDX == RESULT
        USER_COUNT ++
        CALL HO_GET_CHARADATA(CHARA_IDX, "CHARA_ID")
        IF IS_FEMALE(RESULT)
            STR_INDICATOR += @"@C:{カラー_女}@♙@/C@"
        ELSE
            STR_INDICATOR += @"@C:{カラー_男}@♙@/C@"
        ENDIF
    ENDIF
    CHARA_IDX ++
WEND
CALL HO_GET_MODULE_ID(HO_MASTER現在地, MODULE_IDX)
CALL HO_GET_MODULE_DATA(RESULT, "人数")
STR_INDICATOR += @"@C:{カラー_選択不可}@♙@/C@" * (RESULT - USER_COUNT)
RESULTS = %STR_INDICATOR%
RETURN

@HO_SET_MAP_WINDOW_TEXT(WINDOW_ID, ROOM_IDX)
#DIM WINDOW_ID
#DIM ROOM_IDX
#DIM  DYNAMIC BTN_ID
#DIMS DYNAMIC ROOM_NAME
#DIM  DYNAMIC ROOM_ID
#DIM  DYNAMIC FONTCOLOR
CALL HO_GET_ROOM_NAME(ROOM_IDX)
ROOM_NAME = %RESULTS%
IF ROOM_IDX == HO_MASTER現在地
    FONTCOLOR = カラー_オレンジ
ELSE
    FONTCOLOR = カラー_白
ENDIF
BTN_ID = ROOM_IDX + HO_マップボタンID
LOCALS = %FuncTagSetText_ShapeSize(@"@C:{FONTCOLOR}@@B:{BTN_ID}@[%ROOM_NAME%]@/B@@/C@", 12, 1, 2)%
CALL WndMgr_TextSet(WINDOW_ID, 0, LOCALS)

LOCALS '= ""
LOCAL = 0
IF ROOM_IDX == HO_MASTER現在地
    LOCALS += @"@C:{カラー_黄}@İ@/C@"
ENDIF
WHILE _DIC_HO_CHARALIST:LOCAL != "-1"
    CALL HO_GET_CHARADATA(LOCAL, "ROOM_IDX")
    IF ROOM_IDX == RESULT
        CALL HO_GET_CHARADATA(LOCAL, "CHARA_ID")
        IF IS_FEMALE(RESULT)
            LOCALS += @"@C:{カラー_女}@İ@/C@"
        ELSE
            LOCALS += @"@C:{カラー_男}@İ@/C@"
        ENDIF
    ENDIF
    LOCAL ++
WEND
LOCALS = %FuncTagSetText_ShapeSize(LOCALS, 10, 1, 2)%
CALL WndMgr_TextSet(WINDOW_ID, 1, LOCALS)