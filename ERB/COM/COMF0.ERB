;-------------------------------------------------
;愛撫
;愛撫系コマンド　レベル2
;-------------------------------------------------
@COM0

PRINTL 愛撫
STR:0 = 愛撫
CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:0 += 5
LOSEBASE:1 += 50

SOURCE:0 = 80
SOURCE:3 = 80
SOURCE:4 = 50
SOURCE:5 = 60
SOURCE:13 = 20
SOURCE:15 = 30
SOURCE:17 = 20
SOURCE:18 = 20

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:16 += 20

;舌を使う調教では、調教者の舌使いによって、快感のソース増加
IF TALENT:PLAYER:舌使い
	TIMES SOURCE:0 , 1.50
	TIMES SOURCE:3 , 1.50
	TIMES SOURCE:4 , 1.50
ENDIF
;猫舌は減少
IF TALENT:PLAYER:猫舌
	TIMES SOURCE:0 , 0.75
	TIMES SOURCE:3 , 0.75
	TIMES SOURCE:4 , 0.75
ENDIF

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
IF (STAIN:0 >= 2 && (STAIN:0 != 16 && STAIN:0 != 17) && ASSIPLAY == 0 && TALENT:MASTER:汚れ無視 == 0) || TEQUIP:20
;奴隷の口の汚れがあるときキスはない（愛液と母乳よごれはＯＫ、汚れ無視か助手調教時は問題なし）
;ボールギャグ使用中もなし
	SOURCE:15 = 0
	SOURCE:0 /= 2
	SOURCE:4 /= 4
	SOURCE:10 /= 2
ELSE
	;汚臭鈍感
	SIF TALENT:汚臭鈍感
		SOURCE:15 /= 4
	;汚臭敏感
	SIF TALENT:汚臭敏感
		SOURCE:15 *= 3
	;自尊心
	SIF TALENT:プライド高い
		SOURCE:15 *= 2
	IF STAIN:PLAYER:0
	;主人の口の汚れがあると不潔150%
		SOURCE:15 *= 3
		SOURCE:15 /= 2
	ENDIF
	;奴隷の口⇔調教者の口の汚れが移動
	STAIN:0 |= STAIN:PLAYER:0
	STAIN:PLAYER:0 |= STAIN:0
ENDIF


;-------------------------------------------------
;経験の処理
;-------------------------------------------------
A = 5
B = NO:PLAYER + 899
IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	PRINTFORML %EXPNAME:40%+{A}
	EXP:レズ経験 += A
	CFLAG:B += A
ELSEIF TALENT:オトコ && TALENT:PLAYER:オトコ
	PRINTFORML %EXPNAME:41%+{A}
	EXP:ゲイ経験 += A
	CFLAG:B += A
ENDIF

;調教者が猫舌持ちの場合は、調教者に嗜虐快楽経験を追加
IF TALENT:PLAYER:猫舌
	EXP:PLAYER:嗜虐快楽経験 += 1
	PRINTFORML %CALLNAME:PLAYER%の%EXPNAME:32%＋１
ENDIF

;依存度ベクトル
TFLAG:33 = 2

RETURN 1

