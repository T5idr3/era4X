;-------------------------------------------------
;拡張バルーン
;ハードな調教コマンド、レベル4
;-------------------------------------------------
@COM148
PRINTL 拡張バルーン
STR:0 = 拡張バルーン
CALL KOJO_MESSAGE_COM

;着脱の処理
IF TEQUIP:22 == 0
	TEQUIP:22 = 1
ELSE
	TEQUIP:22 = 0
ENDIF

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:0 += 150
LOSEBASE:1 += 200

SOURCE:2 = 200
SOURCE:7 = 800
SOURCE:13 = 300
SOURCE:14 = 400
SOURCE:17 = 700
SOURCE:18 = 700


;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:16 += 700

;Aへの苦痛は先に処理しておく
;EXP:Ａ経験をみる
IF EXP:Ａ経験 < EXPLV:1
	TIMES SOURCE:7 , 3.00
	TIMES SOURCE:18 , 3.00
ELSEIF EXP:Ａ経験 < EXPLV:2
	TIMES SOURCE:7 , 1.20
	TIMES SOURCE:18 , 2.00
ELSEIF EXP:Ａ経験 < EXPLV:3
	TIMES SOURCE:7 , 0.80
	TIMES SOURCE:18 , 1.50
ELSEIF EXP:Ａ経験 < EXPLV:4
	TIMES SOURCE:7 , 0.60
	TIMES SOURCE:18 , 1.20
ELSEIF EXP:Ａ経験 < EXPLV:5
	TIMES SOURCE:7 , 0.40
ELSEIF EXP:Ａ経験 >= EXPLV:5
	TIMES SOURCE:7 , 0.20
ENDIF

;PALAM:潤滑をみる
IF PALAM:4 < PALAMLV:1
	TIMES SOURCE:7 , 1.20
	TIMES SOURCE:18 , 2.00
ELSEIF PALAM:4 < PALAMLV:2
	TIMES SOURCE:7 , 1.00
	TIMES SOURCE:18 , 1.50
ELSEIF PALAM:4 < PALAMLV:3
	TIMES SOURCE:7 , 0.60
ELSEIF PALAM:4 < PALAMLV:4
	TIMES SOURCE:7 , 0.30
ELSEIF PALAM:4 >= PALAMLV:4
	TIMES SOURCE:7 , 0.20
ENDIF


;-------------------------------------------------
;経験の処理
;-------------------------------------------------
EXP:Ａ経験 += 5
PRINTL Ａ経験＋５

IF EXP:Ｖ拡張経験 == 0 && EXP:Ａ拡張経験 == 0 
	EXP:異常経験 += 1
	PRINTL 異常経験＋１
ENDIF

EXP:Ａ拡張経験 += 1
PRINTL Ａ拡張経験＋1

A = 1
B = NO:PLAYER + 899
IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	PRINTFORML %EXPNAME:40%+{A}
	EXP:レズ経験 += A
	CFLAG:B += A
ELSEIF TALENT:オトコ && TALENT:PLAYER:オトコ
	PRINTFORML %EXPNAME:41%+{A}
	EXP:ゲイ経験 += A
	CFLAG:B += A
ENDIF

;依存度ベクトル
TFLAG:33 = 5

;依存度補正値(服従で1、隷属で2）
IF TALENT:服従 && ASSIPLAY == 0
	TFLAG:30 += 1
ELSEIF TALENT:隷属 && ASSIPLAY == 0
	TFLAG:30 += 2
ENDIF


RETURN 1


;-------------------------------------------------
;装着継続の処理
;-------------------------------------------------
@EQUIP_COM148
;拡張バルーン

PRINTL ＜拡張バルーン挿入中＞

LOSEBASE:0 += 75
LOSEBASE:1 += 100

SOURCE:2 += 100
SOURCE:13 += 150
SOURCE:14 += 200
SOURCE:17 += 350

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:16 += 350

;Aへの苦痛は先に処理しておく
;EXP:Ａ経験をみる
A = 400
B = 350
IF EXP:Ａ経験 < EXPLV:1
	TIMES A , 3.00
	TIMES B , 3.00
ELSEIF EXP:Ａ経験 < EXPLV:2
	TIMES A , 1.20
	TIMES B , 2.00
ELSEIF EXP:Ａ経験 < EXPLV:3
	TIMES A , 0.80
	TIMES B , 1.50
ELSEIF EXP:Ａ経験 < EXPLV:4
	TIMES A , 0.60
	TIMES B , 1.20
ELSEIF EXP:Ａ経験 < EXPLV:5
	TIMES A , 0.40
ELSEIF EXP:Ａ経験 >= EXPLV:5
	TIMES A , 0.20
ENDIF

;PALAM:潤滑をみる
IF PALAM:4 < PALAMLV:1
	TIMES A , 1.20
	TIMES B , 2.00
ELSEIF PALAM:4 < PALAMLV:2
	TIMES A , 1.00
	TIMES B , 1.50
ELSEIF PALAM:4 < PALAMLV:3
	TIMES A , 0.60
ELSEIF PALAM:4 < PALAMLV:4
	TIMES A , 0.30
ELSEIF PALAM:4 >= PALAMLV:4
	TIMES A , 0.20
ENDIF
SOURCE:7 += A
SOURCE:18 += B

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
EXP:Ａ経験 += 3
PRINTL Ａ経験＋３

EXP:Ａ拡張経験 += 1
PRINTL Ａ拡張経験＋1

