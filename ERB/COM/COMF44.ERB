;-------------------------------------------------
;バイブ
;道具使用コマンド、レベル2
;-------------------------------------------------
@COM44
;-------------------------------------------------
;特殊派生処理
;-------------------------------------------------
IF TEQUIP:25
	PRINTL 触手挿入
	STR:0 = 触手挿入
	SELECTCOM = 161
ELSE
	PRINTL バイブ
	STR:0 = バイブ
ENDIF
CALL KOJO_MESSAGE_COM

;V経験を伴うコマンドのフラグ(処女膜再生対応)
TFLAG:19 = 1

;着脱の処理
IF TEQUIP:13 == 0
	TEQUIP:13 = 1
	;触手挿入は触手の汚れを追加
	IF TEQUIP:25
		STAIN:3 |= 32
	ENDIF
ELSE
	TEQUIP:13 = 0
ENDIF

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:0 += 30
LOSEBASE:1 += 100

SOURCE:1 = 250
SOURCE:7 = 50
SOURCE:13 = 20
SOURCE:17 = 40
SOURCE:18 = 40


;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:16 += 40

;処女だった場合は、苦痛のソースと反発のソースを追加する
IF TALENT:処女 == 1
	SOURCE:7 += 2000
	SOURCE:18 += 3000
ENDIF

;再生処女だった場合は、苦痛のソースと反発のソースを追加する
IF TALENT:再生処女 == 1
	SOURCE:7 += 500
	SOURCE:18 += 1000
ENDIF

;Vへの苦痛は先に処理しておく
;EXP:Ｖ経験をみる（処女の反感は別途処理済みなので省略）
IF EXP:Ｖ経験 < EXPLV:1
	TIMES SOURCE:7 , 2.50
ELSEIF EXP:Ｖ経験 < EXPLV:2
	TIMES SOURCE:7 , 0.80
	TIMES SOURCE:18 , 1.20
ELSEIF EXP:Ｖ経験 < EXPLV:3
	TIMES SOURCE:7 , 0.40
ELSEIF EXP:Ｖ経験 < EXPLV:4
	TIMES SOURCE:7 , 0.10
ELSEIF EXP:Ｖ経験 < EXPLV:5
	TIMES SOURCE:7 , 0.00
ELSEIF EXP:Ｖ経験 >= EXPLV:5
	TIMES SOURCE:7 , 0.00
ENDIF

;PALAM:潤滑をみる
IF PALAM:4 < PALAMLV:1
	TIMES SOURCE:7 , 1.20
	TIMES SOURCE:18 , 1.50
ELSEIF PALAM:4 < PALAMLV:2
	TIMES SOURCE:7 , 0.70
	TIMES SOURCE:18 , 1.20
ELSEIF PALAM:4 < PALAMLV:3
	TIMES SOURCE:7 , 0.40
ELSEIF PALAM:4 < PALAMLV:4
	TIMES SOURCE:7 , 0.10
ELSEIF PALAM:4 >= PALAMLV:4
	TIMES SOURCE:7 , 0.10
ENDIF


;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;触手挿入なら、触手体液が膣内に付く
SIF TEQUIP:25
	STAIN:6 |= 32

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;V経験
EXP:Ｖ経験 += 1
PRINTL Ｖ経験＋１

;異常経験
IF TALENT:処女 == 1 && TEQUIP:25 == 1
	EXP:異常経験 += 2
	PRINTFORML %EXPNAME:50%＋２
ELSEIF TALENT:処女 == 1
	EXP:異常経験 += 1
	PRINTFORML %EXPNAME:50%＋１
ENDIF

;レズ経験
A = 1
B = NO:PLAYER + 899
IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	PRINTFORML %EXPNAME:40%+{A}
	EXP:レズ経験 += A
	CFLAG:B += A
ELSEIF TALENT:オトコ && TALENT:PLAYER:オトコ
	PRINTFORML %EXPNAME:41%+{A}
	EXP:ゲイ経験 += A
	CFLAG:B += A
ENDIF

;触手召喚時、触手経験のリセットをする
SIF TEQUIP:25 == 1
	T = 0

;依存度ベクトル
TFLAG:33 = 1


RETURN 1


;-------------------------------------------------
;装着継続の処理
;-------------------------------------------------
@EQUIP_COM44
;バイブ挿入中

;V経験を伴うコマンドのフラグ(処女膜再生対応)
TFLAG:19 = 1

IF TEQUIP:25
	PRINTL ＜触手挿入中＞
ELSE
	PRINTL ＜バイブ挿入中＞
ENDIF

LOSEBASE:0 += 10
LOSEBASE:1 += 50

SOURCE:1 += 125
SOURCE:13 += 10
SOURCE:17 += 20

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:16 += 20

;Vへの苦痛は先に処理しておく
;EXP:Ｖ経験をみる（処女はありえない）
A = 25
B = 20
IF EXP:Ｖ経験 < EXPLV:2
	TIMES A , 0.80
	TIMES B , 1.20
ELSEIF EXP:Ｖ経験 < EXPLV:3
	TIMES A , 0.40
ELSEIF EXP:Ｖ経験 < EXPLV:4
	TIMES A , 0.10
ELSEIF EXP:Ｖ経験 < EXPLV:5
	TIMES A , 0.00
ELSEIF EXP:Ｖ経験 >= EXPLV:5
	TIMES A , 0.00
ENDIF

;PALAM:潤滑をみる
IF PALAM:4 < PALAMLV:1
	TIMES A , 1.20
	TIMES B , 1.50
ELSEIF PALAM:4 < PALAMLV:2
	TIMES A , 0.70
	TIMES B , 1.20
ELSEIF PALAM:4 < PALAMLV:3
	TIMES A , 0.40
ELSEIF PALAM:4 < PALAMLV:4
	TIMES A , 0.10
ELSEIF PALAM:4 >= PALAMLV:4
	TIMES A , 0.10
ENDIF
SOURCE:7 += A
SOURCE:18 += B

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;自慰実行時は経験＋１
IF SELECTCOM == 11
	EXP:Ｖ経験 += 2
	PRINTL Ｖ経験＋２
ELSE
	EXP:Ｖ経験 += 1
	PRINTL Ｖ経験＋１
ENDIF

;触手経験加算
SIF TEQUIP:25 == 1
	T += 1


RETURN 1

