;-------------------------------------------------
;クンニ
;愛撫系コマンド　レベル1
;-------------------------------------------------
@COM1
;-------------------------------------------------
;特殊派生処理
;-------------------------------------------------
;前回と今回の調教者が同じ
IF (ASSIPLAY && TFLAG:50) || (ASSIPLAY == 0 && TFLAG:50 == 0)
	;縄使用中でなく、前回の調教がフェラチオかクンニ強制かシックスナインだとシックスナインに
	IF (PREVCOM == 81 || PREVCOM == 120 || PREVCOM == 501) && TEQUIP:19 == 0
		CALL COM_ABLE501
		SIF RESULT == 1
			JUMP COM501
	;縄使用中でなく、前回の調教がパイズリか縦パイズリかパイズリフェラか６９パイズリだと６９パイズリに
	ELSEIF (PREVCOM == 82 || PREVCOM == 87 || PREVCOM == 88 || PREVCOM == 508) && TEQUIP:19 == 0
		CALL COM_ABLE508
		SIF RESULT == 1
			JUMP COM508
	ENDIF
ENDIF

;羞恥プレイ中は岩清水に
IF TEQUIP:30 == 1
	JUMP COM502
ENDIF

PRINTL クンニ
STR:0 = クンニ
CALL KOJO_MESSAGE_COM
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:0 += 5
LOSEBASE:1 += 50

SOURCE:0 = 100
SOURCE:10 = 60
SOURCE:13 = 10
SOURCE:17 = 15
SOURCE:18 = 15

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:16 += 20

;舌を使う調教では、調教者の舌使いによって、快感のソース増加
SIF TALENT:PLAYER:舌使い
	TIMES SOURCE:0 , 1.50

;猫舌では減少
SIF TALENT:PLAYER:猫舌
	TIMES SOURCE:0 , 0.75

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷のＶ⇔調教者の口の汚れが移動
STAIN:3 |= STAIN:PLAYER:0
STAIN:PLAYER:0 |= STAIN:3

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
A = 3
B = NO:PLAYER + 899
IF TALENT:オトコ == 0 && TALENT:PLAYER:オトコ == 0
	PRINTFORML %EXPNAME:40%+{A}
	EXP:レズ経験 += A
	CFLAG:B += A
ELSEIF TALENT:オトコ && TALENT:PLAYER:オトコ
	PRINTFORML %EXPNAME:41%+{A}
	EXP:ゲイ経験 += A
	CFLAG:B += A
ENDIF

;調教者が猫舌持ちの場合は、調教者に嗜虐快楽経験を追加
IF TALENT:PLAYER:猫舌
	EXP:PLAYER:嗜虐快楽経験 += 1
	PRINTFORML %CALLNAME:PLAYER%の%EXPNAME:32%＋１
ENDIF

;依存度ベクトル
TFLAG:33 = 1

RETURN 1

