;-------------------------------------------------
;衣装の初期化
;-------------------------------------------------
@CLOTH_INIT(対象)
#DIM 対象
TRYCALLFORM CLOTH_INIT_{NO:対象}

;-------------------------------------------------
;衣装のリセット
;-------------------------------------------------
@CLOTH_RESET(対象)
#DIM 対象
FOR LOCAL, 0, CLOTH_PART_NUM
	CLOTH:対象:LOCAL = 0
NEXT


;-------------------------------------------------
;ARG:0番の衣装を表示
;-------------------------------------------------
@SHOW_CLOTH(ARG:0)
PRINTFORML ◆%ANAME(ARG:0)%の衣装◆
FOR LOCAL, 0, CLOTH_PART_NUM
	CALL GET_CLOTH_NAME(LOCAL, CLOTH:(ARG:0):LOCAL)
	PRINTFORML   %CLOTH_PART_NAME:LOCAL, 10, LEFT% - %RESULTS%
NEXT



;-------------------------------------------------
;指定キャラの指定部位に指定IDの衣装を装着する
;-------------------------------------------------
@SET_CLOTH(対象, 部位, ID)
#DIM 対象
#DIM 部位
#DIM ID
TRYCCALLFORM CLOTH_NAME_%CLOTH_PART_NAME_ENG:部位%_{ID}
CATCH
	ID = 0
ENDCATCH

IF ID > 0
	SELECTCASE 部位
		CASE 衣装部位_上服, 衣装部位_下服
			CLOTH:対象:衣装部位_上下服 = 0
		CASE 衣装部位_上下服
			CLOTH:対象:衣装部位_上服 = 0
			CLOTH:対象:衣装部位_下服 = 0
		CASE 衣装部位_下着上, 衣装部位_下着下
			CLOTH:対象:衣装部位_上下下着 = 0
		CASE 衣装部位_上下下着
			CLOTH:対象:衣装部位_下着上 = 0
			CLOTH:対象:衣装部位_下着下 = 0
	ENDSELECT
ENDIF

SELECTCASE 部位
	CASE 衣装部位_武器
		CALLFORM CLOTH_SET_EFFECT_%CLOTH_PART_NAME_ENG:部位%_{CLOTH:対象:部位}(対象, -1)
		CALLFORM CLOTH_SET_EFFECT_%CLOTH_PART_NAME_ENG:部位%_{ID}(対象, 1)
		
		IF CFLAG:(対象):所属 == CFLAG:MASTER:所属
			HAVE_CLOTH:(CLOTH:対象:部位) += 1
			HAVE_CLOTH:ID -= 1
			IF HAVE_CLOTH:ID < 0
				HAVE_CLOTH:ID = 0
			ENDIF
			HAVE_CLOTH:0 = 0
			TRYCCALLFORM CLOTH_IS_SYNC_ITEM_%CLOTH_PART_NAME_ENG:部位%_{ID}()
				ITEM:RESULT = HAVE_CLOTH:ID
				HAVE_CLOTH:ID = ITEM:RESULT
			CATCH
			ENDCATCH
		ENDIF
ENDSELECT



CLOTH:対象:部位 = ID

RETURN 1

;-------------------------------------------------
;名称から衣装を設定する 重いので多用しないこと
;-------------------------------------------------
@SET_CLOTH_BY_NAME(対象, 部位, 名称)
#DIM 対象
#DIM 部位
#DIMS 名称

CALL GET_CLOTH_ID_BY_NAME(部位, 名称)
CALL SET_CLOTH(対象, 部位, RESULT)

;-------------------------------------------------
;名称から衣装を取得する 重いので多用しないこと
;-------------------------------------------------
@GET_CLOTH_ID_BY_NAME(部位, 名称)
#DIM 部位
#DIMS 名称
FOR LOCAL, 0, MAX_CLOTH_ID
	TRYCCALLFORM CLOTH_NAME_%CLOTH_PART_NAME_ENG:部位%_{LOCAL}
	SIF RESULTS == 名称
		RETURN LOCAL
	CATCH
	ENDCATCH
NEXT

RETURN 0

;-------------------------------------------------
;衣装名を取得するラッパ関数
;-------------------------------------------------
@GET_CLOTH_NAME(部位, ID)
#DIM 部位
#DIM ID

TRYCCALLFORM CLOTH_NAME_%CLOTH_PART_NAME_ENG:部位%_{ID}()
CATCH
	RESULTS = なし
ENDCATCH


;-------------------------------------------------
;着用衣装表示（ボタンつき）
;-------------------------------------------------
@SHOW_CLOTH_SETTING(対象, ボタン)
#DIM 対象
#DIM ボタン
PRINTFORML ◆%ANAME(対象)%の衣装◆
FOR LOCAL, 0, CLOTH_PART_NUM
	IF ボタン
		PRINTBUTTON @"%"[" + CLOTH_PART_NAME:LOCAL + "]", 12, LEFT%", LOCAL + 100
	ELSE
		PRINTFORM %CLOTH_PART_NAME:LOCAL, 12, LEFT%
	ENDIF
	CALL GET_CLOTH_NAME(LOCAL, CLOTH:対象:LOCAL)
	PRINTFORML %RESULTS%
NEXT

;-------------------------------------------------
;衣装設定メニュー
;-------------------------------------------------
@CLOTH_SETTING_MENU(対象)
#DIM 対象
#DIM FIRST_LINE
FIRST_LINE = LINECOUNT

CALL SHOW_CLOTH_SETTING(対象, 1)
PRINTBUTTON "[戻る]", 999

$INPUT_LOOP
INPUT

IF RESULT == 999
	RETURN
ELSEIF RESULT < 100 && RESULT >= CLOTH_PART_NUM + 100
	CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF
CALL CLOTH_CHANGE_MENU(対象, RESULT - 100)

CLEARLINE LINECOUNT - FIRST_LINE

RESTART

;-------------------------------------------------
;衣装用メニュー その２
;-------------------------------------------------
@CLOTH_CHANGE_MENU(対象, 部位)
#DIM 対象
#DIM 部位
#DIM 利用可否, MAX_CLOTH_ID
#DIM 衣装数
#DIM FIRST_LINE
#DIM ページ数
#DIM ページ
#DIM CONST ページごと衣装数 = 15
VARSET 利用可否, 0
利用可否:0 = 1
衣装数 = 1
FOR LOCAL, 1, MAX_CLOTH_ID
	TRYCCALLFORM CLOTH_NAME_%CLOTH_PART_NAME_ENG:部位%_{LOCAL}()
		IF 部位 == 衣装部位_武器
			TRYCALLFORM GET_WEAPON_LIMITED_{LOCAL}(対象)
			IF RESULT == 1 && (HAVE_CLOTH:(LOCAL) > 0 || LOCAL == 0 || LOCAL == CLOTH:対象:部位)
				利用可否:LOCAL = 1
				衣装数 ++
			ENDIF
		ELSEIF 部位 != 衣装部位_武器
			利用可否:LOCAL = 1
			衣装数 ++
		ENDIF
	CATCH
		利用可否:LOCAL = 0
	ENDCATCH
NEXT

ページ数 = 衣装数 / ページごと衣装数 + 1
ページ = 1


CALL SINGLE_DRAWLINE

FIRST_LINE = LINECOUNT
$SHOW_LOOP

衣装数 = 0
FOR LOCAL, 0, MAX_CLOTH_ID
	SIF 利用可否:LOCAL == 0
		CONTINUE
	衣装数 ++
	SIF 衣装数 < ページごと衣装数 * (ページ - 1) || 衣装数 >= ページ * ページごと衣装数
		CONTINUE
	CALL GET_CLOTH_NAME(部位, LOCAL)
	PRINTBUTTON @"[%RESULTS, 20, LEFT%]", LOCAL

	SELECTCASE 部位
		CASE 衣装部位_武器
			TRYCALLFORM GET_WEAPON_REGULATION_{LOCAL}
			CALL PRINT_FORBIDDEN(RESULT)
			PRINTFORM 【%RESULTS%】
			IF LOCAL != 0
				TRYCCALLFORM CLOTH_IS_SYNC_ITEM_%CLOTH_PART_NAME_ENG:部位%_{LOCAL}()
					HAVE_CLOTH:LOCAL = ITEM:RESULT
					ITEM:RESULT = HAVE_CLOTH:LOCAL
				CATCH
				ENDCATCH
				PRINTFORM 所持数：%TOSTR(HAVE_CLOTH:LOCAL), 2, RIGHT%
			ELSE
				PRINT 　　　　　
			ENDIF
			PRINT 　
			CALL PRINT_STATUS_CHANGE_NEXT(部位, LOCAL, 対象, 1)
	ENDSELECT

	IF CLOTH:対象:部位 == LOCAL
		PRINTFORM  ●着用中
	ENDIF
	PRINTL
NEXT
CALL SINGLE_DRAWLINE
IF ページ数 > 1
	PRINTBUTTON "[前のページ]", 9997
	PRINTFORM  {ページ}/{ページ数}
	PRINTBUTTON "[次のページ]", 9998
	PRINTL
ENDIF
PRINTBUTTON "[戻る]", 9999

$INPUT_LOOP

INPUT

IF RESULT == 9999
	RETURN
ELSEIF RESULT == 9997 && ページ数 > 1
	ページ = ROUND_DECREMENT(ページ, 1, ページ数)
ELSEIF RESULT == 9998 && ページ数 > 1
	ページ = ROUND_INCREMENT(ページ, 1, ページ数)
ELSEIF RESULT >= 0 && RESULT < MAX_CLOTH_ID && 利用可否:RESULT
	CALL SET_CLOTH(対象, 部位, RESULT)
ENDIF

CLEARLINE LINECOUNT - FIRST_LINE
GOTO SHOW_LOOP

;--------------------------------------------------------------------
;武器の規制について
;武器に設定されている規制レベルを表示します
;--------------------------------------------------------------------
@PRINT_FORBIDDEN(ARG)

SELECTCASE ARG
	CASE 0
		RESULTS = 非武装
	CASE 100
		RESULTS = 護身用
	CASE 200 TO 299
		RESULTS = 規制品
	CASE 300 TO 399
		RESULTS = 禁制品
ENDSELECT
;--------------------------------------------------------------------
;衣装装着後のステータス変化を描画
;対象が指定されていない場合は、素のステータス変化値を表示
;(代わりにMASTERを使っています)
;ARG:0が設定されているとステータスの頭文字を併せて表示します
;--------------------------------------------------------------------
@PRINT_STATUS_CHANGE_NEXT(部位, ID, 対象 = -1, ARG:0 = -1)
#DIM 部位
#DIM ID
#DIM 対象

IF 対象:0 < 0 
	FOR LOCAL:0, 0, ステータス変化個数
		ステータス変化前値:(LOCAL:0) = ABL:MASTER:(ステータス文字列:(LOCAL:0))
	NEXT
	CALLFORM CLOTH_SET_EFFECT_%CLOTH_PART_NAME_ENG:部位%_{ID}(MASTER, 1)
	FOR LOCAL:0, 0, ステータス変化個数
		ステータス変化後値:(LOCAL:0) = ABL:MASTER:(ステータス文字列:(LOCAL:0))
	NEXT
	CALLFORM CLOTH_SET_EFFECT_%CLOTH_PART_NAME_ENG:部位%_{ID}(MASTER, -1)
	FOR LOCAL:0, 0, ステータス変化個数
		ステータス変化値:(LOCAL:0) = ステータス変化後値:(LOCAL:0) - ステータス変化前値:(LOCAL:0)
	NEXT

	FOR LOCAL:0, 0, ステータス変化個数
		IF ステータス変化値:(LOCAL:0) > 0
			SETCOLOR 0xE98FB0
		ELSEIF ステータス変化値:(LOCAL:0) < 0
			SETCOLOR 0x8F99E0
		ENDIF
		IF ARG:0 > 0
			PRINTFORM  %CHARATU(ステータス文字列:(LOCAL:0),0)%:%(ステータス変化値:(LOCAL:0) > 0? "↑"# "↓")%%TOSTR(ABS(ステータス変化値:(LOCAL:0))), 3, RIGHT%
		ELSE
			PRINTFORM  %(ステータス変化値:(LOCAL:0) == 0)?"　"#(ステータス変化値:(LOCAL:0) > 0? "↑"# "↓")%%TOSTR(ABS(ステータス変化値:(LOCAL:0))), 3, RIGHT%
		ENDIF
		RESETCOLOR
	NEXT
ELSE

	;ステータス変更後表示
	FOR LOCAL:0, 0, ステータス変化個数
		ステータス変化前値:(LOCAL:0) = ABL:対象:(ステータス文字列:(LOCAL:0))
	NEXT
	
	SELECTCASE 部位
		CASE 衣装部位_武器
			CALLFORM CLOTH_SET_EFFECT_%CLOTH_PART_NAME_ENG:衣装部位_武器%_{CLOTH:対象:衣装部位_武器}(対象, -1)
	ENDSELECT
	
	CALLFORM CLOTH_SET_EFFECT_%CLOTH_PART_NAME_ENG:部位%_{CLOTH:対象:部位}(対象, -1)
	CALLFORM CLOTH_SET_EFFECT_%CLOTH_PART_NAME_ENG:部位%_{ID}(対象, 1)
	FOR LOCAL:0, 0, ステータス変化個数
		ステータス変化後値:(LOCAL:0) = ABL:対象:(ステータス文字列:(LOCAL:0))
	NEXT
	CALLFORM CLOTH_SET_EFFECT_%CLOTH_PART_NAME_ENG:部位%_{ID}(対象, -1)
	CALLFORM CLOTH_SET_EFFECT_%CLOTH_PART_NAME_ENG:部位%_{CLOTH:対象:部位}(対象, 1)
	
	SELECTCASE 部位
		CASE 衣装部位_武器
			CALLFORM CLOTH_SET_EFFECT_%CLOTH_PART_NAME_ENG:衣装部位_武器%_{CLOTH:対象:衣装部位_武器}(対象, -1)
	ENDSELECT
	
	FOR LOCAL:0, 0, ステータス変化個数
		ステータス変化値:(LOCAL:0) = ステータス変化後値:(LOCAL:0) - ステータス変化前値:(LOCAL:0)
	NEXT
	
	FOR LOCAL:0, 0, ステータス変化個数
		IF ステータス変化値:(LOCAL:0) != 0
			IF ステータス変化値:(LOCAL:0) > 0
				SETCOLOR 0xE98FB0
			ELSE
				SETCOLOR 0x8F99E0
			ENDIF
			IF ARG:0 > 0
				PRINTFORM  %CHARATU(ステータス文字列:(LOCAL:0),0)%:%(ステータス変化値:(LOCAL:0) > 0? "↑"# "↓")%%TOSTR(ABS(ステータス変化値:(LOCAL:0))), 3, RIGHT%
			ELSE
				PRINTFORM  %(ステータス変化値:(LOCAL:0) == 0)?"　"#(ステータス変化値:(LOCAL:0) > 0? "↑"# "↓")%%TOSTR(ABS(ステータス変化値:(LOCAL:0))), 3, RIGHT%
			ENDIF
			RESETCOLOR
		ELSE
			SIF ステータス変化値:(LOCAL:0) == 0
				PRINTFORM  %CHARATU(ステータス文字列:(LOCAL:0),0)%:　%TOSTR(ABS(ステータス変化後値:(LOCAL:0))), 3, RIGHT%
		ENDIF
	NEXT
	
ENDIF
