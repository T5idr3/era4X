;-------------------------
; パフォーマンス
;-------------------------
@PERFORMANCE
#DIM MEMBER, MAX_MEMBER
#DIM SONG
#DIM VOLTAGE, 3
#DIM MAX_VOLTAGE, 3
#DIM DIFFICULTY

VARSET MEMBER, 0
SONG = 0

DRAWLINE
PRINTL メンバー選出
PRINTL パフォーマンスを行うメンバーを選択してください
DRAWLINE
CALL SELECT_MEMBER(MEMBER)
CALL SELECT_SONG(SONG)
CALL SELECT_DIFFICULTY
DIFFICULTY = RESULT
CALL PERFORMANCE_MAIN(MEMBER, SONG, DIFFICULTY, VOLTAGE, MAX_VOLTAGE)
CALL AFTER_PERFORMANCE(MEMBER, VOLTAGE, MAX_VOLTAGE, DIFFICULTY)

RETURN 1

@SELECT_MEMBER(MEMBER)
#DIM REF MEMBER
#DIM FRONT_LINE
#DIM ID

$INPUT_LOOP
FRONT_LINE = LINECOUNT
FOR ID, 1, CHARANUM
  FOR LOCAL, 0, 5
    SIF MEMBER:LOCAL == ID
      SETCOLOR 0XFF9999;
  NEXT
  PRINTBUTTON @"[{ID}]%CALLNAME:ID, 30, LEFT%", ID
  PRINTFORML : %GET_CHARA_TYPE(ID)%
  RESETCOLOR
NEXT
PRINTBUTTON "[999] 決定", 999
PRINTL
INPUT
IF RESULT < CHARANUM && RESULT > 0
  FOR ID, 0, 5
    IF MEMBER:ID == 0
      MEMBER:ID = RESULT
      BREAK;
    ELSEIF MEMBER:ID == RESULT
      MEMBER:ID = 0
      BREAK;
    ENDIF
  NEXT
ELSEIF RESULT == 999
  FOR ID, 0, 5
    SIF MEMBER:ID == 0
      BREAK
    SIF CFLAG:(MEMBER:ID):1017 == 0
      CALL SELECT_CHARACTER_TYPE(MEMBER:ID)
  NEXT
  RETURN
ENDIF
CLEARLINE LINECOUNT - FRONT_LINE
GOTO INPUT_LOOP

@SELECT_CHARACTER_TYPE(ID)
#DIM ID
#DIM FRONT_LINE
$INPUT_LOOP
FRONT_LINE = LINECOUNT
PRINTFORM %CALLNAME:ID%の方向性は
SELECTCASE CFLAG:ID:1017
  CASE 0
    PRINT 未決定
  CASE CUTE
    PRINT キュート
  CASE PASSION
    PRINT パッション
  CASE COOL
    PRINT クール
ENDSELECT
PRINTL です
PRINTL これからの方向性を決めてください
PRINTL [1] キュート
PRINTL [2] パッション
PRINTL [3] クール
INPUT
IF RESULT == 1
  CALL ASK_YN("キュートでよろしいですか？")
  IF RESULT == 0
    CLEARLINE LINECOUNT - FRONT_LINE
    GOTO INPUT_LOOP
  ENDIF
  CFLAG:ID:1017 = CUTE
ELSEIF RESULT == 2
  CALL ASK_YN("パッションでよろしいですか？")
  IF RESULT == 0
    CLEARLINE LINECOUNT - FRONT_LINE
    GOTO INPUT_LOOP
  ENDIF
  CFLAG:ID:1017 = PASSION
ELSEIF RESULT == 3
  CALL ASK_YN("クールでよろしいですか？")
  IF RESULT == 0
    CLEARLINE LINECOUNT - FRONT_LINE
    GOTO INPUT_LOOP
  ENDIF
  CFLAG:ID:1017 == COOL
ELSE
  CLEARLINE LINECOUNT - FRONT_LINE
  GOTO INPUT_LOOP
ENDIF

@SELECT_SONG(SONG)
#DIM REF SONG
DRAWLINE
PRINTL 選曲
PRINTL パフォーマンスする曲を決めてください。
DRAWLINE
LOCAL = LINECOUNT
PRINTL [0] ミックス: 平均的な評価を得られる選曲
PRINTL [1] キュート: 可愛い曲調が好きな人から評価を得られる選曲
PRINTL [2] パッション: 明るい曲調が好きな人たちから評価を得られる選曲
PRINTL [3] クール: クールな曲調が好きな人たちから評価を得られる選曲
$INPUT_LOOP
INPUT
IF RESULT == 0
  SONG = 0
ELSEIF RESULT == CUTE
  SONG = CUTE
ELSEIF RESULT == PASSION
  SONG = PASSION
ELSEIF RESULT == COOL
  SONG = COOL
ELSE
  CLEARLINE 1
  GOTO INPUT_LOOP
ENDIF
RETURN 1

@SELECT_DIFFICULTY
#DIM FRONT_LINE
#DIM DIFFICULTY

DRAWLINE
PRINTL 難易度選択
PRINTL 難易度によって客の満足度の上限が変化します。難易度が低い方がボルテージの上限が低く評価が高くなりやすいですが、報酬は低いです
DRAWLINE
PRINTL [0] EASY
PRINTL [1] NORMAL
PRINTL [2] HARD
PRINTL [3] VERY HARD
$INPUT_LOOP
FRONT_LINE = LINECOUNT
INPUT
DIFFICULTY = RESULT
IF RESULT == 0
  CALL ASK_YN("難易度 EASY でよろしいですか？")
  SIF RESULT == 0
    GOTO INPUT_LOOP
ELSEIF RESULT == 1
  CALL ASK_YN("難易度 NORMAL でよろしいですか？")
  SIF RESULT == 0
    GOTO INPUT_LOOP
ELSEIF RESULT == 2
  CALL ASK_YN("難易度 HARD でよろしいですか？")
  SIF RESULT == 0
    GOTO INPUT_LOOP
ELSEIF RESULT == 3
  CALL ASK_YN("難易度 VERY HARD でよろしいですか？")
  SIF RESULT == 0
    GOTO INPUT_LOOP
ELSE
  GOTO INPUT_LOOP
ENDIF
RETURN DIFFICULTY


@PERFORMANCE_MAIN(MEMBER, SONG, DIFFICULTY, VOLTAGE, MAX_VOLTAGE)
#DIM REF MEMBER
#DIM SONG
#DIM DIFFICULTY
#DIMS SONG_NAME
#DIM FRONT_LINE
#DIM REF VOLTAGE
#DIM CONST TOTAL_VOLTAGE = 100, 200, 300, 500
#DIM CONST MIN_VOLTAGE = 10
#DIM REF MAX_VOLTAGE
#DIM CONST BAR_SIZE = 50
#DIM TURN
#DIM CONST MAX_TURN = 10
#DIM ID

; ボルテージの最大値産出
FINDELEMENT MEMBER, 0
SELECTCASE RESULT
  CASE 1
    FOR ID, 1, 4
      IF CFLAG:(MEMBER:0):1017 == ID
        MAX_VOLTAGE:(ID - 1) = TOTAL_VOLTAGE
      ELSE
        MAX_VOLTAGE:(ID - 1) = MIN_VOLTAGE
      ENDIF
    NEXT
  CASE 2 TO 5
    FOR ID, 0, RESULT
      MAX_VOLTAGE:((CFLAG:(MEMBER:ID):1017) - 1) += TOTAL_VOLTAGE:(DIFFICULTY) / RESULT
    NEXT
    FOR ID, 0, 3
      SIF MAX_VOLTAGE:ID == 0
        MAX_VOLTAGE:ID = MIN_VOLTAGE
    NEXT
ENDSELECT

; 選曲による初期ボルテージ設定
TURN = 0
SELECTCASE SONG
  CASE 0
    SONG_NAME = "ミックス"
    VOLTAGE:0 += 5
    VOLTAGE:1 += 5
    VOLTAGE:2 += 5
  CASE 1
    SONG_NAME = "キュート"
    VOLTAGE:0 += 15
  CASE 2
    SONG_NAME = "パッション"
    VOLTAGE:1 += 15
  CASE 3
    SONG_NAME = "クール"
    VOLTAGE:2 += 15
ENDSELECT

PRINTL 
$MAIN_LOOP
TURN ++
SIF TURN > MAX_TURN
  RETURN
FRONT_LINE = LINECOUNT
PRINTFORML 選曲: %SONG_NAME%
PRINTFORM %"ターン", 12, LEFT% [
CALL PRINT_COLORBAR(TURN - 1, MAX_TURN, MAX_TURN, "■", "□", GET_COLOR(TURN - 1), 0X333333)
PRINTL ]
PRINTFORM %"キュート", 12, LEFT% [
CALL PRINT_COLORBAR(VOLTAGE:0, MAX_VOLTAGE:0, BAR_SIZE, "|", " ", GET_COLOR(VOLTAGE:0), 0X333333)
PRINTL ]
PRINTFORM %"パッション", 12, LEFT% [
CALL PRINT_COLORBAR(VOLTAGE:1, MAX_VOLTAGE:1, BAR_SIZE, "|", " ", GET_COLOR(VOLTAGE:1), 0X333333)
PRINTL ]
PRINTFORM %"クール", 12, LEFT% [
CALL PRINT_COLORBAR(VOLTAGE:2, MAX_VOLTAGE:2, BAR_SIZE, "|", " ", GET_COLOR(VOLTAGE:2), 0X333333)
PRINTL ]
DRAWLINE
FOR LOCAL, 0, 1
  TRYCALLFORM P_COMABLE_{LOCAL}(MEMBER)
  SIF RESULT == 0
    CONTINUE
  TRYCALLFORM P_COM_NAME_{LOCAL}
  PRINTBUTTON @"[{LOCAL}] %RESULTS, 16, LEFT%", LOCAL
  SIF LOCAL % 4 == 3
    PRINTL
NEXT
INPUT
ID = RESULT
TRYCALLFORM P_COM_BODY_{ID}(MEMBER, VOLTAGE)
TRYCALLFORM P_COM_MESSAGE_{ID}(MEMBER)
FOR ID, 0, 3
  VOLTAGE:ID = LIMIT(VOLTAGE:ID, 0, MAX_VOLTAGE:ID)
NEXT
CLEARLINE LINECOUNT - FRONT_LINE
GOTO MAIN_LOOP

@GET_COLOR(VOLTAGE)
#FUNCTION
#DIM VOLTAGE

IF VOLTAGE < 10
  RETURNF 0X3333FF
ELSEIF VOLTAGE < 20
  RETURNF 0X5533E0
ELSEIF VOLTAGE < 30
  RETURNF 0X6633CC
ELSEIF VOLTAGE < 50
  RETURNF 0X993399
ELSEIF VOLTAGE < 75
  RETURNF 0XCC3366
ELSEIF VOLTAGE < 83
  RETURNF 0XE03355
ELSEIF VOLTAGE <= 100
  RETURNF 0XFF3333
ENDIF

@GET_CHARA_TYPE(ID)
#FUNCTIONS
#DIM ID

SELECTCASE CFLAG:ID:1017
  CASE CUTE
    RETURNF "キュート"
  CASE PASSION
    RETURNF "パッション"
  CASE COOL
    RETURNF "クール"
  CASEELSE
    RETURNF "方針未決定"
ENDSELECT

@AFTER_PERFORMANCE(MEMBER, VOLTAGE, MAX_VOLTAGE, DIFFICULTY)
#DIM REF MEMBER
#DIM REF VOLTAGE
#DIM REF MAX_VOLTAGE
#DIM DIFFICULTY
#DIM SCORE, 3
#DIM TOTAL_SCORE
#DIM ID
#DIM CONST SALE = 123, 1234, 12345, 123456

DRAWLINE
PRINTL RESULT
DRAWLINE
SCORE:0 = ((VOLTAGE:0 * 100) / MAX_VOLTAGE:0)
SCORE:1 = ((VOLTAGE:1 * 100) / MAX_VOLTAGE:1)
SCORE:2 = ((VOLTAGE:2 * 100) / MAX_VOLTAGE:2)
SIF SCORE:0 == 100
  SCORE:0 += 50
SIF SCORE:1 == 100
  SCORE:1 += 50
SIF SCORE:2 == 100
  SCORE:2 += 50
TOTAL_SCORE = (SCORE:0 + SCORE:1 + SCORE:2) / 3
PRINTFORM CUTE SCORE      {SCORE:0}
IF SCORE:0 == 100
  PRINTFORML 点 + MAX BONUS (+50) {SCORE:0 + 50} 点
ELSE
  PRINTL 点
ENDIF
PRINTFORM PASSION SCORE   {SCORE:1}
IF SCORE:1 == 100
  PRINTFORML 点 + MAX BONUS (+50) {SCORE:1 + 50} 点
ELSE
  PRINTL 点
ENDIF
PRINTFORM COOL SCORE      {SCORE:2}
IF SCORE:2 == 100
  PRINTFORML 点 + MAX BONUS (+50) {SCORE:2 + 50} 点
ELSE
  PRINTL 点
ENDIF
DRAWLINE
PRINTFORML TOTAL SCORE    {TOTAL_SCORE} 点
PRINTW 

FINDELEMENT MEMBER, 0
FOR ID, 0, RESULT
  PRINTFORML %CALLNAME:(MEMBER:ID), 20, LEFT% ステージ経験値 + {TOTAL_SCORE - 60}
  PRINTFORML %CALLNAME:(MEMBER:ID), 20, LEFT% ファン追加 + {TOTAL_SCORE - 60}人
  CFLAG:(MEMBER:ID):1018 = LIMIT(CFLAG:(MEMBER:ID):1018 + (TOTAL_SCORE - 60), 0, 999999999)
  CFLAG:(MEMBER:ID):1002 = LIMIT(CFLAG:(MEMBER:ID):1002 + (TOTAL_SCORE - 60), 0, 999999999)
  CFLAG:(MEMBER:ID):1003 = LIMIT(CFLAG:(MEMBER:ID):1003 + (TOTAL_SCORE - 60), 0, 999999999)
NEXT
LOCAL = LIMIT((TOTAL_SCORE - 60) * SALE:DIFFICULTY, 0, 1000000000)
IF LOCAL == 0
  LOCALS = 0
ELSE
  LOCALS = %TOSTR(LOCAL, "$###,###,###,###")%
ENDIF
PRINTFORMW 売り上げ %LOCALS%
MONEY += LOCAL