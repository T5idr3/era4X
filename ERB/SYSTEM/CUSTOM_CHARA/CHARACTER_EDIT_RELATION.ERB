;-------------------------
;血縁設定
;-------------------------
@CHARACTER_EDIT_RELATION_SETTING(対象)
#DIM 対象
#DIM FIRST_LINE

FIRST_LINE = LINECOUNT

CHARACTER_EDIT_SELECT_CHARA = 対象

SIF CFLAG:CHARACTER_EDIT_SELECT_CHARA:父親 > 0 && CHARACTER_EDIT_FATHER_CANDIDATE == 0
CHARACTER_EDIT_FATHER_CANDIDATE = CFLAG:CHARACTER_EDIT_SELECT_CHARA:父親 + 99

SIF CFLAG:CHARACTER_EDIT_SELECT_CHARA:母親 > 0 && CHARACTER_EDIT_MOTHER_CANDIDATE == 0
CHARACTER_EDIT_MOTHER_CANDIDATE = CFLAG:CHARACTER_EDIT_SELECT_CHARA:母親 + 99

CALL SINGLE_DRAWLINE
PRINTBUTTON "[父親の設定]", 0
IF CHARACTER_EDIT_FATHER_CANDIDATE > 99
	PRINTFORML  現在:%ANAME(ID_TO_CHARA((CHARACTER_EDIT_FATHER_CANDIDATE) - 99))%
ELSE
	PRINTFORML  現在:なし
ENDIF
PRINTBUTTON "[母親の設定]", 1
IF CHARACTER_EDIT_MOTHER_CANDIDATE > 99
	PRINTFORML  現在:%ANAME(ID_TO_CHARA((CHARACTER_EDIT_MOTHER_CANDIDATE) - 99))%
ELSE
	PRINTFORML  現在:なし
ENDIF

PRINTBUTTON "[きょうだいの設定]", 2
IF CHARACTER_EDIT_BROTHER_CANDIDATE_NUM > 0
	PRINTFORM 現在:
	FOR LOCAL, 0, CHARACTER_EDIT_BROTHER_CANDIDATE_NUM
		SIF CHARACTER_EDIT_BROTHER_CANDIDATE:LOCAL > 0
			PRINTFORM %ANAME(ID_TO_CHARA((CHARACTER_EDIT_BROTHER_CANDIDATE:LOCAL) - 99))% 
	NEXT
	PRINTFORML
ELSE
	PRINTFORML 現在:なし
ENDIF

PRINTBUTTON "[息子・娘の設定]", 3
IF CHARACTER_EDIT_CHILD_CANDIDATE_NUM > 0
	PRINTFORM 現在:
	FOR LOCAL, 0, CHARACTER_EDIT_CHILD_CANDIDATE_NUM
		SIF CHARACTER_EDIT_CHILD_CANDIDATE:LOCAL > 0
			PRINTFORM %ANAME(ID_TO_CHARA((CHARACTER_EDIT_CHILD_CANDIDATE:LOCAL) - 99))% 
	NEXT
	PRINTFORML
ELSE
	PRINTFORML 現在:なし
ENDIF

PRINTL
PRINTBUTTON "[設定して戻る]", 999
PRINTL
PRINTBUTTON "[キャンセル]", 9999

INPUT

IF RESULT == 0
	CALL SELECT_CHARA_LIST_ONLY_LOGIC_SLG("CHARACTER_EDIT_FATHER", "NONE")
	IF RESULT != -1
		CHARACTER_EDIT_FATHER_CANDIDATE = GET_ID(RESULT) + 99
		;排他処理
		SIF CHARACTER_EDIT_FATHER_CANDIDATE == CHARACTER_EDIT_MOTHER_CANDIDATE && !HAS_VAGINA((CHARACTER_EDIT_FATHER_CANDIDATE) - 100)
			CHARACTER_EDIT_MOTHER_CANDIDATE = 1

		LOCAL = FINDELEMENT(CHARACTER_EDIT_BROTHER_CANDIDATE, CHARACTER_EDIT_FATHER_CANDIDATE, 0, CHARACTER_EDIT_BROTHER_CANDIDATE_NUM)
		IF LOCAL != -1
			ARRAYSHIFT CHARACTER_EDIT_BROTHER_CANDIDATE, -1, 0, LOCAL
			CHARACTER_EDIT_BROTHER_CANDIDATE_NUM --
		ENDIF

		LOCAL = FINDELEMENT(CHARACTER_EDIT_CHILD_CANDIDATE, CHARACTER_EDIT_FATHER_CANDIDATE, 0, CHARACTER_EDIT_CHILD_CANDIDATE_NUM)
		IF LOCAL != -1
			ARRAYSHIFT CHARACTER_EDIT_CHILD_CANDIDATE, -1, 0, LOCAL
			CHARACTER_EDIT_CHILD_CANDIDATE_NUM --
		ENDIF
	ELSE
		CHARACTER_EDIT_FATHER_CANDIDATE = 1
	ENDIF
ELSEIF RESULT == 1
	CALL SELECT_CHARA_LIST_ONLY_LOGIC_SLG("CHARACTER_EDIT_MOTHER", "NONE")
	IF RESULT != -1
		CHARACTER_EDIT_MOTHER_CANDIDATE = GET_ID(RESULT) + 99
		;排他処理
		SIF CHARACTER_EDIT_MOTHER_CANDIDATE == CHARACTER_EDIT_FATHER_CANDIDATE && !HAS_PENIS((CHARACTER_EDIT_FATHER_CANDIDATE) - 100)
			CHARACTER_EDIT_FATHER_CANDIDATE = 1

		LOCAL = FINDELEMENT(CHARACTER_EDIT_BROTHER_CANDIDATE, CHARACTER_EDIT_MOTHER_CANDIDATE, 0, CHARACTER_EDIT_BROTHER_CANDIDATE_NUM)
		IF LOCAL != -1
			ARRAYSHIFT CHARACTER_EDIT_BROTHER_CANDIDATE, -1, 0, LOCAL
			CHARACTER_EDIT_BROTHER_CANDIDATE_NUM --
		ENDIF

		LOCAL = FINDELEMENT(CHARACTER_EDIT_CHILD_CANDIDATE, CHARACTER_EDIT_MOTHER_CANDIDATE, 0, CHARACTER_EDIT_CHILD_CANDIDATE_NUM)
		IF LOCAL != -1
			ARRAYSHIFT CHARACTER_EDIT_CHILD_CANDIDATE, -1, 0, LOCAL
			CHARACTER_EDIT_CHILD_CANDIDATE_NUM --
		ENDIF
	ELSE
		CHARACTER_EDIT_MOTHER_CANDIDATE = 1
	ENDIF
ELSEIF RESULT == 2
	CALL SELECT_CHARA_LIST_MULTI_ONLY_LOGIC_SLG(CHARACTER_EDIT_CANDIDATE_MAX, "CHARACTER_EDIT_BROTHER", "NONE")
	FOR LOCAL, 0, SELECTED_CHARA_NUM
		CHARACTER_EDIT_BROTHER_CANDIDATE:LOCAL = GET_ID(SELECTED_CHARA:LOCAL) + 99
	NEXT
	CHARACTER_EDIT_BROTHER_CANDIDATE_NUM = SELECTED_CHARA_NUM

	;排他処理
	SIF FINDELEMENT(CHARACTER_EDIT_BROTHER_CANDIDATE, CHARACTER_EDIT_FATHER_CANDIDATE, 0, CHARACTER_EDIT_BROTHER_CANDIDATE_NUM) != -1
		CHARACTER_EDIT_FATHER_CANDIDATE = 1

	SIF FINDELEMENT(CHARACTER_EDIT_BROTHER_CANDIDATE, CHARACTER_EDIT_MOTHER_CANDIDATE, 0, CHARACTER_EDIT_BROTHER_CANDIDATE_NUM) != -1
		CHARACTER_EDIT_MOTHER_CANDIDATE = 1

	FOR LOCAL, 0, CHARACTER_EDIT_BROTHER_CANDIDATE_NUM
		LOCAL:1 = CHARACTER_EDIT_BROTHER_CANDIDATE:LOCAL
		LOCAL:2 = FINDELEMENT(CHARACTER_EDIT_CHILD_CANDIDATE, LOCAL:1, 0, CHARACTER_EDIT_CHILD_CANDIDATE_NUM)

		IF LOCAL:2 != -1
			ARRAYSHIFT CHARACTER_EDIT_CHILD_CANDIDATE, -1, 0, LOCAL:2
			CHARACTER_EDIT_CHILD_CANDIDATE_NUM --
		ENDIF
	NEXT

ELSEIF RESULT == 3
	CALL SELECT_CHARA_LIST_MULTI_ONLY_LOGIC_SLG(CHARACTER_EDIT_CANDIDATE_MAX, "CHARACTER_EDIT_CHILD", "NONE")
	FOR LOCAL, 0, SELECTED_CHARA_NUM
		LOCAL:1 = SELECTED_CHARA:LOCAL
		CHARACTER_EDIT_CHILD_CANDIDATE:LOCAL = GET_ID(LOCAL:1) + 99
	NEXT
	CHARACTER_EDIT_CHILD_CANDIDATE_NUM = SELECTED_CHARA_NUM

	;排他処理
	SIF FINDELEMENT(CHARACTER_EDIT_CHILD_CANDIDATE, CHARACTER_EDIT_FATHER_CANDIDATE, 0, CHARACTER_EDIT_CHILD_CANDIDATE_NUM) != -1
		CHARACTER_EDIT_FATHER_CANDIDATE = 1

	SIF FINDELEMENT(CHARACTER_EDIT_CHILD_CANDIDATE, CHARACTER_EDIT_MOTHER_CANDIDATE, 0, CHARACTER_EDIT_CHILD_CANDIDATE_NUM) != -1
		CHARACTER_EDIT_MOTHER_CANDIDATE = 1

	FOR LOCAL, 0, CHARACTER_EDIT_CHILD_CANDIDATE_NUM
		LOCAL:1 = CHARACTER_EDIT_CHILD_CANDIDATE:LOCAL
		LOCAL:2 = FINDELEMENT(CHARACTER_EDIT_BROTHER_CANDIDATE, LOCAL:1, 0, CHARACTER_EDIT_BROTHER_CANDIDATE_NUM)

		IF LOCAL:2 != -1
			ARRAYSHIFT CHARACTER_EDIT_BROTHER_CANDIDATE, -1, 0, LOCAL:2
			CHARACTER_EDIT_BROTHER_CANDIDATE_NUM --
		ENDIF
	NEXT

ELSEIF RESULT == 999 || 9999
	SIF RESULT == 999
		CALL CHARACTER_EDIT_RELATION_CONFIRM
	CHARACTER_EDIT_FATHER_CANDIDATE = 0
	CHARACTER_EDIT_MOTHER_CANDIDATE = 0
	CHARACTER_EDIT_BROTHER_CANDIDATE_NUM = 0
	CHARACTER_EDIT_CHILD_CANDIDATE_NUM = 0
	RETURN 0
ENDIF

CLEARLINE LINECOUNT - FIRST_LINE
RESTART


@SELECT_CHARA_LIST_SHOW_LOGIC_CHARACTER_EDIT_FATHER(対象)
#DIM 対象
RETURN HAS_PENIS(対象) && 対象 != CHARACTER_EDIT_SELECT_CHARA

@SELECT_CHARA_LIST_SHOW_LOGIC_CHARACTER_EDIT_MOTHER(対象)
#DIM 対象
RETURN HAS_VAGINA(対象) && 対象 != CHARACTER_EDIT_SELECT_CHARA

@SELECT_CHARA_LIST_SHOW_LOGIC_CHARACTER_EDIT_BROTHER(対象)
#DIM 対象
RETURN 対象 != CHARACTER_EDIT_SELECT_CHARA

@SELECT_CHARA_LIST_SHOW_LOGIC_CHARACTER_EDIT_CHILD(対象)
#DIM 対象
RETURN 対象 != CHARACTER_EDIT_SELECT_CHARA

@CHARACTER_EDIT_RELATION_CONFIRM(対象)
#DIM 対象
#DIM 候補, MAX_CHARA_NUM
#DIM 候補数
#DIM 父候補
#DIM 母候補
VARSET 候補, -1
VARSET 候補数, 0

IF CHARACTER_EDIT_FATHER_CANDIDATE > 99
	父候補 = ID_TO_CHARA((CHARACTER_EDIT_FATHER_CANDIDATE) - 99)
	CFLAG:CHARACTER_EDIT_SELECT_CHARA:父親 = GET_ID(父候補)
ELSEIF CHARACTER_EDIT_FATHER_CANDIDATE > 0
	父候補 = 0
	CFLAG:CHARACTER_EDIT_SELECT_CHARA:父親 = 0
ENDIF


IF CHARACTER_EDIT_MOTHER_CANDIDATE > 99
	母候補 = ID_TO_CHARA((CHARACTER_EDIT_MOTHER_CANDIDATE) - 99)
	CFLAG:CHARACTER_EDIT_SELECT_CHARA:母親 = GET_ID(母候補)
ELSEIF CHARACTER_EDIT_MOTHER_CANDIDATE >= 0
	母候補 = 0
	CFLAG:CHARACTER_EDIT_SELECT_CHARA:母親 = 0
ENDIF

IF 父候補 > 0
	CALL VIRGINIZE_LOST(父候補, 初体験_童貞, 母候補 > 0 ? ANAME(母候補) + "の膣" # "不明")
	IF CFLAG:CHARACTER_EDIT_SELECT_CHARA:父親 != 父候補
		EXP:父候補:Ｖ挿入経験 += 5
	ENDIF
ENDIF

IF 母候補 > 0
	CALL VIRGINIZE_LOST(母候補, 初体験_処女, 父候補 > 0 ? ANAME(父候補) # "不明")
	IF CFLAG:CHARACTER_EDIT_SELECT_CHARA:母親 != 母候補
		EXP:母候補:Ｖ性交経験 += 5
		EXP:母候補:膣内射精経験 += 5
		EXP:母候補:出産経験 += 1
	ENDIF
ENDIF

IF CHARACTER_EDIT_BROTHER_CANDIDATE_NUM > 0
	;きょうだい候補のきょうだいも、きょうだいとして追加する
	FOR LOCAL, 0, CHARACTER_EDIT_BROTHER_CANDIDATE_NUM
		LOCAL:1 = ID_TO_CHARA((CHARACTER_EDIT_BROTHER_CANDIDATE:LOCAL) - 99)
		IF LOCAL:1 >= 0
			候補:候補数 = LOCAL:1
			候補数 ++
			FOR LOCAL:2, 0, CHARANUM
				IF IS_PURE_BROTHER(LOCAL:1, LOCAL:2) && FINDELEMENT(候補, LOCAL:2, 0, 候補数) == -1
					候補:候補数 = LOCAL:2
					候補数 ++
				ENDIF
			NEXT
		ENDIF
	NEXT

	IF 父候補 < 0 && 母候補 < 0
		FOR LOCAL, 0, SPERM_MAX_ADDRESS
			IF SPERM_NAME_ARRAY:LOCAL == ""
				LOCAL:2 = LOCAL
				LOCAL --
				BREAK
			ENDIF
		NEXT
		SIF LOCAL == SPERM_MAX_ADDRESS
			THROW SPERM_NAME_ARRAYがいっぱいです

		SPERM_NAME_ARRAY:(LOCAL:2) = 不明
		BROTHER_SPERM_FLAG:(LOCAL:2) = 1
	ENDIF

	FOR LOCAL, 0, 候補数
		CFLAG:(候補:LOCAL):父親 = 父候補 >= 0 ? GET_ID(父候補) # LOCAL:2 * -1
		CFLAG:(候補:LOCAL):母親 = 母候補 >= 0 ? GET_ID(母候補) # LOCAL:2 * -1
	NEXT
	CFLAG:対象:父親 = 父候補 >= 0 ? GET_ID(父候補) # LOCAL:2 * -1
	CFLAG:対象:母親 = 母候補 >= 0 ? GET_ID(母候補) # LOCAL:2 * -1
	VARSET 候補
	VARSET 候補数
ENDIF

IF CHARACTER_EDIT_CHILD_CANDIDATE_NUM > 0
	;子供候補の子供も、子供候補として追加する
	FOR LOCAL, 0, CHARACTER_EDIT_CHILD_CANDIDATE_NUM
		LOCAL:1 = ID_TO_CHARA((CHARACTER_EDIT_CHILD_CANDIDATE:LOCAL) - 99)
		IF LOCAL:1 >= 0
			候補:候補数 = LOCAL:1
			候補数 ++
			FOR LOCAL:2, 0, CHARANUM
				IF IS_PURE_BROTHER(LOCAL:1, LOCAL:2) && FINDELEMENT(候補, LOCAL:2, 0, 候補数)
					候補:候補数 = LOCAL:2
					候補数 ++
				ENDIF
			NEXT
		ENDIF
	NEXT

	FOR LOCAL, 0, SPERM_MAX_ADDRESS
		IF SPERM_NAME_ARRAY:LOCAL == ""
			LOCAL:2 = LOCAL
			LOCAL --
			BREAK
		ENDIF
	NEXT
	SIF LOCAL == SPERM_MAX_ADDRESS
		THROW SPERM_NAME_ARRAYがいっぱいです

	SPERM_NAME_ARRAY:(LOCAL:2) = 不明
	BROTHER_SPERM_FLAG:(LOCAL:2) = 1

	FOR LOCAL, 0, 候補数
		IF IS_MALE(CHARACTER_EDIT_SELECT_CHARA)
			CFLAG:(候補:LOCAL):父親 = GET_ID(CHARACTER_EDIT_SELECT_CHARA)
			CFLAG:(候補:LOCAL):母親 = LOCAL:2 * -1
			CALL VIRGINIZE_LOST(CHARACTER_EDIT_SELECT_CHARA, 初体験_童貞, "不明")
			EXP:CHARACTER_EDIT_SELECT_CHARA:Ｖ挿入経験 += 5

		ELSE
			CFLAG:(候補:LOCAL):父親 = LOCAL:2 * -1
			CFLAG:(候補:LOCAL):母親 = GET_ID(CHARACTER_EDIT_SELECT_CHARA)
			CALL VIRGINIZE_LOST(CHARACTER_EDIT_SELECT_CHARA, 初体験_処女, "不明")
			EXP:CHARACTER_EDIT_SELECT_CHARA:Ｖ性交経験 += 5
			EXP:CHARACTER_EDIT_SELECT_CHARA:膣内射精経験 += 5
			EXP:CHARACTER_EDIT_SELECT_CHARA:出産経験 += 1
		ENDIF
	NEXT
	VARSET 候補
	VARSET 候補数
ENDIF
