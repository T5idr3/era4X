@CHARACTER_EDIT_UI_OTHER(対象)
#DIM 対象

IF FLAG:周回数 > 0
	PRINTFORM 所属:
	IF CFLAG:対象:所属 == 0
		IF CFLAG:対象:特殊状態 == 特殊状態_死亡
			PRINTFORM 無所属(死亡)          
		ELSEIF CFLAG:対象:特殊状態 == 特殊状態_放浪
			PRINTFORM 無所属(放浪)          
		ELSE
			PRINTFORM 無所属(未登場)        
		ENDIF
	ELSE
		PRINTFORM %ANAME(GET_COUNTRY_BOSS(CFLAG:対象:所属)), MAX_CHARANAME_LENGTH + 4, LEFT%
	ENDIF
	IF GET_COUNTRY_BOSS(CFLAG:対象:所属) == 対象
		SETCOLOR カラー_選択不可
		PRINTPLAIN ※君主は変更不可※
		RESETCOLOR
	ELSE
		CALL PRINTBUTTON_EX("[変更] ", 0)
		CALL PRINTBUTTON_EX("[一覧] ", 1)
	ENDIF
	PRINTL

	PRINTFORM 捕虜:
	IF CFLAG:対象:捕虜先 == 0
		PRINTFORM %"なし", MAX_CHARANAME_LENGTH + 4, LEFT%
	ELSE
		PRINTFORM %ANAME(GET_COUNTRY_BOSS(CFLAG:対象:捕虜先)), MAX_CHARANAME_LENGTH + 4, LEFT%
	ENDIF
	IF GET_COUNTRY_BOSS(CFLAG:対象:所属) == 対象
		SETCOLOR カラー_選択不可
		PRINTPLAIN ※君主は変更不可※
		RESETCOLOR
	ELSE
		CALL PRINTBUTTON_EX("[変更] ", 10)
		CALL PRINTBUTTON_EX("[一覧] ", 11)
	ENDIF
	PRINTL
	CALL COLOR_LINE
	PRINTBUTTON "[クールタイム]:", 109
	IF COOLTIME:対象:0 > 0
		PRINTFORM 残り{COOLTIME:対象:0}期
	ELSE
		PRINTFORM なし
	ENDIF
	PRINTL

	CALL COLOR_LINE
ENDIF
;特殊処理
PRINTBUTTON "[      性別]:", 100
PRINTFORML %TOSTR_SEX(TALENT:対象:GETNUM(TALENT, "性別"))%
PRINTBUTTON "[      陰毛]:", 105
PRINTFORML %GET_INMOU_NAME(TALENT:対象:陰毛現在値)%
PRINTBUTTON "[      腋毛]:", 106
PRINTFORML %GET_ARMPIT_NAME(TALENT:対象:腋毛現在値)%
PRINTBUTTON "[  タトゥー]:", 107
CALL PRINT_TATTOO(対象)
PRINTL
PRINTBUTTON "[    ピアス]:", 108
CALL PRINT_PIERCE(対象)
PRINTL
CALL COLOR_LINE
PRINTFORM タグ:
FOR LOCAL, 0, VARSIZE("TAG_NAME")
	CALL PRINT_TOGGLE_BUTTON(@"[%TAG_NAME:LOCAL%]", 1000 + LOCAL, HAS_TAG(対象, LOCAL))
NEXT
PRINTL
CALL COLOR_LINE

CALL SHOW_BODYSIZE(対象)
SIF HAS_VAGINA(対象)
	PRINTFORM 乳房:%TOSTR_BUSTSIZE(GET_BUSTSIZE(対象)), 10, LEFT% 
SIF HAS_PENIS(対象)
	PRINTFORM 陰茎:%TOSTR_PENISSIZE(GET_PENISSIZE(対象)), 10, LEFT%
PRINTFORML   尻:%TOSTR_HIPSIZE(GET_HIPSIZE(対象)), 10, LEFT%

IF HAS_VAGINA(対象)
	PRINTFORM 膣締:%TOSTR_TIGHTNESS(TALENT:(対象):Ｖ締まり), 10, LEFT% 
ENDIF
PRINTFORM 尻締:%TOSTR_TIGHTNESS(TALENT:(対象):Ａ締まり), 10, LEFT% 
PRINTL
PRINT 髪色:
CALL PRINT_HAIR_COLOR(TALENT:対象:髪色) 
PRINTFORML 髪長:%TOSTR_HAIR_LENGTH(TALENT:対象:髪の長さ)%
PRINTBUTTON "[ボディ設定]", 101
IF 対象 != MASTER
	PRINTL
	CALL COLOR_LINE
	PRINTBUTTON "[全ウフフコマンドの実行回数を100に]", 102
	PRINTL
	PRINTBUTTON "[全ウフフコマンドの実行回数を0に]", 103
	PRINTL
ENDIF
PRINTBUTTON "[胃・子宮・腸内精液のクリア]", 104


@CHARACTER_EDIT_INPUT_OTHER(対象, 入力値)
#DIM 対象
#DIM 入力値

SELECTCASE RESULT
	;性別
	CASE 100
		LOCAL:1 = HAS_PENIS(対象)
		LOCAL:2 = IS_MALE(対象)
		TALENT:対象:性別 = ROUND_INCREMENT(TALENT:対象:性別, 0, 5)
		SIF LOCAL:2 != IS_MALE(対象)
			CALL SET_BODYSIZE(対象)
		SIF LOCAL:1 == 0 && HAS_PENIS(対象)
			CALL SET_PENISSIZE(対象)
	;胸
	CASE 101
		CALL BEAUTIFY(対象)
	;性癖
	CASE 102
		PRINTL 全てのコマンドの実行回数を100にします
		PRINTL 既に100を超えているものについては現在の値が維持されます
		PRINTL 主に性癖設定での好みLvを自由に変更するために使います
		CALL ASK_YN("実行", "キャンセル")
		IF RESULT == 0
			FOR LOCAL:0, 0, 900
				COM_EXP:対象:(LOCAL:0) = MAX(COM_EXP:対象:(LOCAL:0), 1000)
				COM_EXP:対象:(LOCAL:0 + 1000) = MAX(COM_EXP:対象:(LOCAL:0), 1000)
				COM_EXP:対象:(LOCAL:0 + 2000) = MAX(COM_EXP:対象:(LOCAL:0), 1000)
			NEXT
		ENDIF
	CASE 103
		PRINTL 全てのコマンドの実行回数を0にします
		PRINTL 主に性癖設定での好みLvを育て直すために使います
		CALL ASK_YN("実行", "キャンセル")
		IF RESULT == 0
			FOR LOCAL:0, 0, 900
				COM_EXP:対象:(LOCAL:0) = 0
				COM_EXP:対象:(LOCAL:0 + 1000) = 0
				COM_EXP:対象:(LOCAL:0 + 2000) = 0
			NEXT
		ENDIF
	CASE 104
		PRINTW 子宮・胃・腸の精液をクリアしました
		CALL CLEAR_CREAMPIE_HISTORY(対象, 1)
	;陰毛
	CASE 105
		CALL ASK_MULTI("パイパン", "つるつる", "うっすら", "柔毛", "ふんわり", "標準", "多め", "ふさふさ", "もっさり", "剛毛", "尻まで", "♥")
		TALENT:対象:陰毛現在値 = RESULT -1
		TALENT:対象:陰毛目標値 = RESULT - 1
	;腋毛
	CASE 106
		CALL ASK_MULTI("脱毛", "つるつる", "手入れ済み", "うっすら", "ふんわり", "普通", "無精", "ふさふさ", "もっさり", "剛毛", "密林")
		TALENT:対象:腋毛現在値 = RESULT -1
		TALENT:対象:腋毛目標値 = RESULT - 1
	CASE 107
		CALL TATTOO_MENU(対象, 1)
	CASE 108
		CALL PIERCE_MENU(対象, 1)
	CASE 109
		PRINTFORML クールタイムをいくらにしますか？(負の数で現状維持)
		INPUT
		IF RESULT < 0
		ELSE
			COOLTIME:対象:0 = RESULT
		ENDIF
	CASE 1000 TO 1000 + VARSIZE("TAG_NAME")
		CALL INVERT_TAG(対象, 入力値 - 1000)
ENDSELECT

IF FLAG:周回数 > 0
	SELECTCASE 入力値
		CASE 0
			SIF GET_COUNTRY_BOSS(CFLAG:対象:所属) == 対象
				RETURN
			IF CFLAG:対象:所属 == 0 && CFLAG:対象:特殊状態 == 0 && !CFLAG:対象:捕虜先
				CFLAG:対象:特殊状態 = 特殊状態_放浪
			ELSEIF CFLAG:対象:所属 == 0 && CFLAG:対象:特殊状態 == 特殊状態_放浪
				CFLAG:対象:特殊状態 = 特殊状態_死亡
			ELSE
				LOCAL:1 = 0
				FOR LOCAL, CFLAG:対象:所属 + 1, MAX_COUNTRY
					SIF !IS_COUNTRY(LOCAL)
						CONTINUE
					CALL CHANGE_COUNTRY(対象, LOCAL)
					LOCAL:1 = 1
					BREAK
				NEXT
				IF LOCAL:1 == 0
					CALL CHANGE_COUNTRY(対象, 0)
					CFLAG:対象:特殊状態 = 0
				ENDIF
			ENDIF
		CASE 1
			SIF GET_COUNTRY_BOSS(CFLAG:対象:所属) == 対象
				RETURN
			FOR LOCAL, 0, MAX_COUNTRY
				SIF !IS_COUNTRY(LOCAL)
					CONTINUE
				PRINTFORML [{LOCAL, 2, RIGHT}] %ANAME(GET_COUNTRY_BOSS(LOCAL))%
			NEXT
			PRINTFORML [{MAX_COUNTRY}] 無所属(未登場)
			PRINTFORML [{MAX_COUNTRY + 1}] 無所属(放浪)
			PRINTFORML [{MAX_COUNTRY + 2}] 無所属(死亡)
			INPUT
			IF INRANGE(RESULT, 0, MAX_COUNTRY - 1)
				CALL CHANGE_COUNTRY(対象, RESULT, 0)
			ELSEIF RESULT == MAX_COUNTRY
				CALL CHANGE_COUNTRY(対象, 0, 1)
				CFLAG:対象:特殊状態 = 0
			ELSEIF RESULT == MAX_COUNTRY + 1
				CALL CHANGE_COUNTRY(対象, 0, 1)
			ELSEIF RESULT == MAX_COUNTRY + 2
				CALL CHANGE_COUNTRY(対象, 0, 1, 1)
			ENDIF
		CASE 10
			SIF GET_COUNTRY_BOSS(CFLAG:対象:所属) == 対象
				RETURN
			LOCAL:1 = 0
			FOR LOCAL:0, CFLAG:対象:捕虜先 + 1, MAX_COUNTRY
				IF IS_COUNTRY(LOCAL)
					CALL CAPTURE(対象, LOCAL)
					LOCAL:1 = 1
					BREAK
				ENDIF
			NEXT
			SIF LOCAL:1 == 0
				CALL CAPTURE(対象, 0)
		CASE 11
			SIF GET_COUNTRY_BOSS(CFLAG:対象:所属) == 対象
				RETURN
			FOR LOCAL, 0, MAX_COUNTRY
				SIF !IS_COUNTRY(LOCAL)
					CONTINUE
				PRINTFORML [{LOCAL, 2, RIGHT}] %ANAME(GET_COUNTRY_BOSS(LOCAL))%
			NEXT
			PRINTFORML [{MAX_COUNTRY}] なし
			INPUT
			IF INRANGE(RESULT, 0, MAX_COUNTRY - 1)
				CALL CAPTURE(対象, RESULT)
			ELSEIF RESULT == MAX_COUNTRY
				CALL CAPTURE(対象, 0)
			ENDIF      
	ENDSELECT
ENDIF
