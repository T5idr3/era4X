;コンフィグ画面とゲーム設定に関する処理

;-------------------------------------------------
;コンフィグの初期設定
;-------------------------------------------------
@INIT_CONFIG
CONFIG:3 = 50
CONFIG:4 = 30
CONFIG:6 = 1
CONFIG:9 = 1
CONFIG:80 = 2
CONFIG:15 = 1

;SLG部分のコンフィグの初期設定
CALL INIT_CONFIG_SLG
;デイリー部分のコンフィグの初期設定
CALL INIT_CONFIG_DAILY
;-------------------------------------------------
;コンフィグ画面の処理
;-------------------------------------------------
@SHOW_CONFIG
#DIM FIRST_LINE
FIRST_LINE = LINECOUNT
CALL SINGLE_DRAWLINE
PRINTL ★★各種設定やデータの操作を行います★★
CALL SINGLE_DRAWLINE
PRINTBUTTON "  0[ＳＬＧパートに関する設定]", 0
PRINTL 
PRINTBUTTON "  1[  交流パートに関する設定]", 1
PRINTL 
PRINTBUTTON "  5[  特殊な性癖に関する設定]", 5
PRINTL 
PRINTBUTTON "  6[  デイリーイベントの設定]", 6
PRINTL 
PRINTBUTTON "  7[    口上デイリーフィルタ]", 7
PRINTL
PRINTBUTTON "  8[      既存のキャラの保存]", 8
PRINTL
PRINTBUTTON " 10[        コマンドフィルタ]", 10
PRINTL 
PRINTL 
PRINTBUTTON " 80[        現在の設定を保存]", 80
PRINTL 
PRINTBUTTON " 81[  保存した設定の読み込み]", 81
PRINTL 
PRINTL 
PRINTBUTTON " 82[        現在の設定を保存(ＳＬＧパートに関する設定以外)]", 82
PRINTL 
PRINTBUTTON " 83[  保存した設定の読み込み(ＳＬＧパートに関する設定以外)]", 83
PRINTL 
PRINTBUTTON " 84[    デイリーの設定を保存]", 84
PRINTL 
PRINTBUTTON " 85[  デイリーの設定をロード]", 85
PRINTL 
CALL COLOR_LINE
PRINTBUTTON " 90[    ヘルプ]", 90
PRINTL 
[IF_DEBUG]
PRINTBUTTON @" {GAMEBASE_VERSION}[  デバッグ]", GAMEBASE_VERSION
PRINTL
[ENDIF]
;PRINTBUTTON " 85[データ操作]", 85
;PRINTL 
CALL SINGLE_DRAWLINE
PRINTBUTTON " 99[      戻る]", 99
PRINTL 
$INPUT_LOOP
INPUT

IF RESULT == 99
	RETURN
ELSEIF RESULT == 0
	;→(SLG\CONFIG_SLG.ERB)
	CALL CONFIG_SLG
ELSEIF RESULT == 1
	CALL CONFIG_OTHERS
ELSEIF RESULT == 5
	;特殊設定
	CALL CONFIG_OTHERS_SP
ELSEIF RESULT == 6
	;イベント設定
	CALL CONFIG_EVENT_DAILY
ELSEIF RESULT == 7
	CALL CONFIG_KOJO_DAILY
ELSEIF RESULT == 8
	CALL CONFIG_SAVE_EXIST_CHARA
ELSEIF RESULT == 10
	CALL CONFIG_COM_FILTER
ELSEIF GROUPMATCH(RESULT, 80, 82)
	CALL CONFIG_SAVE(RESULT == 80)
ELSEIF GROUPMATCH(RESULT, 81, 83)
	CALL CONFIG_LOAD(RESULT == 81)
ELSEIF RESULT == 84
	CALL CONFIG_DAILY_SAVE()
ELSEIF RESULT == 85
	CALL CONFIG_DAILY_LOAD()
ELSEIF RESULT == 90
	CALL CONFIG_HELPMENU
	[IF_DEBUG]
ELSEIF RESULT == GAMEBASE_VERSION
	CALL CONFIG_DATA_EDIT
	[ENDIF]
ENDIF
CLEARLINE LINECOUNT - FIRST_LINE
RESTART

;-------------------------------------------------
;desc  :コンフィグの書き込み
;param :全て:全コンフィグを保存する（指定しない場合SLGを除く)
;-------------------------------------------------
@CONFIG_SAVE(全て)
#DIM 全て
#DIM 番号
CALL SINGLE_DRAWLINE
PRINTFORML どこにセーブしますか？
CALL SINGLE_DRAWLINE

CALL LISTUP_CONFIG

PRINTFORML [99] 戻る

$INPUT_LOOP
INPUT

SIF RESULT == 99
	RETURN

SIF !INRANGE(RESULT, 0, VARSIZE("G_CONFIG_NAME"))
	GOTO INPUT_LOOP

番号 = RESULT

PRINTFORML 設定データの名前を入力して下さい
PRINTFORML (何も入力しないと「設定{番号}」になります)

$INPUT_LOOP_2
INPUTS

IF RESULTS == ""
	G_CONFIG_NAME:番号 = 設定{番号}
ELSE
	G_CONFIG_NAME:番号 = %RESULTS%
ENDIF


SIF 全て
	CALL CONFIG_SAVE_SLG(番号)

CALL CONFIG_SAVE_EXCEPT_SLG(番号)

;-------------------------------------------------
;desc  :コンフィグの読み取り
;param :全て:全コンフィグを読む
;-------------------------------------------------
@CONFIG_LOAD(全て)
#DIM 全て
#DIM 番号
CALL SINGLE_DRAWLINE
PRINTFORML どれをロードしますか？
CALL SINGLE_DRAWLINE

CALL LISTUP_CONFIG

PRINTFORML [99] 戻る

$INPUT_LOOP
INPUT

SIF RESULT == 99
	RETURN

SIF !INRANGE(RESULT, 0, VARSIZE("G_CONFIG_NAME")) || !CONFIG_EXISTS()
	GOTO INPUT_LOOP

番号 = RESULT

SIF 全て
	CALL CONFIG_LOAD_SLG(番号)

CALL CONFIG_LOAD_EXCEPT_SLG(番号)

;-------------------------------------------------
;desc  :コンフィグの書き込み
;param :全て:全コンフィグを保存する（指定しない場合SLGを除く)
;-------------------------------------------------
@CONFIG_DAILY_SAVE()
#DIM 番号
CALL SINGLE_DRAWLINE
PRINTFORML どこにセーブしますか？
CALL SINGLE_DRAWLINE

CALL LISTUP_CONFIG_DAILY

PRINTFORML [99] 戻る

$INPUT_LOOP
INPUT

SIF RESULT == 99
	RETURN

SIF !INRANGE(RESULT, 0, VARSIZE("G_CONFIG_DAILY_NAME"))
	GOTO INPUT_LOOP

番号 = RESULT

PRINTFORML 設定データの名前を入力して下さい
PRINTFORML (何も入力しないと「設定{番号}」になります)

$INPUT_LOOP_2
INPUTS

IF RESULTS == ""
	G_CONFIG_DAILY_NAME:番号 = 設定{番号}
ELSE
	G_CONFIG_DAILY_NAME:番号 = %RESULTS%
ENDIF


CALL CONFIG_SAVE_DAILY(番号)

;-------------------------------------------------
;desc  :コンフィグの読み取り
;param :全て:全コンフィグを読む
;-------------------------------------------------
@CONFIG_DAILY_LOAD()
#DIM 番号
CALL SINGLE_DRAWLINE
PRINTFORML どれをロードしますか？
CALL SINGLE_DRAWLINE

CALL LISTUP_CONFIG_DAILY

PRINTFORML [99] 戻る

$INPUT_LOOP
INPUT

SIF RESULT == 99
	RETURN

SIF !INRANGE(RESULT, 0, VARSIZE("G_CONFIG_DAILY_NAME")) || !CONFIG_DAILY_EXISTS()
	GOTO INPUT_LOOP

番号 = RESULT

CALL CONFIG_LOAD_DAILY(番号)


;-------------------------------------------------
;desc  :コンフィグをリストアップして印字する
;-------------------------------------------------
@LISTUP_CONFIG()
FOR LOCAL, 0, VARSIZE("G_CONFIG_NAME")
	PRINTBUTTON @"[{LOCAL}] 設定{LOCAL} - %G_CONFIG_NAME:LOCAL%", LOCAL
	PRINTL
NEXT

;-------------------------------------------------
;desc  :コンフィグをリストアップして印字する
;-------------------------------------------------
@LISTUP_CONFIG_FILTER()
FOR LOCAL, 0, VARSIZE("G_CONFIG_FILTER_NAME")
	PRINTBUTTON @"[{LOCAL}] 設定{LOCAL} - %G_CONFIG_FILTER_NAME:LOCAL%", LOCAL
	PRINTL
NEXT

;-------------------------------------------------
;desc  :コンフィグをリストアップして印字する
;-------------------------------------------------
@LISTUP_CONFIG_DAILY()
FOR LOCAL, 0, VARSIZE("G_CONFIG_DAILY_NAME")
	PRINTBUTTON @"[{LOCAL}] 設定{LOCAL} - %G_CONFIG_DAILY_NAME:LOCAL%", LOCAL
	PRINTL
NEXT


;-------------------------------------------------
;desc  :指定番号のコンフィグが存在するか返す
;param :番号:コンフィグ配列のインデックス
;return:1か0:存在するかどうか
;-------------------------------------------------
@CONFIG_EXISTS(番号)
#FUNCTION
#DIM 番号
RETURNF G_CONFIG_NAME:番号 != ""

;-------------------------------------------------
;desc  :指定番号のコンフィグが存在するか返す
;param :番号:コンフィグ配列のインデックス
;return:1か0:存在するかどうか
;-------------------------------------------------
@CONFIG_DAILY_EXISTS(番号)
#FUNCTION
#DIM 番号
RETURNF G_CONFIG_DAILY_NAME:番号 != ""


;-------------------------------------------------
;その他の部分に関する設定
;-------------------------------------------------
@CONFIG_OTHERS
#DIM FIRST_LINE
FIRST_LINE = LINECOUNT
CALL SINGLE_DRAWLINE
PRINTL ●交流パートに関する設定●
CALL SINGLE_DRAWLINE

LOCALS:0 = 妊娠の有無
PRINTFORM %LOCALS:0, 30, RIGHT% -
PRINTBUTTON "  1[変更]", 1
PRINT  
SELECTCASE CONFIG:1
	CASE 0
		PRINT 妊娠する
	CASE 1
		PRINT 妊娠しない
ENDSELECT
PRINTL 


LOCALS:0 = ランダムキャラの男性率
PRINTFORM %LOCALS:0, 30, RIGHT% -
PRINTBUTTON "  3[変更]", 3
PRINT  
PRINTFORM {CONFIG:3}\%
PRINTL 

LOCALS:0 = 子供の男性率
PRINTFORM %LOCALS:0, 30, RIGHT% -
PRINTBUTTON "  4[変更]", 4
PRINT  
PRINTFORM {CONFIG:4}\%
PRINTL 


LOCALS:0 = 捕虜の扱い設定
PRINTFORM %LOCALS:0, 30, RIGHT% -
PRINTBUTTON "  7[変更]", 7
PRINT  
SELECTCASE CONFIG:7
	CASE 0
		PRINT 拠点フェイズ終了時に選択
	CASE 1
		PRINT 拠点フェイズのメニューから選択
ENDSELECT
PRINTL 

LOCALS:0 = 男主人公への逆調教の傾向
PRINTFORM %LOCALS:0, 30, RIGHT% -
PRINTBUTTON "  8[変更]", 8
PRINT  
SELECTCASE CONFIG:8
	CASE 0
		PRINT 搾り取る
	CASE 1
		PRINT 射精管理
ENDSELECT
PRINTL 

LOCALS:0 = 潮吹きの有無
PRINTFORM %LOCALS:0, 30, RIGHT% -
PRINTBUTTON "  9[変更]", 9
PRINT  
SELECTCASE CONFIG:9
	CASE 0
		PRINT 無効
	CASE 1
		PRINT 有効
ENDSELECT
PRINTL 

LOCALS:0 = コンドーム自動装着＆補給
PRINTFORM %LOCALS:0, 30, RIGHT% -
PRINTBUTTON " 10[変更]", 10
PRINT  
SELECTCASE CONFIG:10
	CASE 0
		PRINT なし
	CASE 1
		PRINT 有効（主人公以外）
	CASE 2
		PRINT 全員
ENDSELECT
PRINTL 

LOCALS:0 = 「民の慰安」のモブの性別
PRINTFORM %LOCALS:0, 30, RIGHT% -
PRINTBUTTON " 14[変更]", 14
PRINT  
SELECTCASE CONFIG:14
	CASE 0
		PRINT ばらばら
	CASE 1
		PRINT 男のみ（一部無効）
	CASE 2
		PRINT 女のみ（一部無効）
	CASE 3
		PRINT ふたなりのみ（一部無効）
ENDSELECT
PRINTL 

LOCALS:0 = 「民の慰安」で在野武将が出るか
PRINTFORM %LOCALS:0, 30, RIGHT% -
PRINTBUTTON " 15[変更]", 15
PRINT  
SELECTCASE CONFIG:15
	CASE 0
		PRINT でる
	CASE 1
		PRINT でない
ENDSELECT
PRINTL 

LOCALS:0 = 生まれた子供の扱い
PRINTFORM %LOCALS:0, 30, RIGHT% - 
PRINTBUTTON "16[変更]", 16
PRINT  
SELECTCASE CONFIG:16
	CASE 0
		PRINT 都度選択する
	CASE 1
		PRINT 自動で登用する
	CASE 2
		PRINT 自動で非登用にする
ENDSELECT
PRINTL 

;
;LOCALS:0 = 陰毛状態の表示
;PRINTFORM %LOCALS:0, 30, RIGHT% -
;PRINTBUTTON "15[変更]", 15
;PRINT  
;SELECTCASE CONFIG:15
;	CASE 0
;		PRINT 表示する
;		COM_FILTER:113 = 0
;	CASE 2
;		PRINT 表示しない
;	CASE 1
;		PRINT コマンドも表示しない
;		COM_FILTER:113 = 1
;ENDSELECT
;PRINTL 
;
;LOCALS:0 = タトゥの表示
;PRINTFORM %LOCALS:0, 30, RIGHT% -
;PRINTBUTTON "16[変更]", 16
;PRINT  
;SELECTCASE CONFIG:16
;	CASE 0
;		PRINT 表示する
;		COM_FILTER:92 = 0
;	CASE 2
;		PRINT 表示しない
;	CASE 1
;		PRINT コマンドも表示しない
;		COM_FILTER:92 = 1
;ENDSELECT
;PRINTL 
;
LOCALS:0 = 捕虜あなた逆調教の参加者
PRINTFORM %LOCALS:0, 30, RIGHT% -
PRINTBUTTON " 17[変更]", 17
PRINT  
SELECTCASE CONFIG:17
	CASE 0
		PRINT 全員参加しうる
	CASE 1
		PRINT 面識のある相手のみ参加しうる
ENDSELECT
PRINTL 

LOCALS:0 = 東方キャラの口上の表示
PRINTFORM %LOCALS:0, 30, RIGHT% -
PRINTBUTTON " 40[変更]", 40
PRINT  
SELECTCASE CONFIG:40
	CASE 0
		PRINT 表示する
	CASE 1
		PRINT 表示しない
ENDSELECT
PRINTL 

LOCALS:0 = 汎用キャラの口上の表示
PRINTFORM %LOCALS:0, 30, RIGHT% -
PRINTBUTTON " 41[変更]", 41
PRINT  
SELECTCASE CONFIG:41
	CASE 0
		PRINT 表示する
	CASE 1
		PRINT 表示しない
ENDSELECT
PRINTL 

LOCALS:0 = 調教終了後性癖レベル自動上昇
PRINTFORM %LOCALS:0, 30, RIGHT% -
PRINTBUTTON " 42[変更]", 42
PRINT  
SELECTCASE CONFIG:42
	CASE 0
		PRINT しない
	CASE 1
		PRINT する
ENDSELECT
PRINTL 
PRINTL 


CALL SINGLE_DRAWLINE
PRINTBUTTON " 99[決定]", 99
PRINTL 

$INPUT_LOOP
INPUT

IF RESULT == 99
	RETURN
ELSEIF RESULT == 0
	CALL CONFIG_TOGGLE(0)
ELSEIF RESULT == 1
	CALL CONFIG_TOGGLE(1)
ELSEIF RESULT == 2 && CONFIG:1 == 0
	CALL CONFIG_TOGGLE(2)
ELSEIF RESULT == 3
	CONFIG:3 += 10
	IF CONFIG:3 > 100
		CONFIG:3 = 0
	ENDIF
ELSEIF RESULT == 4
	CONFIG:4 += 10
	IF CONFIG:4 > 100
		CONFIG:4 = 0
	ENDIF
ELSEIF RESULT == 7
	CALL CONFIG_TOGGLE(7)
ELSEIF RESULT == 8
	CALL CONFIG_TOGGLE(8)
ELSEIF RESULT == 9
	CALL CONFIG_TOGGLE(9)
ELSEIF RESULT == 10
	CONFIG:10 = ROUND_INCREMENT(CONFIG:10, 0, 2)
ELSEIF RESULT == 14
	CONFIG:14 = ROUND_INCREMENT(CONFIG:14, 0, 3)
ELSEIF RESULT == 15
	CALL CONFIG_TOGGLE(15)
ELSEIF RESULT == 16
	CONFIG:16 = ROUND_INCREMENT(CONFIG:16, 0, 2)
ELSEIF RESULT == 17
	SELECTCASE CONFIG:17
		CASE 0
			CONFIG:17 = 1
		CASE 1
			CONFIG:17 = 0
	ENDSELECT
ELSEIF RESULT == 18
	CALL CONFIG_TOGGLE(18)
ELSEIF RESULT == 40
	CALL CONFIG_TOGGLE(40)
ELSEIF RESULT == 41
	CALL CONFIG_TOGGLE(41)
ELSEIF RESULT == 42
	CALL CONFIG_TOGGLE(42)
ELSE
	GOTO INPUT_LOOP
ENDIF
CLEARLINE LINECOUNT - FIRST_LINE
RESTART

;-------------------------------------------------
;特殊設定(主に好き嫌いが分かれやすい要素の設定)
;-------------------------------------------------
@CONFIG_OTHERS_SP
#DIM L_COUNTER
#DIM FIRST_LINE
#DIM CONST COM_AUTO_ADD_INPUT_NUM = 300	;おまかせ系の入力値に加算する値
#DIM COM_AUTO_INPUT_RESULT_NUM			;おまかせ系の入力値に加算する値を減算した値

FIRST_LINE = LINECOUNT
CALL SINGLE_DRAWLINE
PRINTL ●特殊設定●
CALL SINGLE_DRAWLINE
PRINTL ◇特殊勢力◇

LOCALS:0 = 特殊勢力による捕虜への調教1
PRINTFORM %LOCALS:0, 50, RIGHT% -
PRINTBUTTON @"{80, 4}[変更]", 80
PRINT  
SELECTCASE CONFIG:80
	CASE 0
		PRINT 発生しない
	CASE 1
		PRINT 女性・女ふたなりのみ発生
	CASE 2
		PRINT 全ての性別で発生
ENDSELECT
PRINTL

LOCALS:0 = 特殊勢力による捕虜への調教2
PRINTFORM %LOCALS:0, 50, RIGHT% -
PRINTBUTTON @"{81, 4}[変更]", 81
PRINT  
SELECTCASE CONFIG:81
	CASE 0
		PRINT 全特殊勢力で発生
	CASE 1
		PRINT 自分が所属する特殊勢力のみで発生
	CASE 2
		PRINT 自分が所属する特殊勢力以外で発生
ENDSELECT
PRINTL 

LOCALS:0 = 特殊勢力による捕虜への調教3
PRINTFORM %LOCALS:0, 50, RIGHT% -
PRINTBUTTON @"{82, 4}[変更]", 82
PRINT  
SELECTCASE CONFIG:82
	CASE 0
		PRINT 捕虜・士官に対して発生
	CASE 1
		PRINT 捕虜のみ（士官を弾く）
	CASE 2
		PRINT その勢力の士官のみ（捕虜を弾く）
ENDSELECT
PRINTL 

CALL COLOR_LINE
PRINTL ◇スカ系◇
LOCALS:0 = 絶頂や尿意の限界によるおもらし
PRINTFORM %LOCALS:0, 50, RIGHT% -
PRINTBUTTON @"{90, 4}[変更]", 90
PRINT  
SELECTCASE CONFIG:90
	CASE 0
		PRINT 無効
	CASE 1
		PRINT 有効
ENDSELECT
PRINTL 

LOCALS:0 = 恐怖の異常上昇によるおもらし
PRINTFORM %LOCALS:0, 50, RIGHT% -
PRINTBUTTON @"{91, 4}[変更]", 91
PRINT  
SELECTCASE CONFIG:91
	CASE 0
		PRINT 無効
	CASE 1
		PRINT 有効
ENDSELECT
PRINTL 

LOCALS:0 = デイリーイベントによる排泄経験値の上昇
PRINTFORM %LOCALS:0, 50, RIGHT% -
PRINTBUTTON @"{92, 4}[変更]", 92
PRINT  
SELECTCASE CONFIG:92
	CASE 0
		PRINT 許可
	CASE 1
		PRINT 禁止
ENDSELECT
PRINTL 

CALL COLOR_LINE
PRINTL ◇全般◇


LOCALS:0 = 男同士のコマンドフィルタ（一部）
PRINTFORM %LOCALS:0, 50, RIGHT% -
PRINTBUTTON @"{201, 3}[変更]", 201
PRINT  
SELECTCASE CONFIG:201
	CASE 0
		PRINT 無効
	CASE 1
		PRINT 有効
ENDSELECT
PRINTL 

LOCALS:0 = 調教中に主人公の反応を表示する
PRINTFORM %LOCALS:0, 50, RIGHT% -
PRINTBUTTON "441[変更]", 441
PRINT  
SELECTCASE CONFIG:441
	CASE 0
		PRINT しない
	CASE 1
		PRINT する
ENDSELECT
PRINTL 

CALL COLOR_LINE
PRINTL ◇おまかせ時の行動◇
FOR L_COUNTER, 1, COM_AUTO_FILTER_MAX + 1
	SIF COM_AUTO_FILTER_NAME(L_COUNTER) == ""
		CONTINUE
	LOCALS:0 = %COM_AUTO_FILTER_NAME(L_COUNTER)%
	PRINTFORM %LOCALS:0, 50, RIGHT% -
	PRINTBUTTON @"{L_COUNTER + COM_AUTO_ADD_INPUT_NUM, 4}[変更]", L_COUNTER + COM_AUTO_ADD_INPUT_NUM
	PRINT  
	SELECTCASE GETBIT(CONFIG:11, L_COUNTER)
		CASE 0
			PRINT 無効
		CASE 1
			PRINT 有効
	ENDSELECT
	PRINTL 
NEXT

CALL SINGLE_DRAWLINE
PRINTL [99] 決定

$INPUT_LOOP
INPUT

IF RESULT == 99
	RETURN
ELSEIF RESULT == 70
	CONFIG:70 ++
	IF CONFIG:70 >= 2
		CONFIG:70 = 0
	ENDIF
ELSEIF RESULT == 71
	CONFIG:71 ++
	IF CONFIG:71 >= 3
		CONFIG:71 = 0
	ENDIF
ELSEIF RESULT == 79 && CONFIG:71 == 2
	CONFIG:79 ++
	IF CONFIG:79 >= 3
		CONFIG:79 = 0
	ENDIF
ELSEIF RESULT == 72 && (CONFIG:70 || CONFIG:71)
	CONFIG:72 ++
	IF CONFIG:72 >= 2
		CONFIG:72 = 0
	ENDIF
ELSEIF RESULT == 73 && (CONFIG:70 || CONFIG:71)
	CONFIG:73 ++
	IF CONFIG:73 >= 2
		CONFIG:73 = 0
	ENDIF
ELSEIF RESULT == 77 && (CONFIG:70 || CONFIG:71) && CONFIG:73 == 1
	CONFIG:77 ++
	IF CONFIG:77 >= 6
		CONFIG:77 = 0
	ENDIF
ELSEIF RESULT == 78 && (CONFIG:70 || CONFIG:71) && CONFIG:73 == 1
	CONFIG:78 ++
	IF CONFIG:78 >= 2
		CONFIG:78 = 0
	ENDIF
ELSEIF RESULT == 74 && (CONFIG:70 || CONFIG:71)
	CONFIG:74 ++
	IF CONFIG:74 >= 2
		CONFIG:74 = 0
	ENDIF
ELSEIF RESULT == 75 && (CONFIG:70 || CONFIG:71)
	CONFIG:75 ++
	IF CONFIG:75 >= 2
		CONFIG:75 = 0
	ENDIF
ELSEIF RESULT == 76 && (CONFIG:70 || CONFIG:71) && CONFIG:1 == 0
	CONFIG:76 ++
	IF CONFIG:76 >= 2
		CONFIG:76 = 0
	ENDIF
ELSEIF RESULT == 80
	CONFIG:80 ++
	IF CONFIG:80 >= 3
		CONFIG:80 = 0
	ENDIF
ELSEIF RESULT == 81
	CONFIG:81 ++
	IF CONFIG:81 >= 3
		CONFIG:81 = 0
	ENDIF
ELSEIF RESULT == 82
	CONFIG:82 ++
	IF CONFIG:82 >= 3
		CONFIG:82 = 0
	ENDIF
ELSEIF RESULT == 83
	CONFIG:83 ++
	IF CONFIG:83 >= 3
		CONFIG:83 = 0
	ENDIF
ELSEIF RESULT == 84
	CONFIG:84 ++
	IF CONFIG:84 >= 3
		CONFIG:84 = 0
	ENDIF
ELSEIF RESULT == 85
	CONFIG:85 ++
	IF CONFIG:85 >= 2
		CONFIG:85 = 0
	ENDIF
ELSEIF RESULT == 86
	CONFIG:86 ++
	IF CONFIG:86 >= 3
		CONFIG:86 = 0
	ENDIF
ELSEIF RESULT == 87
	CONFIG:87 ++
	IF CONFIG:87 >= 2
		CONFIG:87 = 0
	ENDIF
ELSEIF RESULT == 90
	CONFIG:90 ++
	IF CONFIG:90 >= 2
		CONFIG:90 = 0
	ENDIF
ELSEIF RESULT == 91
	CONFIG:91 ++
	IF CONFIG:91 >= 2
		CONFIG:91 = 0
	ENDIF
ELSEIF RESULT == 92
	CONFIG:92 ++
	IF CONFIG:92 >= 2
		CONFIG:92 = 0
	ENDIF
	
ELSEIF RESULT == 201
	CONFIG:201 = !CONFIG:201
ELSEIF RESULT == 441
	CALL CONFIG_TOGGLE(441)
;おまかせの設定
ELSEIF RESULT >= 1 + COM_AUTO_ADD_INPUT_NUM && RESULT <= COM_AUTO_FILTER_MAX + COM_AUTO_ADD_INPUT_NUM
	COM_AUTO_INPUT_RESULT_NUM = RESULT - COM_AUTO_ADD_INPUT_NUM
	IF GETBIT(CONFIG:11, COM_AUTO_INPUT_RESULT_NUM)
		CLEARBIT CONFIG:11, COM_AUTO_INPUT_RESULT_NUM
	ELSE
		SETBIT CONFIG:11, COM_AUTO_INPUT_RESULT_NUM
	ENDIF
ELSE
	GOTO INPUT_LOOP
ENDIF
CLEARLINE LINECOUNT - FIRST_LINE
RESTART

;-------------------------------------------------
;既存のキャラをカスタムキャラとして保存する
;-------------------------------------------------
@CONFIG_SAVE_EXIST_CHARA()
#DIM FIRST_LINE, 2
#DIM 対象
#DIM ページ
FIRST_LINE:0 = LINECOUNT
CALL SINGLE_DRAWLINE
PRINTFORML 既存のキャラをカスタムキャラとして残せます
PRINTFORML (血縁関係など、一部設定は消去されます)
CALL SINGLE_DRAWLINE
CALL SELECT_CHARA_LIST_ONLY_LOGIC_SLG("NONE", "NONE")

IF RESULT == -1
	RETURN 0
ENDIF

対象 = RESULT

FIRST_LINE:1 = LINECOUNT

$LISTUP_LOOP

CALL LISTUP_CUSTOM_CHARACTER(0, ページ, 20)
CALL SINGLE_DRAWLINE
PRINTBUTTON "[戻る]", 10000

INPUT

IF RESULT == 10000
	CLEARLINE LINECOUNT - FIRST_LINE
	RESTART
ELSEIF RESULT == MAX_CUSTOM_CHARA
	ページ = ROUND_DECREMENT(ページ, 0, MAX_CUSTOM_CHARA / 20)
ELSEIF RESULT == MAX_CUSTOM_CHARA + 1
	ページ = ROUND_INCREMENT(ページ, 0, MAX_CUSTOM_CHARA / 20)
ELSEIF INRANGE(RESULT, 0, MAX_CUSTOM_CHARA)
	LOCAL = RESULT
	IF FIND_CHARADATA(TOSTR(LOCAL))
		PRINTFORML {LOCAL}番にはカスタムキャラが存在します
		PRINTFORML 上書きしますか？
		CALL ASK_YN()
		IF RESULT == 1
			CLEARLINE LINECOUNT - FIRST_LINE:1
			GOTO LISTUP_LOOP
		ENDIF
	ENDIF
	CALL SAVE_CUSTOM_DATA(LOCAL, 対象)
	PRINTFORML セーブしました
	CLEARLINE LINECOUNT - FIRST_LINE
	RESTART
ENDIF

CLEARLINE LINECOUNT - FIRST_LINE:1
GOTO LISTUP_LOOP

;-------------------------------------------------
;コマンドフィルタ
;-------------------------------------------------
@CONFIG_COM_FILTER
#DIM FIRST_LINE
#DIM 有効コマンド数
#DIM ページ数
#DIM 現在ページ
#DIM 始点
#DIM 終点
#DIM ページ内表示数
#DIM 改行カウンタ

;ページ数の計算
有効コマンド数 = 0
FOR LOCAL:0, 0, 1000
	IF TRAINNAME:(LOCAL:0) != ""
		有効コマンド数 ++
	ENDIF
NEXT
ページ数 = (有効コマンド数 - 1) / 66 + 1
現在ページ = 1

CALL SINGLE_DRAWLINE
PRINTFORML 緑色表示のコマンドはシステムの都合上フィルタ不可です
CALL SINGLE_DRAWLINE
FIRST_LINE = LINECOUNT
$SHOW_LOOP

;コマンドの表示
有効コマンド数 = 0
始点 = (現在ページ - 1) * 88
終点 = 現在ページ * 88
改行カウンタ = 0
FOR LOCAL:0, 0, 1000
	IF TRAINNAME:(LOCAL:0) != ""
		IF 有効コマンド数 >= 始点 && 有効コマンド数 < 終点
			IF 改行カウンタ >= 1 && 改行カウンタ % 4 == 0
				PRINTL 
			ENDIF
			RESULT = 0
			TRYCALLFORM COM_FILTER_FORCE_OFF{LOCAL:0}
			IF RESULT
				SETCOLOR カラー_パ緑
				PRINTPLAINFORM [{LOCAL:0, 3}] %TRAINNAME:(LOCAL:0), 21, LEFT%
			ELSE
				IF COM_FILTER:(LOCAL:0)
					SETCOLOR カラー_選択不可
				ENDIF
				PRINTFORM [{LOCAL:0, 3}] %TRAINNAME:(LOCAL:0), 21, LEFT%
			ENDIF
			RESETCOLOR
			改行カウンタ ++
		ENDIF
		有効コマンド数 ++
	ENDIF
NEXT
PRINTL 
CALL SINGLE_DRAWLINE

PRINT [990] 前のページ     
PRINTPLAINFORM page{現在ページ}/{ページ数}    
PRINTL [991] 次のページ


CALL SINGLE_DRAWLINE
PRINTL [997] セーブ
PRINTL [998] ロード
PRINTL [999] 戻る

REDRAW 0

$INPUT_LOOP
INPUT

IF RESULT == 999
	REDRAW 1
	RETURN
ELSEIF RESULT == 997
	CALL CONFIG_COM_FILTER_SAVE()
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP
ELSEIF RESULT == 998
	CALL CONFIG_COM_FILTER_LOAD()
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP
ELSEIF RESULT == 990
	現在ページ --
	IF 現在ページ < 1
		現在ページ = ページ数
	ENDIF
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP
ELSEIF RESULT == 991
	現在ページ ++
	IF 現在ページ > ページ数
		現在ページ = 1
	ENDIF
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP
ELSEIF RESULT >= 0 && RESULT < 1000
	LOCAL:5 = RESULT
	IF TRAINNAME:(LOCAL:5) != ""
		RESULT = 0
		TRYCALLFORM COM_FILTER_FORCE_OFF{LOCAL:5}
		IF !RESULT
			COM_FILTER:(LOCAL:5) = !COM_FILTER:(LOCAL:5)
			CLEARLINE LINECOUNT - FIRST_LINE
			GOTO SHOW_LOOP
		ENDIF
	ENDIF
ENDIF
CLEARLINE 1
GOTO INPUT_LOOP

;-------------------------------------------------
;DESC  :コマンドフィルタのセーブ
;ARG   ::
;-------------------------------------------------
@CONFIG_COM_FILTER_SAVE()
#DIM 番号

CALL SINGLE_DRAWLINE
PRINTFORML どこにセーブしますか？
CALL SINGLE_DRAWLINE

CALL LISTUP_CONFIG_FILTER

PRINTFORML [99] 戻る

$INPUT_LOOP
INPUT

SIF RESULT == 99
	RETURN

SIF !INRANGE(RESULT, 0, VARSIZE("G_CONFIG_NAME"))
	GOTO INPUT_LOOP

番号 = RESULT

PRINTFORML 設定データの名前を入力して下さい
PRINTFORML (何も入力しないと「設定{番号}」になります)

$INPUT_LOOP_2
INPUTS

IF RESULTS == ""
	G_CONFIG_FILTER_NAME:番号 = 設定{番号}
ELSE
	G_CONFIG_FILTER_NAME:番号 = %RESULTS%
ENDIF

FOR LOCAL, 0, VARSIZE("COM_FILTER")
	G_FILTER:番号:(LOCAL:0) = COM_FILTER:(LOCAL:0)
NEXT

;グローバル変数を保存
SAVEGLOBAL
RETURN


;-------------------------------------------------
;desc  :コンフィグの読み取り
;-------------------------------------------------
@CONFIG_COM_FILTER_LOAD()
#DIM 番号
CALL SINGLE_DRAWLINE
PRINTFORML どれをロードしますか？
CALL SINGLE_DRAWLINE

FOR LOCAL, 0, VARSIZE("G_CONFIG_FILTER_NAME")
	PRINTBUTTON @"[{LOCAL}] 設定{LOCAL} - %G_CONFIG_FILTER_NAME:LOCAL%", LOCAL
	PRINTL
NEXT

PRINTFORML [99] 戻る

$INPUT_LOOP
INPUT

SIF RESULT == 99
	RETURN

SIF !INRANGE(RESULT, 0, VARSIZE("G_CONFIG_FILTER_NAME")) || G_CONFIG_FILTER_NAME:RESULT == ""
	GOTO INPUT_LOOP

番号 = RESULT

FOR LOCAL, 0, VARSIZE("COM_FILTER")
	COM_FILTER:(LOCAL:0) = G_FILTER:番号:(LOCAL:0)
NEXT



;-------------------------------------------------
;0か1だけのCONFIG切り替え
;-------------------------------------------------
@CONFIG_TOGGLE(ARG)
CONFIG:ARG ++
CONFIG:ARG &= 0x1
RETURN 0
