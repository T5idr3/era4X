;アイテムインターフェース改良用の関数郡
@PRINT_ITEMMENU
A = 0

PRINTL 表示するアイテムを選択してください
PRINTL 
PRINTL [0] 調教道具
PRINTL [1] 消耗品
PRINTL [2] 素質アイテム
PRINTL [3] 薬品類
PRINTL [4] 特殊アイテム
PRINTL [5] すべて
PRINTL [9] 戻る

$INPUT_LOOP
INPUT
IF RESULT == 9
	A = -1
ELSEIF RESULT == 0
	REPEAT 60
		A = 40+COUNT
		ITEMSALES:A = 0
	REND
ELSEIF RESULT == 1
	REPEAT 40
		ITEMSALES:COUNT = 0
	REND
	REPEAT 40
		A = 60+COUNT
		ITEMSALES:A = 0
	REND
ELSEIF RESULT == 2
	REPEAT 60
		ITEMSALES:COUNT = 0
	REND
	REPEAT 30
		A = 70+COUNT
		ITEMSALES:A = 0
	REND
ELSEIF RESULT == 3
	REPEAT 70
		ITEMSALES:COUNT = 0
	REND
	REPEAT 10
		A = 90+COUNT
		ITEMSALES:A = 0
	REND
ELSEIF RESULT == 4
	REPEAT 90
		ITEMSALES:COUNT = 0
	REND
ELSEIF RESULT < 0 || RESULT > 6
	GOTO INPUT_LOOP
ENDIF

;アイテムの値段を変数Aに格納して返す関数
@ITEM_MONEY
SIF BOUGHT == 0
	A = 3000
SIF BOUGHT == 1
	A = 9000
SIF BOUGHT == 2
	A = 3000
SIF BOUGHT == 3
	A = 12000
SIF BOUGHT == 4
	A = 4500
SIF BOUGHT == 5
	A = 4500
SIF BOUGHT == 6
	A = 4500
SIF BOUGHT == 7
	A = 6000
SIF BOUGHT == 8
	A = 6000
SIF BOUGHT == 9
	A = 6000
SIF BOUGHT == 10
	A = 3000
SIF BOUGHT == 11
	A = 4500
SIF BOUGHT == 12
	A = 4500
SIF BOUGHT == 13
	A = 3000
SIF BOUGHT == 14
	A = 6000
SIF BOUGHT == 15
	A = 4500
SIF BOUGHT == 16
	A = 3000
SIF BOUGHT == 17
	A = 3000
SIF BOUGHT == 18
	A = 15000
SIF BOUGHT == 19
	A = 9000
SIF BOUGHT == 20
	A = 6000
SIF BOUGHT == 21
	A = 15000
SIF BOUGHT == 22
	A = 6000
SIF BOUGHT == 23
	A = 15000
SIF BOUGHT == 24
	A = 15000
SIF BOUGHT == 25
	A = 30000
SIF BOUGHT == 26
	A = 6000
SIF BOUGHT == 40
	A = 600
SIF BOUGHT == 41
	A = 6000
SIF BOUGHT == 42
	A = 3000
SIF BOUGHT == 43
	A = 1500
SIF BOUGHT == 44
	A = 900
SIF BOUGHT == 45
	A = 300
SIF BOUGHT == 46
	A = 6000
SIF BOUGHT == 47
	A = 3000
SIF BOUGHT == 60
	A = 30000
SIF BOUGHT == 61
	A = 240000
SIF BOUGHT == 62
	A = 240000
SIF BOUGHT == 63
	A = 240000
SIF BOUGHT == 70
	A = 3000
SIF BOUGHT == 71
	A = 60000
SIF BOUGHT == 72
	A = 30000
SIF BOUGHT == 73
	A = 60000
SIF BOUGHT == 74
	A = 60000
SIF BOUGHT == 75
	A = 60000
SIF BOUGHT == 76
	A = 30000
SIF BOUGHT == 77
	A = 15000
SIF BOUGHT == 78
	A = 15000
SIF BOUGHT == 79
	A = 60000
SIF BOUGHT == 90
	A = 300000
SIF BOUGHT == 91
	A = 6000
SIF BOUGHT == 92
	A = 6000

;アイテム出現条件
@SALEITEM_CHECK
;初期化
REPEAT 100
	ITEMSALES:COUNT = 0
REND


;基本アイテム
REPEAT 27
	SIF COUNT == 25
		CONTINUE
	IF ITEM:COUNT
		ITEMSALES:COUNT = 0
	ELSE
		ITEMSALES:COUNT = 1
	ENDIF
REND

;ローション、媚薬、ビデオテープなどはとりあえず99個まで
REPEAT 8
	B = 40 + COUNT
	IF ITEM:B >= 99
		ITEMSALES:B = 0
	ELSE
		ITEMSALES:B = 1
	ENDIF	
REND

;ビデオカメラが無いときはビデオテープを、カメラが無いときはビデオの販売をしない
SIF ITEM:18 == 0
	ITEMSALES:43 = 0
SIF ITEM:19 == 0
	ITEMSALES:44 = 0

;汚れ無視
IF TALENT:MASTER:汚れ無視
	ITEMSALES:60 = 0
ELSE
	ITEMSALES:60 = 1
ENDIF

;調合知識
;主人禁断の知識を持っていて、技巧が4以上かつ陥落済みの助手が調合知識を持っている場合、調合知識が買えるようになる
ITEMSALES:61 = 0
IF TALENT:MASTER:禁断の知識 && TALENT:MASTER:調合知識 == 0 && ABL:MASTER:2 >= 4 && ASSI >= 1
	SIF TALENT:ASSI:調合知識 && (TALENT:ASSI:恋慕 || TALENT:ASSI:淫乱 || TALENT:ASSI:服従 || TALENT:ASSI:親愛 || TALENT:ASSI:娼婦 || TALENT:ASSI:隷属)
		ITEMSALES:61 = 1
ENDIF

;禁断の知識
SIF TALENT:MASTER:禁断の知識 == 0
	ITEMSALES:62 = 1

;主人か助手が『調合知識』を持っている
P = 0
REPEAT CHARANUM
	IF COUNT == MASTER && TALENT:COUNT:調合知識
		P = 1
	ELSEIF CFLAG:COUNT:1 >= 1 && ISASSI:COUNT == 1
		SIF TALENT:COUNT:調合知識
			P = 1
	ENDIF
REND

;主人か助手が『禁断の知識』を持っている
F = 0
REPEAT CHARANUM
	IF COUNT == MASTER && TALENT:COUNT:禁断の知識
		F = 1
		ITEMSALES:62 = 0
	ELSEIF CFLAG:COUNT:1 >= 1 && ISASSI:COUNT == 1
		SIF TALENT:COUNT:禁断の知識
			F = 1
	ENDIF
REND

;触手使役術
ITEMSALES:25 = 0
SIF F == 1 && ITEM:25 == 0
	ITEMSALES:25 = 1

;第三の瞳
ITEMSALES:63 = 0
SIF F == 1 && TALENT:MASTER:覚 == 0
	ITEMSALES:63 = 1

;元気になる薬（条件は調合知識のみ）
ITEMSALES:70 = 0
IF P
	REPEAT CHARANUM
		SIF BASE:COUNT:0 != MAXBASE:COUNT:0 && CFLAG:COUNT:12 == 0
			ITEMSALES:70 = 1
	REND
ENDIF

;対象が奴隷で禁断の知識が条件にかかわるアイテム
IF F || P
	REPEAT CHARANUM
		;労役中のキャラは除外
		SIF CFLAG:COUNT:12 > 0
			CONTINUE
		;ふたなり化できるか
		;既になっていたらダメ
		IF TALENT:COUNT:ふたなり == 0 && TALENT:COUNT:半人半妖 == 0 && TALENT:COUNT:オトコ == 0
			;禁断の知識がない場合、対象に調合知識か不思議な根か具現が必要
			SIF F || (TALENT:COUNT:調合知識 || TALENT:COUNT:不思議な根 || TALENT:COUNT:具現)
				ITEMSALES:71 = 1
		ENDIF
		;ふたなりを消去できるか
		SIF TALENT:COUNT:ふたなり == 1 && TALENT:COUNT:半人半妖 == 0 && TALENT:COUNT:オトコ == 0
			ITEMSALES:72 = 1
		;母乳体質にできるか、禁断の知識があり母乳体質でないとダメ
		SIF F && TALENT:COUNT:母乳体質 == 0 && TALENT:COUNT:オトコ == 0
			ITEMSALES:73 = 1
		;処女膜再生できるか、処女および再生処女でなく禁断の知識がないとダメ
		SIF F && TALENT:COUNT:処女 == 0 && TALENT:COUNT:再生処女 == 0 && TALENT:COUNT:オトコ == 0
			ITEMSALES:74 = 1
		;アリスはロリ化する(東方専用)
		;SIF F && NO:COUNT == 14 && (TALENT:COUNT:処女 || TALENT:COUNT:再生処女) && CFLAG:COUNT:0 == 0
		;	ITEMSALES:74 = 1
		;豊胸できるか、禁断の知識があり爆乳以外でないとダメ
		SIF F && TALENT:COUNT:爆乳 == 0 && TALENT:COUNT:オトコ == 0
			ITEMSALES:75 = 1
		;縮胸できるか、禁断の知識があり絶壁以外でないとダメ
		SIF F && TALENT:COUNT:絶壁 == 0 && TALENT:COUNT:オトコ == 0
			ITEMSALES:76 = 1
		;母乳治療できるか、禁断の知識があり母乳体質じゃないとダメ
		SIF F && TALENT:COUNT:母乳体質 && TALENT:COUNT:オトコ == 0 
			ITEMSALES:77 = 1
		;お漏らし治療できるか、禁断の知識がありお漏らし癖じゃないとダメ
		SIF F && TALENT:COUNT:おもらし癖
			ITEMSALES:78 = 1
		;卵を産ませる、禁断の知識があり妊娠中で臨月になっていないとダメ
		SIF F && TALENT:COUNT:妊娠 && (CFLAG:COUNT:110 - 3) > DAY
			ITEMSALES:79 = 1
	REND
ENDIF

;記憶消去薬
ITEMSALES:90 = 0
IF F && P
	REPEAT CHARANUM
		SIF COUNT == MASTER
			CONTINUE
		SIF CFLAG:COUNT:12 == 0
			ITEMSALES:90 = 1
	REND
ENDIF

;排卵誘発剤
ITEMSALES:91 = 0
REPEAT CHARANUM
	SIF CFLAG:COUNT:12 == 0 && CFLAG:COUNT:109 == 0 && TALENT:COUNT:オトコ == 0
		ITEMSALES:91 = 1
REND

;子宮内避妊具
ITEMSALES:92 = 0
REPEAT CHARANUM
	SIF CFLAG:COUNT:12 == 0 && CFLAG:COUNT:108 == 0 && TALENT:COUNT:オトコ == 0
		ITEMSALES:92 = 1
REND

@EVENTBUY
;アイテムメニュー以外での購入のキャンセル
IF BOUGHT >= 0 && BOUGHT <= 99
	ITEM:BOUGHT -= 1
	CALL ITEM_MONEY
	MONEY += A
ENDIF

;アイテム購入
@BUY_ITEM
;表示されたアイテムが無い場合
A = 0
REPEAT 100
	SIF ITEMSALES:COUNT == 1
		A += 1
REND
IF A == 0
	PRINTW 購入できるアイテムはありません
	RETURN 0
ENDIF

PRINTL [100] 戻る
PRINTL 
PRINTL 購入したいアイテムの番号を選択してください
$INPUT_LOOP
INPUT
IF RESULT == 100
	RETURN 0
ELSEIF RESULT < 0 || RESULT > 100 || ITEMSALES:RESULT == 0
	GOTO INPUT_LOOP
ENDIF

BOUGHT = RESULT
CALL ITEM_MONEY
IF MONEY < A
	PRINTL お金が足りません
	RETURN 0
ENDIF


;購入の意志を再確認するように
$INPUT_LOOP_2
IF BOUGHT <= 26 && BOUGHT != 25
	PRINTFORML 本当に%ITEMNAME:BOUGHT%を購入しますか？
	PRINTL [0] 購入する
	PRINTL [1] やめる
	INPUT
	IF RESULT == 1
		RETURN 0
	ELSEIF RESULT < 0 || RESULT > 1
		GOTO INPUT_LOOP_2
	ENDIF
	PRINTFORMW ＜%ITEMNAME:BOUGHT%を購入しました＞
ELSEIF (BOUGHT >= 60 && BOUGHT <= 69) || BOUGHT == 25
	PRINTFORML 本当に%ITEMNAME:BOUGHT%を身につけますか？
	PRINTL [0] 身につける
	PRINTL [1] やめる
	INPUT
	IF RESULT == 1
		RETURN 0
	ELSEIF RESULT < 0 || RESULT > 1
		GOTO INPUT_LOOP_2
	ENDIF
	PRINTFORMW ＜%ITEMNAME:BOUGHT%を身につけました＞
ENDIF
;調教アイテムは買ったらショップから消える
IF BOUGHT <= 26
	CALL ITEM_MONEY
	MONEY -= A
	ITEMSALES:BOUGHT = 0
	ITEM:BOUGHT = 1
;素質アイテムは買ったらショップ、手持ちから消える
ELSEIF BOUGHT >= 60 && BOUGHT <= 69
	CALL ITEM_MONEY
	MONEY -= A
	ITEMSALES:BOUGHT = 0
	ITEM:BOUGHT = 0
	;素質アイテム【汚れ無視】
	SIF BOUGHT == 60
		TALENT:MASTER:汚れ無視 = 1
	;素質アイテム【調合知識】
	SIF BOUGHT == 61
		TALENT:MASTER:調合知識 = 1
	;素質アイテム【禁断の知識】
	SIF BOUGHT == 62
		TALENT:MASTER:禁断の知識 = 1
	;素質アイテム【覚】
	SIF BOUGHT == 63
		TALENT:MASTER:覚 = 1
ENDIF

;複数所持可能なアイテムの場合
SIF BOUGHT >= 40 && BOUGHT <= 59
	CALL BUY_PLURAL
;主に奴隷を対象とするアイテムの場合
SIF (BOUGHT >= 70 && BOUGHT <= 89) || (BOUGHT >= 90 && BOUGHT <= 99)
	CALL USE_ITEM


;複数個所持可能なアイテムについて、同時購入個数を選択
@BUY_PLURAL
;現在の購入可能数、Bは所持可能最大数（現在100個）、Cは所持金によるもの
;Aはアイテムの単価、DはBとCのうち小さい方、EはB<=Cであれば0、B>Cであれば1
CALL ITEM_MONEY
B = 99-ITEM:BOUGHT
C = MONEY/A
IF B <= C
	D = B
	E = 0
ELSE
	D = C
	E = 1
ENDIF
DRAWLINE
PRINTFORM %ITEMNAME:BOUGHT%をいくつ買いますか？ （1-
PRINTV D
PRINTL 、0で戻る）

$INPUT_LOOP
INPUT
IF RESULT == 0
	RETURN 0
ELSEIF RESULT < 0
	GOTO INPUT_LOOP
ELSEIF RESULT > D
	IF RESULT > D && E == 0
		PRINTL そんなに持てません
	ELSEIF RESULT > D && E == 1
		PRINTL お金が足りません
	ENDIF
	GOTO INPUT_LOOP
ELSEIF RESULT == 1
	PRINTFORMW ＜%ITEMNAME:BOUGHT%を購入しました＞
	ITEM:BOUGHT += RESULT
	MONEY -= A*RESULT
	RETURN 0
ELSE
	PRINTFORMW ＜%ITEMNAME:BOUGHT%を{RESULT}個購入しました＞
	ITEM:BOUGHT += RESULT
	MONEY -= A*RESULT
	RETURN 0
ENDIF

;アイテム使用
@USE_ITEM
DRAWLINE
;アイテムの効果を表示する
SIF BOUGHT == 70
	PRINTFORML %ITEMNAME:BOUGHT%:体力を回復します
SIF BOUGHT == 71
	PRINTFORML %ITEMNAME:BOUGHT%:ふたなりになります
SIF BOUGHT == 72
	PRINTFORML %ITEMNAME:BOUGHT%:ふたなりを消去します
SIF BOUGHT == 73
	PRINTFORML %ITEMNAME:BOUGHT%:母乳体質になります
SIF BOUGHT == 74
	PRINTFORML %ITEMNAME:BOUGHT%:処女膜が再生します
SIF BOUGHT == 75
	PRINTFORML %ITEMNAME:BOUGHT%:胸を大きくします
SIF BOUGHT == 76
	PRINTFORML %ITEMNAME:BOUGHT%:胸を小さくします
SIF BOUGHT == 77
	PRINTFORML %ITEMNAME:BOUGHT%:母乳が出なくなります
SIF BOUGHT == 78
	PRINTFORML %ITEMNAME:BOUGHT%:お漏らし癖が治ります
SIF BOUGHT == 79
	PRINTFORML %ITEMNAME:BOUGHT%:強制的に卵を産ませます
SIF BOUGHT == 90
	PRINTFORML %ITEMNAME:BOUGHT%:調教の記憶を消去します
SIF BOUGHT == 91
	PRINTFORML %ITEMNAME:BOUGHT%:一日だけ妊娠確率を上昇させます
SIF BOUGHT == 92
	PRINTFORML %ITEMNAME:BOUGHT%:一日だけ膣射経験の増加を防ぎます

CALL ITEM_MONEY
IF MONEY < A
	PRINTL お金が足りません
	RETURN 0
ENDIF

PRINTL 誰に使いますか？

REPEAT CHARANUM
	TFLAG:COUNT = 0
	;主人公は排除、ただしふたなり、処女再生、胸関連、妊娠関連は可能
	SIF COUNT == MASTER && BOUGHT != 71 && BOUGHT != 72 && BOUGHT != 74 && BOUGHT != 75 && BOUGHT != 91 && BOUGHT != 92
		CONTINUE
	;労役中のキャラも排除
	SIF CFLAG:COUNT:12 > 0
		CONTINUE
	SIF BOUGHT == 70 && BASE:COUNT:0 == MAXBASE:COUNT:0
		CONTINUE
	SIF BOUGHT == 71 && (TALENT:COUNT:ふたなり || TALENT:COUNT:半人半妖 || TALENT:COUNT:オトコ)
		CONTINUE
	SIF BOUGHT == 71 && F == 0 && TALENT:COUNT:調合知識 == 0 && TALENT:COUNT:不思議な根 == 0 && TALENT:COUNT:具現 == 0
		CONTINUE
	SIF BOUGHT == 72 && (TALENT:COUNT:ふたなり == 0 || TALENT:COUNT:半人半妖 || TALENT:COUNT:オトコ)
		CONTINUE
	SIF BOUGHT == 73 && (TALENT:COUNT:母乳体質 || TALENT:COUNT:オトコ)
		CONTINUE
	IF BOUGHT == 74 && (TALENT:COUNT:処女 || TALENT:COUNT:再生処女 || TALENT:COUNT:オトコ)
	;	SIF NO:COUNT != 14 || CFLAG:COUNT:0
			CONTINUE
	ENDIF
	SIF BOUGHT == 75 && (TALENT:COUNT:爆乳 || TALENT:COUNT:オトコ)
		CONTINUE
	SIF BOUGHT == 76 && (TALENT:COUNT:絶壁 || TALENT:COUNT:オトコ)
		CONTINUE
	SIF BOUGHT == 77 && (TALENT:COUNT:母乳体質 == 0 || TALENT:COUNT:オトコ)
		CONTINUE
	SIF BOUGHT == 78 && TALENT:COUNT:おもらし癖 == 0
		CONTINUE
	SIF BOUGHT == 79 && (TALENT:COUNT:オトコ || TALENT:COUNT:妊娠 == 0 || (TALENT:COUNT:妊娠 && CFLAG:COUNT:110 - 3 <= DAY) || BASE:COUNT:0 < 1000)
		CONTINUE
	SIF BOUGHT == 91 && (CFLAG:COUNT:109 || TALENT:COUNT:オトコ)
		CONTINUE
	SIF BOUGHT == 92 && (CFLAG:COUNT:108 || TALENT:COUNT:オトコ)
		CONTINUE
	PRINT 体力
	A = BASE:COUNT:0
	SIF BASE:COUNT:0 < 0
		A = 0
	BAR A,MAXBASE:COUNT:0,16
	PRINTFORM ({A}/{MAXBASE:COUNT:0})
	PRINTFORM  [{COUNT}]%NAME:COUNT% 
	SIF CFLAG:COUNT:1 == 2
		PRINT 助手可
	PRINTL 
	TFLAG:COUNT = 1
REND
PRINTL [100] 戻る

$INPUT_LOOP
INPUT
IF RESULT == 100
	ITEM:BOUGHT = 0
	RETURN 0
ELSE
	IF RESULT < 0 || RESULT >= CHARANUM
		GOTO INPUT_LOOP
	ELSEIF TFLAG:RESULT == 0
		GOTO INPUT_LOOP
	ENDIF
ENDIF

B = RESULT
CALL ITEM_MONEY
MONEY -= A
RESULT = B

;体力回復
IF BOUGHT == 70
	PRINTFORMW ＜%NAME:RESULT%の体力が回復した＞
	BASE:RESULT:0 += 300
	SIF BASE:RESULT:0 > MAXBASE:RESULT:0
		BASE:RESULT:0 = MAXBASE:RESULT:0
	CFLAG:RESULT:10 += 5
ENDIF
;ふたなり化
IF BOUGHT == 71
	PRINTFORMW ＜%NAME:RESULT%はふたなりになった＞
	TALENT:RESULT:不思議な根 = 0
	TALENT:RESULT:具現 = 0
	TALENT:RESULT:ふたなり = 1
	CFLAG:RESULT:10 += 2
ENDIF
;ふたなりを消去
IF BOUGHT == 72
	PRINTFORMW ＜%NAME:RESULT%はふたなりではなくなった＞
	TALENT:RESULT:ふたなり = 0
	CFLAG:RESULT:10 += 2
ENDIF
;母乳体質
IF BOUGHT == 73
	PRINTFORMW ＜%NAME:RESULT%は母乳が出るようになった＞
	TALENT:RESULT:母乳体質 = 1
	CFLAG:RESULT:10 += 2
ENDIF
;処女膜再生
IF BOUGHT == 74
	;条件が揃っているならアリスをSP化(東方専用)
	;IF NO:RESULT == 14 && (TALENT:RESULT:処女 || TALENT:RESULT:再生処女) && CFLAG:RESULT:0 == 0
	;	SIF TARGET >= 0
	;		T = NO:TARGET
	;	SIF ASSI >= 0
	;		A = NO:ASSI
	;	DELCHARA RESULT
	;	ADDSPCHARA 14
	;	PRINTFORMW ＜アリスは子供の姿になった＞
	;	TARGET = CHARANUM-1
	;	CALL CLOTHES_INITIALIZE
	;	REPEAT CHARANUM
	;		SIF NO:COUNT == T
	;			TARGET = COUNT
	;		SIF NO:COUNT == A
	;			ASSI = COUNT
	;	REND
	;	SIF A == 14
	;		ASSI = -1
	;ELSE
		PRINTFORMW ＜%NAME:RESULT%の処女膜が再生された＞
		TALENT:RESULT:再生処女 = 1
	;ENDIF
	CFLAG:RESULT:10 += 2
ENDIF

;豊乳化
IF BOUGHT == 75
	;絶壁→貧乳
	IF TALENT:RESULT:絶壁
		PRINTFORMW ＜%NAME:RESULT%の胸がほんの少しだけ膨らんだ＞
		TALENT:RESULT:絶壁 = 0
		TALENT:RESULT:貧乳 = 1
	;貧乳→普乳
	ELSEIF TALENT:RESULT:貧乳
		PRINTFORMW ＜%NAME:RESULT%の胸が人並みに大きくなった＞
		TALENT:RESULT:貧乳 = 0
	;普乳→巨乳
	ELSEIF TALENT:RESULT:巨乳 == 0
		PRINTFORMW ＜%NAME:RESULT%の胸が豊満になった＞
		TALENT:RESULT:巨乳 = 1
	;巨乳→爆乳
	ELSE
		;小柄体型、小人体型
		IF TALENT:RESULT:小柄体型 || TALENT:RESULT:小人体型
			PRINTFORMW ＜%NAME:RESULT%の胸が体に不釣合いなまでに大きくなった＞
		;それ以外
		ELSE
			PRINTFORMW ＜%NAME:RESULT%の胸が溢れんばかりの大きさになった＞
		ENDIF
		TALENT:RESULT:巨乳 = 0
		TALENT:RESULT:爆乳 = 1
	ENDIF
	CFLAG:RESULT:10 += 2
ENDIF

;縮乳化
IF BOUGHT == 76
	;爆乳→巨乳
	IF TALENT:RESULT:爆乳
		PRINTFORMW ＜%NAME:RESULT%の豊かすぎる胸が一回り小さくなった＞
		TALENT:RESULT:爆乳 = 0
		TALENT:RESULT:巨乳 = 1
	;巨乳→普乳
	ELSEIF TALENT:RESULT:巨乳
		PRINTFORMW ＜%NAME:RESULT%の胸が人並みに小さくなった＞
		TALENT:RESULT:巨乳 = 0
	;普乳→貧乳
	ELSEIF TALENT:RESULT:貧乳 == 0
		PRINTFORMW ＜%NAME:RESULT%の胸が小さくなった＞
		TALENT:RESULT:貧乳 = 1
	;貧乳→絶壁
	ELSE
		PRINTFORMW ＜%NAME:RESULT%の胸が極端に薄くなった＞
		TALENT:RESULT:貧乳 = 0
		TALENT:RESULT:絶壁 = 1
		IF TALENT:RESULT:母乳体質
			PRINTFORMW ＜%NAME:RESULT%の母乳が出なくなった＞
			TALENT:RESULT:母乳体質 = 0
			CFLAG:RESULT:10 += 1
		ENDIF
	ENDIF
	CFLAG:RESULT:10 += 2
ENDIF

;母乳治療
IF BOUGHT == 77
	PRINTFORMW ＜%NAME:RESULT%は母乳が出なくなった＞
	TALENT:RESULT:母乳体質 = 0
	CFLAG:RESULT:10 += 2
ENDIF
;お漏らし治療
IF BOUGHT == 78
	PRINTFORMW ＜%NAME:RESULT%のお漏らし癖が治った＞
	TALENT:RESULT:おもらし癖 = 0
	EXP:RESULT:放尿経験 = 0
	CFLAG:RESULT:10 += 2
ENDIF
;黄色いキャンディ
IF BOUGHT == 79
	A = RESULT
	CALL LAY_EGG
ENDIF

;記憶消去
IF BOUGHT == 90
	ADDCHARA NO:RESULT
	TARGET = CHARANUM-1
	SIF TALENT:RESULT:処女
		TALENT:TARGET:処女 = 1
	SIF TALENT:RESULT:再生処女
		TALENT:TARGET:再生処女 = 1
	SIF TALENT:RESULT:ふたなり
		TALENT:TARGET:ふたなり = 1
	SIF TALENT:RESULT:母乳体質
		TALENT:TARGET:母乳体質 = 1
	EXP:TARGET:Ｖ経験 = EXP:RESULT:Ｖ経験
	EXP:TARGET:Ａ経験 = EXP:RESULT:Ａ経験
	EXP:TARGET:絶頂経験 = EXP:RESULT:絶頂経験
	EXP:TARGET:射精経験 = EXP:RESULT:射精経験
	EXP:TARGET:性交経験 = EXP:RESULT:性交経験
	EXP:TARGET:膣射経験 = EXP:RESULT:膣射経験
	EXP:TARGET:自慰経験 = EXP:RESULT:自慰経験
	EXP:TARGET:調教自慰経験 = EXP:RESULT:調教自慰経験
	EXP:TARGET:精液経験 = EXP:RESULT:精液経験
	EXP:TARGET:奉仕快楽経験 = EXP:RESULT:奉仕快楽経験
	EXP:TARGET:フェラ経験 = EXP:RESULT:フェラ経験
	EXP:TARGET:愛情経験 = EXP:RESULT:愛情経験
	EXP:TARGET:苦痛快楽経験 = EXP:RESULT:苦痛快楽経験
	EXP:TARGET:放尿経験 = EXP:RESULT:放尿経験
	EXP:TARGET:嗜虐快楽経験 = EXP:RESULT:嗜虐快楽経験
	EXP:TARGET:レズ経験 = EXP:RESULT:レズ経験
	EXP:TARGET:異常経験 = EXP:RESULT:異常経験
	EXP:TARGET:緊縛経験 = EXP:RESULT:緊縛経験
	EXP:TARGET:Ｖ拡張経験 = EXP:RESULT:Ｖ拡張経験
	EXP:TARGET:Ａ拡張経験 = EXP:RESULT:Ａ拡張経験
	EXP:TARGET:噴乳経験 = EXP:RESULT:噴乳経験
	EXP:TARGET:触手経験 = EXP:RESULT:触手経験
	EXP:TARGET:吸血経験 = EXP:RESULT:吸血経験
	EXP:TARGET:出産経験 = EXP:RESULT:出産経験
	EXP:TARGET:家事経験 = EXP:RESULT:家事経験
	EXP:TARGET:撮影経験 = EXP:RESULT:撮影経験
	EXP:TARGET:被写経験 = EXP:RESULT:被写経験
	EXP:TARGET:歌唱経験 = EXP:RESULT:歌唱経験
	EXP:TARGET:調教経験 = EXP:RESULT:調教経験
	ABL:TARGET:3 = ABL:RESULT:3
	ABL:TARGET:4 = ABL:RESULT:4
	ABL:TARGET:5 = ABL:RESULT:5
	ABL:TARGET:6 = ABL:RESULT:6
	PRINTFORMW ＜%NAME:RESULT%は調教の記憶を失った＞
	DELCHARA RESULT
	TARGET = CHARANUM-1
	CALL CLOTHES_INITIALIZE
ENDIF

;排卵誘発剤
IF BOUGHT == 91
	PRINTFORMW ＜%NAME:RESULT%に排卵誘発剤を使った＞
	CFLAG:RESULT:109 = 1
ENDIF

;子宮内避妊具
IF BOUGHT == 92
	PRINTFORMW ＜%NAME:RESULT%に子宮内避妊具を使った＞
	CFLAG:RESULT:108 = 1
ENDIF

;アイテムによる崩壊の処理
SIF CFLAG:RESULT:10 >= 100 && TALENT:RESULT:崩壊 == 0
	CALL COLLAPSE_MIND_ITEM

RETURN 0


;産卵イベント　イメージとしては、非常にリスキーだが上手くやれば妊娠を無効にできるイベント
@LAY_EGG
;イベント口上
U = 0
IF ASSI >= 0
	SIF ASSI == RESULT
		U = 1
ENDIF
T = TARGET
TARGET = RESULT
TFLAG:13 = 10
CALL KOJO_MESSAGE_EVENT
RESULT = TARGET
T = TARGET
SIF U
	ASSI = RESULT

PRINTFORMW 卵を産んだ途端、%NAME:RESULT%のお腹は妊娠する前の大きさに戻った
PRINTFORMW %NAME:RESULT%は[%TALENTNAME:153%]を失った
TALENT:RESULT:妊娠 = 0
;妊娠イベントで減った体力の最大値が回復（少なめ）
MAXBASE:RESULT:0 += 300
;胸の縮小処理を実行
PRINTW また、胸の大きさも小さくなったようだ
C = RESULT
CALL REVERSE_B_SIZE
CALL CLEAR_FLAG

RESULT = A
;ストレス値の補正の決定
;ABL:従順をみる
IF ABL:RESULT:0 == 0
	A = 0
ELSEIF ABL:RESULT:0 == 1
	A = 2
ELSEIF ABL:RESULT:0 == 2
	A = 4
ELSEIF ABL:RESULT:0 == 3
	A = 6
ELSEIF ABL:RESULT:0 == 4
	A = 8
ELSE
	A = 10
ENDIF

;ABL:Ｖ感覚をみる
IF ABL:RESULT:4 == 0
	B = 0
ELSEIF ABL:RESULT:4 == 1
	B = 2
ELSEIF ABL:RESULT:4 == 2
	B = 4
ELSEIF ABL:RESULT:4 == 3
	B = 6
ELSEIF ABL:RESULT:4 == 4
	B = 8
ELSE
	B = 10
ENDIF

;EXP:Ｖ経験をみる
IF EXP:RESULT:Ｖ経験 < EXPLV:1
	C = 0
ELSEIF EXP:RESULT:Ｖ経験 < EXPLV:2
	C = 2
ELSEIF EXP:RESULT:Ｖ経験 < EXPLV:3
	C = 4
ELSEIF EXP:RESULT:Ｖ経験 < EXPLV:4
	C = 6
ELSEIF EXP:RESULT:Ｖ経験 < EXPLV:5
	C = 8
ELSE
	C = 10
ENDIF

;EXP:出産経験をみる
IF EXP:RESULT:出産経験 < EXPLV:2
	D = 100
ELSEIF EXP:RESULT:出産経験 < EXPLV:4
	D = 60
ELSEIF EXP:RESULT:出産経験 < EXPLV:5
	D = 40
ELSE
	D = 20
ENDIF

;陥落素質を見る
SIF TALENT:RESULT:恋慕
	E = 120
SIF TALENT:RESULT:淫乱
	E = 80
SIF TALENT:RESULT:服従
	E = 60
SIF TALENT:RESULT:親愛
	E = 150
SIF TALENT:RESULT:娼婦
	E = 60
SIF TALENT:RESULT:隷属
	E = 40
SIF TALENT:RESULT:崩壊
	E = 0

F = D*(100-A+B+C)*E/10000

EXP:RESULT:Ｖ経験 += 5
EXP:RESULT:Ｖ拡張経験 += 1
EXP:RESULT:出産経験 += 1
PRINTL Ｖ経験＋５
PRINTL Ｖ拡張経験＋１
PRINTL 出産経験＋１
IF EXP:RESULT:出産経験 == 1 && EXP:RESULT:Ｖ拡張経験 == 1
	EXP:RESULT:異常経験 += 3
	PRINTFORML 異常経験＋3
ELSEIF EXP:RESULT:出産経験 == 1
	EXP:RESULT:異常経験 += 2
	PRINTFORML 異常経験＋2
ELSEIF EXP:RESULT:Ｖ拡張経験 == 1
	EXP:RESULT:異常経験 += 1
	PRINTFORML 異常経験＋1
ENDIF
CFLAG:RESULT:10 += F
BASE:RESULT:0 = 10
SIF CFLAG:RESULT:10 >= 100 && TALENT:RESULT:崩壊 == 0
	CALL COLLAPSE_MIND_ITEM

PRINTFORMW %NAME:RESULT%は体力を激しく消耗している
PRINTFORMW 少し休ませたほうがよさそうだ
;休憩フラグを立てる
FLAG:0 = 1
BEGIN TURNEND


;アイテムで【崩壊】を得た場合のイベント
@COLLAPSE_MIND_ITEM
PRINTFORMW %CALLNAME:RESULT%の様子がおかしい
PRINTFORMW 投薬の影響によって、%CALLNAME:RESULT%の精神に異常をきたしてしまったようだ
PRINTFORMW %NAME:RESULT%は【崩壊】してしまった
TALENT:RESULT:崩壊 = 1
IF TALENT:RESULT:恋慕 || TALENT:RESULT:淫乱 || TALENT:RESULT:服従 || TALENT:RESULT:親愛 || TALENT:RESULT:娼婦 || TALENT:RESULT:隷属
	PRINTFORM %CALLNAME:RESULT%は[
	SIF TALENT:RESULT:恋慕
		PRINTS TALENTNAME:3
	SIF TALENT:RESULT:淫乱
		PRINTS TALENTNAME:4
	SIF TALENT:RESULT:服従
		PRINTS TALENTNAME:5
	SIF TALENT:RESULT:親愛
		PRINTS TALENTNAME:6
	SIF TALENT:RESULT:娼婦
		PRINTS TALENTNAME:7
	SIF TALENT:RESULT:隷属
		PRINTS TALENTNAME:8
	PRINTW ]を失った
	SIF TALENT:RESULT:恋慕
		TALENT:RESULT:恋慕 = 0
	SIF TALENT:RESULT:淫乱
		TALENT:RESULT:淫乱 = 0
	SIF TALENT:RESULT:服従
		TALENT:RESULT:服従 = 0
	SIF TALENT:RESULT:親愛
		TALENT:RESULT:親愛 = 0
	SIF TALENT:RESULT:娼婦
		TALENT:RESULT:娼婦 = 0
	SIF TALENT:RESULT:隷属
		TALENT:RESULT:隷属 = 0
ENDIF

