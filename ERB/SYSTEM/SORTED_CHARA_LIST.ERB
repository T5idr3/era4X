;-------------------------------------------------
;ソート済のキャラリストを作成
;
;引数
;  CHARA_LIST キャラリストを代入させる配列 (参照型, 長さCHARANUM以上必要、MAX_CHARA_NUM推奨)
;  SORT_KEY   ソート条件
;  ORDER_DESC 真なら降順にソート。省略した場合SHOP画面の設定と同じ
;返値
;  RESULT     CHARA_LISTに入っているキャラ数
;-------------------------------------------------
@CREATE_SORTED_CHARA_LIST(CHARA_LIST, SORT_KEY, ORDER_DESC)
#DIM REF CHARA_LIST
#DIM SORT_KEY
#DIM ORDER_DESC

#DIM SORT_VALUE,  MAX_CHARA_NUM
#DIMS SORT_STRING, MAX_CHARA_NUM
#DIM SORT_BY_STRING
#DIM CHARA_IDX
VARSET CHARA_LIST, -1

VARSET SORT_VALUE, -1
VARSET SORT_STRING
SORT_BY_STRING = 0
SIF SORT_KEY == ソート_手動
	CALL CHECK_MANUAL_SORT_CHARA_LIST

CHARA_IDX = 0
FOR LOCAL, 0, CHARANUM
	SELECTCASE SORT_KEY
		CASE ソート_所属
			SORT_VALUE:CHARA_IDX = CFLAG:LOCAL:所属
		CASE ソート_NO
			SORT_VALUE:CHARA_IDX = NO:LOCAL
		CASE ソート_星
			SORT_VALUE:CHARA_IDX = TMP_CHARA_STARS:LOCAL
		CASE ソート_名前
			SORT_STRING:CHARA_IDX = %ANAME(LOCAL)%
			SORT_BY_STRING = 1
		CASE ソート_好感度
			SORT_VALUE:CHARA_IDX = CFLAG:LOCAL:好感度
		CASE ソート_従属度
			SORT_VALUE:CHARA_IDX = CFLAG:LOCAL:従属度
		CASE ソート_支配度
			SORT_VALUE:CHARA_IDX = CFLAG:LOCAL:支配度
		CASE ソート_妊娠
			IF CFLAG:LOCAL:行動不能状態 == 行動不能_臨月
				SORT_VALUE:CHARA_IDX = 2
			ELSEIF TALENT:LOCAL:妊娠
				SORT_VALUE:CHARA_IDX = 1
			ELSEIF CFLAG:LOCAL:行動不能状態 == 行動不能_育児
				SORT_VALUE:CHARA_IDX = 3
			ELSEIF CFLAG:LOCAL:行動不能状態 == 行動不能_子供
				SORT_VALUE:CHARA_IDX = 4
			ELSE
				SORT_VALUE:CHARA_IDX = __INT_MAX__
			ENDIF
		CASE ソート_性別
			SORT_VALUE:CHARA_IDX = TALENT:LOCAL:性別
		CASE ソート_面識
			SORT_VALUE:CHARA_IDX = CFLAG:LOCAL:面識
		CASE ソート_武闘
			SORT_VALUE:CHARA_IDX = ABL:LOCAL:白兵
		CASE ソート_防衛
			SORT_VALUE:CHARA_IDX = ABL:LOCAL:操艦
		CASE ソート_知略
			SORT_VALUE:CHARA_IDX = ABL:LOCAL:知略
		CASE ソート_政治
			SORT_VALUE:CHARA_IDX = ABL:LOCAL:政治
		CASE ソート_歌唱
			SORT_VALUE:CHARA_IDX = ABL:LOCAL:歌唱
		CASE ソート_料理
			SORT_VALUE:CHARA_IDX = ABL:LOCAL:料理
		CASE ソート_妖術
			SORT_VALUE:CHARA_IDX = ABL:LOCAL:超能力
		CASE ソート_四能力
			SORT_VALUE:CHARA_IDX = ABL:LOCAL:白兵 + ABL:LOCAL:操艦 + ABL:LOCAL:知略 + ABL:LOCAL:政治
		CASE ソート_七能力
			SORT_VALUE:CHARA_IDX = ABL:LOCAL:白兵 + ABL:LOCAL:操艦 + ABL:LOCAL:知略 + ABL:LOCAL:政治 + ABL:LOCAL:料理 + ABL:LOCAL:歌唱 + ABL:LOCAL:超能力
		CASE ソート_手動
			SIF GETBIT(MANUAL_SORTED:LOCAL:0, 手動ソート_非表示)
				CONTINUE
			FINDELEMENT MANUAL_SORT_CHARA_LIST, LOCAL
			SORT_VALUE:CHARA_IDX = RESULT
		CASE ソート_夜這い助手
			SORT_VALUE:CHARA_IDX = YOBAI_ZYOSYU_CALC_POINT(LOCAL)
		CASE ソート_手引助手
			SORT_VALUE:CHARA_IDX = TEBIKI_ZYOSYU_CALC_POINT(LOCAL)
	ENDSELECT

	CHARA_LIST:CHARA_IDX = LOCAL
	CHARA_IDX++
NEXT

FOR LOCAL, 1, CHARA_IDX
	FOR LOCAL:11, LOCAL,0, -1
		LOCAL:12 = CHARA_LIST:(LOCAL:11-1)
		LOCAL:13 = CHARA_LIST:(LOCAL:11)
		IF !SORT_BY_STRING
			IF (ORDER_DESC && SORT_VALUE:(LOCAL:12) < SORT_VALUE:(LOCAL:13)) || (!ORDER_DESC && SORT_VALUE:(LOCAL:12) > SORT_VALUE:(LOCAL:13))
				SWAP CHARA_LIST:(LOCAL:11), CHARA_LIST:(LOCAL:11 -1)
			ELSE
				BREAK
			ENDIF
		ELSE
			IF (ORDER_DESC && SORT_STRING:(LOCAL:12) > SORT_STRING:(LOCAL:13)) || (!ORDER_DESC && SORT_STRING:(LOCAL:12) < SORT_STRING:(LOCAL:13))
				SWAP CHARA_LIST:(LOCAL:11), CHARA_LIST:(LOCAL:11 -1)
			ELSE
				BREAK
			ENDIF
		ENDIF
	NEXT
NEXT

RETURN CHARA_IDX

;-------------------------------------------------
;ソート条件選択
;-------------------------------------------------
@SORT_COND_SELECT(CANCEL_ENABLED = 1, AUTO_ONLY = 0)
#DIM CANCEL_ENABLED
#DIM AUTO_ONLY
#DIM SORTKEY_ITER

CALL SINGLE_DRAWLINE
FOR SORTKEY_ITER, 0, NUM_SORTKEY
	SIF SORTKEY_ITER == ソート_手動 && AUTO_ONLY
		CONTINUE
	PRINTFORML [{SORTKEY_ITER, 2, RIGHT}] %STR_NOW_SORTKEY:SORTKEY_ITER%
NEXT
SIF CANCEL_ENABLED
	PRINTFORML [99] キャンセル

$INPUT_LOOP
INPUT

IF RESULT == ソート_手動 && !AUTO_ONLY
	RETURN RESULT
ELSEIF INRANGE(RESULT, 0, NUM_SORTKEY - 1) && RESULT != ソート_手動
	RETURN RESULT
ELSEIF RESULT == 99 && CANCEL_ENABLED
	RETURN -1
ENDIF
CLEARLINE 1
GOTO INPUT_LOOP

;-------------------------------------------------
;手動ソート用関数
;-------------------------------------------------

;他の自動ソートを選択し、手動ソートリストにコピーする
@INIT_MANUAL_SORT_CHARA_LIST
#DIM CHARA_IDX
#DIM CHARA_LIST, MAX_CHARA_NUM
#DIM SORT_KEY
#DIM ORDER_DESC
#DIM LIST_LENGTH

CALL SORT_COND_SELECT(0, 1)
SORT_KEY = RESULT
PRINTL ソート順を選択してください
CALL ASK_MULTI("昇順", "降順")
ORDER_DESC = RESULT
CALL CREATE_SORTED_CHARA_LIST(CHARA_LIST, SORT_KEY, ORDER_DESC)
LIST_LENGTH = RESULT

VARSET MANUAL_SORT_CHARA_LIST, -1
FOR CHARA_IDX, 0, CHARANUM
	CLEARBIT MANUAL_SORTED:CHARA_IDX:0, 手動ソート_設定済
NEXT
FOR CHARA_IDX, 0, LIST_LENGTH
	MANUAL_SORT_CHARA_LIST:CHARA_IDX = CHARA_LIST:CHARA_IDX
	SETBIT MANUAL_SORTED:(CHARA_LIST:CHARA_IDX):0, 手動ソート_設定済
NEXT

;手動ソートに追加されていないキャラがいるか確認し、いれば追加する
;その後必要ならば設定画面を呼ぶ
@CHECK_MANUAL_SORT_CHARA_LIST(NEED_SETTING = 0)
#DIM NEED_SETTING
#DIM LIST_LENGTH
#DIM CHARA_IDX

FINDELEMENT MANUAL_SORT_CHARA_LIST, -1
IF RESULT == -1
	NEED_SETTING = 1
	VARSET MANUAL_SORT_CHARA_LIST, -1
	LIST_LENGTH = 0
ELSE
	LIST_LENGTH = RESULT
ENDIF

FOR CHARA_IDX, 0, CHARANUM
	SIF GETBIT(MANUAL_SORTED:CHARA_IDX:0, 手動ソート_設定済)
		CONTINUE
	MANUAL_SORT_CHARA_LIST:LIST_LENGTH = CHARA_IDX
	LIST_LENGTH++
	SETBIT MANUAL_SORTED:CHARA_IDX:0, 手動ソート_設定済
NEXT

SIF NEED_SETTING
	CALL SETTING_MANUAL_SORT_CHARA_LIST
RETURN

; キャラの並び替え設定UI
@SETTING_MANUAL_SORT_CHARA_LIST
#DIM LIST_LENGTH
#DIM CHARA_IDX
#DIM CHARA_ID
#DIM SELECTED_CHARA_IDX

FINDELEMENT MANUAL_SORT_CHARA_LIST, -1
LIST_LENGTH = RESULT
SELECTED_CHARA_IDX = -1

$INPUT_LOOP

CALL SINGLE_DRAWLINE
FOR CHARA_IDX, 0, LIST_LENGTH
	CHARA_ID = MANUAL_SORT_CHARA_LIST:CHARA_IDX
	IF SELECTED_CHARA_IDX == CHARA_IDX
		SETCOLOR 0x00FF00
		PRINTFORM [{CHARA_IDX, 4, RIGHT}] %ANAME(CHARA_ID), MAX_CHARANAME_LENGTH%
		RESETCOLOR
	ELSEIF GETBIT(MANUAL_SORTED:CHARA_ID:0, 手動ソート_非表示)
		SETCOLOR 0x666666
		PRINTFORM [{CHARA_IDX, 4, RIGHT}] %ANAME(CHARA_ID), MAX_CHARANAME_LENGTH%
		RESETCOLOR
	ELSE
		PRINTFORM [{CHARA_IDX, 4, RIGHT}] %ANAME(CHARA_ID), MAX_CHARANAME_LENGTH%
	ENDIF
	PRINTS " " * 8
	IF GETBIT(MANUAL_SORTED:CHARA_ID:0, 手動ソート_非表示) && MASTER != CHARA_ID
		PRINTBUTTON "[表示する]", 1p12 + CHARA_IDX
		PRINTS " " * 2
		SETCOLOR 0x666666
		PRINTS "[表示しない]"
		RESETCOLOR
	ELSEIF MASTER != CHARA_ID
		SETCOLOR 0x666666
		PRINTS "[表示する]"
		RESETCOLOR
		PRINTS " " * 2
		PRINTBUTTON "[表示しない]", 1p12 + CHARA_IDX
	ENDIF
	PRINTL 
NEXT
PRINTFORML [9998] 自動ソートをコピー
PRINTFORML [9999] 設定完了

INPUT

IF RESULT == 9999
	RETURN
ELSEIF RESULT == 9998
	CALL INIT_MANUAL_SORT_CHARA_LIST
	FINDELEMENT MANUAL_SORT_CHARA_LIST, -1
	LIST_LENGTH = RESULT
	SELECTED_CHARA_IDX = -1
ELSEIF RESULT & 1p12
	RESULT &= ~1p12
	IF RESULT >= 0 && RESULT < LIST_LENGTH && MASTER != CHARA_ID
		INVERTBIT MANUAL_SORTED:(MANUAL_SORT_CHARA_LIST:RESULT):0, 手動ソート_非表示
	ENDIF
ELSEIF RESULT >= 0 && RESULT < LIST_LENGTH
	IF SELECTED_CHARA_IDX == -1
		SELECTED_CHARA_IDX = RESULT
	ELSEIF SELECTED_CHARA_IDX == RESULT
		SELECTED_CHARA_IDX = -1
	ELSE
		SWAP MANUAL_SORT_CHARA_LIST:RESULT, MANUAL_SORT_CHARA_LIST:SELECTED_CHARA_IDX
		SELECTED_CHARA_IDX = -1
	ENDIF
ENDIF

GOTO INPUT_LOOP
