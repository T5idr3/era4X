@SP_COUNTRY_SETTING(強制出現 = 0, 出現設定 = 1)
#DIM 強制出現
#DIM 出現設定
#DIM FIRST_LINE
#DIM 勢力定数
#DIM 選択肢

SIF 強制出現
	VARSET SP_COUNTRY_RANK, 1

CALL SINGLE_DRAWLINE
PRINTL 特殊勢力（捕えた女の子にスケベなことをする勢力）について設定します
PRINTL ※初回プレイ時は強さ0（出現しない）にすることをおすすめします
PRINTL ※「強さ3」・「途中から出現」で相応の手応えです
PRINTL ※性別設定は、ターンエンド時の捕虜調教描写は非対応です
CALL SINGLE_DRAWLINE

FIRST_LINE = LINECOUNT

$SHOW_LOOP

FOR LOCAL, 0, MAX_SP_COUNTRY
	SIF GET_COUNTRY_FROM_ID(SP_COUNTRY_ID:LOCAL) == -1
		CALL PRINT_EACH_SP_COUNTRY_SETTING(LOCAL, 強制出現, 出現設定)
NEXT

CALL SINGLE_DRAWLINE
PRINTBUTTON "[完了]", 9999

INPUT

SELECTCASE RESULT
	CASE 0 TO 999
		勢力定数 = RESULT / 10
		選択肢 = RESULT % 10
		SIF !INRANGE(勢力定数, 0, MAX_SP_COUNTRY - 1) || 勢力定数 == 特殊勢力_ダミー || !INRANGE(選択肢, 0, 5)
			GOTO SKIPPED
		SP_COUNTRY_RANK:勢力定数 = 選択肢
	CASE 1000 TO 1999
		RESULT = RESULT - 1000
		勢力定数 = RESULT / 10
		選択肢 = RESULT % 10
		SIF !INRANGE(勢力定数, 0, MAX_SP_COUNTRY - 1) || 勢力定数 == 特殊勢力_ダミー || !INRANGE(選択肢, 0, 3)
			GOTO SKIPPED
		SP_COUNTRY_BOSSNAME:勢力定数 = 選択肢
		IF 選択肢 == 3
			PRINTL 姓を入力して下さい
			PRINTL (『チルノ』のような姓なしキャラにしたい場合、入力しない)
			INPUTS
			SP_COUNTRY_BOSSNAME_LAST_NAME:勢力定数  = %RESULTS%
			PRINTL 名前を入力して下さい
			$NAME_INPUT_LOOP
			INPUTS
			IF RESULTS == ""
				PRINTL 名前は入力必須です
				PRINTL 再入力して下さい
				GOTO NAME_INPUT_LOOP
			ENDIF
			SP_COUNTRY_BOSSNAME_FIRST_NAME:勢力定数 = %RESULTS%
		ENDIF
	CASE 2000 TO 2999
		RESULT = RESULT - 2000
		勢力定数 = RESULT / 10
		選択肢 = RESULT % 10
		SIF !INRANGE(勢力定数, 0, MAX_SP_COUNTRY - 1) || 勢力定数 == 特殊勢力_ダミー || !INRANGE(選択肢, 0, 1) || !出現設定
			GOTO SKIPPED
		SP_COUNTRY_INITIAL_RISE:勢力定数 = 選択肢
	CASE 3000, 3001
		SIF !出現設定
			GOTO SKIPPED
		勢力定数 = 特殊勢力_野盗
		選択肢 = RESULT - 3000
		BANDIT_HAS_INITIAL_CITIES = 選択肢
	CASE 4000 TO 4999
		RESULT = RESULT - 4000
		勢力定数 = RESULT / 10
		選択肢 = RESULT % 10 - 1
		SIF !INRANGE(勢力定数, 0, MAX_SP_COUNTRY - 1) || !INRANGE(選択肢, -1, 2)
			GOTO SKIPPED
		SP_COUNTRY_GENDER:勢力定数 = 選択肢
	CASE 9999
		FOR LOCAL, 0, MAX_SP_COUNTRY
			SIF LOCAL != 特殊勢力_野盗 && GET_COUNTRY_FROM_ID(SP_COUNTRY_ID:LOCAL) == -1 && SP_COUNTRY_RANK:LOCAL > 0 && SP_COUNTRY_INITIAL_RISE:LOCAL
				TRYCALLFORM %SP_COUNTRY_NAME_ENG:LOCAL%_RISE()
			IF LOCAL == 特殊勢力_野盗 && GET_COUNTRY_FROM_ID(SP_COUNTRY_ID:特殊勢力_野盗) == -1 && SP_COUNTRY_INITIAL_RISE:LOCAL
				CALL BANDIT_INIT(1)
				SIF BANDIT_HAS_INITIAL_CITIES
					CALL BANDIT_RISE()
			ENDIF
		NEXT
		RETURN
ENDSELECT

$SKIPPED

CLEARLINE LINECOUNT - FIRST_LINE
GOTO SHOW_LOOP

;-------------------------------------------------------
;個々の勢力の設定ボタンを出力
;-------------------------------------------------------
@PRINT_EACH_SP_COUNTRY_SETTING(定数, 強制出現 = 0, 出現設定 = 1)
#DIM 定数
#DIM 強制出現
#DIM 出現設定

;ダミー勢力は禁止
SIF 定数 == 特殊勢力_ダミー
	RETURN

;定数範囲外を渡されると死ぬので防御
SIF !INRANGE(定数, 0, MAX_SP_COUNTRY - 1)
	RETURN

;既に存在する場合飛ばす(シナリオファイルでRISEしたあとのNEWLOOP側での設定を想定）
SIF GET_COUNTRY_FROM_ID(SP_COUNTRY_ID:定数) != -1
	RETURN 

SIF 強制出現 && SP_COUNTRY_RANK:定数 == 0
	SP_COUNTRY_RANK:定数 = 1

PRINTFORM %SP_COUNTRY_NAME:定数, 16, LEFT%
PRINTL

PRINTFORM 強さ:
;強さボタンの表示
FOR LOCAL, 0, 6
	SIF 強制出現 && LOCAL == 0
		CONTINUE
	CALL PRINTBUTTON_EX(@"[{LOCAL}]", 定数 * 10 + LOCAL, 1, SP_COUNTRY_RANK:定数 == LOCAL)
NEXT

PRINTL

PRINTFORM 名前:
CALL PRINTBUTTON_EX("[勢力名]",   定数 * 10 + 1000, SP_COUNTRY_RANK:定数, SP_COUNTRY_BOSSNAME:定数 == 0)
CALL PRINTBUTTON_EX("[固有名]",   定数 * 10 + 1001, SP_COUNTRY_RANK:定数, SP_COUNTRY_BOSSNAME:定数 == 1)
CALL PRINTBUTTON_EX("[ランダム]", 定数 * 10 + 1002, SP_COUNTRY_RANK:定数, SP_COUNTRY_BOSSNAME:定数 == 2)
CALL PRINTBUTTON_EX("[手入力]",   定数 * 10 + 1003, SP_COUNTRY_RANK:定数, SP_COUNTRY_BOSSNAME:定数 == 3)

PRINTL

PRINTFORM 性別:
CALL PRINTBUTTON_EX("[デフォルト]", 定数 * 10 + 4000, SP_COUNTRY_RANK:定数, SP_COUNTRY_GENDER:定数 == -1)
CALL PRINTBUTTON_EX("[男]",         定数 * 10 + 4001, SP_COUNTRY_RANK:定数, SP_COUNTRY_GENDER:定数 == 0)
CALL PRINTBUTTON_EX("[女]",         定数 * 10 + 4002, SP_COUNTRY_RANK:定数, SP_COUNTRY_GENDER:定数 == 1)
CALL PRINTBUTTON_EX("[女双]",       定数 * 10 + 4003, SP_COUNTRY_RANK:定数, SP_COUNTRY_GENDER:定数 == 2)
PRINTL


SIF !出現設定
	RETURN

PRINTFORM 出現:
CALL PRINTBUTTON_EX("[最初から]", 定数 * 10 + 2001, SP_COUNTRY_INITIAL_RISE:定数 || (SP_COUNTRY_RANK:定数 && GET_COUNTRY_NUM() + MATCH(SP_COUNTRY_INITIAL_RISE, 1) < MAX_COUNTRY - 1), SP_COUNTRY_INITIAL_RISE:定数 == 1)
CALL PRINTBUTTON_EX("[途中から]", 定数 * 10 + 2000, SP_COUNTRY_RANK:定数, SP_COUNTRY_INITIAL_RISE:定数 == 0)
PRINTL

IF 定数 == 特殊勢力_野盗
	PRINTFORM 出現時に領土を
	CALL PRINTBUTTON_EX("[奪う]" , 3001, SP_COUNTRY_RANK:定数,     BANDIT_HAS_INITIAL_CITIES == 1)
	CALL PRINTBUTTON_EX("[奪わない]" , 3000, SP_COUNTRY_RANK:定数, BANDIT_HAS_INITIAL_CITIES == 0)
	PRINTL
ENDIF

