;=============================================================================
; 搭乗可能な奴隷の一覧表示
;=============================================================================
@SHOW_BOARDING_SLAVE_LIST
#DIM ITER
#DIM FIRST_LINE
#DIMS MESSAGE

;搭乗員設定のページ数の計算
LOCAL:2 = 0
FOR ITER, 0, CHARANUM
	;マスターと所属勢力が同じキャラかつ捕虜でない
	SIF !CFLAG:(ITER):捕虜先 && CFLAG:ITER:所属 == 国家ID_プレイヤー
		LOCAL:2 ++
NEXT
;最大ページ数
LOCAL:2 = (LOCAL:2 - 1) / 30 + 1
;現在のページ数
LOCAL:3 = 1
FIRST_LINE = LINECOUNT

$SHOW_LOOP
;所属キャラクター数のカウント用
LOCAL:4 = 0
;ページに表示する最大キャラクター数(30人毎)
LOCAL:5 = (LOCAL:3 - 1) * 30
LOCAL:6 = LOCAL:3 * 30
DRAWLINE
PRINTFORM %TOSTR_SPACE(22)%   兵|  操|  知|  政|  超|  歌|  料
PRINTL
FOR ITER, 0, CHARANUM
	SIF CFLAG:ITER:捕虜先 == 国家ID_プレイヤー || CFLAG:ITER:所属 != 国家ID_プレイヤー
		CONTINUE
	IF LOCAL:4 >= LOCAL:5 && LOCAL:4 < LOCAL:6
		PRINTFORM [{ITER, 3, RIGHT}] %CALLNAME:ITER, MAX_CHARANAME_LENGTH, LEFT%
		;能力値の表示
		CALL PRINT_ALPHABET_RANK(ランク_ＳＬＧ, ABL:(ITER):白兵)
		PRINTFORM {ABL:(ITER):白兵, 3}|
		CALL PRINT_ALPHABET_RANK(ランク_ＳＬＧ, ABL:(ITER):操艦)
		PRINTFORM {ABL:(ITER):操艦, 3}|
		CALL PRINT_ALPHABET_RANK(ランク_ＳＬＧ, ABL:(ITER):知略)
		PRINTFORM {ABL:(ITER):知略, 3}|
		CALL PRINT_ALPHABET_RANK(ランク_ＳＬＧ, ABL:(ITER):政治)
		PRINTFORM {ABL:(ITER):政治, 3}|
		CALL PRINT_ALPHABET_RANK(ランク_ＳＬＧ, ABL:(ITER):超能力)
		PRINTFORM {ABL:(ITER):超能力, 3}|
		CALL PRINT_ALPHABET_RANK(ランク_ＳＬＧ, ABL:(ITER):歌唱)
		PRINTFORM {ABL:(ITER):歌唱, 3}|
		CALL PRINT_ALPHABET_RANK(ランク_ＳＬＧ, ABL:(ITER):料理)
		PRINTFORM {ABL:(ITER):料理, 3}
		MESSAGE = フリー
		IF BOARDING_SHIP_ID:ITER != -1
			MESSAGE = %SHIP_NAME:(BOARDING_SHIP_ID:ITER)%の艦長
		ELSEIF RIDING_WITH:ITER != -1
			MESSAGE = %SHIP_NAME:(RIDING_WITH:ITER)%に搭乗中
		ENDIF
		PRINTFORM %TOSTR_SPACE(5)% %MESSAGE%
		PRINTL
	ENDIF
	LOCAL:4 ++
NEXT

DRAWLINE
IF LOCAL:2 >= 2
	IF LOCAL:3 > 1
		PRINT [1001] 前のページ            
	ELSE
		PRINT                             
	ENDIF
	LOCALS:0 = page{LOCAL:3}/{LOCAL:2}
	PRINTPLAINFORM %LOCALS:0, 28, LEFT%
	IF LOCAL:3 < LOCAL:2
		PRINT [1002] 後のページ
	ENDIF
	PRINTL 
	DRAWLINE
ENDIF
PRINT [9999] 戻る
PRINTL

INPUT
;ページ数の増減
IF RESULT == 1001 && LOCAL:3 > 1
	LOCAL:3 --
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP
ELSEIF RESULT == 1002 && LOCAL:3 < LOCAL:2
	LOCAL:3 ++
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP
ELSE
	;入力値をそのまま返す 呼び出し先で9999の場合の戻る先の設定が必要
	RETURN RESULT
ENDIF

;旧表示をコメントアウト
;@SHOW_BOARDING_SLAVE_LIST
;#DIM ITER
;FOR ITER, 0, CHARANUM
;	SIF CFLAG:ITER:捕虜先 == 国家ID_プレイヤー || CFLAG:ITER:所属 != 国家ID_プレイヤー
;		CONTINUE
;	IF BOARDING_SHIP_ID:ITER != -1
;		PRINTBUTTON @"[{ITER, 2, RIGHT}] %CALLNAME:ITER, 30, LEFT% %SHIP_NAME:(BOARDING_SHIP_ID:ITER)%の艦長", ITER
;	ELSEIF RIDING_WITH:ITER != -1
;		PRINTBUTTON @"[{ITER, 2, RIGHT}] %CALLNAME:ITER, 30, LEFT% %SHIP_NAME:(RIDING_WITH:ITER)%搭乗中", ITER
;	ELSE
;		PRINTBUTTON @"[{ITER, 2, RIGHT}] %CALLNAME:ITER, 30, LEFT%", ITER
;	ENDIF
;PRINTL
;NEXT

@MOORING(CHARA_ID = 0)
#FUNCTION
#DIM CHARA_ID
#DIM ID

FOR ID, 0, 星系数
	SIF 星系座標ID:ID == CHARACTER_POSITION:CHARA_ID
		RETURNF 1
NEXT

FOR ID, 0, MAX_PORT
	SIF PORT_POSITION_ID:ID == CHARACTER_POSITION:CHARA_ID
		RETURNF 1
NEXT

RETURNF 0

;座標からどの星にいるかを返却する
@GET_STAR_ID_FROM_POS(POS)
#DIM POS

FINDELEMENT 星系座標ID, POS
RETURN RESULT

;座標からどの星にいるかを返却する
@GET_PORT_ID_FROM_POS(POS)
#DIM POS

FINDELEMENT PORT_POSITION_ID, POS
RETURN RESULT

;各種タスクから解放する
@RELEASE_FROM_TASK(ID)
#DIM ID
#DIM 塔乗艦ID

塔乗艦ID = BOARDING_SHIP_ID:ID
IF 塔乗艦ID != -1
	BOARDING_SHIP_ID:ID = -1
	SHIP_MISSION:塔乗艦ID = 0
	SHIP_FOLLOW:塔乗艦ID = -1
	SHIP_PATROL:塔乗艦ID = -1
ENDIF
塔乗艦ID = RIDING_WITH:ID
SIF 塔乗艦ID != -1
	RIDING_WITH:ID = -1
FINDELEMENT R_SUPERVISER_IS, ID
SIF RESULT != -1
	R_SUPERVISER_IS:RESULT = 0
FINDELEMENT SHOP_IN_CHARGE, ID
SIF RESULT != -1
	SHOP_IN_CHARGE:RESULT = -1


; 乗船以外のジョブから解任
@DISMISSAL_JOB(ID)
#DIM ID
FINDELEMENT R_SUPERVISER_IS, ID
IF RESULT != -1
	R_SUPERVISER_IS:RESULT = 0
	R_SUPERVISER_COST:RESULT = 0
ENDIF
SIF R_MILITARY_ADVISER == ID
	R_MILITARY_ADVISER = -1

@TO_FREE(ID)
#DIM ID
CALL RELEASE_FROM_TASK(ID)
CALL DISMISSAL_JOB(ID)
