;調教対象選択(eratohoR+ 1.31)
@CHANGE_TARGET
IF TARGET >= 0
	PRINT 現在
	PRINTFORML %NAME:TARGET%を調教中
ENDIF
PRINTL 今回は誰を調教しますか？
R = 1
CALL LIFE_LIST
PRINTFORML  [100]戻る

$INPUT_LOOP
INPUT
IF RESULT == 100
	RETURN 0
ELSEIF RESULT < 1 || RESULT >= CHARANUM
	GOTO INPUT_LOOP
ELSE
	SIF CFLAG:RESULT:12 > 0
		GOTO INPUT_LOOP
	C = RESULT
	CALL CHECK_CHILD_CARE
	SIF RESULT == 1
		GOTO INPUT_LOOP
ENDIF

SIF C != 0
	TARGET = C
SIF TARGET == ASSI
	ASSI = -1

@SELECT_ASSI
PRINT 現在
IF ASSI >= 0
	PRINTFORML [{ASSI}]%NAME:ASSI%が助手
ELSE
	PRINTFORML 助手はいません
ENDIF
PRINTL 誰を助手にしますか？(現在の助手は☆)

PRINTFORML   [ 0]助手なし
REPEAT CHARANUM
	TFLAG:COUNT = 0
	;0（主人公）とASSI以外は排除
	SIF COUNT == MASTER
		CONTINUE
	SIF CFLAG:COUNT:1 < 2
		CONTINUE
	SIF CFLAG:COUNT:12 > 0
		CONTINUE
	;育児部屋に移動しているキャラは除外
	C = COUNT
	CALL CHECK_CHILD_CARE
	SIF RESULT == 1
		CONTINUE
	IF COUNT == ASSI
		PRINT ☆
	ELSE
		PRINT 　
	ENDIF
	IF COUNT<10
		PRINTFORM [ {COUNT}]
	ELSE
		PRINTFORM [{COUNT}]
	ENDIF
	CALL ARRANGE_CHARANAME
	IF COUNT == ASSI
		PRINT 　　　　　
	ELSEIF ISASSI:COUNT == 1
		PRINT [元助手] 
	ELSE
		PRINT [未経験] 
	ENDIF
	PRINTFORM %ABLNAME:2%Lv{ABL:COUNT:2} %ABLNAME:11%Lv{ABL:COUNT:11} %ABLNAME:17%Lv{ABL:COUNT:17} 
	SIF TALENT:COUNT:両刀
		PRINT 両刀
	PRINTFORML 
REND
PRINTFORML  [100]戻る

$INPUT_LOOP
INPUT
IF RESULT == 100
	RETURN 0
ELSEIF RESULT == 0
	ASSI = -1
	RETURN 0
ELSEIF RESULT < 0 || RESULT >= CHARANUM
	GOTO INPUT_LOOP
ELSEIF RESULT == MASTER
	GOTO INPUT_LOOP
ELSEIF CFLAG:RESULT:12 > 0
	GOTO INPUT_LOOP
ELSEIF CFLAG:RESULT:1 < 2
	GOTO INPUT_LOOP
ELSE
	C = RESULT
	CALL CHECK_CHILD_CARE
	SIF RESULT == 1
		GOTO INPUT_LOOP
ENDIF

ASSI = C
CALL ASSI_CHANGE
ISASSI:ASSI = 1
SIF ASSI == TARGET
	TARGET = -1


@LIFE_LIST
#DIM CHARA_ID
#DIM 体力
DRAWLINE
PRINTFORML 　[0]%NAME:MASTER%(主人)
DRAWLINE
FOR CHARA_ID, 1, CHARANUM
	;主人公と助手対象と調教中キャラは排除
	SIF CHARA_ID == MASTER
		CONTINUE
	;労役中のキャラは除外
	SIF CFLAG:CHARA_ID:12 > 0
		CONTINUE
	;育児部屋に移動しているキャラは除外
	CALL CHECK_CHILD_CARE
	SIF RESULT == 1
		CONTINUE
	IF CHARA_ID == TARGET
		PRINT ☆
	ELSEIF CHARA_ID == ASSI
		PRINT ★
	ELSEIF ISASSI:CHARA_ID == 1
		PRINT ●
	ELSEIF CFLAG:CHARA_ID:1 == 2
		PRINT ○
	ELSE
		PRINT 　
	ENDIF
	PRINTBUTTON @"[{CHARA_ID, 2, RIGHT}] %CALLNAME:CHARA_ID, 20, LEFT%", CHARA_ID
	PRINT 体力
	体力 = BASE:CHARA_ID:0
	SIF BASE:CHARA_ID:0 < 0
		体力 = 0
	BAR 体力,MAXBASE:CHARA_ID:0,10
	PRINTFORM ({体力}/{MAXBASE:CHARA_ID:0})

	SIF TALENT:CHARA_ID:恋慕
		PRINT 恋慕 
	SIF TALENT:CHARA_ID:淫乱
		PRINT 淫乱 
	SIF TALENT:CHARA_ID:服従
		PRINT 服従 
	SIF TALENT:CHARA_ID:親愛
		PRINT 親愛 
	SIF TALENT:CHARA_ID:娼婦
		PRINT 娼婦 
	SIF TALENT:CHARA_ID:隷属
		PRINT 隷属 
	SIF TALENT:CHARA_ID:崩壊
		PRINT 崩壊 
	SIF CFLAG:CHARA_ID:0 > 0
		PRINT EX 
	IF TALENT:CHARA_ID:オトコ
		PRINT 男 
	ELSE
		SIF TALENT:CHARA_ID:処女
			PRINT 処女
		SIF TALENT:CHARA_ID:再生処女
			PRINT 再生処女
	ENDIF
	PRINTL 
NEXT
PRINTL 　　　　☆：調教中　★：助手　○：助手可　●：元助手　を表しています

;ステータス表示
@SHOW_CHARADATA
CALL LIFE_LIST
PRINTFORML  [100]戻る

$INPUT_LOOP
INPUT
IF RESULT == 100
	RETURN 0
ELSEIF RESULT < 0 || RESULT >= CHARANUM
	GOTO INPUT_LOOP
ENDIF

PRINTL ･･･････････････････････････････････････････････････････････････････････････････
T = RESULT
PRINTFORM %NAME:T%　　
CALL SHOW_RACE
IF T != MASTER && TALENT:MASTER:テレパシー
	PRINTFORM (依存度:{CFLAG:T:2} ストレス値{CFLAG:T:10} 
	PRINTL 
ELSEIF T == MASTER
	PRINTFORM (堕した人数:{FLAG:30+FLAG:31+FLAG:32}人)　
	PRINT 難易度:
	IF FLAG:4 == 1
		PRINTL EASY
	ELSEIF FLAG:4 == 2
		PRINTL NORMAL
	ELSEIF FLAG:4 == 3
		PRINTL HARD
	ENDIF
ENDIF
DRAWLINE
PRINTL □ 珠
CALL SHOW_INFO_JUEL
CALL SHOW_INFO(T)
RESTART

;------------------------------
; キャラ購入
;------------------------------
@CHARA_BUY

$INPUT_LOOP
CALL CHARA_BUY_LIST

INPUT
IF RESULT >= 0 && RESULT <= 4
	CALL RANDOM_CHARA(RESULT, 1000)
	GOTO INPUT_LOOP
ELSEIF RESULT == 9
	RETURN
ENDIF

; キャラ一覧
@CHARA_BUY_LIST
DRAWLINE
PRINTFORML 奴隷商「 ようこそ。ご案内させていただきます」
DRAWLINE
PRINTFORML 所持金: %TOSTR(MONEY, "$###,###,###,###")%
PRINTBUTTON "[0]適当に見繕う($1,000)", 0
PRINTL
PRINTBUTTON "[1]JK($1,000)", 1
PRINTL
PRINTBUTTON "[2]JD($1,000)", 2
PRINTL
PRINTBUTTON "[3]OL($1,000)", 3
PRINTL
PRINTBUTTON "[4]未亡人($1,000)", 4
PRINTL
DRAWLINE
PRINTBUTTON "[9] 戻る", 9
PRINTL
