;電子生命体/幽霊にロボット/人形を与えるデイリー
;謎のロボット作成者/人形造形士は電子生命体/幽霊に欲情する変態です
;毎回性別が変わるけど、別人なのか、同一人物が性転換してるのかは謎

;---------------------
;基本的な発生確率(1000分率 100で10%)
;これにコンフィグ項目の発生頻度がかけられるので、必ずしもこの通りにはならない
;---------------------
@EVENT_DAILY_DOLL_CREATER_RATE()
RETURN 50

;---------------------
;確率以外の発生判定
;〇期以降になると発生するとか、デイリー側で利用している変数が-1だったら起こさない　みたいなはじき方をするときに使う
;---------------------
@EVENT_DAILY_DOLL_CREATER_DECISION()
;星か宇宙港に居れば発生する
CALL GET_STAR_ID_FROM_POS(CHARACTER_POSITION)
SIF RESULT >= 0
	RETURN 1
CALL GET_PORT_ID_FROM_POS(CHARACTER_POSITION)
SIF RESULT >= 0
	RETURN 1
RETURN 0

;---------------------
;ジャンル
;コンフィグ設定で使用
;---------------------
@EVENT_DAILY_DOLL_CREATER_GENRE()
RETURN デイリー_ジャンル_トレーナー

;---------------------
;特定の条件を満たすキャラをランダムに選択する場合に利用
;他の関数は必須だが、これだけはなくてもよい　というかパフォーマンスへ影響するので不要なら作ってはならない
;対象が存在せずデイリーを開始できない場合は0を返すことでデイリーの発生をキャンセルする
;---------------------
@EVENT_DAILY_DOLL_CREATER_SETTARGET()
#DIM D_T, 2, 10000
#DIM D_T_NUM, 2
LOCAL:1 = 0
FOR LOCAL, 0, CHARANUM
	;死んでいる、子供、捕虜、他所属のキャラは駄目
	SIF CFLAG:LOCAL:特殊状態 == 特殊状態_死亡
		CONTINUE
	SIF CFLAG:LOCAL:特殊状態 == 行動不能_子供
		CONTINUE
	SIF CFLAG:LOCAL:捕虜先
		CONTINUE
	SIF	CFLAG:LOCAL:所属 != CFLAG:MASTER:所属
		CONTINUE
	;電子生命体（非ロボット）の数を数える
	IF HAS_TAG(LOCAL, タグ_電子生命体) && !HAS_TAG(LOCAL, タグ_ロボット)
		D_T:0:(D_T_NUM:0) = LOCAL
		D_T_NUM:0 ++
		SETBIT LOCAL:1, 0
	ENDIF
	;幽霊（非人形）の数を数える
	IF HAS_TAG(LOCAL, タグ_幽霊) && !HAS_TAG(LOCAL, タグ_人形)
		D_T:1:(D_T_NUM:1) = LOCAL
		D_T_NUM:1 ++
		SETBIT LOCAL:1, 1
	ENDIF
NEXT
[IF_DEBUG]
IF LOCAL:1 >= 3
	PRINTL ※デバッグ中！
	PRINTL 売り物
	CALL ASK_MULTI("ロボット","人形","ランダム")
	LOCAL:1 = RESULT + 1
ENDIF
[ENDIF]

IF LOCAL:1 == 0
	RETURN 0
ELSEIF LOCAL:1 >= 3
	LOCAL:1 = RAND:2 + 1
ENDIF
DVAR:謎のロボ人形作者_売り物 = LOCAL:1 - 1
FOR LOCAL, 0, DAILY_TARGET_NUM
	DAILY_TARGET:DAILY_TARGET_NUM = D_T:(DVAR:謎のロボ人形作者_売り物):(D_T_NUM:(DVAR:謎のロボ人形作者_売り物))
NEXT

;作者の性別を決める
DVAR:謎のロボ人形作者_作者性別 = RAND:6

[IF_DEBUG]
PRINTL ※デバッグ中！
PRINTL 作者の性別
CALL ASK_MULTI("男","女","女双","男双","男の娘","男の娘双","ランダム")
SIF RESULT >= 0 && RESULT <= 5
	DVAR:謎のロボ人形作者_作者性別 = RESULT
[ENDIF]

RETURN 1

;---------------------
;イベントの進行度合いに応じてイベント名を変更したい場合に利用
;変更する必要がない場合、この関数は不要
;---------------------
@EVENT_DAILY_DOLL_CREATER_NAME()
SELECTCASE DVAR:謎のロボ人形作者_売り物
	CASE 0
		RESULTS = 謎のロボット作成者
	CASE 1
		RESULTS = 謎の人形造形士
ENDSELECT

;---------------------
;本体
;イベントが発生した場合は1、発生しなかった場合は0を返す
;発生しなかったというのは例えば、特定条件を満たすキャラからランダムに一人選ぶデイリーで、そもそもその条件を満たすキャラが一人もいないみたいなとき
;旧仕様で作ったことある人向けにいうと「旧仕様のデイリー本体冒頭で-1を返すような状況」
;---------------------
@EVENT_DAILY_DOLL_CREATER()
#DIMS 作者,2
#DIM 竿穴
#DIM 対象
SELECTCASE DVAR:謎のロボ人形作者_作者性別
	CASE 0, 3	;男
		作者:0 = "彼"
		作者:1 = "彼"
	CASE 1, 2	;女
		作者:0 = "彼女"
		作者:1 = "彼女"
	CASE 4, 5	;男の娘
		作者:0 = "彼女"
		作者:1 = "彼"
ENDSELECT
SELECTCASE DVAR:謎のロボ人形作者_作者性別
	CASE 0, 4	;男、男の娘
		竿穴 = 1
	CASE 1	;女
		竿穴 = 2
	CASE 2, 3, 5	;双
		竿穴 = 3
ENDSELECT

IF HAS_TAG(MASTER, タグ_電子生命体) && !HAS_TAG(MASTER, タグ_ロボット)
	PRINTFORML %ANAME(MASTER)%の元に。特殊\@DVAR:謎のロボ人形作者_売り物 ? 人形 # ロボット\@の製造者を名乗る者が現れた。
ELSE
	PRINTFORML %ANAME(MASTER)%が町をぶらぶら歩いていたら突然声をかけられた。
ENDIF
PRINTFORML どうやら%作者%は自身の作った\@DVAR:謎のロボ人形作者_売り物 ? 幽霊が憑依できる人形 # 電子生命体が乗り移れるロボット\@が動く様を見るのが生きがいらしい。
PRINTL とはいえ、当然材料費も生活費も必要なので作ってもらう対価は必要だが。
CALL ASK_YN("話を聞く" ,"追い払う")
IF RESULT != 0
	PRINTFORMW %作者%は肩を落として去って行った…
	RETURN 0
ENDIF
PRINTFORMW %ANAME(MASTER)%は%作者%から話を聞くことにした。
PRINTL 
PRINTFORML %作者%が\@DVAR:謎のロボ人形作者_売り物 ? 憑依できる人形を持たない幽霊 # 乗り移れるロボットを持たない電子生命体\@に心当たりが無いか聞いてきたので、
IF EVENT_DAILY_DOLL_CREATER_MASTER_RACE() && DAILY_TARGET_NUM == 1
	PRINTFORMW %ANAME(MASTER)%自身しかいないことを伝えてみた。
ELSE
	PRINTFORML 自身\@EVENT_DAILY_DOLL_CREATER_MASTER_RACE() ? も含めて # の\@クルーに\@DAILY_TARGET_NUM > 1 ? 何人か # \@居ることを伝えてみた。
ENDIF
PRINTL 
PRINTFORML するとすごい勢いで喰いついてきたので、\@DVAR:謎のロボ人形作者_売り物 ? 人形 # ロボット\@を作る条件について聞いてみた。
PRINTL 
PRINTFORML %作者%が言う所によると\@DVAR:謎のロボ人形作者_売り物 ? 幽霊が憑依できる人形 # 電子生命体が乗り移れるロボット\@というのは通常の物とは違い特殊な技術や材料が必要だという。
PRINTL そこで、$2,000,000を払って欲しいそうだ。
PRINTL なお、最大でこれだけ払うことになるというだけで、場合によっては一部返すとの事。
;お金：サーベラスII型の開発費1,200,000と購入費100,000を足して1.5倍で1,950,000　キリよく2,000,000
PRINTFORML 返すかどうかは\@DVAR:謎のロボ人形作者_売り物 ? 幽霊 # 電子生命体\@自身の資質で変動するので、まだこの場では明言できないとのことだが、
PRINTL 
PRINTL 誰に作ってあげますか？
CALL SINGLE_DRAWLINE
CALL SELECT_CHARA_LIST_ONLY_LOGIC_SEX("DOLL_CREATER", "DOLL_CREATER")
IF MONEY < 2000000
	PRINTFORMW お金が足りないことを伝えると、%作者%は肩を落として去って行った…
	RETURN 0
ELSEIF RESULT == -1
	PRINTFORMW やめることを伝えると、%作者%は肩を落として去って行った…
	RETURN 0
ENDIF
MONEY -= 2000000
対象 = RESULT
PRINTFORMW %ANAME(MASTER)%はお金を払うことにした。
PRINTFORML \@DVAR:謎のロボ人形作者_売り物 ? 人形 # ロボット\@を作るために詳しく調べたいが、秘伝の技術とかもあるので本人以外に見られるのは嫌らしい。
PRINTL 詳しく調べれば節約できる所も見つかるので、一部の支払いを返せるのはまず確実とのことだが。
PRINTL 
PRINTFORML 二人きりに\@対象 == MASTER ? な # す\@る？

CALL ASK_MULTI("同意する" ,"拒否する")
LOCAL = RESULT
IF RESULT == 1
	PRINTFORMW %作者%は残念そうにしていたが、それはそれとしてちゃんと仕事をした。
ELSE
;▼▼▼▼▼▼▼▼▼▼▼▼▼▼▼▼▼▼▼▼▼▼▼▼▼加筆希望▼▼▼▼▼▼▼▼▼▼▼▼▼▼▼▼▼▼▼▼▼▼▼▼▼
	;経験を加算する物をLOCALSに追記する
	LOCALS = 欲望,露出,自慰
	PRINTFORML 二人きりになった途端、%作者%は大喜びで服を脱ぎだした。
	SIF 作者 != 作者:1
		PRINTFORML なんと、%作者%は%作者:1%だった！
	PRINTFORML %作者:1%は鼻息荒く%ANAME(対象)%\@DVAR:謎のロボ人形作者_売り物 ?  # のモニター\@に詰め寄り、オナニーの見せあいを要求した。
	PRINTFORM と思ったら、まだ返事をしていない%ANAME(対象)%を穴が開くほどに見ながら、勝手に自身の
	SELECTCASE 竿穴
		CASE 1
			PRINT チンポを扱き
		CASE 2
			PRINT クリトリスと乳首を弄り
		CASE 3
			PRINT チンポとヴァギナを弄り
	ENDSELECT
	PRINTFORML 始めた。
	PRINTFORM %ANAME(対象)%は
	;Ｃ自慰・Ｐ自慰を知らない場合
	IF ABL:対象:性知識 < 1 && ((!HAS_PENIS(対象) && !COM_KNOWLEDGE:対象:100) || (HAS_PENIS(対象) && !COM_KNOWLEDGE:対象:120))
		PRINTFORML %作者:1%が何をしているかわからずに戸惑っていると、%作者:1%がなんやかんや教えてくれた。
		IF HAS_PENIS(対象)
			PRINTFORML %ANAME(対象)%は\@GETBIT(竿穴,0) ? %作者:1%の真似をして # \@ペニスを弄り始めた。
			PRINTL 
			CALL SET_COM_KNOWLEDGE(対象, 120)
		ELSE
			PRINTFORML %ANAME(対象)%は\@竿穴 == 2 ? %作者:1%の真似をして # \@クリトリスを弄り始めた。
			PRINTL 
			CALL SET_COM_KNOWLEDGE(対象, 100)
		ENDIF
		LOCALS += ",Ｃ"
	;その他
	ELSEIF TALENT:対象:淫乱系
		IF GETBIT(TALENT:対象:淫乱系,素質_淫乱_自慰狂い) || ABL:対象:自慰 > 5
			PRINTFORM 目の前の%作者:1%のことなど一瞬で忘れて、
			LOCALS += ",自慰"
		ELSEIF IS_MASTER(対象) && 対象 != MASTER
			PRINTFORM %ANAME(対象)%のことを思い出しながら、
		ELSE
			PRINTFORM %作者:1%の痴態を見ながら、
		ENDIF
		;弄る場所は二か所だけにする
		CALL EVENT_DAILY_DOLL_CREATER_SELECT_PLAY_PRINT(対象)
		PRINTL を激しく弄っている。
		PRINTL 
		SIF GETBIT(RESULT,0)
			LOCALS += ",Ｖ"
		SIF GETBIT(RESULT,1)
			LOCALS += ",Ａ"
		SIF GETBIT(RESULT,2)
			LOCALS += ",Ｂ"
		SIF GETBIT(RESULT,3)
			LOCALS += ",Ｃ"
		SIF GETBIT(RESULT,4)
			LOCALS += ",Ｕ"
	ELSEIF ABL:対象:欲望 > 5 || ABL:対象:露出 > 5 || ABL:対象:自慰 > 5
		PRINTFORM %ANAME(対象)%もノリノリで%作者:1%をオカズに
		;弄る場所は二か所だけにする
		CALL EVENT_DAILY_DOLL_CREATER_SELECT_PLAY_PRINT(対象)
		IF RESULT
			SIF GETBIT(RESULT,0)
				LOCALS += ",Ｖ"
			SIF GETBIT(RESULT,1)
				LOCALS += ",Ａ"
			SIF GETBIT(RESULT,2)
				LOCALS += ",Ｂ"
			SIF GETBIT(RESULT,3)
				LOCALS += ",Ｃ"
			SIF GETBIT(RESULT,4)
				LOCALS += ",Ｕ"
		ELSE
			PRINTFORM \@HAS_PENIS(対象) ? ペニス # クリトリス\@
			LOCALS += ",Ｃ"
		ENDIF
		PRINTL を弄っている。
		PRINTL 
	ELSE
		PRINTFORML 仕方ないので%ANAME(対象)%も%作者:1%をオカズにして自慰をしている。
		LOCALS += ",Ｃ"
		PRINTL 
	ENDIF
	PRINTFORML %LOCALS%
	CALL FUCK(対象, LOCALS)
;▲▲▲▲▲▲▲▲▲▲▲▲▲▲▲▲▲▲▲▲▲▲▲▲▲加筆希望▲▲▲▲▲▲▲▲▲▲▲▲▲▲▲▲▲▲▲▲▲▲▲▲▲
	PRINTW 
ENDIF
PRINTL 
PRINTL ………
PRINTL ……
PRINTW …

IF DVAR:謎のロボ人形作者_売り物 == 0
	CALL ADD_TAG(対象, タグ_ロボット)
ELSE
	CALL ADD_TAG(対象, タグ_人形)
ENDIF

SIF IS_MASTER(対象) && 対象 != MASTER && !(HAS_TAG(MASTER, タグ_電子生命体) && !HAS_TAG(MASTER, タグ_ロボット))
	PRINTFORM %ANAME(MASTER)%に抱き着いて
PRINTFORMW 物理的な身体を得たことを喜んでいる%ANAME(対象)%を尻目に%作者%\@作者 != 作者:1 && LOCAL == 0 ? （%作者:1%） # \@は去っていった。
	;見抜きあいをした場合、支払いの1/4が返ってくる
IF LOCAL == 0
	PRINTL 払い戻しのお金はいつの間にかテーブルの上に置いてあった。
	PRINTW $500,000戻ってきた！
	MONEY += 500000
ENDIF
RETURN 1

@EVENT_DAILY_DOLL_CREATER_MASTER_RACE()
#FUNCTION
SIF DVAR:謎のロボ人形作者_売り物 == 0 && HAS_TAG(MASTER, タグ_電子生命体) && !HAS_TAG(MASTER, タグ_ロボット)
	RETURNF 1
SIF DVAR:謎のロボ人形作者_売り物 == 1 && HAS_TAG(MASTER, タグ_幽霊) && !HAS_TAG(MASTER, タグ_人形)
	RETURNF 1
RETURNF 0

@EVENT_DAILY_DOLL_CREATER_SELECT_PLAY_PRINT(対象)
#DIM 対象
#DIM CO
#DIM RET
CO = 0
LOCAL = 0,0,0,0,0

IF GETBIT(TALENT:対象:淫乱系,素質_淫乱_淫壷) || ABL:対象:Ｖ感 > 5
	LOCAL:CO = 0
	CO ++
ENDIF
IF GETBIT(TALENT:対象:淫乱系,素質_淫乱_尻穴狂い) || ABL:対象:Ａ感 > 5
	LOCAL:CO = 1
	CO ++
ENDIF
IF GETBIT(TALENT:対象:淫乱系,素質_淫乱_淫乳) || ABL:対象:Ｂ感 > 5
	LOCAL:CO = 2
	CO ++
ENDIF
IF GETBIT(TALENT:対象:淫乱系,素質_淫乱_淫核) || (GETBIT(TALENT:対象:淫乱系,素質_淫乱_射精狂い) && HAS_PENIS(対象)) || ABL:対象:Ｃ感 > 5
	LOCAL:CO = 3
	CO ++
ENDIF
IF GETBIT(TALENT:対象:淫乱系,素質_淫乱_尿道狂い) || ABL:対象:Ｕ感 > 5
	LOCAL:CO = 4
	CO ++
ENDIF

FOR COUNT, 0, 2
	SIF !CO
		RETURN RET
	SIF COUNT
		PRINT と
	RAND CO
	SELECTCASE LOCAL:RESULT
		CASE 0
			PRINT ヴァギナ
			SETBIT RET, 0
		CASE 1
			PRINT アナル
			SETBIT RET, 1
		CASE 2
			PRINT 乳首
			SETBIT RET, 2
		CASE 3
			PRINTFORM \@HAS_PENIS(対象) ? ペニス # クリトリス\@
			SETBIT RET, 3
		CASE 4
			PRINT 尿道 
			SETBIT RET, 4
	ENDSELECT
	;選択したら削除、ランダム数も減らす
	ARRAYREMOVE LOCAL, RESULT, 1
	CO --
NEXT
RETURN RET

@EVENT_DAILY_DOLL_CREATER_NAME()
SELECTCASE DVAR:謎のロボ人形作者_売り物
	CASE 0
		RESULTS = "謎のロボット作成者"
	CASE 1
		RESULTS = "謎の人形造形士"
ENDSELECT

@SELECT_CHARA_LIST_SHOW_LOGIC_DOLL_CREATER(対象)
#DIM 対象
IF DVAR:謎のロボ人形作者_売り物 ==0
	LOCAL:0 = タグ_電子生命体
	LOCAL:1 = タグ_ロボット
ELSE
	LOCAL:0 = タグ_幽霊
	LOCAL:1 = タグ_人形
ENDIF
RETURN CFLAG:LOCAL:特殊状態 != 特殊状態_死亡 && CFLAG:LOCAL:特殊状態 != 行動不能_子供 && CFLAG:対象:所属 == CFLAG:MASTER:所属 && HAS_TAG(対象, LOCAL:0) && !HAS_TAG(対象, LOCAL:1)

@SELECT_CHARA_LIST_SELECT_LOGIC_DOLL_CREATER(対象)
#DIM 対象
RETURN CFLAG:対象:捕虜先 == 0
