
;-------------------------------------------------
;ARG:0 = 1 で周回モード
;-------------------------------------------------
@NEWLOOP(ARG:0=0)
;変数の初期化
CALL NEWLOOP_RESET_VARS

COUNTRY_BOSS:1                   = GET_ID(NAME_TO_CHARA("あなた"))
CFLAG:NAME_TO_CHARA("あなた"):所属 = GET_COUNTRY_FROM_BOSS_NAME("あなた")
MONEY += 10000

RETURN 1

;-------------------------------------
;新しく周回する時用、変数のリセット処理
;-------------------------------------
@NEWLOOP_RESET_VARS
#DIM ITER
#DIM DEL_COUNT
;日付を1日目に戻す
DAY = 1

;触手部屋の管理者を初期化
FLAG:触手部屋管理者 = 0

;フェイズを拠点フェイズにする
TIME = 0

;シナリオ専用変数をリセット
VARSET SCVAR, 0

;デイリーイベント変数をリセット
VARSET DVAR, 0
VARSET DSTR, 
;口上用デイリーイベント変数をリセット
FOR LOCAL, 0, VARSIZE("KDVAR")
	CVARSET KDVAR, LOCAL, 0
	CVARSET KDSTR, LOCAL, 
NEXT


;捕虜について削除
VARSET PRISONER_TARGET, -1

;特別訓練回数をリセット
VARSET SPECIAL_TRAIN_COUNT, 0

;変数の初期化
TARGET = -1
ASSI = -1

;クリアフラグを解除
SHOP_TIME = 0

;観戦モードフラグを0
FLAG:観戦モード = 0

;SHOW_INFOの初期画面を基本情報にする
FLAG:能力表示モード = 0

;-------------------------------------------------
;主人公選択
;-------------------------------------------------
@SELECT_MASTER

@SELECT_CHARA_LIST_SHOW_LOGIC_SELECT_MASTER(対象)
#DIM 対象
;どこかの勢力を選択した場合は、そこ所属のキャラ以外は弾く。
SIF FLAG:OP選択勢力 > 0 && CFLAG:対象:所属 != FLAG:OP選択勢力
	RETURN 0
;特殊なスタートを選択した場合、君主は弾く。
SIF FLAG:OP選択勢力 <= 0 && GET_COUNTRY_BOSS(CFLAG:対象:所属) == 対象
	RETURN 0
;「あなた」をまだ作ってないならダメ
SIF !FLAG:あなた作成済み && 対象 == NAME_TO_CHARA("あなた")
	RETURN 0
RETURN 1

;-------------------------------------------------
;君主である東方キャラ使用時に、他勢力との関係をMASTERに移しておく関数
;SWAP_CHARAと併用する
;ARG:0 = 交換対象
;-------------------------------------------------
@NEWGAME_SWAP_RELATIONS(ARG:0)
#DIM REL_LIKE_COPY,1
#DIM REL_HATE_COPY,1
FOR LOCAL, 0, 1000
	REL_LIKE_COPY = REL_LIKE:(ARG:0):LOCAL
	REL_HATE_COPY = REL_HATE:(ARG:0):LOCAL
	REL_LIKE:(ARG:0):LOCAL = REL_LIKE:MASTER:LOCAL
	REL_HATE:(ARG:0):LOCAL = REL_HATE:MASTER:LOCAL
	REL_LIKE:MASTER:LOCAL = REL_LIKE_COPY
	REL_HATE:MASTER:LOCAL = REL_HATE_COPY
NEXT
FOR LOCAL, 0, CHARANUM
	REL_LIKE_COPY = REL_LIKE:(LOCAL):MASTER
	REL_HATE_COPY = REL_HATE:(LOCAL):MASTER
	REL_LIKE:(LOCAL):MASTER = REL_LIKE:(LOCAL):(ARG:0)
	REL_HATE:(LOCAL):MASTER = REL_HATE:(LOCAL):(ARG:0)
	REL_LIKE:(LOCAL):(ARG:0) = REL_LIKE_COPY
	REL_HATE:(LOCAL):(ARG:0) = REL_HATE_COPY
NEXT

;-------------------------------------------------
;ランダムキャラと性知識の設定
;-------------------------------------------------
@EXTRA_SETTING
#DIM FIRST_LINE
#DIM ランダムキャラ
#DIM 性知識最大
#DIM 君主
FIRST_LINE = LINECOUNT
CALL SINGLE_DRAWLINE

IF FLAG:OPランダムキャラ使用 == 0
	PRINTFORM ランダムキャラ:
	CALL PRINT_SELECT_BUTTON("[使う]", 0, ランダムキャラ)
	CALL PRINT_SELECT_BUTTON("[使わない]", 1, !ランダムキャラ)
	PRINTL 
	PRINTL （非使用を推奨しています）
ENDIF

IF !FLAG:観戦モード
	PRINTFORM         性知識:
	CALL PRINT_SELECT_BUTTON("[5にする]", 10, 性知識最大)
	CALL PRINT_SELECT_BUTTON("[そのまま]", 11, !性知識最大)
	PRINTL
ENDIF

IF FLAG:OP選択勢力 > 0 && COUNTRY_BOSS:(CFLAG:MASTER:所属) != GET_ID(MASTER) && !IS_SP_COUNTRY(CFLAG:MASTER:所属)
	PRINTFORM           君主:
	CALL PRINT_SELECT_BUTTON("[君主としてスタート]", 20, 君主)
	CALL PRINT_SELECT_BUTTON("[そのまま]", 21, !君主)
	PRINTL
ENDIF

CALL SINGLE_DRAWLINE
PRINTBUTTON "[完了]", 100

INPUT

IF RESULT == 100
	IF FLAG:OPランダムキャラ使用 == -1
		FLAG:ランダムキャラ使用 = 0
	ELSEIF FLAG:OPランダムキャラ使用 == 1
		FLAG:ランダムキャラ使用 = 1
	ELSEIF ランダムキャラ
		FLAG:ランダムキャラ使用 = 1
	ELSEIF !ランダムキャラ
		FLAG:ランダムキャラ使用 = 0
	ENDIF
	IF 性知識最大
		ABL:MASTER:性知識 = 5
	ENDIF
	IF 君主
		COUNTRY_BOSS:(CFLAG:MASTER:所属) = GET_ID(MASTER)
	ENDIF
	RETURN
ELSEIF RESULT == 0 && FLAG:OPランダムキャラ使用 == 0
	ランダムキャラ = 1
ELSEIF RESULT == 1
	ランダムキャラ = 0
ELSEIF RESULT == 10
	性知識最大 = 1
ELSEIF RESULT == 11
	性知識最大 = 0
ELSEIF RESULT == 20 && FLAG:OP選択勢力 > 0 && COUNTRY_BOSS:(CFLAG:MASTER:所属) != GET_ID(MASTER) && !IS_SP_COUNTRY(CFLAG:MASTER:所属)
	君主 = 1
ELSEIF RESULT == 21
	君主 = 0
ENDIF

CLEARLINE LINECOUNT - FIRST_LINE
RESTART

;-------------------------------------------------
;ランダムキャラを使用するかどうか選択
;-------------------------------------------------
@SELECT_RCHARA
IF FLAG:OPランダムキャラ使用 == -1
	FLAG:ランダムキャラ使用 = 0
ELSEIF FLAG:OPランダムキャラ使用 == 1
	FLAG:ランダムキャラ使用 = 1
ELSE
	CALL SINGLE_DRAWLINE
	PRINTFORML このシナリオではランダムキャラ（ランダムな名前をもつモブ的なキャラ）ありの設定にできます
	PRINTFORML ランダムキャラを使用しますか？
	CALL ASK_YN
	IF RESULT == 0
		FLAG:ランダムキャラ使用 = 1
	ELSE
		FLAG:ランダムキャラ使用 = 0
	ENDIF
ENDIF



;-------------------------------------------------
;カスタムキャラ等の設定
;-------------------------------------------------
@SETUP_SPECIAL_CHARACTERS()
#DIM 候補, MAX_CHARA_NUM
#DIM 候補数
#DIM 配置先, MAX_CHARA_NUM
#DIM FIRST_LINE
#DIM ページ

VARSET 候補,   -1
VARSET 候補数, 0
VARSET 配置先, -1
VARSET ページ

FOR LOCAL, 1, CHARANUM
	SIF IS_TOHO_CHARA(LOCAL) || IS_SP_COUNTRY_CHARA(LOCAL) || NAME_TO_CHARA("あなた") == LOCAL || LOCAL == MASTER
		CONTINUE
	候補:候補数 = LOCAL
	候補数 ++
NEXT

FOR LOCAL, 0, MAX_CUSTOM_CHARA
	SIF !FIND_CHARADATA(TOSTR(LOCAL))
		CONTINUE
	CALL LOAD_CUSTOM_DATA(LOCAL)
	TALENT:RESULT:カスタムキャラ = 2
	候補:候補数 = RESULT
	候補数 ++
NEXT

SIF 候補数 == 0
	RETURN 0

FIRST_LINE = LINECOUNT

$SHOW_LOOP

CALL SINGLE_DRAWLINE
PRINTFORML 引き継いだキャラ・カスタムキャラの出現設定を行います
CALL SINGLE_DRAWLINE

FOR LOCAL, ページ * 20, MIN(ページ * 20 + 20, 候補数)
	PRINTFORM [{LOCAL}]%ANAME(候補:LOCAL)% - 
	IF 配置先:LOCAL == -1
		PRINTFORML 登場しない
	ELSEIF 配置先:LOCAL == 0
		PRINTFORML 放浪
	ELSE
		PRINTFORML %ANAME(GET_COUNTRY_BOSS(配置先:LOCAL))%
	ENDIF
NEXT
CALL SINGLE_DRAWLINE
PRINTBUTTON "[前のページ]", 10000
PRINTFORM {ページ+1}/{候補数 / 20 + 1}
PRINTBUTTON "[次のページ]", 10001
PRINTL
CALL SINGLE_DRAWLINE
PRINTBUTTON "[一括]", 10003
PRINTL
CALL SINGLE_DRAWLINE
PRINTBUTTON "[完了]", 10002

INPUT

IF RESULT == 10000
	ページ = ROUND_DECREMENT(ページ, 0, 候補数 / 20)
ELSEIF RESULT == 10001
	ページ = ROUND_INCREMENT(ページ, 0, 候補数 / 20)
ELSEIF RESULT == 10003
	FOR LOCAL, 0, MAX_COUNTRY
		SIF !IS_COUNTRY(LOCAL)
			CONTINUE
		PRINTFORML [{LOCAL}] %ANAME(GET_COUNTRY_BOSS(LOCAL))%
	NEXT
	PRINTBUTTON "[放浪]", 0
	PRINTL
	PRINTBUTTON "[登場しない]", -1
	PRINTL
	PRINTBUTTON "[中止]", 99
	INPUT
	IF RESULT == 99
		CLEARLINE LINECOUNT - FIRST_LINE
		GOTO SHOW_LOOP
	ELSEIF (INRANGE(RESULT, 1, MAX_COUNTRY - 1) && IS_COUNTRY(RESULT)) || GROUPMATCH(RESULT, 0, -1)
		LOCAL:1 = RESULT
		FOR LOCAL, 0, 候補数
			配置先:LOCAL = LOCAL:1
		NEXT
	ENDIF
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP
ELSEIF RESULT == 10002
	FOR LOCAL, 候補数 - 1, -1,  -1
		IF 配置先:LOCAL == -1
			CALL DELETE_CHARA(候補:LOCAL)
		ELSEIF 配置先:LOCAL == 0
			CFLAG:(候補:LOCAL):所属 = 0
			CFLAG:(候補:LOCAL):特殊状態 = 特殊状態_放浪
		ELSE
			CFLAG:(候補:LOCAL):所属 = 配置先:LOCAL
			CFLAG:(候補:LOCAL):特殊状態 = 0
		ENDIF
	NEXT
	RETURN
ENDIF

IF INRANGE(RESULT, 0, 候補数)
	LOCAL:1 = RESULT
	FOR LOCAL, 0, MAX_COUNTRY
		SIF !IS_COUNTRY(LOCAL)
			CONTINUE
		PRINTFORML [{LOCAL}] %ANAME(GET_COUNTRY_BOSS(LOCAL))%
	NEXT
	PRINTBUTTON "[放浪]", 0
	PRINTL
	PRINTBUTTON "[登場しない]", -1
	PRINTL
	INPUT
	IF GROUPMATCH(RESULT, 0, -1)
		配置先:(LOCAL:1) = RESULT
	ELSEIF IS_COUNTRY(RESULT)
		配置先:(LOCAL:1) = RESULT
	ENDIF
ENDIF

CLEARLINE LINECOUNT - FIRST_LINE
GOTO SHOW_LOOP

;-------------------------------------------------
;ユーザー選択による最初のゲーム設定
;-------------------------------------------------
@GAME_INIT
#DIM ゲームモード
CALL SELECT_GAMEMODE
ゲームモード = RESULT
PRINTL チュートリアルを見ますか?
CALL ASK_YN
SIF RESULT == 0
	CALL TUTORIAL
RETURN ゲームモード
;-------------------------------------------------
;ゲームモード選択
;-------------------------------------------------
@SELECT_GAMEMODE

$INPUT_LOOP
PRINTFORML %CALLNAME:MASTER%の身分は？

PRINTBUTTON "[0] 二人旅", 0
PRINTL 
PRINTL カスタムキャラ/オリジナルキャラを一人仲間にした状態でゲームを始められます。所持金$10,000
PRINTL 
PRINTBUTTON "[1] リッチエクスプローラー", 1
PRINTL 
PRINTL 大金を持った状態でゲームを始められます。所持金$1,000,000
PRINTL 
PRINTBUTTON "[2] 脱出者", 2
PRINTL
PRINTL わずかな所持金のみを持った状態でゲームを開始します。所持金$10,000
PRINTL 
PRINTBUTTON "[3] 宇宙へ", 3
PRINTL
PRINTL 多数のカスタムキャラを仲間にしていき、壊れた戦艦を修理して宇宙に飛び立つモードです。所持金$10,000
PRINTL 
PRINTBUTTON "[4] あなたたちの野望", 4
PRINTL
PRINTL 多数のカスタムキャラを仲間にした状態で始めるモードです。所持金$10,000
PRINTL 
PRINTBUTTON "[5] 壊滅まぐろ漁船団", 5
PRINTL
PRINTL わずかな所持金と漁船を所有した状態で始めるモードです。所持金$10,000
PRINTL 

INPUT
IF RESULT == 0
	$SELECT_CHARA_TYPE
	PRINTL カスタムキャラとユニークキャラ、どちらを連れて行きますか？
	PRINTBUTTON "[0] カスタムキャラ", 0
	PRINTL
	PRINTBUTTON "[1] ユニークキャラ", 1
	INPUT
	IF RESULT == 0
		CALL SELECT_CUSTOM_CHARA
	ELSEIF RESULT == 1
		CALL SELECT_UNIQEU_CHARA
	ELSE
		GOTO SELECT_CHARA_TYPE
	ENDIF
	LOCAL = RESULT
	CALL ASK_EDIT_MASTER
	CALL CLOTH_INIT_MASTER
	CALL ASK_EDIT_CLOTH_MATER
	CALL OPENING_4X_0(LOCAL)
	RETURN 0
ELSEIF RESULT == 1
	MONEY = 1000000
	CALL ASK_EDIT_MASTER
	CALL CLOTH_INIT_MASTER
	CALL ASK_EDIT_CLOTH_MATER
	CALL OPENING_4X_1
	RETURN 1
ELSEIF RESULT == 2
	MONEY = 10000
	CALL ASK_EDIT_MASTER
	CALL CLOTH_INIT_MASTER
	CALL ASK_EDIT_CLOTH_MATER
	CALL OPENING_4X_2
	RETURN 2
ELSEIF RESULT == 3
	CALL SELECT_CUSTOM_CHARAS
	CALL ASK_EDIT_MASTER
	CALL CLOTH_INIT_MASTER
	CALL ASK_EDIT_CLOTH_MATER
	CALL OPENING_4X_3
	RETURN 3
ELSEIF RESULT == 4
	CALL SELECT_CUSTOM_CHARAS
	CALL ASK_EDIT_MASTER
	CALL CLOTH_INIT_MASTER
	CALL ASK_EDIT_CLOTH_MATER
	CALL OPENING_4X_4
	FOR LOCAL, 0, CHARANUM
		SIF CFLAG:LOCAL:初期キャラ
			CFLAG:LOCAL:所属 = 国家ID_プレイヤー
	NEXT
	RETURN 4
ELSEIF RESULT == 5
	MONEY = 10000
	CALL ASK_EDIT_MASTER
	CALL CLOTH_INIT_MASTER
	CALL ASK_EDIT_CLOTH_MATER
	CALL OPENING_4X_5
	RETURN 5
ENDIF

@SELECT_CUSTOM_CHARA
#DIM 入力値
#DIM ページ
#DIM CHARA_BUFFER

$INPUT_LOOP
CALL LISTUP_CUSTOM_CHARACTER(0, ページ, 20)
INPUT
入力値 = RESULT
IF RESULT == 1000 && ページ > 1
	ページ --
	GOTO INPUT_LOOP
ELSEIF RESULT == 1001 && ページ < 50
	ページ ++
	GOTO INPUT_LOOP
ELSEIF !FIND_CHARADATA(TOSTR(入力値))
	LOCAL = RESULT
	IF INRANGE(RESULT, 0, MAX_CUSTOM_CHARA - 1)
		CALL INIT_CUSTOM_DATA
		CHARA_BUFFER = RESULT
		CALL EDIT_CUSTOM_DATA(LOCAL, CHARA_BUFFER)
		SIF CHARA_BUFFER < CHARANUM
			CALL DELETE_CHARA(CHARA_BUFFER)
	ENDIF
	GOTO INPUT_LOOP
ENDIF
CALL LOAD_CUSTOM_DATA(入力値)
TALENT:RESULT:カスタムキャラ = 2
CFLAG:RESULT:所属 = 国家ID_プレイヤー
RETURN RESULT

@SELECT_UNIQEU_CHARA
$INPUT_LOOP

PRINTBUTTON "[4] ジェイン" ,4
PRINTL
PRINTL 性別：女 年齢：20歳
PRINTL 国籍：機械文明（出身は民主主義国家）
PRINTL 所属：アシモフ大学素粒子物理学部・元教授
PRINTL 性格：達観したような性格。皮肉屋ではない。
PRINTL 
PRINTBUTTON "[5] リーシャ" ,5
PRINTL
PRINTL 性別：女 年齢：24歳
PRINTL 国籍：企業連合
PRINTL 所属：企業連合 傭兵ギルド
PRINTL 性格：物静か（酒が入ると明るくなる）
PRINTL 
PRINTBUTTON "[8] なずな" ,8
PRINTL
PRINTL 性別：女 年齢：1X歳
PRINTL 国籍：豊秋津皇国
PRINTL 所属：なし
PRINTL 性格：冷静
PRINTL 
INPUT
SELECTCASE RESULT
	CASE 4
		CFLAG:NAME_TO_CHARA("ジェイン"):所属 = 国家ID_プレイヤー
	CASE 5
		CFLAG:NAME_TO_CHARA("リーシャ"):所属 = 国家ID_プレイヤー
	CASE 8
		CFLAG:NAME_TO_CHARA("なずな"):所属 = 国家ID_プレイヤー
	CASEELSE
		GOTO INPUT_LOOP
ENDSELECT

RETURN RESULT

@SELECT_CUSTOM_CHARAS
#DIM 入力値
#DIM ページ
#DIM CHARA_BUFFER
#DIM SELECTED_LIST, MAX_CUSTOM_CHARA
VARSET SELECTED_LIST, 0

$INPUT_LOOP
CALL LISTUP_SELECTED_CUSTOM_CHARACTER(0, ページ, 20, SELECTED_LIST)

DRAWLINE
PRINTBUTTON "[9999]　完了", 9999
INPUT
入力値 = RESULT
IF 入力値 == 9999
	LOCAL:1 = 0
	FOR LOCAL, 0, MAX_CUSTOM_CHARA
		SIF SELECTED_LIST:LOCAL
			LOCAL:1 ++
	NEXT
	IF LOCAL:1 < 3
		DRAWLINE
		SETCOLOR カラー_注意
		PRINTFORMW 3人以上選択してください
		RESETCOLOR
		DRAWLINE
		GOTO INPUT_LOOP
	ENDIF
ELSEIF RESULT == 1000 && ページ > 1
	ページ --
	GOTO INPUT_LOOP
ELSEIF RESULT == 1001 && ページ < 50
	ページ ++
	GOTO INPUT_LOOP
ELSEIF !FIND_CHARADATA(TOSTR(入力値))
	LOCAL = RESULT
	IF INRANGE(RESULT, 0, MAX_CUSTOM_CHARA - 1)
		CALL INIT_CUSTOM_DATA
		CHARA_BUFFER = RESULT
		CALL EDIT_CUSTOM_DATA(LOCAL, CHARA_BUFFER)
		SIF CHARA_BUFFER < CHARANUM
			CALL DELETE_CHARA(CHARA_BUFFER)
	ENDIF
	GOTO INPUT_LOOP
ELSEIF 入力値 >= 0 && 入力値 < MAX_CUSTOM_CHARA
	SELECTED_LIST:入力値 = !SELECTED_LIST:入力値
	GOTO INPUT_LOOP
ELSE
	GOTO INPUT_LOOP
ENDIF

FOR LOCAL, 0, MAX_CUSTOM_CHARA
	SIF !SELECTED_LIST:LOCAL
		CONTINUE
	CALL LOAD_CUSTOM_DATA(LOCAL)
	TALENT:RESULT:カスタムキャラ = 2
	CFLAG:RESULT:初期キャラ = 1
NEXT

@ASK_EDIT_MASTER
PRINTL あなたをカスタムしますか？
CALL ASK_YN
SIF RESULT == 0
	CALL MASTER_EDIT

@ASK_EDIT_CLOTH_MATER
PRINTL あなたの衣装をカスタムしますか？
CALL ASK_YN
SIF RESULT == 0
	CALL CLOTH_SETTING_MENU(0)