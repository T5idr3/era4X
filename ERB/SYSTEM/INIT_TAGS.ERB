@TAG_INIT(対象, 呼び出し)
#DIM 対象
#DIM 呼び出し
FOR LOCAL, 0, MAX_TAG
	TAG:対象:LOCAL = -1
NEXT
TRYCALLFORM INIT_TAG_{呼び出し}(対象)


@ADD_TAG(対象, タグ)
#DIM 対象
#DIM タグ
FOR LOCAL, 0, MAX_TAG
	SIF TAG:対象:LOCAL == タグ
		RETURN 0
NEXT
FOR LOCAL, 0, MAX_TAG
	IF TAG:対象:LOCAL == -1
		TAG:対象:LOCAL = タグ
		BREAK
	ENDIF
NEXT

@REMOVE_TAG(対象, タグ)
#DIM 対象
#DIM タグ
FOR LOCAL, 0, MAX_TAG
	IF TAG:対象:LOCAL == タグ
		TAG:対象:LOCAL = -1
		BREAK
	ENDIF
NEXT

@INVERT_TAG(対象, タグ)
#DIM 対象
#DIM タグ
IF HAS_TAG(対象, タグ)
	CALL REMOVE_TAG(対象, タグ)
ELSE
	CALL ADD_TAG(対象, タグ)
ENDIF

;存在しないタグを全て消去
@REMOVE_NULL_TAG(対象)
#DIM 対象
FOR LOCAL, 0, MAX_TAG
	SIF TAG:対象:LOCAL >= VARSIZE("TAG_NAME")
		TAG:対象:LOCAL = -1
NEXT

@SORT_TAG(対象)
#DIM 対象
;タグを小さい順にソート
FOR LOCAL:0, 0, MAX_TAG
	FOR LOCAL:1, 0, MAX_TAG - 1
		IF TAG:対象:(LOCAL:1) > TAG:対象:((LOCAL:1) + 1)
			LOCAL:2 = TAG:対象:(LOCAL:1)
			TAG:対象:(LOCAL:1) = TAG:対象:((LOCAL:1) + 1)
			TAG:対象:((LOCAL:1) + 1) = LOCAL:2
		ENDIF
	NEXT
NEXT
;-1を後ろに移動
FOR LOCAL:1, 0, MAX_TAG
	SIF TAG:対象:(LOCAL:1) != -1
		BREAK
NEXT
LOCAL:1 -= 1
FOR LOCAL:0, 0, MAX_TAG - (LOCAL:1)
	LOCAL:2 = TAG:対象:(LOCAL:1+LOCAL:0)
	TAG:対象:(LOCAL:1+LOCAL:0) = TAG:対象:(LOCAL:0)
	TAG:対象:(LOCAL:0) = LOCAL:2
NEXT

@HAS_TAG(対象, タグ)
#FUNCTION
#DIM 対象
#DIM タグ
FOR LOCAL, 0, MAX_TAG
	SIF TAG:対象:LOCAL == タグ
		RETURNF 1
NEXT

RETURNF 0

@IS_TAG_EMPTY(対象)
#FUNCTION
#DIM 対象
FOR LOCAL, 0, MAX_TAG
	SIF TAG:対象:LOCAL != -1
		RETURNF 0
NEXT

RETURNF 1

