;-------------------------------------------------
;「クラフト」の処理
;-------------------------------------------------
@HOUSING_CRAFT_MODE_MAIN
#DIM FIRST_LINE_ITEM_MAIN
#DIM INPUT_RESULT
REDRAW 0
FIRST_LINE_ITEM_MAIN = LINECOUNT

WHILE 1
	CALL HOUSING_CRAFT_PRINT_HEADER
	CALL HOUSING_CRAFT_PRINT_STOCK
	CALL HOUSING_CRAFT_PRINT_ROOM_OPTION
	CALL HOUSING_CRAFT_PRINT_MODULE_OPTION
	IF INPUT_RESULT >= 100000 && INPUT_RESULT < 200000
		CALL HOUSING_CRAFT_PRINT_ROOM_CRAFTING_MESSAGE(INPUT_RESULT)
	ELSEIF INPUT_RESULT >= 200000 && INPUT_RESULT < 300000
		CALL HOUSING_CRAFT_PRINT_MODULE_CRAFTING_MESSAGE(INPUT_RESULT)
	ENDIF
	CALL HOUSING_CRAFT_PRINT_FOOTER
	CALL HOUSING_CRAFT_INPUT
	INPUT_RESULT = RESULT
	SIF INPUT_RESULT == -1
		BREAK
	CLEARLINE LINECOUNT - FIRST_LINE_ITEM_MAIN
WEND


;-------------------------------------------------
;ヘッダー
;-------------------------------------------------
@HOUSING_CRAFT_PRINT_HEADER
CALL SINGLE_DRAWLINE
PRINTPLAIN  
CALL COLUMN_RIGHT_TITLE("クラフトするルーム・モジュールを選択", "", "", "0", "----")
PRINTL 
CALL SINGLE_DRAWLINE
;-------------------------------------------------
;フッター
;-------------------------------------------------
@HOUSING_CRAFT_PRINT_FOOTER
CALL SINGLE_DRAWLINE("-",カラー_灰色)
CALLF FuncTagSetText_Print(@"@B:999999@[戻る]@/B@", 4)

;-------------------------------------------------
;入力受付
;-------------------------------------------------
@HOUSING_CRAFT_INPUT
ONEINPUT
;アイテム選択ボタン押下
IF RESULT >= 100000 && RESULT < 300000
	RETURN RESULT
ELSEIF RESULT >= 300000 && RESULT < 400000
	CALL HOUSING_CRAFT_EXEC_ROOM_CRAFT(RESULT)
ELSEIF RESULT >= 400000 && RESULT < 500000
	CALL HOUSING_CRAFT_EXEC_MODULE_CRAFT(RESULT)
;戻るボタン押下
ELSEIF RESULT == 999999
	RETURN -1
ENDIF

;-------------------------------------------------
;所持アイテム表示
;-------------------------------------------------
@HOUSING_CRAFT_PRINT_STOCK
CALL COLOR_PRINTL(" 所持 ", カラー_注釈, "金:", カラー_選択不可, @"%NUM_FORMAT(MONEY)%", カラー_選択中)
LOCAL:1 = 999
CALL COLOR_PRINT(" ルーム", カラー_注釈)
FOR LOCAL:0, 0, HO_MAX_ROOMTYPE
	IF HO_ROOM_STOCK:(LOCAL:0) > 0
		IF LOCAL:1 > 70
			LOCAL:1 = 0
			PRINTL 
			PRINTFORM %TOSTR_SPACE(8)%
		ENDIF
		CALL HO_GET_ROOM_DATA(LOCAL:0, "ROOM_NAME")
		LOCALS:0 = %RESULTS%
		LOCALS:0 = %LOCALS:0% ×{HO_ROOM_STOCK:(LOCAL:0)}
		LOCAL:1 += STRLENS(LOCALS:0) + 2
		PRINTFORM %LOCALS:0%　
	ENDIF
NEXT
PRINTL
LOCAL:1 = 999
CALL COLOR_PRINT(@"%" モジュール", 8, LEFT%", カラー_注釈)
FOR LOCAL:0, 0, HO_MAX_MODULETYPE
	IF HO_MODULE_STOCK:(LOCAL:0) > 0
		IF LOCAL:1 > 80
			LOCAL:1 = 0
			PRINTL 
			PRINTFORM %TOSTR_SPACE(8)%
		ENDIF
		CALL HO_GET_MODULE_DATA(LOCAL:0, "MODULE_NAME")
		LOCALS:0 = %RESULTS%
		LOCALS:0 = %LOCALS:0% ×{HO_MODULE_STOCK:(LOCAL:0)}
		LOCAL:1 += STRLENS(LOCALS:0) + 2
		PRINTFORM %LOCALS:0%　
	ENDIF
NEXT
PRINTL
CALL SINGLE_DRAWLINE("-",カラー_灰色)

;-------------------------------------------------
;ルームの作成候補
;-------------------------------------------------
@HOUSING_CRAFT_PRINT_ROOM_OPTION
#DIM DYNAMIC FCOL
#DIM CONST BTN_ID = 100000
VARSET LOCAL

CALL COLOR_PRINT(" ルームの作成　　 ", カラー_注釈, "選択後に説明表示", カラー_選択不可)
PRINTL 
PRINTFORM %TOSTR_SPACE(8)%
FOR LOCAL:0, 0, HO_MAX_ROOMTYPE
	IF LOCAL:1 > 0 && LOCAL:1 % 4 == 0 && LOCAL:1 != LOCAL:2
		PRINTL 
		PRINTFORM %TOSTR_SPACE(8)%
		LOCAL:2 = LOCAL:1
	ENDIF
	TRYCCALLFORM HO_ROOM_IS_CRAFTABLE_{LOCAL:0}
		IF RESULT
			LOCAL:1 ++
			CALL HO_GET_ROOM_DATA(LOCAL:0, "ROOM_NAME")
			LOCALS:0 '= @"[%RESULTS, MAX_CHARANAME_LENGTH, LEFT%]"
			CALLF FuncTagSetText_Print(@"@B:{(LOCAL:0) + BTN_ID}@%LOCALS:0%  @/B@", 4)
		ENDIF		
	CATCH
		CONTINUE
	ENDCATCH
NEXT
PRINTL
PRINTL
RETURN

;-------------------------------------------------
;モジュールの作成候補
;-------------------------------------------------
@HOUSING_CRAFT_PRINT_MODULE_OPTION
#DIM DYNAMIC FCOL
#DIM CONST BTN_ID = 200000
VARSET LOCAL

CALL COLOR_PRINT(" モジュールの作成 ", カラー_注釈)
PRINTL 
PRINTFORM %TOSTR_SPACE(8)%
FOR LOCAL:0, 0, HO_MAX_MODULETYPE
	IF LOCAL:1 > 0 && LOCAL:1 % 4 == 0 && LOCAL:1 != LOCAL:2
		PRINTL 
		PRINTFORM %TOSTR_SPACE(8)%
		LOCAL:2 = LOCAL:1
	ENDIF
	TRYCCALLFORM HO_MODULE_IS_CRAFTABLE_{LOCAL:0}
		IF RESULT
			LOCAL:1 ++
			CALL HO_GET_MODULE_DATA(LOCAL:0, "MODULE_NAME")
			LOCALS:0 '= @"[%FuncTagSetText_ShapeSize(TOHALF(RESULTS), MAX_CHARANAME_LENGTH, 1, 0)%]"
			CALLF FuncTagSetText_Print(@"@B:{(LOCAL:0) + BTN_ID}@%LOCALS:0%  @/B@", 4)
		ENDIF		
	CATCH
		CONTINUE
	ENDCATCH
NEXT
PRINTL 
PRINTL
RETURN
CALL SINGLE_DRAWLINE("-",カラー_灰色)

;-------------------------------------------------
;作成対象ルームの詳細説明
;-------------------------------------------------
@HOUSING_CRAFT_PRINT_ROOM_CRAFTING_MESSAGE(INPUT_RESULT)
#DIM INPUT_RESULT
#DIM  DYNAMIC ROOM_ID
#DIM  DYNAMIC USE_RESOURCE, ITEM_LENGTH
#DIM  DYNAMIC HO_PRICE
#DIMS DYNAMIC ROOM_NAME
#DIMS DYNAMIC RESOURCE_NAME
#DIMS DYNAMIC _LIST_ROOM_TAG

ROOM_ID = INPUT_RESULT - 100000
CALL HO_GET_ROOM_DATA(ROOM_ID, "ROOM_NAME")
ROOM_NAME = %RESULTS%
CALL HO_GET_ROOM_DATA(ROOM_ID, "ROOM_TAG")
_LIST_ROOM_TAG = %RESULTS%

CALL SINGLE_DRAWLINE("-",カラー_灰色)
PRINTFORM  ★ %ROOM_NAME, MAX_CHARANAME_LENGTH, LEFT%
SETCOLOR カラー_注釈
PRINTFORM ルームタグ:
FOR LOCAL, 0, LIST_COUNT(_LIST_ROOM_TAG)
	LOCALS = %LIST_GET(_LIST_ROOM_TAG, LOCAL)%
	PRINTFORM <%LOCALS%>  
NEXT
PRINTL
CALL SINGLE_DRAWLINE("-",カラー_灰色)
RESETCOLOR
PRINTFORM %TOSTR_SPACE(4)%
PRINTFORM 作成に必要なリソース
PRINTL

TRYCCALLFORM HO_ROOM_CRAFT_RESOURCE_{ROOM_ID}(USE_RESOURCE)
	HO_PRICE = RESULT
	PRINTFORM %TOSTR_SPACE(8)%
	CALL COLOR_PRINT(@"%"資金",MAX_CHARANAME_LENGTH, LEFT%", カラー_注釈)
	CALL COLOR_PRINT(@"%NUM_FORMAT(HO_PRICE)%", カラー_選択中)
	PRINTL
	FOR LOCAL, 0, VARSIZE("USE_RESOURCE")
		IF USE_RESOURCE:LOCAL > 0
			CALL GET_ITEM_NAME(LOCAL)
			RESOURCE_NAME = %RESULTS%
			PRINTFORM %TOSTR_SPACE(8)%
			CALL COLOR_PRINT(@"%RESOURCE_NAME, MAX_CHARANAME_LENGTH, LEFT%", カラー_注釈)
			CALL COLOR_PRINT(@"%NUM_FORMAT(USE_RESOURCE:LOCAL), 8, LEFT%", カラー_選択中)
			RESULT = GET_RESOURCE_NUM(CHARACTER_POSITION, 国家ID_プレイヤー, LOCAL)
			CALL COLOR_PRINT(@"(所持数:{RESULT})", カラー_選択不可)
			PRINTL
		ENDIF
	NEXT
	CALL SINGLE_DRAWLINE("-",カラー_灰色)
	VARSET RESULTS
	VARSET LOCALS
	;とりあえず10行まで
	TRYCALLFORM HO_ROOM_DESC_{ROOM_ID}
	FOR LOCAL, 0 , 10
		LOCALS:LOCAL = %RESULTS:LOCAL%
	NEXT
	FOR LOCAL, 0 , 10
		SIF LOCALS:LOCAL == ""
			BREAK
		PRINTFORM %TOSTR_SPACE(8)%
		CALL COLOR_PRINT(LOCALS:LOCAL, カラー_注釈)
		PRINTL
	NEXT
	PRINTL
	PRINTFORM %TOSTR_SPACE(4)%
	CALLF FuncTagSetText_Print(@"@B:{ROOM_ID + 300000}@[これを作成する]@/B@", 5)
CATCH
	RETURN
ENDCATCH
RETURN

;-------------------------------------------------
;作成対象モジュールの詳細説明
;-------------------------------------------------
@HOUSING_CRAFT_PRINT_MODULE_CRAFTING_MESSAGE(INPUT_RESULT)
#DIM INPUT_RESULT
#DIM  DYNAMIC MODULE_ID
#DIM  DYNAMIC USE_RESOURCE, ITEM_LENGTH
#DIM  DYNAMIC HO_PRICE
#DIMS DYNAMIC MODULE_NAME
#DIMS DYNAMIC RESOURCE_NAME
#DIMS DYNAMIC _LIST_制限リスト

MODULE_ID = INPUT_RESULT - 200000
CALL HO_GET_MODULE_DATA(MODULE_ID, "MODULE_NAME")
MODULE_NAME = %RESULTS%
CALL HO_GET_MODULE_DATA(MODULE_ID, "制限")
_LIST_制限リスト = %RESULTS%

CALL SINGLE_DRAWLINE("-",カラー_灰色)
PRINTFORM  ★ %MODULE_NAME, MAX_CHARANAME_LENGTH, LEFT%
SETCOLOR カラー_注釈
PRINTFORM 設置可能なルームタグ:
FOR LOCAL, 0, LIST_COUNT(_LIST_制限リスト)
	LOCALS = %LIST_GET(_LIST_制限リスト, LOCAL)%
	PRINTFORM <%LOCALS%>  
NEXT
PRINTL
CALL SINGLE_DRAWLINE("-",カラー_灰色)
RESETCOLOR
PRINTFORM %TOSTR_SPACE(4)%
PRINTFORM 作成に必要なリソース
PRINTL

TRYCCALLFORM HO_MODULE_CRAFT_RESOURCE_{MODULE_ID}(USE_RESOURCE)
	HO_PRICE = RESULT
	PRINTFORM %TOSTR_SPACE(8)%
	CALL COLOR_PRINT(@"%"資金",MAX_CHARANAME_LENGTH, LEFT%", カラー_注釈)
	CALL COLOR_PRINT(@"%NUM_FORMAT(HO_PRICE)%", カラー_選択中)
	PRINTL
	FOR LOCAL, 0, VARSIZE("USE_RESOURCE")
		IF USE_RESOURCE:LOCAL > 0
			CALL GET_ITEM_NAME(LOCAL)
			RESOURCE_NAME = %RESULTS%
			PRINTFORM %TOSTR_SPACE(8)%
			CALL COLOR_PRINT(@"%RESOURCE_NAME, MAX_CHARANAME_LENGTH, LEFT%", カラー_注釈)
			CALL COLOR_PRINT(@"%NUM_FORMAT(USE_RESOURCE:LOCAL), 8, LEFT%", カラー_選択中)
			RESULT = GET_RESOURCE_NUM(CHARACTER_POSITION, 国家ID_プレイヤー, LOCAL)
			CALL COLOR_PRINT(@"(所持数:{RESULT})", カラー_選択不可)
			PRINTL
		ENDIF
	NEXT
	CALL SINGLE_DRAWLINE("-",カラー_灰色)
	VARSET RESULTS
	VARSET LOCALS
	;とりあえず10行まで
	TRYCALLFORM HO_MODULE_DESC_{MODULE_ID}
	FOR LOCAL, 0 , 10
		LOCALS:LOCAL = %RESULTS:LOCAL%
	NEXT
	FOR LOCAL, 0 , 10
		SIF LOCALS:LOCAL == ""
			BREAK
		PRINTFORM %TOSTR_SPACE(8)%
		CALL COLOR_PRINT(LOCALS:LOCAL, カラー_注釈)
		PRINTL
	NEXT
	PRINTL
	PRINTFORM %TOSTR_SPACE(4)%
	CALLF FuncTagSetText_Print(@"@B:{MODULE_ID + 400000}@[これを作成する]@/B@", 5)
CATCH
	RETURN
ENDCATCH
RETURN

;-------------------------------------------------
;ルーム作成処理実行
;-------------------------------------------------
@HOUSING_CRAFT_EXEC_ROOM_CRAFT(INPUT_RESULT)
#DIM INPUT_RESULT
#DIM  DYNAMIC ROOM_ID
#DIM  DYNAMIC USE_RESOURCE, ITEM_LENGTH
#DIM  DYNAMIC HO_PRICE
#DIM  DYNAMIC ERR_FLG
#DIMS DYNAMIC ROOM_NAME
#DIMS DYNAMIC RESOURCE_NAME
#DIMS DYNAMIC _LIST_ROOM_TAG

ROOM_ID = INPUT_RESULT - 300000
CALL HO_GET_ROOM_DATA(ROOM_ID, "ROOM_NAME")
ROOM_NAME = %RESULTS%
CALL HO_GET_ROOM_DATA(ROOM_ID, "ROOM_TAG")
_LIST_ROOM_TAG = %RESULTS%


;リソース量をチェックし問題なければ作成処理
TRYCCALLFORM HO_ROOM_CRAFT_RESOURCE_{ROOM_ID}(USE_RESOURCE)
	HO_PRICE = RESULT
	IF MONEY < HO_PRICE
		CALL COLOR_PRINTL(@"所持金が不足しています", カラー_注意)
		ERR_FLG ++
	ENDIF
	FOR LOCAL, 0, VARSIZE("USE_RESOURCE")
		IF USE_RESOURCE:LOCAL > 0
			CALL GET_ITEM_NAME(LOCAL)
			RESOURCE_NAME = %RESULTS%
			RESULT = GET_RESOURCE_NUM(CHARACTER_POSITION, 国家ID_プレイヤー, LOCAL)
			IF RESULT < USE_RESOURCE:LOCAL 
				CALL COLOR_PRINTL(@"%RESOURCE_NAME%が不足しています", カラー_注意)
				ERR_FLG ++
			ENDIF
		ENDIF
	NEXT
	IF ERR_FLG == 0
		MONEY -= HO_PRICE
		FOR LOCAL, 0, VARSIZE("USE_RESOURCE")
			IF USE_RESOURCE:LOCAL > 0
				CALLF REMOVE_RESOURCE(CHARACTER_POSITION, 国家ID_プレイヤー, LOCAL, USE_RESOURCE:LOCAL)
			ENDIF
		NEXT
		HO_ROOM_STOCK:ROOM_ID ++
		CALL COLOR_PRINTW(@"%ROOM_NAME%を作成しました", カラー_注意)
		RETURN
	ELSE
		WAIT
		RETURN
	ENDIF
CATCH
	RETURN
ENDCATCH

RETURN

;-------------------------------------------------
;モジュール作成処理実行
;-------------------------------------------------
@HOUSING_CRAFT_EXEC_MODULE_CRAFT(INPUT_RESULT)
#DIM INPUT_RESULT
#DIM  DYNAMIC MODULE_ID
#DIM  DYNAMIC USE_RESOURCE, ITEM_LENGTH
#DIM  DYNAMIC HO_PRICE
#DIM  DYNAMIC ERR_FLG
#DIMS DYNAMIC MODULE_NAME
#DIMS DYNAMIC RESOURCE_NAME
#DIMS DYNAMIC _LIST_MODULE_TAG

MODULE_ID = INPUT_RESULT - 400000
CALL HO_GET_MODULE_DATA(MODULE_ID, "MODULE_NAME")
MODULE_NAME = %RESULTS%
CALL HO_GET_MODULE_DATA(MODULE_ID, "MODULE_TAG")
_LIST_MODULE_TAG = %RESULTS%


;リソース量をチェックし問題なければ作成処理
TRYCCALLFORM HO_MODULE_CRAFT_RESOURCE_{MODULE_ID}(USE_RESOURCE)
	HO_PRICE = RESULT
	IF MONEY < HO_PRICE
		CALL COLOR_PRINTL(@"所持金が不足しています", カラー_注意)
		ERR_FLG ++
	ENDIF
	FOR LOCAL, 0, VARSIZE("USE_RESOURCE")
		IF USE_RESOURCE:LOCAL > 0
			CALL GET_ITEM_NAME(LOCAL)
			RESOURCE_NAME = %RESULTS%
			RESULT = GET_RESOURCE_NUM(CHARACTER_POSITION, 国家ID_プレイヤー, LOCAL)
			IF RESULT < USE_RESOURCE:LOCAL 
				CALL COLOR_PRINTL(@"%RESOURCE_NAME%が不足しています", カラー_注意)
				ERR_FLG ++
			ENDIF
		ENDIF
	NEXT
	IF ERR_FLG == 0
		MONEY -= HO_PRICE
		FOR LOCAL, 0, VARSIZE("USE_RESOURCE")
			IF USE_RESOURCE:LOCAL > 0
				CALLF REMOVE_RESOURCE(CHARACTER_POSITION, 国家ID_プレイヤー, LOCAL, USE_RESOURCE:LOCAL)
			ENDIF
		NEXT
		HO_MODULE_STOCK:MODULE_ID ++
		CALL COLOR_PRINTW(@"%MODULE_NAME%を作成しました", カラー_注意)
		RETURN
	ELSE
		WAIT
		RETURN
	ENDIF
CATCH
	RETURN
ENDCATCH
RETURN