;-------------------------------------------------
;「調教」の名称
;-------------------------------------------------
@SHOP_LIFE_NAME13
RESULTS:0 '= "調教"

;-------------------------------------------------
;「調教」の選択可否
;-------------------------------------------------
@SHOP_LIFE_CHECK13
#DIM _MASTER, MAX_CHARA_NUM
#DIM SLAVE, MAX_CHARA_NUM

SIF CFLAG:MASTER:捕虜先
	RETURN 0
SIF !CFLAG:MASTER:所属
	RETURN 0
SIF !GET_TRAIN_PAIR(_MASTER, SLAVE)
	RETURN 0
RETURN 1

;-------------------------------------------------
;「調教」でヤられる側の選択可能キャラ存在判定
;-------------------------------------------------
@SHOP_LIFE_CHECKCHARA13(ARG:0)
#DIM 対象
#DIM _MASTER, MAX_CHARA_NUM
#DIM SLAVE, MAX_CHARA_NUM

;閨に呼ぶの選択可能キャラ存在判定
CALL SHOP_LIFE_CHECKCHARA_NEYA(ARG:0)
対象 = RESULT
SIF ARG:0 == MASTER
	RETURN 0
SIF 対象 && CFLAG:(ARG:0):慰安参加者
	RETURN 2
SIF !GET_TRAIN_PAIR(_MASTER, SLAVE)
	RETURN 0
FINDELEMENT SLAVE, ARG:0
SIF RESULT == -1
	RETURN 0
RETURN CHECK91(ARG:0, 2, 対象)

;-------------------------------------------------
;「調教」で調教先になる可否判定
;　無限ループ回避のため性知識が必要
;-------------------------------------------------
@SHOP_LIFE_CHECKCHARA_SUB13(ARG:0)
#DIM _MASTER, MAX_CHARA_NUM
#DIM SLAVE, MAX_CHARA_NUM

SIF ARG:0 == MASTER
	RETURN 0

SIF CFLAG:(ARG:0):閨に呼ぶで選択中 && !CFLAG:(ARG:0):慰安参加者
	RETURN 2

SIF !GET_TRAIN_PAIR(_MASTER, SLAVE)
	RETURN 0
FINDELEMENT _MASTER, ARG:0
SIF RESULT == -1
	RETURN 0

LOCAL:0 = TMP_COUNTRY_RELATION:(CFLAG:MASTER:所属):(CFLAG:(ARG:0):所属)
;捕虜でない、非主人公、同一勢力or共闘勢力、性知識1以上
RETURN CHECK91(ARG:0, 2, ARG:0 != MASTER && !CFLAG:(ARG:0):捕虜先 && LOCAL:0 >= 2 && ABL:(ARG:0):性知識 > 0 && CFLAG:(ARG:0):行動不能状態 != 行動不能_臨月)
RETURN 0

;-------------------------------------------------
;「調教」の左カラムメニューの入力処理
;-------------------------------------------------
@SHOP_LIFE_EVENTBUY13
FLAG:拠点フェイズページ = 1
FLAG:夜這い = 0
RETURN 0

;-------------------------------------------------
;「調教」の右カラムキャラリスト１のボタン（関数未指定なら選択中色なし表示）
;-------------------------------------------------
@SHOP_LIFE_LIST1_BUTTON13(ARG:0, ARG:1)
;キャラ, 選択中カラー表示フラグ, ボタン番号に追加する数値, CHECKCHARAの戻り値
CALL COLUMN_RIGHT_CHARALIST_BUTTON(ARG:0, CFLAG:(ARG:0):閨に呼ぶで選択中, SHOP_LIFE_LIST1_ADD_INPUT, ARG:1, 0, "TOP")
RETURN 0

;-------------------------------------------------
;「調教」の右カラムキャラリスト２のボタン（関数未指定なら選択中色なし表示）
;-------------------------------------------------
@SHOP_LIFE_LIST2_BUTTON13(ARG:0, ARG:1)
;キャラ, 選択中カラー表示フラグ, ボタン番号に追加する数値, CHECKCHARAの戻り値
CALL COLUMN_RIGHT_CHARALIST_BUTTON(ARG:0, CFLAG:(ARG:0):慰安参加者, SHOP_LIFE_LIST2_ADD_INPUT, ARG:1, 0, "BUTTOM")
RETURN 0

;-------------------------------------------------
;「調教」の右カラムキャラリストボタン（上）表示に追尾させる情報
;　デフォルトの情報を表示しない場合はRETURN 1
;-------------------------------------------------
@SHOP_LIFE_BUTTON_ADD_TOP13(ARG:0)
CALL SHOP_LIFE_BUTTON_ADD_BOTTOM_PRISONER(ARG:0)
RETURN RESULT

;-------------------------------------------------
;「調教」の右カラムキャラリストボタン（上）表示に追尾させる情報
;　デフォルトの情報を表示しない場合はRETURN 1
;-------------------------------------------------
@SHOP_LIFE_BUTTON_ADD_BOTTOM13(ARG:0)
CALL SHOP_LIFE_BUTTON_ADD_TOP_PRISONER(ARG:0)
RETURN RESULT


;-------------------------------------------------
;「調教」の右カラム表示処理
;-------------------------------------------------
@SHOP_LIFE_EVENTBUY_SHOW13
#DIM 選択人数
#DIM 調教先
#DIM あてがわれる側
#DIM 最大人数

最大人数 = MIN(CALC_SHOP_TIME() - SHOP_TIME, 8)
選択人数 = 0
調教先 = 0
あてがわれる側 = 0

FOR LOCAL:0, 0, CHARANUM
	;選択可能な条件を満たしているかどうか判定
	RESULT = 0
	IF CFLAG:(LOCAL:0):慰安参加者
		選択人数 ++
		調教先 ++
	ENDIF
	TRYCALLFORM SHOP_LIFE_CHECKCHARA{FLAG:拠点フェイズ選択コマンド}(LOCAL:0)
	IF RESULT == 1
		;選択中の人数を数える
		IF CFLAG:(LOCAL:0):閨に呼ぶで選択中
			選択人数 ++
			あてがわれる側 ++
			IF 選択人数 > 最大人数
				CFLAG:(LOCAL:0):閨に呼ぶで選択中 = 0
			ENDIF
		ENDIF
	ELSE
		;条件を満たしていなければ強制的にＯＦＦ
		CFLAG:(LOCAL:0):閨に呼ぶで選択中 = 0
	ENDIF
NEXT

CALL COLUMN_RIGHT_TITLE("対象者選択", TOSTR(選択人数), TOSTR(最大人数), "人数", "避妊時のみ3,000")
CALL COLUMN_RIGHT_PRINTL
CALL COLUMN_RIGHT_LINE
CALL COLUMN_RIGHT_PRINTL

IF 調教先 > 0 && あてがわれる側 > 0
	PRINTBUTTON "[このペアで決定]", 1
ELSE
	SETCOLOR カラー_選択不可
	PRINTPLAINFORM [このペアで決定]
	RESETCOLOR
ENDIF

PRINTPLAIN   
IF FLAG:慰安避妊薬
	PRINTBUTTON "[避妊あり]", 2
ELSE
	IF MONEY > 選択人数 * 3000
		PRINTBUTTON "[避妊なし]", 2
	ELSE
		SETCOLOR カラー_選択不可
		PRINTPLAINFORM [避妊なし]
		RESETCOLOR
	ENDIF
ENDIF

PRINTPLAIN   
CALL COLOR_PRINT("ご主人様と、一緒に調教を受ける人を選んでください", カラー_注釈)
CALL COLUMN_RIGHT_PRINTL
CALL COLUMN_RIGHT_LINE
CALL COLUMN_RIGHT_PRINTL

;２段組キャラリスト上とページ移動
CALL COLUMN_RIGHT_CHARALIST_TOP(1)
CALL COLUMN_RIGHT_PRINTL
CALL COLUMN_RIGHT_LINE
CALL COLUMN_RIGHT_PRINTL

PRINTFORM 調教先（する側）のキャラを選択してください
PRINTPLAIN   
SIF 選択人数 >= 最大人数
	SETCOLOR カラー_オレンジ
PRINTFORM (現在{選択人数}/{最大人数}人)
RESETCOLOR
CALL COLUMN_RIGHT_PRINTL
CALL COLUMN_RIGHT_LINE
CALL COLUMN_RIGHT_PRINTL

;２段組キャラリスト下とページ移動
CALL COLUMN_RIGHT_CHARALIST_BOTTOM(1)

RETURN 0

;-------------------------------------------------
;「調教」の右カラムボタン入力処理
;-------------------------------------------------
@SHOP_LIFE_EVENTBUY_SUB13(ARG:0)
;このメンバーで決定
IF ARG:0 == 1
	CALL DECIDE_TYOKYO_MEMBER()
	SIF !RESULT
		RETURN 0
;避妊ありなし
ELSEIF ARG:0 == 2
	LOCAL:1 = 0
	FOR LOCAL, 0, CHARANUM
		SIF CFLAG:LOCAL:閨に呼ぶで選択中
			LOCAL:1 ++
	NEXT
	SIF FLAG:慰安避妊薬 || MONEY >= LOCAL:1 * 3000
		FLAG:慰安避妊薬 = !FLAG:慰安避妊薬
	RETURN 0
ENDIF
RETURN 0

;-------------------------------------------------
;「調教」のリスト１入力処理
;-------------------------------------------------
@SHOP_LIFE_USERSHOP13(ARG:0)
;参加キャラの数を数える
LOCAL:5 = 0
FOR LOCAL:0, 0, CHARANUM
	IF CFLAG:(LOCAL:0):閨に呼ぶで選択中 || CFLAG:(LOCAL:0):慰安参加者
		LOCAL:5 ++
	ENDIF
NEXT
IF !CFLAG:(ARG:0):慰安参加者 && (CFLAG:(ARG:0):閨に呼ぶで選択中 || LOCAL:5 < MIN(CALC_SHOP_TIME() - SHOP_TIME, 8))
	;調教参加フラグを反転
	CFLAG:(ARG:0):閨に呼ぶで選択中 = !CFLAG:(ARG:0):閨に呼ぶで選択中
	LOCAL:5 += CFLAG:(ARG:0):閨に呼ぶで選択中 == 0 ? -1 # 1
	SIF FLAG:慰安避妊薬 && LOCAL:5 * 3000 > MONEY
		FLAG:慰安避妊薬 = 0
ENDIF
RETURN 0

;-------------------------------------------------
;「調教」のリスト２入力処理
;-------------------------------------------------
@SHOP_LIFE_USERSHOP_SUB13(ARG:0)
;参加キャラの数を数える
LOCAL:5 = 0
FOR LOCAL:0, 0, CHARANUM
	IF CFLAG:(LOCAL:0):閨に呼ぶで選択中 || CFLAG:(LOCAL:0):慰安参加者
		LOCAL:5 ++
	ENDIF
NEXT
IF !CFLAG:(ARG:0):閨に呼ぶで選択中 && (CFLAG:(ARG:0):慰安参加者 || LOCAL:5 < MIN(CALC_SHOP_TIME() - SHOP_TIME, 8))
	;調教参加フラグを反転
	CFLAG:(ARG:0):慰安参加者 = !CFLAG:(ARG:0):慰安参加者
	LOCAL:5 += CFLAG:(ARG:0):慰安参加者 == 0 ? -1 # 1
	SIF FLAG:慰安避妊薬 && LOCAL:5 * 3000 > MONEY
		FLAG:慰安避妊薬 = 0
ENDIF
RETURN 0

;-------------------------------------------------
;「調教」の右カラムボタン入力処理本体
;-------------------------------------------------
@DECIDE_TYOKYO_MEMBER()
#DIM 調教先
#DIM あてがわれる側
#DIM 参加人数

調教先 = 0
あてがわれる側 = 0
参加人数 = 0
FOR LOCAL:0, 0, CHARANUM
	IF CFLAG:(LOCAL:0):閨に呼ぶで選択中 || CFLAG:(LOCAL:0):慰安参加者
		CFLAG:(LOCAL:0):調教参加フラグ  = 1
		CFLAG:(LOCAL:0):強制友好化 = 1
		IF CFLAG:(LOCAL:0):閨に呼ぶで選択中
			あてがわれる側 ++
			CFLAG:(LOCAL:0):閨に呼ぶで選択中 = 0
		ELSE
			調教先 ++
		ENDIF
		IF FLAG:慰安避妊薬
			PRINTFORML %ANAME(LOCAL:0)%は避妊薬を服用した
			CFLAG:(LOCAL:0):避妊薬残ターン += 1
			MONEY -= 3000
		ENDIF
		参加人数 ++
	ENDIF
NEXT
IF あてがわれる側 == 0 || 調教先 == 0
	;参加キャラがいないなら処理せず0を返す
	RETURN 0
ENDIF

; MASTERを強制的に調教対象に
CFLAG:MASTER:調教参加フラグ = 1
CFLAG:MASTER:強制友好化 = 1

;START_TRAIN_COMMONでSHOP_TIMEが+1されるので、それをひいておく
SHOP_TIME --

SHOP_TIME += 参加人数

FLAG:慰安モード = 2
ITEM:A_ローター = 1
ITEM:A_バイブ = 1
ITEM:A_アナルバイブ = 1
ITEM:A_ペニスバンド = 1
ITEM:A_オナホール = 1
ITEM:A_クリキャップ = 1
ITEM:A_ニプルキャップ = 1
ITEM:A_麻薬 = 99
ITEM:A_縄 = 1
ITEM:A_鞭 = 1
ITEM:A_目隠し = 1
ITEM:A_口枷 = 1
ITEM:A_鼻フック = 1
ITEM:A_マジック = 1
ITEM:A_ローション = 99
ITEM:A_コンドーム = 99
ITEM:A_媚薬 = 99
ITEM:A_排卵誘発剤 = 99
ITEM:A_桃源香 = 99
ITEM:A_絶倫丸 = 99

FLAG:調教モード = 7

;ウフフフラグをONに
FLAG:ウフフフラグ = 1

;行動開始時の共通処理
CALL START_TRAIN_COMMON
RETURN 1

;-------------------------------------------------
;「調教」を受けるペアの抽出
;-------------------------------------------------
@GET_TRAIN_PAIR(_MASTER, SLAVE)
#FUNCTION
#DIM REF _MASTER
#DIM REF SLAVE
#DIM 対象
#DIM パートナー
#DIM ITER

ITER = 0
FOR 対象, 1, CHARANUM
	FOR パートナー, 対象 + 1, CHARANUM
		IF GROUPMATCH(REL_PROGRESS:対象:パートナー, 関係_主人, 関係_奴隷)
			IF REL_PROGRESS:対象:パートナー == 関係_主人
				_MASTER:ITER = 対象
				SLAVE:ITER = パートナー
			ELSE
				_MASTER:ITER = パートナー
				SLAVE:ITER = 対象
			ENDIF
			ITER ++
		ENDIF
	NEXT
NEXT

RETURNF ITER