;------------------------
; バーチャルアイドル
;------------------------
@WORK_VIDOL(ID)
#DIM ID
#DIM 魅力値
#DIM 視聴者数
#DIM 登録者
#DIM ランダム流入
#DIM 報酬

登録者 = CFLAG:ID:Vアイドル人気
視聴者数 = 0

; ベースの人気
; 技能
魅力値 = ABL:ID:話術 * 20
魅力値 += ABL:ID:社交 * 7
魅力値 += ABL:ID:露出 * 200

;TALENT
SIF TALENT:ID:臆病
	魅力値 -= 100
SIF TALENT:ID:反抗的
	魅力値 -= 100
SIF TALENT:ID:大人しい
	魅力値 -= 100
SIF TALENT:ID:プライド高い
	魅力値 -= 100
SIF TALENT:ID:生意気
	魅力値 -= 100
SIF TALENT:ID:無関心
	魅力値 -= 100
SIF TALENT:ID:感情乏しい
	魅力値 -= 100
SIF TALENT:ID:保守的
	魅力値 -= 100
SIF TALENT:ID:貞操観念
	魅力値 -= 100
SIF TALENT:ID:抑圧
	魅力値 -= 100
SIF TALENT:ID:孤高
	魅力値 -= 100
SIF TALENT:ID:サボり魔
	魅力値 -= 100
SIF TALENT:ID:男嫌い
	魅力値 -= 200
SIF TALENT:ID:気丈
	魅力値 += 100
SIF TALENT:ID:好奇心
	魅力値 += 100
SIF TALENT:ID:目立ちたがり
	魅力値 += 100
SIF TALENT:ID:解放
	魅力値 += 100
SIF TALENT:ID:献身的
	魅力値 += 200
SIF TALENT:ID:魅惑
	魅力値 += 500
SIF TALENT:ID:謎の魅力
	魅力値 += 500
SIF TALENT:ID:美脚
	魅力値 += 300
SIF TALENT:ID:美尻
	魅力値 += 300
SIF TALENT:ID:天の御遣い
	魅力値 += 1000
SIF TALENT:ID:求心力
	魅力値 += 1000
; 今回の配信
PRINTFORM %CALLNAME:ID%は
SELECTCASE RAND:3
	CASE 0
		; 料理動画
		PRINTL 料理放送を行ったようだ
		魅力値 += ABL:ID:料理 * 200
		EXP:ID:料理経験値 += 2
		EXP:ID:話術経験値 += 3
		SETCOLOR カラー_注釈
		PRINTL 料理経験値+2
		PRINTL 話術経験値+3
	CASE 1
		; 歌動画
		PRINTL 生歌放送を行ったようだ
		魅力値 += ABL:ID:歌唱 * 200
		EXP:ID:歌唱経験値 += 2
		EXP:ID:話術経験値 += 3
		SETCOLOR カラー_注釈
		PRINTL 歌唱経験値+2
		PRINTL 話術経験値+3
	CASE 2
		; 雑談動画
		PRINTL 雑談放送を行ったようだ
		魅力値 += ABL:ID:話術 * 200
		SETCOLOR カラー_注釈
		PRINTL 話術経験値+5
		EXP:ID:話術経験値 += 5
ENDSELECT
; 共通経験値
PRINTL 撮影経験+1
PRINTL 被写経験+1
RESETCOLOR
EXP:ID:62 += 1
EXP:ID:63 += 1

; ランダム流入(記入レベルに依存)
ランダム流入 = RAND:(500 + CFLAG:ID:1007 * 150) + CFLAG:ID:1008 * 150 + 300

視聴者数 += ランダム流入

; 登録者からの流入
視聴者数 += 登録者 / 33


; ベースの人気から算出して一定の割合で登録者になる
IF 魅力値 < 1000
	登録者 += ランダム流入 / 10
ELSEIF 魅力値 < 2000
	登録者 += ランダム流入 / 6
ELSEIF 魅力値 < 3000
	登録者 += ランダム流入 / 3
ELSE
	登録者 += ランダム流入 / 2
ENDIF

; 放送再生による報酬
報酬 = 視聴者数 / 200 + 1

; スパ〇
FOR LOCAL, 0, 視聴者数 / 100
	SELECTCASE RAND:5
		CASE 0
			報酬 += 1
		CASE 1
			報酬 += 5
		CASE 2
			報酬 += 10
		CASE 3
			報酬 += 50
		CASE 4
			報酬 += 100
	ENDSELECT
NEXT

PRINTFORML ------------%CALLNAME:ID%の生放送------------ 
PRINTFORML 今回の生放送の視聴者は{視聴者数}だった
PRINTFORML チャンネル登録者が{登録者 - CFLAG:ID:1003}人増えた
PRINTFORML 現在のチャンネル登録者数 {登録者}
PRINTFORML --------------------------------------------

; 登録者数追加
CFLAG:ID:1003 = 登録者
RETURN 報酬
