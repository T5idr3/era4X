;-------------------
;存在判定
;なにもさせないこと
;-------------------
@TERRITORY_EVENT_NOPPERABOU_EXISTS()
#DIM 対象
RETURN 1

;-------------------
;発生判定
;-------------------
@TERRITORY_EVENT_NOPPERABOU_DECISION(対象)
#DIM 対象
RETURN 1

;-------------------
;本体
;-------------------
@TERRITORY_EVENT_NOPPERABOU(対象)
#DIM 対象
PRINTFORML 夜道を行く%ANAME(対象)%が、ささやくような泣き声を聞いた
PRINTFORML 川の近くに佇む柳の下に、少女がうずくまっている
PRINTFORML 家出娘だろうか。面倒ごとでないといいが……
PRINTFORML 
CALL ASK_YN("声をかける", "立ち去る")

IF RESULT == 1
	PRINTFORML 触らぬ神に祟りなし
	PRINTFORML さっさと立ち去ることにした
	RETURN 1
ENDIF

PRINTFORML 放っておくのもなんだと、%ANAME(対象)%は少女に声を掛ける
PRINTFORMW ……ゆっくりとこちらを振りかえる少女には、顔がなかった！
PRINTFORMW のっぺらぼうだ！

IF LIMIT(MAXBASE:対象:気力, 1500, 2500) > RAND:3000
	PRINTFORML %ANAME(対象)%は悲鳴をあげそうになったところをこらえる
	PRINTFORMW よく見ると顔の真ん中に小さな視覚センサーが付いているアンドロイドだ
	PRINTFORMW 話を聞くと、事故で最近アンドロイドに換装したのだが、新しい見た目が気に入らずさめざめと泣いていたらしい
	PRINTFORMW %ANAME(対象)%は少女を根気よく励まし、少女を家へ返すことに成功した
	CALL PRINT_ADD_EXP(対象, "話術経験値", RAND:5 + 1, 1)
	MAXBASE:対象:気力 += 50
	CALL ICPRINT(@"%ANAME(対象)%の気力が<50>上昇し、<{MAXBASE:対象:気力}>になった", "W", カラー_注意)
ELSE
	PRINTFORML %ANAME(対象)%はあまりのことに悲鳴をあげ、逃げ出した！
	PRINTFORMW 道中思い切り転んで、怪我をしてしまった……
	CALL ICPRINT(@"治療費として$1,000払った", "W", カラー_注意)
	MONEY -= 1000
ENDIF

RETURN 1