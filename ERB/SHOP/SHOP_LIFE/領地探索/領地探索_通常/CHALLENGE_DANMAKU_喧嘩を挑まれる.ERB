;-------------------
;存在判定
;なにもさせないこと
;-------------------
@TERRITORY_EVENT_CHALLENGE_KENKA_EXISTS()
#DIM 対象
RETURN 1

;-------------------
;発生判定
;-------------------
@TERRITORY_EVENT_CHALLENGE_KENKA_DECISION(対象)
#DIM 対象
RETURN 1

;-------------------
;本体
;-------------------
@TERRITORY_EVENT_CHALLENGE_KENKA(対象)
#DIM 対象
PRINTFORML 道を歩いていると、軍人崩れのサイボーグに絡まれた
PRINTFORML 身なりはそれなりなので、勝てば金品は得られそうだが
PRINTL
CALL PRINT_ABILITY(対象, GETNUM(ABL, "白兵"))
CALL PRINT_ABILITY(対象, GETNUM(ABL, "知略"))
CALL ASK_YN("返り討ちにする", "逃げる")
IF RESULT == 1
	PRINTFORML そんな暇はない
	PRINTFORML とっととその場を後にした
	RETURN 1
ENDIF

PRINTFORML ・
PRINTFORML ・
PRINTFORMW ・
IF LIMIT(ABL:対象:白兵 + ABL:対象:知略, 75, 150) > RAND:150
	PRINTFORML 見事勝利した！
	PRINTFORML 気絶して倒れているサイボーグの端末からクレジットを奪ってその場を後にした
	CALL ICPRINT("賞金として$5000を手に入れた", "L", カラー_注意)
	CALL PRINT_ADD_EXP(対象, "白兵経験値", RAND:8 + 6, 1)
	CALL PRINT_ADD_EXP(対象, "知略経験値", RAND:8 + 6, 1)
	MONEY += 5000
	RETURN 1
ENDIF

PRINTFORML 負けてしまった
PRINTFORMW $3000を奪われた……
MONEY -= 3000
CALL ADD_COOLTIME(対象:0, 2)
RETURN 1