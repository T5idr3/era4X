;-------------------
;存在判定
;なにもさせないこと
;-------------------
@TERRITORY_EVENT_ARM_WRESLING_EXISTS()
#DIM 対象
RETURN 1

;-------------------
;発生判定
;-------------------
@TERRITORY_EVENT_ARM_WRESLING_DECISION(対象)
#DIM 対象
RETURN IS_COUNTRY(CFLAG:対象:所属)

;-------------------
;本体
;-------------------
@TERRITORY_EVENT_ARM_WRESLING(対象)
#DIM 対象

PRINTFORML 街角で人だかりをみかけた
PRINTFORML 力自慢のサイボーグ男が腕相撲の挑戦者を募っているようだ
PRINTFORMW 彼の目の前には、かつての挑戦者たちが置いて行った金が乱暴に積まれている
PRINTL
CALL PRINT_ABILITY(対象, GETNUM(ABL, "白兵"))
CALL ASK_YN("挑戦する", "やめておく")

IF RESULT == 1
	PRINTFORML そういうのはガラではない
	PRINTFORML 放っておくことにした
	RETURN 1
ENDIF

PRINTFORML 興味をひかれた%ANAME(対象)%は男に挑む事にした
PRINTFORML 男の腕は丸太のように太く、このような商売をするだけはあるようだ
PRINTFORMW 男は%ANAME(対象)%が勝負を持ち掛けると、鼻で笑ってきた
PRINTFORML ・
PRINTFORML ・
PRINTFORMW ・

IF LIMIT(ABL:対象:白兵, 30, 100) > RAND:100
	PRINTFORML %ANAME(対象)%は見事男に勝利した！
	PRINTFORMW 肩から丸太のような腕が外れ唖然としている男を他所に、%ANAME(対象)%は挑戦金とプラスアルファをもらいその場を後にした
	PRINTW +$500
	MONEY += 500
	RETURN 1
ENDIF

PRINTFORML やられてしまった…
PRINTFORMW 男は口ほどにもないと%ANAME(対象)%を嘲笑った
PRINTFORMW 掛け金を失った
PRINTW -$100
MONEY -= 100

RETURN 1