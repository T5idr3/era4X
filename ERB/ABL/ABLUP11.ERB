;レズっ気の可否
@ISABLUP11
;恋慕、親愛、淫乱、娼婦はMAXLV10
IF ABL:11 >= 10
	RETURN 0
ELSEIF ABL:11 >= 5 && TALENT:恋慕 == 0 && TALENT:親愛 == 0 && TALENT:淫乱 == 0 && TALENT:娼婦 == 0
	RETURN 0
ENDIF

CALL DECIDE_ABLUP11

IF Z == 0
	RETURN 0
ELSE
	RETURN 1
ENDIF


;レズっ気
@ABLUP11
DRAWLINE
;恋慕、親愛、淫乱、娼婦はMAXLV10
IF ABL:11 >= 10
	PRINTW 既にMAXです
	RETURN 0
ELSEIF ABL:11 >= 5 && TALENT:恋慕 == 0 && TALENT:親愛 == 0 && TALENT:淫乱 == 0 && TALENT:娼婦 == 0
	PRINTW 既にMAXです
	RETURN 0
ENDIF

CALL DECIDE_ABLUP11

;ＬＶ３から４に上げるときは異常経験必要（素質：[両刀]なら無視できる）
IF ABL:11 == 3 && TALENT:両刀 == 0
	PRINTS EXPNAME:50
	PRINTL あり、かつ
;ＬＶ４から５に上げるときは異常経験3以上必要（素質：[両刀]なら無視できる）
ELSEIF ABL:11 == 4 && TALENT:両刀 == 0
	PRINTS EXPNAME:50
	PRINTL 3以上、かつ
;ＬＶ８から９に上げるときは異常経験5以上必要（素質：[両刀]なら無視できる）
ELSEIF ABL:11 == 8 && TALENT:両刀 == 0
	PRINTS EXPNAME:50
	PRINTL 5以上、かつ
ENDIF

PRINT [0] - 
PRINTS PALAMNAME:6
PRINT の珠×
PRINTV A
IF C > 0
	PRINT 、
	PRINTS PALAMNAME:7
	PRINT の珠×
	PRINTV C
ENDIF
PRINT 、
PRINTS EXPNAME:40
PRINTV B
PRINT 以上
PRINT ……

IF I == 0
	PRINT ＯＫ
ELSE
	SIF I & 1
		PRINT 珠不足 
	SIF I & 2
		PRINT 経験不足 
	SIF I & 4
		PRINT 能力不足 
ENDIF
PRINTL 

IF D > 0
	PRINT [1] - 
	PRINTS PALAMNAME:0
	PRINT の珠×
	PRINTV D
	PRINT 、
	PRINTS EXPNAME:40
	PRINTV B
	PRINT 以上
	PRINT ……
	
	IF J == 0
		PRINT ＯＫ
	ELSE
		SIF J & 1
			PRINT 珠不足 
		SIF J & 2
			PRINT 経験不足 
		SIF J & 4
			PRINT 能力不足 
	ENDIF
	PRINTL 
ELSE
	J = 256
ENDIF

PRINTL [100] - やめる


INPUT
IF (RESULT < 0 || RESULT > 1) && RESULT != 100
	RESTART
ELSEIF I != 0 && RESULT == 0
	PRINTL 条件を満たしていません
	RESTART
ELSEIF J == 256 && RESULT == 1
	RESTART
ELSEIF J != 0 && RESULT == 1
	PRINTL 条件を満たしていません
	RESTART
ELSEIF RESULT == 100
	RETURN 0
ENDIF

ABL:11 += 1

IF RESULT == 0
	JUEL:6 -= A
	JUEL:7 -= C
ELSEIF RESULT == 1
	JUEL:0 -= D
ENDIF

PRINTS ABLNAME:11
PRINT のレベルが
PRINTV ABL:11
PRINTW になりました


;レズっ気のLVUP判定
@DECIDE_ABLUP11
;条件別にＯＫかダメかを記録する
I = 0
J = 0
;能力アップ可能なら1にする
Z = 0

IF ABL:11 == 0
	A = 200
	B = 50
	C = 0
	D = 1000
ELSEIF ABL:11 == 1
	A = 1000
	B = 200
	C = 0
	D = 5000
ELSEIF ABL:11 == 2
	A = 3000
	B = 500
	C = 1000
	D = 0
ELSEIF ABL:11 == 3
	A = 8000
	B = 1000
	C = 2000
	D = 0
ELSEIF ABL:11 == 4
	A = 20000
	B = 2000
	C = 5000
	D = 0
ELSEIF ABL:11 == 5
	A = 25000
	B = 2300
	C = 6000
	D = 0
ELSEIF ABL:11 == 6
	A = 30000
	B = 2600
	C = 8000
	D = 0
ELSEIF ABL:11 == 7
	A = 35000
	B = 3000
	C = 10000
	D = 0
ELSEIF ABL:11 == 8
	A = 40000
	B = 3500
	C = 15000
	D = 0
ELSEIF ABL:11 == 9
	A = 50000
	B = 4000
	C = 20000
	D = 0
ENDIF

;淫乱
SIF TALENT:淫乱
	TIMES A , 0.80
;娼婦
SIF TALENT:娼婦
	TIMES A , 0.50
;一線越えない
IF TALENT:一線越えない && (TALENT:恋慕 == 0 && TALENT:淫乱 == 0 && TALENT:服従 == 0 && TALENT:親愛 == 0 && TALENT:娼婦 == 0 && TALENT:隷属 == 0)
	TIMES A , 2.00
	TIMES B , 2.00
	TIMES C , 2.00
	TIMES D , 2.00
ENDIF

;ＬＶ３から４に上げるときは異常経験必要（素質：[両刀]なら無視できる）
IF ABL:11 == 3 && TALENT:両刀 == 0
	IF EXP:異常経験 == 0
		;異常経験が不足
		I |= 2
		J |= 2
	ENDIF
;ＬＶ４から５に上げるときは異常経験3以上必要（素質：[両刀]なら無視できる）
ELSEIF ABL:11 == 4 && TALENT:両刀 == 0
	IF EXP:異常経験 < 3
		;異常経験が不足
		I |= 2
		J |= 2
	ENDIF
;ＬＶ８から９に上げるときは異常経験5以上必要（素質：[両刀]なら無視できる）
ELSEIF ABL:11 == 8 && TALENT:両刀 == 0
	IF EXP:異常経験 < 5
		;異常経験が不足
		I |= 2
		J |= 2
	ENDIF
ENDIF

;欲情の珠で上げる
SIF JUEL:6 < A
	I |= 1
;屈服の珠で上げる
SIF JUEL:7 < C
	I |= 1
;レズ経験が必要
SIF EXP:レズ経験 < B
	I |= 2

IF I == 0
	Z = 1
ENDIF

IF D > 0
	;快Ｃの珠で上げる
	SIF JUEL:0 < D
		J |= 1
	;レズ経験が必要
	SIF EXP:レズ経験 < B
		J |= 2
	
	IF J == 0
		Z = 1
	ENDIF
ELSE
	J = 256
ENDIF
RETURN 1

