;撮影技能の可否
@ISABLUP13
;恋慕、親愛、服従、隷属はMAXLV10
IF ABL:13 >= 10
	RETURN 0
ELSEIF ABL:13 >= 5 && TALENT:恋慕 == 0 && TALENT:親愛 == 0 && TALENT:服従 == 0 && TALENT:隷属 == 0
	RETURN 0
ENDIF

CALL DECIDE_ABLUP13

IF Z == 0
	RETURN 0
ELSE
	RETURN 1
ENDIF

;撮影技能
@ABLUP13
DRAWLINE
;恋慕、親愛、服従、隷属はMAXLV10
IF ABL:13 >= 10
	PRINTW 既にMAXです
	RETURN 0
ELSEIF ABL:13 >= 5 && TALENT:恋慕 == 0 && TALENT:親愛 == 0 && TALENT:服従 == 0 && TALENT:隷属 == 0
	PRINTW 既にMAXです
	RETURN 0
ENDIF

CALL DECIDE_ABLUP13

PRINT [0] - 
PRINTS PALAMNAME:8
PRINT の珠×
PRINTV A
PRINT 、
PRINTS EXPNAME:62
PRINTV B
PRINT 以上
PRINT ……
IF I == 0
	PRINT ＯＫ
ELSE
	SIF I & 1
		PRINT 珠不足 
	SIF I & 2
		PRINT 経験不足 
ENDIF
PRINTL 

PRINTL [100] - やめる


INPUT
IF (RESULT < 0 || RESULT > 0) && RESULT != 100
	RESTART
ELSEIF I != 0 && RESULT == 0
	PRINTL 条件を満たしていません
	RESTART
ELSEIF RESULT == 100
	RETURN 0
ENDIF

ABL:13 += 1

IF RESULT == 0
	JUEL:8 -= A
ENDIF

PRINTS ABLNAME:13
PRINT のレベルが
PRINTV ABL:13
PRINTW になりました


;撮影技能LVUP判定
@DECIDE_ABLUP13
;条件別にＯＫかダメかを記録する
I = 0
;能力アップ可能なら1にする
Z = 0

IF ABL:13 == 0
	A = 75
	B = 5
ELSEIF ABL:13 == 1
	A = 500
	B = 10
ELSEIF ABL:13 == 2
	A = 3000
	B = 30
ELSEIF ABL:13 == 3
	A = 9000
	B = 50
ELSEIF ABL:13 == 4
	A = 24000
	B = 100
ELSEIF ABL:13 == 5
	A = 30000
	B = 200
ELSEIF ABL:13 == 6
	A = 35000
	B = 300
ELSEIF ABL:13 == 7
	A = 40000
	B = 400
ELSEIF ABL:13 == 8
	A = 45000
	B = 500
ELSEIF ABL:13 == 9
	A = 50000
	B = 600
ENDIF

;恋慕
IF TALENT:恋慕
	TIMES A , 0.60
	TIMES B , 0.60
ENDIF
;親愛
IF TALENT:親愛
	TIMES A , 0.40
	TIMES B , 0.40
ENDIF
;一線越えない
IF TALENT:一線越えない && (TALENT:恋慕 == 0 && TALENT:淫乱 == 0 && TALENT:服従 == 0 && TALENT:親愛 == 0 && TALENT:娼婦 == 0 && TALENT:隷属 == 0)
	TIMES A , 2.00
	TIMES B , 2.00
ENDIF

;習得の珠で上げる
SIF JUEL:8 < A
	I |= 1

SIF EXP:撮影経験 < B
	I |= 2

IF I == 0
	Z = 1
ENDIF

RETURN 1


