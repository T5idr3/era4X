;_KOJO_SYSTEM.ERBから呼ばれる口上用関数
;実行前・実行後・第三者実行前・第三者実行後に呼ばれるコマンド口上の関数
;同じカテゴリ分岐を通るのでカテゴリ分岐を下部の@KOJO_COM_CHECK_K7にまとめた
;─────────────────────────────────────── 
;●実行前
;─────────────────────────────────────── 
@KOJO_COM_BEFORE_K7
#DIM エル_対象
#DIM エル

;エル口上非表示をチェックする前にエルをとっておく
エル = NAME_TO_CHARA("エル")

;エル口上非表示
SIF CFLAG:エル:400 == 1
	RETURN 0

;口上カラーのセット
SETCOLOR エル_口上カラー

;衣装を取得し'エル_衣装'に格納
CALL GET_CLOTH_NAME_K7(エル)

;普段は主人公が対象
エル_対象 = MASTER

;カテゴリを決定して各口上を呼び出す
CALL KOJO_COM_CHECK_K7("前", エル_対象)

;口上カラーのリセット
RESETCOLOR

;描写のオンオフを渡す
RETURN RESULT

;─────────────────────────────────────── 
;●実行後
;─────────────────────────────────────── 
@KOJO_COM_AFTER_K7
#DIM エル_対象
#DIM エル

エル = NAME_TO_CHARA("エル")

SIF CFLAG:エル:400 == 1
	RETURN 0

SETCOLOR エル_口上カラー

エル_対象 = MASTER

CALL KOJO_COM_CHECK_K7("後", エル_対象)

RESETCOLOR

RETURN RESULT

;─────────────────────────────────────── 
;●第三者実行前
;─────────────────────────────────────── 
@KOJO_COM_THIRD_BEFORE_K7
#DIM エル_対象
#DIM エル

エル = NAME_TO_CHARA("エル")

SIF CFLAG:エル:400 == 1
	RETURN 0

SETCOLOR エル_口上カラー

;暫定的にメイン相手を主導権所有者に決定
エル_対象 = FLAG:主導権所有者
;主導権者がエル本人あるいはいなければ
IF エル_対象 == エル || エル_対象 == -1
	;エルが実行のとき対象
	IF IS_MPLY(エル)
		エル_対象 = MTAR:0
	;そうでなければ実行
	ELSE
		エル_対象 = MPLY:0
	ENDIF
ENDIF

SIF エル_対象 == -1
	RETURN 0

CALL KOJO_COM_CHECK_K7("前", エル_対象)

RESETCOLOR

RETURN RESULT

;─────────────────────────────────────── 
;●第三者実行後
;─────────────────────────────────────── 
@KOJO_COM_THIRD_AFTER_K7
#DIM エル
#DIM エル_対象

エル = NAME_TO_CHARA("エル")

SIF CFLAG:エル:400 == 1
	RETURN 0

SETCOLOR エル_口上カラー

エル_対象 = FLAG:主導権所有者
;主導権者がエル本人あるいはいなければ
IF エル_対象 == エル || エル_対象 == -1
	IF IS_MPLY(エル)
		エル_対象 = MTAR:0
	ELSE
		エル_対象 = MPLY:0
	ENDIF
ENDIF

SIF エル_対象 == -1
	RETURN 0

CALL KOJO_COM_CHECK_K7("後", エル_対象)

RESETCOLOR

RETURN RESULT

;─────────────────────────────────────── 
;●カテゴリ分岐
;　前後 = 実行前か実行後か
;　エル_対象 = 普段はMASTER 第三者なら相手を渡しておく
;─────────────────────────────────────── 
@KOJO_COM_CHECK_K7(前後, エル_対象)
#DIMS 前後
#DIM エル_対象
#DIM エル
#DIMS エル口上カテゴリ

エル = NAME_TO_CHARA("エル")
エル口上カテゴリ '= ""

;怒り哀しみによる離脱済み
;IF TCVAR:エル:53 > 1
;ENDIF

;虚ろ崩壊
IF TALENT:エル:虚ろ || TALENT:エル:崩壊
	エル口上カテゴリ '= "虚ろ"

;快楽失神or酒酔睡眠や離脱状態（KOJO_SYSTEM.ERBで、TCVAR:52 || TCVAR:53なら口上が抑制されるため、体力不足以外は死に分岐）
ELSEIF TCVAR:エル:51 || TCVAR:エル:52 || TCVAR:エル:53 == 1
	エル口上カテゴリ '= "失神"

;ターゲットなら喋れない苦痛系コマンド 87フィスト 88Ａフィスト 94首絞め 97腹パン
ELSEIF ( IS_MTAR(エル) && GROUPMATCH(SELECTCOM, 87, 88, 94, 97) ) || IS_EQUIP_TARGET(エル, 87, 88, 94, 97)
	エル口上カテゴリ '= "叫び"

;キス等の喋れないコマンド（単発継続）
ELSEIF GROUPMATCH(SELECTCOM, 20, 340, 341, 342) || IS_EQUIP(エル, 20, 340, 341, 342)
	エル口上カテゴリ '= "口塞"

;ターゲットなら喋れないコマンド 86口枷 90イラマチオ 101顔面騎乗 102Ａ顔面騎乗 105Ｗ顔面騎乗 132口内放尿 210触手口辱
ELSEIF ( IS_MTAR(エル) && GROUPMATCH(SELECTCOM, 86, 90, 101, 102, 105, 132, 210) ) || IS_EQUIP_TARGET(エル, 86, 90, 101, 102, 105, 132, 210)
	エル口上カテゴリ '= "口塞"

;プレイヤーなら喋れないコマンド 2クンニ 8アナル舐め 9乳首吸い 11フェラ 104足舐め 330日常フェラ
ELSEIF ( IS_MPLY(エル) && GROUPMATCH(SELECTCOM, 2, 8, 9, 11, 20, 104, 107, 330) ) || IS_EQUIP_PLAYER(エル, 2, 8, 9, 11, 20, 104, 107, 330)
	エル口上カテゴリ '= "口塞"

;閨コマンド
ELSEIF SELECTCOM >= 0 && SELECTCOM <= 299　|| GROUPMATCH(SELECTCOM, 331, 332, 333)
	エル口上カテゴリ '= "閨"

;日常コマンド（なりゆきまかせ未満）
ELSEIF SELECTCOM >= 300 && SELECTCOM < 393
	エル口上カテゴリ '= "日常"

ENDIF

;カテゴリ分岐例外処理
;日常フェラは前：日常、後：口塞として扱う
IF SELECTCOM == 330
	IF 前後 == "前"
		エル口上カテゴリ '= "日常"
	ELSE
		エル口上カテゴリ '= "口塞"
	ENDIF
ENDIF
;閨の「なにもしない」を猥談に流す。
IF SELECTCOM == 292 && FLAG:ウフフフラグ == 1 && CHECK_K7("陥落", エル_対象)
	IF 前後 == "前"
		エル口上カテゴリ '= "日常"
	ELSE
		エル口上カテゴリ '= "閨"
	ENDIF
ENDIF 

;カテゴリによる分岐
IF 前後 == "前"
	IF エル口上カテゴリ == "虚ろ"
		;CALL KOJO_K7_HOLLOW(エル_対象)
	ELSEIF エル口上カテゴリ == "失神"
		;CALL KOJO_K7_SYNCOPE(エル_対象)
	ELSEIF エル口上カテゴリ == "叫び"
		;CALL KOJO_K7_SHOUT(エル_対象)
	ELSEIF エル口上カテゴリ == "口塞"
		CALL KOJO_K7_CANNOT_TALK(エル_対象)
	ELSEIF エル口上カテゴリ == "閨"
		CALL KOJO_K7_NIGHT(エル_対象,"前")
	ELSEIF エル口上カテゴリ == "日常"
		CALL KOJO_K7_NOON(エル_対象,"前")
	ENDIF
ELSE
	IF エル口上カテゴリ == "閨"
		CALL KOJO_K7_NIGHT(エル_対象,"後")
	ELSEIF エル口上カテゴリ == "日常"
		CALL KOJO_K7_NOON(エル_対象,"後")
	ELSEIF エル口上カテゴリ == "口塞"
		CALL KOJO_K7_NOTTALK_AFTER(エル_対象)
	ENDIF
ENDIF

RETURN RESULT

