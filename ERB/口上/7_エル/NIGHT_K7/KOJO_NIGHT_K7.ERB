;─────────────────────────────────────── 
;■閨の攻め受け分岐
;　KOJO_K7_前.ERBとKOJO_K7_後.ERBで閨と判断されると呼び出される
;　閨は実行対象や主導権だけでは攻め受け関係が判断しにくいので
;　ここを通してまとめて分岐させる
;─────────────────────────────────────── 
@KOJO_K7_NIGHT(エル_対象, 前後)
#DIM エル
#DIM エル_対象
#DIMS 前後
#DIMS エル口上攻受カテゴリ

IF !エル_対象
	エル_対象 = MASTER
ENDIF

;リセット
エル = NAME_TO_CHARA("エル")
エル口上攻受カテゴリ '= ""
エル_エルが受け = 0

;─────────────────────────────────────── 
;●詳細カテゴリ決定
;─────────────────────────────────────── 
;エルが実行
IF IS_MPLY(エル)
	;土下座・おねだり（主導権に関わらず実行が受け）
	IF GROUPMATCH(SELECTCOM, 110, 111)
		エル口上攻受カテゴリ '= "エル受け"
	;100自慰 101顔面騎乗 102Ａ顔面騎乗 （主導権がなければ受け）
	ELSEIF GROUPMATCH(SELECTCOM, 100, 101, 102) && !IS_INITIATIVE(エル)
		エル口上攻受カテゴリ '= "エル受け"
	;主導権なし排泄者　130放尿する 131尿を掛ける 132口内放尿 150放屁　151顔騎放屁（主導権がなければ受け）
	ELSEIF GROUPMATCH(SELECTCOM, 130, 131, 132, 150, 151) && !IS_INITIATIVE(エル)
		エル口上攻受カテゴリ '= "エル受け"
	;それ以外は実行が攻め
	ELSE
		エル口上攻受カテゴリ '= "エル攻め"
	ENDIF
;エルがエル_対象
ELSEIF IS_MTAR(エル)
	;土下座・おねだり（主導権に関わらず対象が攻め）
	IF GROUPMATCH(SELECTCOM, 110, 111)
		エル口上攻受カテゴリ '= "エル攻め"
	;100自慰 101顔面騎乗 102Ａ顔面騎乗（主導権があれば攻め）
	ELSEIF GROUPMATCH(SELECTCOM, 100, 101, 102) && IS_INITIATIVE(エル)
		エル口上攻受カテゴリ '= "エル攻め"
	;主導権あり排泄指示者　130放尿する 131尿を掛ける 132口内放尿 150放屁　151顔騎放屁（主導権があれば攻め）
	ELSEIF GROUPMATCH(SELECTCOM, 130, 131, 132, 150, 151) && IS_INITIATIVE(エル)
		エル口上攻受カテゴリ '= "エル攻め"
	;それ以外は対象が受け
	ELSE
		エル口上攻受カテゴリ '= "エル受け"
	ENDIF
ENDIF

;─────────────────────────────────────── 
;●詳細カテゴリによる分岐
;─────────────────────────────────────── 
IF エル口上攻受カテゴリ == "エル受け"
	;文末決定関数で使用する
	エル_エルが受け = 1
	;前文か後文か
	IF 前後 == "前"
		CALL KOJO_K7_NIGHT_BEFORE_BOTTOM(エル_対象)
	ELSEIF 前後 == "後"
		CALL KOJO_K7_NIGHT_AFTER_BOTTOM(エル_対象)
	ENDIF
ELSEIF エル口上攻受カテゴリ == "エル攻め"
	;前文か後文か
	IF 前後 == "前"
		CALL KOJO_K7_NIGHT_BEFORE_TOP(エル_対象)
	ELSEIF 前後 == "後"
		CALL KOJO_K7_NIGHT_AFTER_TOP(エル_対象)
	ENDIF
ENDIF

;─────────────────────────────────────── 
;●終了
;─────────────────────────────────────── 
エル_エルが受け = 0
RESETCOLOR
RETURN RESULT

